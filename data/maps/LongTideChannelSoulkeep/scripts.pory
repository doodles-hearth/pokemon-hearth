const FLAG_HIDE_BOAT_GUY = FLAG_TEMP_1

mapscripts LongTideChannelSoulkeep_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_SHIP_SABERSIDE_SOULKEEP, 0: LongTideChannelSoulkeep_EventScript_AnchorBoat
	]
	MAP_SCRIPT_ON_TRANSITION {
		if (var(VAR_SHIP_SABERSIDE_SOULKEEP) == 0) {
			setobjectxyperm(LOCALID_LONGTIDE_BOAT_GUY, 57, 16)
			setobjectxyperm(LOCALID_LONGTIDE_WINGULL, 58, 15)

		} elif (var(VAR_SHIP_SABERSIDE_SOULKEEP) != 1) {
			setflag(FLAG_HIDE_BOAT_GUY)
		}
	}
}

script LongTideChannelSoulkeep_EventScript_AnchorBoat {
	lockall
	
	waitse
	playse(SE_LEDGE)
	applymovement(LOCALID_PLAYER, moves(jump_right, walk_down, delay_16, face_up))
	waitse

	delay(16)
	playse(SE_LEDGE)
	applymovement(LOCALID_LONGTIDE_BOAT_GUY, moves(jump_2_right, delay_16, face_down))
	waitmovement(LOCALID_LONGTIDE_BOAT_GUY)
	waitse

	playmoncry(SPECIES_WINGULL, CRY_MODE_NORMAL)
	applymovement(LOCALID_LONGTIDE_WINGULL, moves(walk_right * 2, walk_down))
	waitmoncry

	setvar(VAR_SHIP_SABERSIDE_SOULKEEP, 2)

	waitmovement(0)

	releaseall
}

script LongTideChannelSoulkeep_RouteSign {
	msgbox(format("{LEFT_ARROW} To the Open Sea"), MSGBOX_SIGN)
}

script LongTideChannelSoulkeep_EventScript_Wingull {
	lock
    faceplayer
	setseenmon(SPECIES_WINGULL)
	setspeakertomonname(SPECIES_WINGULL)
	playmoncry(SPECIES_WINGULL, CRY_MODE_NORMAL)
    msgbox(format(
        "Eeeh!"
    ))
    waitmoncry
    release
}

script LongTideChannelSoulkeep_EventScript_BoatGuy {
	lock
	faceplayer
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SEASIDE_OLD_MAN EMOTE_NORMAL}{SPEAKER NAME_OLD_MAN}I'm on my way to Saberside Town.\n"
		"Want to hop along?"
	))

	mcdialogueB(
		"All aboard!",
		"No need."
	)
	closemessage

	if (var(VAR_RESULT) == 0) {
		waitse
		playse(SE_LEDGE)
		applymovement(VAR_LAST_TALKED, moves(jump_2_left, delay_16 * 2))
		waitmovement(VAR_LAST_TALKED)
		waitse

		playmoncry(SPECIES_WINGULL, CRY_MODE_NORMAL)
		applymovement(LOCALID_LONGTIDE_WINGULL, moves(walk_up, walk_left * 2, walk_in_place_left * 5))
		waitmoncry

		hidefollower
		applymovement(LOCALID_PLAYER, moves(walk_up))
		waitmovement(LOCALID_PLAYER)
		playse(SE_LEDGE)
		applymovement(LOCALID_PLAYER, moves(jump_left, delay_16, face_down, delay_16))
		waitmovement(LOCALID_PLAYER)
		waitse

		setvar(VAR_SHIP_SABERSIDE_SOULKEEP, 0)
		playse(SE_MUGSHOT)
		warpsilent(MAP_SABERSIDE_TOWN, WARP_ID_SABERSIDE_BOAT)
	}

	release
}

script LongTideChannelSoulkeep_EventScripts_WhereChatot {
	lock
	faceplayer
	setnamedmon(SPECIES_CHATOT)
	if (flag(FLAG_POST_CHATOT)) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_BOY EMOTE_NORMAL}{SPEAKER NAME_LITTLE_BOY}Where's Chatot? I wanna see Chatot!"
		))
		closemessage
	} else {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_BOY EMOTE_NORMAL}{SPEAKER NAME_LITTLE_BOY}Oooh, Chatot's here!!\nI love Chatot!\p"
			"How would I talk to my friends in Yifu City without Chatot? We'd have to WRITE stuff to each other, YUCK!\p"
			"I love how it flicks its tail to keep a rhythm while it speaks!\lAnd it imitates any voice perfectly!"
		))
		closemessage

		setdescribedmon(SPECIES_CHATOT)
	}
	release
}
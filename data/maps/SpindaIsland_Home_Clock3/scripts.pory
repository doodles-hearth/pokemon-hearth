mapscripts SpindaIsland_Home_Clock3_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (flag(FLAG_SLOWPOKE_EVOLVED)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWBRO))
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWPOKE))
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SPINDA_ISLAND_INTRO_STATE, 2: SpindaIsland_Home_EventScript_MainQuestRecap
    ]
	MAP_SCRIPT_ON_LOAD {
		if (flag(FLAG_CAMERA_EXPLODED)) {
			setmetatile(28, 44, METATILE_SpindaIslandSecondary_PathBottom, FALSE)
			setmetatile(28, 45, METATILE_SpindaIsland_Grass, FALSE)
		}
		if (flag(FLAG_OPENED_CHEST)) {
			setmetatile(27, 6, METATILE_SpindaIsland_EmptyChestSide, TRUE)
		}
	}
}

script SpindaIsland_Home_Clock3_EventScript_OldWoman {
	lock
	faceplayer
	if (!flag(FLAG_SHUPPET_QUEST_STARTED)) {
		faceplayer
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"I think my house is haunted. There's something living in my attic.\p"
			"I don't know what to do…"
		))
		removefieldmugshot
		closemessage
		setflag(FLAG_SHUPPET_QUEST_STARTED)
	} elif (!flag(FLAG_HIDE_SHUPPETS)) {
		faceplayer
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"Oh, I'm too scared to go back inside…"
		))
		removefieldmugshot
		closemessage
	}

	if (flag(FLAG_HIDE_SHUPPETS)) {
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"Oh, I'm too scared to go back inside…"
		))
		removefieldmugshot
		closemessage

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
		speakername("Little Archie")
		msgbox(format(
			"Don't worry granny! There were some Shuppet in your attic, but I chased them away!"
		))
		removefieldmugshot
		closemessage

		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"Shuppet, you say?"
		))
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_HAPPY)
		msgbox(format(
			"How adorable of you to help out a poor old lady…\p"
			"How can I repay you?\p"
			"You look like you're hungry… Let me fix you a little snack!"
		))
		removefieldmugshot
		closemessage
		
		giveitem(ITEM_PB_AND_J)

		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_HAPPY)
		msgbox(format(
			"Have a good day, child."
		))
		removefieldmugshot
		closemessage

		turnobject(VAR_LAST_TALKED, DIR_WEST)
		removeobject(VAR_LAST_TALKED)
		playse(SE_EXIT)
		waitse
		setflag(FLAG_HIDE_OLD_WOMAN)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Cool! Lemme pack this up for later.\p"
			"Let's hope it doesn't taste like old people or something."
		))
		removefieldmugshot
		closemessage

		call(SpindaIsland_Common_CheckGotAllItemsForNewFaucet)
	}
	release
}

script SpindaIsland_Home_Clock3_EventScript_EmptyHouseSign {
	lock
	msgbox(format(
		"   "
    ))

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	speakername("Little Archie")
    msgbox(format(
		"… …\p"
		"A blank sign?"
    ))
	removefieldmugshot
	closemessage
	release
}

script SpindaIsland_Home_Clock3_EventScript_EmptyHouseDoor {
	lock
	speakername("God")
    msgbox(format(
		"“YEAH, DON'T BOTHER.”\p"
        "“I CREATED A CRAPTON OF HOUSES AND DIDN'T HAVE TIME TO FILL THEM ALL UP.\p"
        "“SORRY ABOUT THAT, HOPE YOU'RE HAVING FUN AND ALL.”"
    ))
	release
}
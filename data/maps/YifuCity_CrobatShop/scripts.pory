const LOCALID_CROBAT_GUARD = 4
const FLAG_CROBAT_PASSWORD_FIRST_HALF_CORRECT = FLAG_TEMP_1
const FLAG_CROBAT_PASSWORD_SECOND_HALF_CORRECT = FLAG_TEMP_2
const FLAG_HIDE_SHOPKEEPER = FLAG_TEMP_3

mapscripts YifuCity_CrobatShop_MapScripts {
	
	MAP_SCRIPT_ON_TRANSITION: YifuCity_CrobatShop_SetupNpcs
}

script YifuCity_CrobatShop_SetupNpcs {
	if (flag(FLAG_CROBAT_PASSWORD_CORRECT)) {
		setobjectmovementtype(LOCALID_CROBAT_GUARD, MOVEMENT_TYPE_FACE_LEFT)
		setobjectxyperm(LOCALID_CROBAT_GUARD, 7, 4)
	}

	if (var(VAR_CROBAT_HIDEOUT_STATE) < 4) {
		setflag(FLAG_HIDE_SHOPKEEPER)
	}
}

script YifuCity_CrobatShop_Cashier {
	lock
	faceplayer
	speakername("Shopkeeper")
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_ZUBAT EMOTE_NORMAL}G'day uh, welcome to the ye olde Crobat Shoppe!\p"
		"Have a lookee at our totally legal tchotchquees and memoralibia!"
	))
	waitmessage

	pokemart(Crobat_Mart)

	speakername("Shopkeeper")
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_ZUBAT EMOTE_NORMAL}We thank thee fer yer business! Tell yer friends!\p"
		"{FONT_SMALL}Unless yer friends are the police!{FONT_NORMAL}\p"
		"Wussthat? I said nothin', ya must be hearin' things!"
	))
	closemessage
	release
}

script YifuCity_CrobatShop_CashierLegit {
	lock
	faceplayer
	speakername("Shopkeeper")
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_CLERK EMOTE_NORMAL}Hello there! Welcome to the Yifu City shop!"
	))
	waitmessage

	pokemart(Legit_Mart)

	speakername("Shopkeeper")
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_CLERK EMOTE_NORMAL}We hope you'll come again! Have a good day!"
	))
	closemessage
	release
}

script YifuCity_CrobatShop_CashierLegitTM {
	lock
	faceplayer
	speakername("Shopkeeper")
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_CLERK EMOTE_NORMAL}Hi! I'm prepping up here, but come back soon for some excusive new items!"
	))
	// waitmessage

	// pokemart(Legit_Mart)

	// speakername("Shopkeeper")
	// msgbox(format(
		// "We hope you'll come again! Have a good day!"
	// ))
	closemessage
	release
}

mart Crobat_Mart {
	ITEM_OINTMENT
	ITEM_SUPER_OINTMENT
	ITEM_REVIVAL_POWDER
	ITEM_REVIVAL_BALM
	ITEM_ULTRA_BALL_KNOCKOFF
	ITEM_ESCAPE_ROPE_KNOCKOFF
	ITEM_SLOWPOKE_TAIL
}

mart Legit_Mart {
	ITEM_OINTMENT
	ITEM_SUPER_OINTMENT
	ITEM_REVIVAL_POWDER
	ITEM_REVIVAL_BALM
	ITEM_POKE_BALL
	ITEM_GREAT_BALL
	ITEM_ESCAPE_ROPE
}

// mart Legit_Mart_TM {
// 	ITEM_OINTMENT
// 	ITEM_SUPER_OINTMENT
// 	ITEM_POKE_BALL
// 	ITEM_GREAT_BALL
// 	ITEM_ESCAPE_ROPE
// 	ITEM_TM
// }

script YifuCity_CrobatShop_Guard {
	lock
	faceplayer
	if (flag(FLAG_CROBAT_PASSWORD_CORRECT)) {
		speakername("Shady man")
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_ZUBAT EMOTE_NORMAL}Standin' guard is boring… Yawn…"
		))
		release
		end
	}

	if (var(VAR_FACING) == DIR_NORTH) {
		applymovement(VAR_LAST_TALKED, moves(walk_in_place_faster_down))
	} else {
		applymovement(VAR_LAST_TALKED, moves(walk_in_place_faster_left))
	}

	applymovement(VAR_LAST_TALKED, moves(lock_facing_direction, walk_in_place_faster_left, unlock_facing_direction))
	speakername("Shady man")
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_ZUBAT EMOTE_NORMAL}Password?"
	))

	if (!flag(FLAG_GOT_CROBAT_PASSWORD_FIRST_HALF) || !flag(FLAG_GOT_CROBAT_PASSWORD_SECOND_HALF)) {
		mcdialogue(
			"Uh…",
			"Ehm…",
			"Well…"
		)
		closemessage

		delay(24)

		applymovement(VAR_LAST_TALKED, moves(emote_angry))
		speakername("Shady man")
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_ZUBAT EMOTE_NORMAL}That ain't the password! Scram!"
		))
		waitmovement(0)
		closemessage

		if (var(VAR_FACING) == DIR_NORTH) {
			applymovement(VAR_LAST_TALKED, moves(walk_down, lock_facing_direction, walk_up, unlock_facing_direction))
			applymovement(OBJ_EVENT_ID_PLAYER, moves(lock_facing_direction, lock_anim, jump_2_down, unlock_facing_direction, unlock_anim))
		} else {
			applymovement(VAR_LAST_TALKED, moves(walk_left, lock_facing_direction, walk_right, unlock_facing_direction))
			applymovement(OBJ_EVENT_ID_PLAYER, moves(lock_facing_direction, lock_anim, jump_2_left, unlock_facing_direction, unlock_anim))
		}

		waitmovement(VAR_LAST_TALKED)
		waitmovement(OBJ_EVENT_ID_PLAYER)

		applymovement(VAR_LAST_TALKED, moves(walk_in_place_faster_left))
		waitmovement(0)
		release
	} else {
		if (flag(FLAG_GOT_CROBAT_PASSWORD_FIRST_HALF)) {
			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"Noivern…",
				"Crobat…",
				"Swoobat…"
			)
			closemessage

			if (var(VAR_RESULT) == 1) {
				setflag(FLAG_CROBAT_PASSWORD_FIRST_HALF_CORRECT)
			}

			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"…is awesome!",
				"…is amazing!",
				"…is legendary!"
			)

			if (var(VAR_RESULT) == 0) {
				setflag(FLAG_CROBAT_PASSWORD_SECOND_HALF_CORRECT)
			}

			delay(24)

			if (flag(FLAG_CROBAT_PASSWORD_FIRST_HALF_CORRECT) && flag(FLAG_CROBAT_PASSWORD_SECOND_HALF_CORRECT)) {
				applymovement(VAR_LAST_TALKED, moves(emote_pensive))
				speakername("Shady man")
				msgbox(format(
					"{CREATE_MUGSHOT MUGSHOT_ZUBAT EMOTE_NORMAL}Hmmm… Ya don't look like the typical Zubat…\p"
					"… … … …\n… …Alright, ya can go through.\l"
					"Next time wear the damn uniform though, will ya?"
				))
				closemessage

				setflag(FLAG_CROBAT_PASSWORD_CORRECT)

				if (var(VAR_FACING) == DIR_NORTH) {
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left, face_right))
				}

				applymovement(VAR_LAST_TALKED, moves(walk_down, face_left))
				waitmovement(0)

			} else {
				delay(24)

				applymovement(VAR_LAST_TALKED, moves(emote_angry))
				speakername("Shady man")
				msgbox(format(
					"{CREATE_MUGSHOT MUGSHOT_ZUBAT EMOTE_NORMAL}That ain't the password! Scram!"
				))
				waitmovement(0)
				closemessage

				if (var(VAR_FACING) == DIR_NORTH) {
					applymovement(VAR_LAST_TALKED, moves(walk_down, lock_facing_direction, walk_up, unlock_facing_direction))
					applymovement(OBJ_EVENT_ID_PLAYER, moves(lock_facing_direction, lock_anim, jump_2_down, unlock_facing_direction, unlock_anim))
				} else {
					applymovement(VAR_LAST_TALKED, moves(walk_left, lock_facing_direction, walk_right, unlock_facing_direction))
					applymovement(OBJ_EVENT_ID_PLAYER, moves(lock_facing_direction, lock_anim, jump_2_left, unlock_facing_direction, unlock_anim))
				}

				waitmovement(VAR_LAST_TALKED)
				waitmovement(OBJ_EVENT_ID_PLAYER)

				clearflag(FLAG_CROBAT_PASSWORD_FIRST_HALF_CORRECT)
				clearflag(FLAG_CROBAT_PASSWORD_SECOND_HALF_CORRECT)
			}
		}
	}

	release
}

script YifuCity_CrobatShop_Customer1 {
	lock
	faceplayer
	speakername("Man")
	if (flag(FLAG_CLEARED_CROBAT_HIDEOUT)) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_UUME_MAN_2 EMOTE_NORMAL}Woah! So I was buying illegal items? That's wild!"
		))
	} else {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_UUME_MAN_2 EMOTE_NORMAL}This place is amazing!\nHow is everything so cheap?!"
		))
	}
	
	closemessage
	release
}

script YifuCity_CrobatShop_Customer2 {
	lock
	faceplayer
	speakername("Girl")
	if (flag(FLAG_CLEARED_CROBAT_HIDEOUT)) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_UUME_GIRL_1 EMOTE_NORMAL}I knew it! I knew this shop was shady!"
		))
	} else {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_UUME_GIRL_1 EMOTE_NORMAL}These Ultra Balls look… different, somehow…"
		))
	}
	closemessage
	release
}

script YifuCity_CrobatShop_EventScript_Samurai {
	lock
	faceplayer
	speakername("Samurai")
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAMURAI EMOTE_NORMAL}Shhh, don't look at me!\nSomeone tipped me off that the Crobat Shadows' band of misfits has a secret hideout right under the city!\p"
		"I'm trying to know more by standing around and listening!"
	))
	closemessage
	release
}
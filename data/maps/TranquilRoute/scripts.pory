const LOCALID_BERRYWOMAN = 5
const LOCALID_SAMURAI = 21
const LOCALID_CUT_MASTER = 22
const LOCALID_CROBAT_SHADOW_BOSS = 25
const LOCALID_CROBAT_SHADOW_GRUNT_TOP = 23
const LOCALID_CROBAT_SHADOW_GRUNT_RIGHT = 26
const LOCALID_CROBAT_SHADOW_GRUNT_LEFT = 24
const LOCALID_KECLEON = 27
const FLAG_HIDE_CUT_MASTER = FLAG_TEMP_1
const VAR_DO_CUT_MASTER_CUTSCENE = VAR_TEMP_0
const VAR_HOUR_OF_DAY = VAR_0x8000
const FLAG_HIDE_FLETCHLING = FLAG_TEMP_2

mapscripts TranquilRoute_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		call(TranquilRoute_EventScript_CheckCutGrass)
		call(TranquilRoute_EventScript_SpawnBirds)
	}
	MAP_SCRIPT_ON_TRANSITION: TranquilRoute_EventScript_SetupCutGrassCutscene
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_DO_CUT_MASTER_CUTSCENE, 1: TranquilRoute_EventScript_GrassCutscene
	]
}

script TranquilRoute_EventScript_SpawnBirds {
	// Random birds on roofs
	// Fletchling
	setflag(FLAG_HIDE_FLETCHLING)

	gettime
	// Day
	if (var(VAR_HOUR_OF_DAY) >= 9 && var(VAR_HOUR_OF_DAY) <= 17) {
		clearflag(FLAG_HIDE_FLETCHLING)
	}
}

// Cut grass cutscene
script TranquilRoute_EventScript_GrassCutscene {
	lock
	// They walk to the center of the garden
	applymovement(LOCALID_CUT_MASTER, moves(walk_left * 5, walk_up * 3, face_up))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 6, walk_up * 2))
	waitmovement(LOCALID_CUT_MASTER)
	waitmovement(OBJ_EVENT_ID_PLAYER)

	delay(24)

	speakername("Cut master")
	msgbox(format(
		"… … … … … …\n… … … … … …"
	))
	closemessage

	delay(16)

	applymovement(LOCALID_CUT_MASTER, TranquilRoute_CutMasterWalkLeft)
	waitmovement(0)
	delay(24)
	speakername("Cut master")
	msgbox(format(
		"Was it always this big?"
	))
	closemessage

	applymovement(LOCALID_CUT_MASTER, TranquilRoute_CutMasterWalkRight)
	waitmovement(0)
	delay(24)
	delay(24)
	speakername("Cut master")
	msgbox(format(
		"It doesn't even look like the same garden…"
	))
	closemessage

	applymovement(LOCALID_CUT_MASTER, TranquilRoute_CutMasterWalkBackToPlayer)
	waitmovement(0)
	delay(48)
	speakername("Cut master")
	msgbox(format(
		"I cannot thank you enough for this.\p"
		"I shall tend to my garden again, thanks to you."
	))
	closemessage

	// TODO reward

	setflag(FLAG_CUT_QUEST_COMPLETED)
	setvar(VAR_DO_CUT_MASTER_CUTSCENE, 0)

	release
}

script TranquilRoute_EventScript_SetupCutGrassCutscene {
	// The cut master is only out once the cut grass quest is completed, and only during the day
	if (!flag(FLAG_CUT_QUEST_COMPLETED)) {
		if (flag(FLAG_CUT_QUEST_CUT_ALL_GRASS)) {
			// If the player has just exited the cut master's house after having cut all the grass,
			// get ready for cutscene
			getplayerxy(VAR_TEMP_2, VAR_TEMP_3)
			if (var(VAR_TEMP_2) == 53 && var(VAR_TEMP_3) == 33) {
				setobjectmovementtype(LOCALID_CUT_MASTER, MOVEMENT_TYPE_FACE_LEFT)
				setobjectxyperm(LOCALID_CUT_MASTER, 52, 33)
				setvar(VAR_DO_CUT_MASTER_CUTSCENE, 1)
			}
		} else {
			setflag(FLAG_HIDE_CUT_MASTER)
		}
	} else {
		gettime
		if (var(VAR_HOUR_OF_DAY) < 7 || var(VAR_HOUR_OF_DAY) > 19) {
			setflag(FLAG_HIDE_CUT_MASTER)
		}
	}
}

// Cut master
script TranquilRoute_EventScript_CutMaster{
	speakername("Cut master")
	msgbox(format(
		"I'm enjoying tending my garden again.\p"
		"I like watching it slowly blossom into something beautiful."
	), MSGBOX_NPC)
}

script TranquilRoute_EventScript_CheckCutGrass{
	call_if_set(FLAG_CUT_QUEST_CUT_ALL_GRASS, TranquilRoute_EventScript_CutGrass)
}

script TranquilRoute_EventScript_CutGrass {
	// there is probably a smarter way to do this but here we are
	// set all the tiles that were cut to just be normal grass
	setmetatile(47, 31, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	
	setmetatile(48, 30, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	
	setmetatile(46, 29, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(47, 29, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	
	setmetatile(46, 28, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(47, 28, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	setmetatile(48, 28, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	
	setmetatile(46, 27, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(47, 27, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(48, 27, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	
	setmetatile(47, 26, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(48, 26, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	
	setmetatile(47, 25, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	
	setmetatile(47, 24, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	
	setmetatile(46, 23, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(47, 23, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	
	setmetatile(41, 22, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(42, 22, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(46, 22, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(47, 22, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)

	setmetatile(40, 21, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(41, 21, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(42, 21, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	setmetatile(43, 21, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(45, 21, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	setmetatile(46, 21, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(47, 21, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(48, 21, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)

	setmetatile(41, 20, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(42, 20, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(43, 20, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(44, 20, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	setmetatile(45, 20, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(46, 20, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(47, 20, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(48, 20, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)

	setmetatile(40, 19, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(42, 19, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	setmetatile(43, 19, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(44, 19, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(45, 19, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(46, 19, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(47, 19, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(48, 19, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(49, 19, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)

	setmetatile(41, 18, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(43, 18, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	setmetatile(44, 18, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(45, 18, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	setmetatile(46, 18, METATILE_PorytilesPrimaryTutorial_Grass2, FALSE)
	setmetatile(47, 18, METATILE_PorytilesPrimaryTutorial_Grass3, FALSE)
	setmetatile(48, 18, METATILE_PorytilesPrimaryTutorial_Grass, FALSE)
	return
}

script TranquilRoute_EventScript_Chikao {
	trainerbattle_single(
		TRAINER_CHIKAO,
		format("I've been studying Pokémon at school! I can't lose!"),
		format("I should have listened in class…")
	)
	speakername("Chikao")
	msgbox(format(
		"Some Pokémon have more attack, and some have more special attack!\p"
		"What's the difference? Uhh… It's too complicated to explain."
	))
	closemessage
	end
}

script TranquilRoute_EventScript_Kodomo {
	trainerbattle_single(
		TRAINER_KODOMO,
		format("Ha! Are you trying to avoid the tall grass?"),
		format("Go back to the grass, you animal!")
	)
	speakername("Kodomo")
	msgbox(format(
		"I should look for stronger Pokémon in the tall grass, but I'm a little scared of getting jumped by surprise!"
	))
	closemessage
	end
}

script TranquilRoute_EventScript_Takeshi {
	trainerbattle_single(
		TRAINER_TAKESHI,
		format("Stay right there! I'll defeat you with my ninja skills!"),
		format("I had the sun in my eyes!")
	)
	speakername("Takeshi")
	msgbox(format("I must hone my ninja skills!"), MSGBOX_AUTOCLOSE)
	end
}

script TranquilRoute_EventScript_Hana {
	trainerbattle_single(
		TRAINER_HIINA,
		format("Ugh, how can Kenji paint a portrait of me if people keep walking between us?"),
		format("Great, now my hair is all messed up.")
	)
	speakername("Hiina")
	msgbox(format("Kenji really is an amazing artist. I feel very lucky."), MSGBOX_AUTOCLOSE)
	end
}

script TranquilRoute_EventScript_Kenji {
	trainerbattle_single(
		TRAINER_KENJI,
		format("You're interrupting my creative flow!!"),
		format("Such beauty! What an artistic fight this was!")
	)
	speakername("Kenji")
	msgbox(format(
		"Hiina is the best subject I could dream of.\p"
		"…Hey, uh, please don't tell her I said that."
	), MSGBOX_AUTOCLOSE)
	end
}

movement TranquilRoute_Movement_MysteriousSamuraiWalksAway {
	walk_right * 6
}

script TranquilRoute_EventScript_Girl {
	speakername("Girl")
	msgbox(format("What? Oh, I don't fight.\pMy Pokémon and I are just strolling."), MSGBOX_NPC)
}

script TranquilRoute_EventScript_WhiteslateRoadblock {
	lock
	applymovement(LOCALID_SAMURAI, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)

	speakername("Samurai")
	msgbox(format("Sorry, you can't go through. The road ahead is blocked."))
	closemessage

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right))
	waitmovement(0)

	release
}

script TranquilRoute_EventScript_Shellos {
	lock
	faceplayer
	playmoncry(SPECIES_SHELLOS, CRY_MODE_NORMAL)
    if (var(VAR_RESULT)) {
        speakername("Shellos")
    } else {
        speakername("Slug")
    }
	msgbox(format(
		"Clelel!"
	))
	waitmoncry
	closemessage
	release
	end
}

script TranquilRoute_EventScript_BerryWoman {
	lock
	faceplayer
	// TODO EVA add speech bubble with  little musical notes
	if (flag(FLAG_RECEIVED_WAILMER_PAIL)) {
		speakername("Woman")
		msgbox(format("Enjoy gardening!"))
		closemessage
	}
	else {
		speakername("Woman")
		msgbox(format("Hi, young one! See this?"))
		closemessage
		applymovement(LOCALID_BERRYWOMAN, Common_Movement_FaceRight)
		waitmovement(0)
		delay(48)
		applymovement(LOCALID_BERRYWOMAN, Common_Movement_FacePlayer)
		waitmovement(0)
		speakername("Woman")
		msgbox(format(
			"You can plant berries in the ground in fertile spots like this.\p"
			"The seeds will eventually grow into a healthy berry tree, ready to be harvested!\p"
			"I see you're a wielder. Well, different berries have different effects when they're fed to Pokémon.\p"
			"I love berries, hopefully you'll soon learn to love them too!\p"
			"Here, try planting a few!"
		))
		closemessage
		giveitem(ITEM_ORAN_BERRY, 3)
		goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)

		speakername("Woman")
		msgbox(format(
			"Oh, right, I was almost forgetting!\p"
			"You must remember to water them regularly, or they won't grow!\p"
			"I like you. Take this!"
		))
		closemessage
		giveitem(ITEM_WAILMER_PAIL)
		goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)

		speakername("Woman")
		msgbox(format(
			"Isn't it cute? It's friend-shaped!\p"
			"Don't worry about it, I have a spare one.\p"
			"Have fun gardening!"
		))
		closemessage

		setflag(FLAG_RECEIVED_WAILMER_PAIL)
	}
	release
	end
}

script TranquilRoute_EventScript_Mareep {
	lock
	playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_MAREEP)
	msgbox(format("Beeeeeh…"))
	playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
	waitmoncry
	release
}

script TranquilRoute_EventScript_Mareep2 {
	lock
	playmoncry(SPECIES_MAREEP, CRY_MODE_WEAK)
	setspeakertomonname(SPECIES_MAREEP)
	msgbox(format("Baaaaah…"))
	playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
	waitmoncry
	release
}

script TranquilRoute_TrainerTips1 {
	msgbox(format(
		"Did you know?\p"
		"Some Pokémon only come out in the morning, in the evening, or at night.\p"
		"Visit the same spot at different times of day to have a chance of seeing rare Pokémon!"
	), MSGBOX_SIGN)
}

script TranquilRoute_EventScript_CrobatShadows_Camera {
	lock
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 2))
	waitmovement(0)
	call(TranquilRoute_EventScript_CrobatShadows_Cutscene)
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 2))
	waitmovement(0)
	special(RemoveCameraObject)
	release
}

script TranquilRoute_EventScript_CrobatShadows {
	lock
	call(TranquilRoute_EventScript_CrobatShadows_Cutscene)
	release
}

script TranquilRoute_EventScript_CrobatShadows_Cutscene {
	setvar(VAR_CROBAT_SHADOWS_FIRST_ENCOUNTERS_STATE, 1)

	speakername("Shady men")
	msgbox(format(
		"…Excellent idea…\p"
		"…Yeah, and then…\p"
		"… … …"
	))
	closemessage

	delay(20)

	applymovement(LOCALID_CROBAT_SHADOW_BOSS, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)

	speakername("Shady man")
	msgbox(format(
		"Alright boys, let's move."
	))
	closemessage

	applymovement(LOCALID_CROBAT_SHADOW_GRUNT_RIGHT, moves(walk_up, face_down))
	applymovement(LOCALID_CROBAT_SHADOW_GRUNT_LEFT, moves(walk_up, face_down))
	waitmovement(LOCALID_CROBAT_SHADOW_GRUNT_TOP)
	waitmovement(LOCALID_CROBAT_SHADOW_GRUNT_RIGHT)

	// o o o
	//   o
	// TODO EVA: Zubats aligning anim
	playse(SE_LEDGE)
	applymovement(LOCALID_CROBAT_SHADOW_GRUNT_LEFT, moves(face_down, zubats_align, delay_16, delay_16))
	applymovement(LOCALID_CROBAT_SHADOW_GRUNT_TOP, moves(face_down, zubats_align, delay_16, delay_16))
	applymovement(LOCALID_CROBAT_SHADOW_GRUNT_RIGHT, moves(face_down, zubats_align, delay_16, delay_16))
	waitmovement(LOCALID_CROBAT_SHADOW_GRUNT_LEFT)
	waitmovement(LOCALID_CROBAT_SHADOW_GRUNT_TOP)
	waitmovement(LOCALID_CROBAT_SHADOW_GRUNT_RIGHT)
	waitse

	//
	// oooo
	//
	applymovement(LOCALID_CROBAT_SHADOW_GRUNT_TOP, moves(walk_left * 5, delay_16))
	applymovement(LOCALID_CROBAT_SHADOW_GRUNT_LEFT, moves(walk_left * 5, delay_16))
	applymovement(LOCALID_CROBAT_SHADOW_GRUNT_RIGHT, moves(walk_left * 5, delay_16))
	applymovement(LOCALID_CROBAT_SHADOW_BOSS, moves(walk_up * 2, walk_left * 5))
	waitmovement(LOCALID_CROBAT_SHADOW_GRUNT_TOP)
	waitmovement(LOCALID_CROBAT_SHADOW_GRUNT_LEFT)
	waitmovement(LOCALID_CROBAT_SHADOW_GRUNT_RIGHT)
	waitmovement(LOCALID_CROBAT_SHADOW_BOSS)

	removeobject(LOCALID_CROBAT_SHADOW_GRUNT_TOP)
	removeobject(LOCALID_CROBAT_SHADOW_GRUNT_LEFT)
	removeobject(LOCALID_CROBAT_SHADOW_GRUNT_RIGHT)
	removeobject(LOCALID_CROBAT_SHADOW_BOSS)
	setflag(FLAG_HIDE_CROBAT_SHADOWS_TRANQUIL_RT)
}

script TranquilRoute_EventScript_TravelingMerchantFirstTime {
	lock
	speakername("Man")
	msgbox(format(
		"Mumble mumble… Should restock on ointment… Damn, where did I put my purse?…"
	))
	closemessage

	delay(24)
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
	waitse
	waitmovement(0)
	delay(48)

	faceplayer

	speakername("Man")
	msgbox(format(
		"Oh, hello! Sorry, shop's closed at the moment!\p"
		"… …Oh, sorry, we haven't met, have we? I'm not good with faces…\p"
		"I'm Okada, the traveling merchant! I travel through all of Toku on foot, selling useful goods and rare finds!\p"
		"I'm sold out on, well, pretty much everything right now… But you're definitely going to see me around!\p"
		"What's your name? {PLAYER}? It's nice to meet you, {PLAYER}!"
		"I see you're a young wielder, eh? Maybe we can even have a friendly battle next time!\p"
		"Now if you'll excuse me, I'm running late! See you around, {PLAYER}!"
	))
	closemessage

	turnobject(VAR_LAST_TALKED, DIR_EAST)

	speakername("Man")
	msgbox(format(
		"Come on, Keke."
	))
	closemessage

	specialvar(VAR_RESULT, GetPlayerFacingDirection)
	if (var(VAR_RESULT) == DIR_EAST) {
		applymovement(VAR_LAST_TALKED, moves(walk_up, walk_left * 4, walk_up * 6))
		applymovement(LOCALID_KECLEON, moves(walk_left, walk_up, walk_left * 5, walk_up * 5))
	} else {
		applymovement(VAR_LAST_TALKED, moves(walk_left * 4, walk_up * 7))
		applymovement(LOCALID_KECLEON, moves(walk_left * 5, walk_up * 6))
	}
	waitmovement(VAR_LAST_TALKED)
	waitmovement(LOCALID_KECLEON)

	removeobject(VAR_LAST_TALKED)
	removeobject(LOCALID_KECLEON)

	setflag(FLAG_MET_TRAVELING_MERCHANT)
	release
}

script TranquilRoute_EventScript_Kecleon {
	lock
	faceplayer

	playmoncry(SPECIES_KECLEON, CRY_MODE_NORMAL)
	speakername("Kecleon")
	msgbox(format(
		"Keckecke!"
	))
	waitmoncry
	closemessage

	release
}

script TranquilRoute_TrainerTips2 {
	msgbox(format(
		"Did you know?\p"
		"You can swap the Pokémon in your party using {R_BUTTON}.\p"
		"Set the lead Pokémon with {SELECT_BUTTON}."
	), MSGBOX_SIGN)
	// TODO EVA save that for when the player actually has several items to assign
	// msgbox(format(
	// 	"Did you know?\p"
	// 	"Several items can be assigned to {SELECT_BUTTON} from your bag."
	// ), MSGBOX_SIGN)
}

script TranquilRoute_DirectionSign {
	if (flag(FLAG_SAKU_KURA_REUNITED)) {
		msgbox(format(
			"{LEFT_ARROW} Whiteslate route\n"
			"{UP_ARROW} Sakura Village\p"
			"It looks like the sign has been written over recently…"
		), MSGBOX_SIGN)
	} else {
		msgbox(format(
			"{LEFT_ARROW} Whiteslate route\n"
			"{UP_ARROW} Saku and Kura Villages"
		), MSGBOX_SIGN)
	}
}

movement TranquilRoute_CutMasterWalkLeft {
	walk_left * 2
}

movement TranquilRoute_CutMasterWalkRight {
	walk_right * 3
}

movement TranquilRoute_CutMasterWalkBackToPlayer {
	walk_left
	walk_in_place_faster_down
}

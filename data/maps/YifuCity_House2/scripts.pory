const LOCALID_TAROUNTULA = 3

mapscripts YifuCity_House2_MapScripts {
	
}

script YifuCity_House2_Seamstress {
	lock
	faceplayer
	speakername("Seamstress")
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_UUME_OLD_WOMAN EMOTE_NORMAL}I've been working day and night on a dress for the Samurai Chief's daugther.\p"
		"This will be my greatest work!"
	))
	closemessage

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceDown)
	waitmovement(0)

	speakername("Seamstress")
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_UUME_OLD_WOMAN EMOTE_NORMAL}Tarountula, could you bring me some more of that red thread?"
	))
	closemessage

	applymovement(LOCALID_TAROUNTULA, moves(jump_in_place_right, walk_right * 2, walk_up, walk_right * 2, face_up, delay_16, jump_in_place_up, delay_16, walk_left * 2, walk_down, walk_left * 2))
	waitmovement(0)

	release
}

script YifuCity_House2_Model {
	lock
	faceplayer
	
	if(flag(FLAG_OBTAINED_CHOICE_SCARF)) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_MASQUERAIN_GIRL EMOTE_NORMAL}That Tarountula creeps me out…\nIt has so many legs…"
		))
		closemessage
	} else {
		speakername("Model")
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_MASQUERAIN_GIRL EMOTE_NORMAL}Yawn… Being a model is so tiring! My feet hurt from standing here for hours…\p"
			"I do get some sweet free stuff though! Here, you can have this, I already have this color!"
		))
		closemessage

		giveitem(ITEM_CHOICE_SCARF)
		setflag(FLAG_OBTAINED_CHOICE_SCARF)
	}
	
	release
}

script YifuCity_House2_Tarountula {
	lock
	faceplayer
	setseenmon(SPECIES_TAROUNTULA)
	playmoncry(SPECIES_TAROUNTULA, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_TAROUNTULA)
	msgbox(format("Thshh!"))
	waitmoncry
	closemessage

	if (!flag(FLAG_OBTAINED_DESTINY_KNOT)) {
		applymovement(LOCALID_TAROUNTULA, moves(lock_facing_direction, jump_in_place_down, unlock_facing_direction))
		waitmovement(0)

		giveitem(ITEM_DESTINY_KNOT)
		setflag(FLAG_OBTAINED_DESTINY_KNOT)
	}
	release
}
const FLAG_HIDE_MOM = FLAG_TEMP_1

mapscripts YifuCity_House3_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		getweekday
		if (var(VAR_RESULT) == WEEKDAY_TUE) {
			setflag(FLAG_HIDE_MOM)
		}
	}
}

script YifuCity_House3_EggGirlsMom {
	lock
	faceplayer
	setspeaker(SP_NAME_WOMAN)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_UUME_GIRL_2 0}I help out at the Pokémon daycare west of Windycape on tuesdays. Sometimes they'll let me bring back an egg for my daughter!"
	))
	release
}

script YifuCity_House3_EventScript_Togepi {
	lock
	faceplayer
	setseenmon(SPECIES_TOGEPI)
	setspeakertomonname(SPECIES_TOGEPI)
	msgbox(format(
        "Preee!"
    ))
    playmoncry(SPECIES_TOGEPI, CRY_MODE_NORMAL)
    waitmoncry
    release
    end
}

script YifuCity_House3_EventScript_EggGirl {
	lock
	faceplayer

	if (var(VAR_EGG_SPECIES_GIVEN_TO_EGG_GIRL) == SPECIES_NONE) {
		call(YifuCity_House3_NotTradedYet)
	} else {
		// TODO Make egg hatch after a while (nb steps? badge?)
		applymovement(VAR_LAST_TALKED, moves(emote_happy))
		setspeaker(SP_NAME_LITTLE_GIRL)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}Can't wait for my egg to hatch!"
		))
	}
	release
}

script YifuCity_House3_NotTradedYet {
	setspeaker(SP_NAME_LITTLE_GIRL)
	special(GetPlayerSirMissString)
	applymovement(VAR_LAST_TALKED, moves(emote_happy))
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}Hello {STR_VAR_1}! Do you like eggs?"
	))

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Yes!",
		"Yes, they're delicious!",
		"Not really.",
		"Eggs?"
	)
	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			applymovement(VAR_LAST_TALKED, moves(emote_happy))
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}Really?! Me too!! Pokémon eggs are so cool!\l"
				"I love not knowing what will jump out of the egg!\p"
				"Wanna trade me a mystery egg for a mystery egg?\l"
				"That way we'll both have a surprise!"
			))
		case 1:
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}WHAT? Ewww, gross! I don't mean EATING them!\p"
				"I just love waiting for a Pokémon to jump of of its egg, especially when I have no idea what it's gonna be!\p"
				"Wanna trade me a mystery egg for a mystery egg?\l"
				"That way we'll both have a surprise!"
			))
		case 2:
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}Oh… Alright…"
			))
			release
			end
		case 3:
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}Eggs! Pokémon eggs!"
				"I just love waiting for a Pokémon to jump of of its egg, especially when I have no idea what it's gonna be!\p"
				"Wanna trade me a mystery egg for a mystery egg?\l"
				"That way we'll both have a surprise!"
			))
	}

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Sure!",
		"No."
	)
	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			call(YifuCity_House3_TradeEgg)
		case 1:
			applymovement(VAR_LAST_TALKED, moves(emote_pensive))
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}Oh… Alright…"
			))
	}

	release
}

script YifuCity_House3_TradeEgg {
	special(ChoosePartyMon)
	waitstate
	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
		setspeaker(SP_NAME_LITTLE_GIRL)
		msgbox(format("{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}Have you changed your mind?"))
		closemessage
	} else {
		special(IsSelectedMonEgg)
		if (var(VAR_RESULT)) {
			specialvar(VAR_EGG_SPECIES_GIVEN_TO_EGG_GIRL, ScriptGetPartyMonSpeciesEvenIfEgg)
			special(DestroySelectedPartyMon)
			// TODO EVA random egg from pool
			// randomelement(SPECIES_TREECKO, SPECIES_TORCHIC, SPECIES_MUDKIP)
			// giveegg(VAR_RESULT)
			giveegg(SPECIES_WYNAUT)
			playfanfare(MUS_OBTAIN_ITEM)
			message(format("{PLAYER} received the mysterious egg!"))
			waitmessage
			waitfanfare

			applymovement(VAR_LAST_TALKED, moves(emote_happy))
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}Yay!!! I can't wait for my egg to hatch!\p"
				"Aren't you wondering what you're gonna get?"
			))
			closemessage
		} else {
			applymovement(VAR_LAST_TALKED, moves(emote_happy))
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format("{CREATE_MUGSHOT MUGSHOT_UUME_LITTLE_GIRL 0}That's not an egg, you silly!"))
			closemessage
		}
	}
}
mapscripts Soulkeep_Refuge_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setrespawn(HEAL_LOCATION_SOULKEEP)
	}
}

script Soulkeep_Refuge_EventScript_NurseSleep {
	createfieldmugshot(MUGSHOT_UUME_NURSE)
	setvar(VAR_0x8009, MUGSHOT_UUME_NURSE)
	call(Common_EventScript_PkmnCenterNurse_Sleep)
	waitmessage
	waitbuttonpress
	release
	end
}

script Soulkeep_Refuge_EventScript_Nurse {
	createfieldmugshot(MUGSHOT_UUME_NURSE)
	setvar(VAR_0x8009, MUGSHOT_UUME_NURSE)
	setvar(VAR_0x800B, LOCALID_NURSE_SOULKEEP)
	setvar(VAR_0x800A, LOCALID_CHANSEY_SOULKEEP)
	call(Common_EventScript_PkmnCenterNurse_CutTheBullshit)
	waitmessage
	waitbuttonpress
	release
	end
}

script Soulkeep_Refuge_EventScript_Pasture {
	createfieldmugshot(MUGSHOT_UUME_MAN_2)
	setvar(VAR_0x8009, MUGSHOT_UUME_MAN_2)
	call(Common_EventScript_PcGuy)
	release
	end
}

script Soulkeep_Refuge_EventScript_Man {
	lock
	faceplayer
	setspeaker(SP_NAME_FARMER)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_FARMER 0}We sure don't see a lot of trav'lers 'round these parts…\p"
		"You here for the Dojo challenge? Dojo's closed, unfortunately."
	))
	closemessage
	release
}

script Soulkeep_Refuge_EventScript_NewDojo {
	lock
	faceplayer
	setspeaker(SP_NAME_FARMER)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_FARMER 0}Some of the town's farmers got together with Guang and said they were going to reopen the old Dojo.\p"
		"Guang says he's tired of “living in the shadows”, whatever that means."
	))
	closemessage
	release
}

script Soulkeep_Refuge_EventScript_WhereCoreef {
	lock
	waitse
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, moves(emote_pensive))
	msgbox(format(
		"{SPEAKER NAME_TRAVELER}Hmmm…"
	))
	closemessage
	waitmovement(VAR_LAST_TALKED)
	
	delay(24)

	faceplayer

	if (flag(FLAG_READ_BOOK_ABOUT_SHRINES_AND_DECAY)) {
		msgbox(format(
			"{SPEAKER NAME_TRAVELER}Hello! I am an expert in geography.\nI just LOVE maps.\p"
			"Coreef Isle? It's a small islet off the coast of Saberside Town.\p"
			"There isn't much there. Just a bunch of fishermen and old people."
		))
	} else {
		msgbox(format(
			"{SPEAKER NAME_TRAVELER}Hello! I am an expert in geography.\nI just LOVE maps."
		))
	}
	closemessage

	applymovement(VAR_LAST_TALKED, moves(face_up))
	waitmovement(VAR_LAST_TALKED)

	release
}
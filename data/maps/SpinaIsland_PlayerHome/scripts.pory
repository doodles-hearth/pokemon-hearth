const LOCALID_MOM = 1

mapscripts SpinaIsland_PlayerHome_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		if (var(VAR_SPINDA_ISLAND_INTRO_STATE) < 1) {
			setvar(VAR_SPINDA_ISLAND_INTRO_STATE, 1)
		}
	}

	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_REPAIRED_FAUCET, 1: SpinaIsland_PlayerHome_EventScript_FixedFaucet
    ]
}

script SpinaIsland_PlayerHome_EventScript_FixedFaucet {
	lock
	applymovement(OBJ_EVENT_ID_PLAYER, SpinaIsland_PlayerHome_Movement_ArchieWalksUp)
	waitmovement(0)
	setvar(VAR_REPAIRED_FAUCET, 2)
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
	speakername("Little Archie")
	msgbox(format(
		"MOM! MOM!"
	))
	removefieldmugshot
	closemessage

	turnobject(LOCALID_MOM, DIR_SOUTH)

	applymovement(OBJ_EVENT_ID_PLAYER, SpinaIsland_PlayerHome_Movement_ArchieWalksUpToMom)
	waitmovement(0)

	turnobject(LOCALID_MOM, DIR_WEST)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
	speakername("Little Archie")
	msgbox(format(
		"Open the tap!! The {COLOR BLUE}WATER{COLOR DARK_GRAY}'s running!!!"
	))
	createfieldmugshot(MUGSHOT_MOM, EMOTE_HAPPY)
	speakername("Mom")
	msgbox(format(
		"Sure, honey! Thank you!\p"
		"(You sure took your sweet time!)"
	))
	removefieldmugshot
	closemessage

	turnobject(LOCALID_MOM, DIR_NORTH)
	delay(48)
	createfieldmugshot(MUGSHOT_MOM, EMOTE_NORMAL)
	speakername("Mom")
	msgbox(format(
		"… … …\p"
		"Aahhhhhhh, finally…"
	))
	removefieldmugshot
	closemessage

	delay(20)
	turnobject(LOCALID_MOM, DIR_WEST)

	createfieldmugshot(MUGSHOT_MOM, EMOTE_HAPPY)
	speakername("Mom")
	msgbox(format(
		"Alright, Archie, as promised, here's the {COLOR RED}sail{COLOR DARK_GRAY} I've sewn for you!"
	))
	removefieldmugshot
	closemessage

	giveitem(ITEM_BADASS_SAIL)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
	speakername("Little Archie")
	msgbox(format(
		"Yay!!! It looks so COOL!\p"
		"Thanks mom kaybye!!!!"
	))
	removefieldmugshot
	closemessage

	applymovement(OBJ_EVENT_ID_PLAYER, SpinaIsland_PlayerHome_Movement_ArchieWalksUpToMom)
	waitmovement(0)

	warp(MAP_SPINDA_ISLAND_HOME, 10, 43)

	release
}

movement SpinaIsland_PlayerHome_Movement_ArchieWalksUp {
	walk_up
}

movement SpinaIsland_PlayerHome_Movement_ArchieExits {
	walk_faster_left * 2
	walk_faster_down * 4
	walk_faster_right * 2
	walk_faster_down
}

movement SpinaIsland_PlayerHome_Movement_ArchieWalksUpToMom {
	walk_left * 2
	walk_up * 4
	walk_right * 2
}

script SpinaIsland_PlayerHome_EventScript_Mom {
	lock
	faceplayer

	if (flag(FLAG_REPAIRED_FAUCET)) {
		createfieldmugshot(MUGSHOT_MOM, EMOTE_HAPPY)
		msgbox(format(
			"Have fun, sweetie! Don't drown, please!"
		))
		removefieldmugshot
		closemessage
	} elif (checkitem(ITEM_SALTY_LEMONADE) && !flag(FLAG_GAVE_MOM_LEMONADE)) {
			msgbox(format(
				"Give mom the Salty Lemonade?"
			))

			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"Give",
				"Don't give"
			)
			closemessage

			if (var(VAR_RESULT) == 0) {
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
				speakername("Little Archie")
				msgbox(format(
					"Here, mom!"
				))
				removefieldmugshot
				closemessage

				createfieldmugshot(MUGSHOT_MOM, EMOTE_HAPPY)
				speakername("Mom")
				msgbox(format(
					"Oh, good, some fresh lemonade! I'm so thirsty!\p"
					"Thanks, honey!"
				))
				removefieldmugshot
				closemessage

				delay(10)
				playse(SE_BALL)
				waitse
				playse(SE_BALL)
				waitse
				playse(SE_BALL)
				waitse
				delay(20)

				playse(SE_PIN)
				applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
				waitmovement(0)
				delay(48)

				createfieldmugshot(MUGSHOT_MOM, EMOTE_ANGRY)
				speakername("Mom")
				msgbox(format(
					"What is wrong with you? This is super salty!\p"
					"Please take this away and don't do that again."
				))
				removefieldmugshot
				closemessage

				setflag(FLAG_GAVE_MOM_LEMONADE)
		} elif (checkitem(ITEM_WATER_STONE_2) == TRUE && !flag(FLAG_GAVE_MOM_WATER_STONE)) {
			msgbox(format(
				"Give mom the {COLOR BLUE}WATER{COLOR DARK_GRAY} stone?"
			))

			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"Give",
				"Don't give"
			)
			closemessage

			if (var(VAR_RESULT) == 0) {
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
				speakername("Little Archie")
				msgbox(format(
					"Here, mom!"
				))
				removefieldmugshot
				closemessage

				createfieldmugshot(MUGSHOT_MOM, EMOTE_NORMAL)
				speakername("Mom")
				msgbox(format(
					"What am I supposed to do with this? Lick it?\p"
					"Please turn the faucet on so I can drink some actual {COLOR BLUE}WATER{COLOR DARK_GRAY}!"
				))
				removefieldmugshot
				closemessage

				setflag(FLAG_GAVE_MOM_WATER_STONE)
			}
		} else {
			createfieldmugshot(MUGSHOT_MOM, EMOTE_NORMAL)
			speakername("Mom")
			msgbox(format(
				"Hurry up and turn the big faucet back on, please!\p"
				"I'll give you your {COLOR RED}sail{COLOR DARK_GRAY} when the tap's running."
			))
			removefieldmugshot
			closemessage	
		}
	} else {
		createfieldmugshot(MUGSHOT_MOM, EMOTE_NORMAL)
		speakername("Mom")
		msgbox(format(
			"Hurry up and turn the big faucet back on, please!\p"
			"I'll give you your {COLOR RED}sail{COLOR DARK_GRAY} when the tap's running."
		))
		removefieldmugshot
		closemessage
	}

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	
	release
}


script SpindaIsland_PlayerHome_EventScript_Diary {
	lock
	msgbox(format(
		"“Dear diarry,\n"
		"The ship is almost finished.\nIt is gonna be awsome.\p"
		"It will be even more beautiful then dad's.”"
	))
	release
}

script SpinaIsland_PlayerHome_EventScript_SpindaManual {
	msgbox(
		"“Super cool trick i taught spinda:\n"
		"using L and R makes her {COLOR RED}spin{COLOR DARK_GRAY}!\l"
		"i love my spinda, shes so awsome”",
	MSGBOX_SIGN
	)
}

script SpindaIsland_PlayerHome_EventScript_Poem {
	lock
	msgbox(format(
		"“A POEM BY ARCHIE\p"
		"Roses are blue\l"
		"Violetts are blue\l"
		"Everything is blue\l"
		"because its my favorite color.”"
	), MSGBOX_DEFAULT)
	closemessage

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
    speakername("Little Archie")
    msgbox(format(
		"I am such a poet."
    ))
    closemessage
	removefieldmugshot

	release
}

script SpindaIsland_PlayerHome_EventScript_Map {
	lock
	
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
    speakername("Little Archie")
    msgbox(format(
		"It's a map of the island!"
    ))
    closemessage
	removefieldmugshot

	release
}

script SpinaIsland_PlayerHome_EventScript_Window {
	lock

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
    speakername("Little Archie")
    msgbox(format(
		"Something's moving outside."
    ))
    closemessage
	removefieldmugshot
    
    release
}

script SpinaIsland_PlayerHome_EventScript_Window_Front {
	lock

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
    speakername("Little Archie")
    msgbox(format(
		"I can see the beach!"
    ))
    closemessage
	removefieldmugshot
    
    release
}

script SpinaIsland_PlayerHome_EventScript_EnterLivingRoom {
	lock

	playse(SE_PIN)
	applymovement(LOCALID_MOM, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)
	applymovement(LOCALID_MOM, Common_Movement_WalkInPlaceFasterLeft)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)

	createfieldmugshot(MUGSHOT_MOM, EMOTE_HAPPY)
    speakername("Mom")
    msgbox(format(
		"Morning, little Archie!\p"
		"You're quite the early bird today!"
    ))
	removefieldmugshot
    closemessage
	removefieldmugshot

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
    speakername("Little Archie")
    msgbox(format(
		"That's cause today's the big day!!"
    ))
	removefieldmugshot
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
	msgbox(format(
		"You haven't forgotten about my super secret thing, right?"
    ))
	closemessage
	removefieldmugshot

	createfieldmugshot(MUGSHOT_MOM, EMOTE_NORMAL)
    speakername("Mom")
    msgbox(format(
		"Of course not, darling! Your super secret thing is almost finished.\p"
		"I'll give it to you as soon as I'm done.\p"
		"But first, I sure could go for a nice, big, tall glass of icy {COLOR BLUE}WATER{COLOR DARK_GRAY}…"
    ))
	removefieldmugshot
    closemessage
	removefieldmugshot

	turnobject(LOCALID_MOM, DIR_NORTH)
	delay(40)
	playse(SE_PIN)
	applymovement(LOCALID_MOM, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)
	waitmovement(0)

	createfieldmugshot(MUGSHOT_MOM, EMOTE_NORMAL)
    speakername("Mom")
    msgbox(format(
		"Look at that, I forgot to turn the {COLOR BLUE}WATER{COLOR DARK_GRAY} back on…"
    ))
	turnobject(LOCALID_MOM, DIR_WEST)
	removefieldmugshot
	createfieldmugshot(MUGSHOT_MOM, EMOTE_HAPPY)
	msgbox(format(
		"Archie, would you be a dear and go round the house to turn the big faucet on?\p"
		"And then I'll give it to you, I promise!"
    ))
	removefieldmugshot
    closemessage
	turnobject(LOCALID_MOM, DIR_NORTH)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SAD)
    speakername("Little Archie")
    msgbox(format(
		"Ughhhhhh, fine…"
    ))
	closemessage
	removefieldmugshot

	if (!flag(FLAG_TOOK_SPINDA)) {
		createfieldmugshot(MUGSHOT_MOM, EMOTE_HAPPY)
		speakername("Mom")
		msgbox(format(
			"Oh, and where's Spinda? Take her for a little walk, will you?"
		))
		removefieldmugshot
		closemessage
	}

	setvar(VAR_SPINDA_ISLAND_INTRO_STATE, 2)
    
    release
}

script SpinaIsland_PlayerHome_EventScript_GetSpinda {
	lock

	removeobject(VAR_LAST_TALKED)
	givemon(SPECIES_SPINDA, 20)
	setflag(FLAG_RECEIVED_STARTER)
	
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
    speakername("Little Archie")
    msgbox(format(
		"I could never forget you, Spinda!"
    ))
	removefieldmugshot

	release
}

script SpinaIsland_PlayerHome_EventScript_WailmerDoll {
	lock

    createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
    speakername("Little Archie")
    msgbox(format(
		"How you doing, Wailmy?"
    ))
    closemessage
	removefieldmugshot
    
    release
}

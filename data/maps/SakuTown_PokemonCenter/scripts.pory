const LOCALID_NURSE = 1
const LOCALID_GRUNT = 3
const LOCALID_FRONT_SCYTHER = 8
const LOCALID_FRONT_FARMER_1 = 9
const LOCALID_FRONT_FARMER_2 = 10
const LOCALID_BED_SCYTHER = 13
const LOCALID_BED_FARMER_1 = 11
const LOCALID_BED_FARMER_2 = 12

const VAR_GET_QUEST_LOG_STATE_SAKURA_LOVERS = VAR_TEMP_0
const VAR_GET_QUEST_LOG_STATE_SAKURA_ESPEON = VAR_TEMP_1
const VAR_GET_QUEST_LOG_STATE_SAKURA_SCYTHER = VAR_TEMP_2

mapscripts SakuTown_PokemonCenter_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setrespawn(HEAL_LOCATION_SAKU_TOWN)

		returnqueststate(QUEST_SAKURALOVERS)
		copyvar(VAR_GET_QUEST_LOG_STATE_SAKURA_LOVERS, VAR_RESULT)
		returnqueststate(QUEST_SAKURAESPEON)
		copyvar(VAR_GET_QUEST_LOG_STATE_SAKURA_ESPEON, VAR_RESULT)
		returnqueststate(QUEST_SAKURASCYTHER)
		copyvar(VAR_GET_QUEST_LOG_STATE_SAKURA_SCYTHER, VAR_RESULT)

		setflag(FLAG_HIDE_SAKU_REFUGE_FARMERS_1)
		setflag(FLAG_HIDE_SAKU_REFUGE_FARMERS_2)

		// If conditions for scyther quest are met, then farmers_1 are shown
		if (
			var(VAR_GET_QUEST_LOG_STATE_SAKURA_LOVERS) == 4
			&& var(VAR_GET_QUEST_LOG_STATE_SAKURA_ESPEON) == 4
			&& var(VAR_GET_QUEST_LOG_STATE_SAKURA_SCYTHER) == 1
		) {
			clearflag(FLAG_HIDE_SAKU_REFUGE_FARMERS_1)
			setflag(FLAG_HIDE_SAKU_REFUGE_FARMERS_2)

			// If scyther quest is reward-ready or complete
		} elif (var(VAR_GET_QUEST_LOG_STATE_SAKURA_SCYTHER) == 3 || var(VAR_GET_QUEST_LOG_STATE_SAKURA_SCYTHER) == 4){
			setflag(FLAG_HIDE_SAKU_REFUGE_FARMERS_1)
			setflag(FLAG_HIDE_SAKU_REFUGE_FARMERS_2)

			// If scyther quest is active, show farmers in bed area
		} elif (var(VAR_SAKU_KURA_QUEST_SCYTHER_STATE) >= 2) {
			clearflag(FLAG_HIDE_SAKU_REFUGE_FARMERS_2)
			setflag(FLAG_HIDE_SAKU_REFUGE_FARMERS_1)
		} 
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_SAKU_KURA_QUEST_SCYTHER_STATE, 1: SakuTown_PokemonCenter_EventScript_ScytherQuest
	]
}

script SakuTown_PokemonCenter_EventScript_Nurse {
	// setvar(VAR_0x800B, LOCALID_NURSE)
	// call(Common_EventScript_PkmnCenterNurse_CutTheBullshit)
	// waitmessage
	// waitbuttonpress
	lock

	faceplayer
	speakername("Nurse")
	msgbox(format(
		"Hello! Apologies for the mess, we're not fully set up yet!\p"
		"Let me heal your Pokémon!"
	))

	fadescreenswapbuffers(FADE_TO_BLACK)
	playfanfare(MUS_HEAL)
	waitfanfare
	special(HealPlayerParty)
	callnative(UpdateFollowingPokemon)
	fadescreenswapbuffers(FADE_FROM_BLACK)

	speakername("Nurse")
	msgbox(format(
		"Come back soon!"
	))

	turnobject(VAR_LAST_TALKED, DIR_SOUTH)

	release
	end
}

script SakuTown_PokemonCenter_EventScript_BewareOfFallingBricks {
	lock
	faceplayer
	speakername("Nurse")
	msgbox(format(
		"This Pokémon refuge is brand-new, courtesy of the Toku Council!\p"
		"As you can see, it's still being built. Watch out for the falling bricks, ha ha, just kidding!\p"
		"Seriously though, do watch out. We're held responsible if anything happens to you."
	))
	closemessage
	release
	end
}

script SakuTown_PokemonCenter_EventScript_ScytherQuest {
	lock
	setvar(VAR_SAKU_KURA_QUEST_SCYTHER_STATE, 2)
	applymovement(LOCALID_NURSE, moves(face_right))
	waitmovement(0)
	speakername("Nurse")
	msgbox(format("I'm sorry, but there's nothing I can do yet. We haven't received the tools that allow us to heal a Pokémon in this dire of a state."))
	waitmessage
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 5))
	speakername("Farmer")
	msgbox(format("Well y'have to do somet'in'! Can'tcha see she's in pain?!"))
	waitmessage
	closemessage
	applymovement(LOCALID_NURSE, moves(walk_in_place_down))
	waitmovement(0)
	speakername("Nurse")
	msgbox(format("She can stay here for as long as she needs to. I'll try to keep her in as good of a shape as I can."))
	waitmessage
	closemessage
}

script SakuTown_PokemonCenter_EventScript_Farmer1 {
	lock
	faceplayer
	speakername("Farmer")
	returnqueststate(QUEST_SAKURASCYTHER)
	switch(var(VAR_RESULT))
	{
		case 1:
			call(FarmerGivesQuest)
			break
		case 2:
			call(FarmerWaitsMedicine)
			break
	}
	release
	end
}

script(local) FarmerGivesQuest {
	msgbox(format("The nurse 'ere said that the new fangled technology from the Toku Council isn't here yet. As such, she can't be cured of whatever's ailin' her.\pPlease! If you can spare some time, can you help us find someone or something to cure our girl?"))
	startquest(QUEST_SAKURASCYTHER)
	applymovement(LOCALID_FRONT_FARMER_1, moves(face_original_direction))
	return
}

script(local) FarmerWaitsMedicine {
	msgbox(format("So? Have ya found anythin' yet?"))
	checkitem(ITEM_SPECIAL_MEDICINE, 1)
	if(var(VAR_SAKU_KURA_QUEST_SCYTHER_STATE) == 6 && var(VAR_RESULT) == TRUE){
		call(HealedScytherScript)
	}
	return
}

script(local) HealedScytherScript{
	mcdialogue("Yep!", "Right here.")
	applymovement(LOCALID_BED_FARMER_1, moves(emote_double_exclamation_mark))
	playse(SE_PIN)
	waitmovement(0)
	speakername("Farmer")
	msgbox(format("That's it! Can ye hand it please, weilder?"))
	waitmessage
	closemessage
	removeitem(ITEM_SPECIAL_MEDICINE, 1)
	applymovement(LOCALID_BED_FARMER_1, moves(face_right))
	waitmovement(0)
	speakername("Farmer")
	msgbox(format("Here girl… This'll make ya feel better…"))
	waitmessage
	closemessage
	applymovement(LOCALID_BED_SCYTHER, moves(face_left))
	playmoncry(SPECIES_SCYTHER, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_SCYTHER)
	msgbox(format("{FONT_SMALL} Gyooo…"))
	waitmessage
	closemessage
	playse(SE_USE_ITEM)
	applymovement(LOCALID_BED_SCYTHER, moves(emote_double_exclamation_mark, jump_in_place_left * 2, walk_fast_down * 3, walk_fast_right * 2, walk_fast_up * 2, walk_fast_left * 2, walk_up, face_original_direction))
	waitmovement(0)
	speakername("Farmer")
	applymovement(LOCALID_BED_FARMER_1, moves(face_player))
	waitmovement(0)
	msgbox(format("Weilder, I don't know how to thank ya. She wouldn't be alright with you! Where did you get this medicine?"))
	waitmessage
	closemessage
	mcdialogue("From a wise healer in Kura Village.")
	speakername("Farmer")
	msgbox(format("Kura Village, eh…"))
	applymovement(LOCALID_BED_FARMER_1, moves(face_right))
	waitmovement(0)
	applymovement(LOCALID_BED_SCYTHER, moves(face_left, delay_16, face_down, delay_16, face_right, delay_16, face_up, delay_16, emote_heart, delay_16, face_original_direction))
	hidefollower
	playmoncry(SPECIES_SCYTHER, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_SCYTHER)
	msgbox(format("Gyaaa!"))
	waitmessage
	closemessage
	speakername("Farmer")
	msgbox(format("{FONT_SMALL} Maybe they're not s'bad afta' all…"))
	waitmessage
	closemessage
	applymovement(LOCALID_BED_FARMER_1, moves(face_player))
	waitmovement(0)
	speakername("Farmer")
	msgbox(format("Thank you, Weilder. I can give ye a reward for your effors. Just meet us in the fields outside."))
	waitmessage
	closemessage
	applymovement(LOCALID_BED_FARMER_2, moves(walk_right, walk_down, walk_right * 5, walk_right * 2, walk_right))
	applymovement(LOCALID_BED_SCYTHER, moves(walk_right * 2, walk_down, walk_right * 5, walk_right * 2, walk_right))
	applymovement(LOCALID_BED_FARMER_1, moves(walk_right * 3, walk_down, walk_right * 5, walk_right * 2, walk_right))
	waitmovement(LOCALID_BED_FARMER_2)
	waitmovement(LOCALID_BED_SCYTHER)
	waitmovement(LOCALID_BED_FARMER_1)
	removeobject(LOCALID_BED_FARMER_2)
	removeobject(LOCALID_BED_SCYTHER)
	removeobject(LOCALID_BED_FARMER_1)
	questmenu(QUEST_MENU_SET_REWARD, QUEST_SAKURASCYTHER)
	setvar(VAR_SAKU_KURA_QUEST_SCYTHER_STATE, 7)
	return
}

script SakuTown_PokemonCenter_EventScript_Farmer2 {
	lock
	speakername("Farmer")
	msgbox(format("There, there, girl… It'll be okay…"))
	closemessage
	playmoncry(SPECIES_SCYTHER, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_SCYTHER)
	msgbox(format("{FONT_SMALL} Gyooo…"))
	closemessage
	release
	end
}

script SakuTown_PokemonCenter_EventScript_Pushing {
	delay(30)

	faceplayer
	speakername("Shady man")
	msgbox(format(
		"Watchu lookin' at? Scram!"
	))
	closemessage

	switch(var(VAR_FACING)) {
		case DIR_EAST:
			applymovement(VAR_LAST_TALKED, SakuTown_PokemonCenter_Movement_PushesPlayer)
			applymovement(OBJ_EVENT_ID_PLAYER, SakuTown_PokemonCenter_Movement_PlayerPushed)
		case DIR_SOUTH:
			applymovement(VAR_LAST_TALKED, SakuTown_PokemonCenter_Movement_PushesPlayerUp)
			applymovement(OBJ_EVENT_ID_PLAYER, SakuTown_PokemonCenter_Movement_PlayerPushedUp)
	}

	waitmovement(VAR_LAST_TALKED)
	waitmovement(OBJ_EVENT_ID_PLAYER)
}

script SakuTown_PokemonCenter_EventScript_Grunt1 {
	lock
	speakername("Shady man")
	msgbox(format(
		"Great plan…"
	))
	closemessage

	call(SakuTown_PokemonCenter_EventScript_Pushing)

	release
	end
}

script SakuTown_PokemonCenter_EventScript_Grunt2 {
	lock
	speakername("Shady man")
	msgbox(format(
		"We should do this next…"
	))
	closemessage
	
	call(SakuTown_PokemonCenter_EventScript_Pushing)

	release
	end
}

script SakuTown_PokemonCenter_EventScript_Grunt3 {
	lock
	speakername("Shady man")
	msgbox(format(
		"On Beachbound Route…"
	))
	closemessage
	
	call(SakuTown_PokemonCenter_EventScript_Pushing)

	release
	end
}

movement SakuTown_PokemonCenter_Movement_PlayerPushed {
	lock_facing_direction
	jump_2_left
	unlock_facing_direction
}

movement SakuTown_PokemonCenter_Movement_PushesPlayer {
	walk_left
	delay_16
	walk_right
	face_original_direction
}

movement SakuTown_PokemonCenter_Movement_PlayerPushedUp {
	lock_facing_direction
	jump_2_up
	unlock_facing_direction
}

movement SakuTown_PokemonCenter_Movement_PushesPlayerUp {
	walk_up
	delay_16
	walk_down
	face_original_direction
}

script SakuTown_PokemonCenter_EventScript_Makuhita {
	lock
	setseenmon(SPECIES_MAKUHITA)
	setspeakertomonname(SPECIES_MAKUHITA)
	playmoncry(SPECIES_MAKUHITA, CRY_MODE_WEAK)
	msgbox(format("Kuhu…"))
	waitmoncry
	waitmessage
	closemessage
	msgbox(format("The Pokémon is resting."))
	release
}
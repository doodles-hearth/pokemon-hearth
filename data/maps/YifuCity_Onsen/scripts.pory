const VAR_EXITED_HOT_SPRINGS = VAR_TEMP_0

mapscripts YifuCity_Onsen_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		// Exiting hot springs
		if (flag(FLAG_PAID_HOT_SPRINGS)) {
			setobjectxyperm(LOCALID_HOT_SPRINGS_OWNER, 2, 2)
			setvar(VAR_EXITED_HOT_SPRINGS, 1)
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_EXITED_HOT_SPRINGS, 1: YifuCity_Onsen_EventScript_ExitHotSprings
	]
}

script YifuCity_Onsen_EventScript_ExitHotSprings {
	getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
	if (var(VAR_TEMP_1) == 1 && var(VAR_TEMP_2) == 2) {
		lockall
		applymovement(LOCALID_PLAYER, moves(walk_down, face_up))
		waitmovement(0)
		applymovement(LOCALID_HOT_SPRINGS_OWNER, moves(walk_left, face_down))
		waitmovement(0)

		setspeaker(SP_NAME_OWNER)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HOT_OLD_WOMAN EMOTE_NORMAL}I hope you feel mellow and relaxed."
		))
		closemessage

		clearflag(FLAG_PAID_HOT_SPRINGS)
		setvar(VAR_EXITED_HOT_SPRINGS, 0)

		releaseall
	} else {
		clearflag(FLAG_PAID_HOT_SPRINGS)
		setvar(VAR_EXITED_HOT_SPRINGS, 0)
	}
}

script YifuCity_Onsen_EventScript_Owner {
	lock
	faceplayer
	if (flag(FLAG_PAID_HOT_SPRINGS)) {
		setspeaker(SP_NAME_OWNER)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HOT_OLD_WOMAN EMOTE_NORMAL}Enjoy the hot springs!\nI'll be right behind you now."
		))
		closemessage
	} else {
		setspeaker(SP_NAME_OWNER)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HOT_OLD_WOMAN EMOTE_NORMAL}You can access the hot springs for ¥500.\p"
			"Be careful though, some bathers are there to relax, some to battle!"
		))

		showmoneybox(18, 0)

		if (checkmoney(500)) {
			mcdialogueB(
				"One entry!",
				"No, thanks."
			)
			closemessage

			switch(var(VAR_RESULT)) {
				case 0:
					playse(SE_SHOP)
					removemoney(500)
					updatemoneybox
					waitse

					closemessage

					hidemoneybox

					setflag(FLAG_PAID_HOT_SPRINGS)

					if (var(VAR_FACING) == DIR_WEST) {
						applymovement(VAR_LAST_TALKED, moves(walk_slow_left, face_down))
					} else {
						applymovement(VAR_LAST_TALKED, moves(walk_slow_right, face_down))
					}
					waitmovement(0)

					setspeaker(SP_NAME_OWNER)
					msgbox(format(
						"{CREATE_MUGSHOT MUGSHOT_HOT_OLD_WOMAN EMOTE_NORMAL}Enjoy the hot springs!\nI'll be right behind you now."
					))
					closemessage

				default:
					hidemoneybox

					setspeaker(SP_NAME_OWNER)
					msgbox(format(
						"{CREATE_MUGSHOT MUGSHOT_HOT_OLD_WOMAN EMOTE_NORMAL}Sure, sure."
					))
					closemessage
			}

		} else {
			mcdialogue(
				"I don't have enough…"
			)
			closemessage

			hidemoneybox

			setspeaker(SP_NAME_OWNER)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_HOT_OLD_WOMAN EMOTE_NORMAL}Sorry but I run a business, not a charity."
			))
			closemessage
		}
	}

	release
}

script YifuCity_Onsen_EventScript_Man {
	lock
	faceplayer
	msgbox((
		"{CREATE_MUGSHOT MUGSHOT_UUME_MAN_1 EMOTE_NORMAL}{SPEAKER NAME_MAN}Whew…\n"
		"The hot spring regulars are tireless!\lThey battle visitors every single day!"
	))
	closemessage

	release
}


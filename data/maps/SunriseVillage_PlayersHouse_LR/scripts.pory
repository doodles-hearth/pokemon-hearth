const LOCALID_BIGSIS = 1
const LOCALID_RIVALS_NOTE = 2

mapscripts SunriseVillage_PlayersHouse_LR_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		// Put rival's note on the table
		if (!flag(FLAG_RECEIVED_STARTER)) {
			setmetatile(4, 4, METATILE_HouseBeige_Scroll, TRUE)
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SUNRISE_VILLAGE_INTRO_STATE, 2: SunriseVillage_PlayersHouse_LR_EventScript_BigSisSaysHi
    ]	
}

script SunriseVillage_PlayersHouse_LR_EventScript_BigSisSaysHi {
    lock
	playse(SE_PIN)
	applymovement(LOCALID_BIGSIS, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_BIGSIS, Common_Movement_Delay48)
	waitmovement(0)
	turnobject(LOCALID_BIGSIS, DIR_EAST)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)

	special(GetPlayerBrotherSisterString)
	setspeaker(SP_NAME_BIG_SIS)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_BIG_SIS 0}Oh, {PLAYER}, you're up! Ready for the big event?\p"
		"Are you excited? I sure hope you are, because I'm VERY EXCITED!\p"
		"I have to head out! Don't be late, I know you, little {STR_VAR_1}!\p"
		"By the way, your friend Natsuki left you a note.\l"
		"Make sure you read it!"
	))
		// "And don't forget to pick up the bag I made for you before you leave!\p"
	closemessage
	applymovement(LOCALID_BIGSIS, moves(
		walk_down * 2
		walk_right * 2
		walk_down * 2
		step_end
	))
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	waitmovement(0)
	playse(SE_EXIT)
	removeobject(LOCALID_BIGSIS)
	waitse
	
	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 3)
	setflag(FLAG_HIDE_BIGSIS_HOME)

    release
    end
}

script SunriseVillage_PlayersHouse_LR_EventScript_BigSis {
	lock
	faceplayer
	setspeaker(SP_NAME_BIG_SIS)
	if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) <= 4) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_BIG_SIS 0}What are you still doing here? You're late for the distribution!\p"
			"Go to Kaba's house right now!"
		))
		closemessage
	} elif (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) <= 6) {
		special(GetPlayerBrotherSisterString)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_BIG_SIS 0}Hey little {STR_VAR_1}! See how cute my new Pokémon is?\p"
			"By the way, I heard Natsuki was waiting for you at Elder Kaba's house. You should probably go see them."
		))
		closemessage
	} elif (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) == 7) {
		special(GetPlayerBrotherSisterString)
		bufferspeciesname(STR_VAR_2, VAR_STARTER_MON)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_BIG_SIS 0}Hey little {STR_VAR_1}! See how cute my new Pokémon is?\p"
			"Oh, who is that lovely little thing with you?\N"
			"{STR_VAR_2}, huh?\p"
			"Looks like it already likes you a lot! You're going to make a great Wielder,\lI know it!\p"
			"… … … … … …\n… … … … … …\p"
			"So you're going on a journey…\p"
			"That's great! I'm proud of you, {PLAYER}.\p"
			"I know you'll get lost though, so I'll lend you my map of Toku!"
		))
		closemessage

		giveitem(ITEM_TOKU_MAP)

		special(GetPlayerBrotherSisterString)
		setspeaker(SP_NAME_BIG_SIS)
		msgbox((
			"{CREATE_MUGSHOT MUGSHOT_BIG_SIS 0}Alrighty! You all set? Have a safe\njourney, little {STR_VAR_1}.\p"
			"Maybe I'll see you around!\p"
			"Come home sometimes though, will you?\nYou and your Pokémon can rest here!\p"
			"See you soon!\nI'll send you Chatot messages!"
		))
		closemessage

		setnamedmon(SPECIES_CHATOT)

		setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 8)

		// Default
	} else {
		special(GetPlayerBroSisString)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_BIG_SIS 0}Hey {PLAYER}! How's it going?\p"
			"Here, rest for a bit."
		))
		closemessage
		call(Common_EventScript_OutOfCenterPartyHeal)
		setspeaker(SP_NAME_BIG_SIS)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_BIG_SIS 0}See you around, little {STR_VAR_1}."
		))
		closemessage
	}
	release
}

script SunriseVillage_PlayersHouse_LR_EventScript_Eevee {
	lock
	faceplayer
	setseenmon(SPECIES_EEVEE)
	setspeakertomonname(SPECIES_EEVEE)
	playmoncry(SPECIES_EEVEE, CRY_MODE_NORMAL)
	msgbox(format("Eee!"))
	waitmoncry
	release
}

script SunriseVillage_PlayersHouse_LR_EventScript_RivalsNote {
    lock
	msgbox(format(
		"“Hey {PLAYER},\n"
		"Going down to the beach before the giveaway, if you wanna hang out.\l"
		"-Natsuki”"
	))
	closemessage
    release
    end
}

script SunriseVillage_PlayersHouse_LR_EventScript_Gong {
	lock
	if (flag(FLAG_HIDE_BIGSIS_HOME)) {
		msgbox(format(
			"It's a gong.\nGONG!… … …"
		))
	} else {
		msgbox(format(
			"It's a gong.\nGONG!… … …\p"
			"Let's not annoy Big Sis too much."
		))
	}
	closemessage
	release
}
const LOCALID_CUTMASTER = 1
const VAR_HOUR_OF_DAY = VAR_0x8000
const FLAG_HIDE_CUT_MASTER = FLAG_TEMP_1

mapscripts TranquilRoute_CutHouse_MapScripts {
	MAP_SCRIPT_ON_LOAD: TranquilRoute_CutHouse_EventScript_CheckCutGrassQuestComplete
	MAP_SCRIPT_ON_TRANSITION {
		if (flag(FLAG_CUT_QUEST_COMPLETED)) {
			gettime
			if (var(VAR_HOUR_OF_DAY) >= 7 && var(VAR_HOUR_OF_DAY) <= 19) {
				setflag(FLAG_HIDE_CUT_MASTER)
			}
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 1: TranquilRoute_CutHouse_EventScript_CutGrassCutscene
    ]
}

script TranquilRoute_CutHouse_EventScript_CheckCutGrassQuestComplete {
	if (flag(FLAG_CUT_QUEST_CUT_ALL_GRASS) && !flag(FLAG_CUT_QUEST_COMPLETED)) {
		setvar(VAR_TEMP_0, 1)
	}
}

script TranquilRoute_CutHouse_EventScript_CutGrassCutscene {
	lock
	setvar(VAR_TEMP_0, 0)
	playse(SE_PIN)
	applymovement(LOCALID_CUTMASTER, TranquilRoute_CutHouse_Movement_CutMasterSurprise)
	waitmovement(0)
	delay(48)

	msgbox(format(
		"You actually cut ALL the tall grass?\p"
		"Wait, let us go outside. I want to see it for myself."
	))
	closemessage

	delay(48)

	warp(MAP_TRANQUIL_ROUTE, 43, 33)
	release
}

movement TranquilRoute_CutHouse_Movement_CutMasterSurprise {
	face_down
	emote_exclamation_mark
	delay_16 * 3
}

script TranquilRoute_CutHouse_EventScript_CutMaster {
	lock
	faceplayer

	// Cut already received
	if(checkitem(ITEM_SECRET_CUT)) {
			// Home for the night
			if(flag(FLAG_CUT_QUEST_CUT_ALL_GRASS)){
				setspeaker(SP_NAME_OLD_MAN)
				msgbox(format(
					"{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_MAN 0}The sun is setting on another day.\p"
					"Life is good."
				))
				closemessage
			} else {
				setspeaker(SP_NAME_OLD_MAN)
				msgbox(format(
					"{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_MAN 0}Are you finding the Secret of Blade useful?\p"
					"No, I still haven't trimmed my garden. I really should get to it…"
				))
				closemessage
			}
	}
	// First time
	else {
		setspeaker(SP_NAME_OLD_MAN)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_MAN 0}Hello, there. I don't get many visitors, so sit down, have some tea!"
		))
		closemessage

		playse(SE_PIN)
		applymovement(VAR_LAST_TALKED, moves(emote_question_mark, delay_16 * 6))
		waitmovement(VAR_LAST_TALKED)

		setspeaker(SP_NAME_OLD_MAN)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_MAN 0}What? My garden is overrun by weeds?\p"
			"Ho ho ho…\p"
			"You're right, I have been neglecting it lately. You see, my old back isn't as strong as it used to be.\p"
			"I'm going to let you in on a secret…\p"
			"Long ago, a powerful swordmaster came up with a secret cutting technique.\p"
			"I just so happen to have it written down right here!\p"
			"You don't look much like a swordmaster… But surely a Wielder like you could teach their Pokémon the Secret of Blade!"
		))
		closemessage

		giveitem(ITEM_SECRET_CUT)

		setspeaker(SP_NAME_OLD_MAN)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_MAN 0}There is a variety of slicing moves that your Pokémon can use.\p"
			"They will now be able to use those moves to cut tall grass and little trees that block your path.\p"
			"You will need at least one official Dojo Token for that, though.\p"
			"We can't be going around cutting trees irresponsibly, now can we?\p"
			"Take this as well, if your Pokémon\ndon't know any slicing moves yet.\p"
			"A scroll can be used to teach your Pokémon a move!"
		))
		closemessage

		giveitem(ITEM_TM_NICK)

		setspeaker(SP_NAME_OLD_MAN)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_MAN 0}Want to know another secret?\p"
			"That powerful swordmaster I mentioned…\p"
			"That was me in my youthful days,\nho ho ho…"
		))
		closemessage
	}

	release
}

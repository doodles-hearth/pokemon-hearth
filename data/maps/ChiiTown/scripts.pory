const FLAG_TAILLOW_1 = FLAG_TEMP_1
const FLAG_TAILLOW_2 = FLAG_TEMP_2
const FLAG_FLETCHLING_1 = FLAG_TEMP_3
const FLAG_FLETCHLING_2 = FLAG_TEMP_4

mapscripts ChiiTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		call(ChiiTown_NPCSchedules)
	}
	MAP_SCRIPT_ON_LOAD {
        call(ChiiTown_EventScript_SpawnBirds)
    }
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_CHII_TOWN_OLD_MAN_TRESPASS_STATE, 2: ChiiTown_EventScript_SetTrespassVar
    ]
}

script ChiiTown_EventScript_MomNPCDialogue {

}

script ChiiTown_EventScript_MomDroppingOff{
	gettimeofday
	speakername("Woman")
	switch(var(VAR_TIME_OF_DAY)){
		case TIME_MORNING:
			msgbox(format(
				"I'm dropping off my children to school.\p"
				"With all the recent Pokémon attacks, I will not let them leave my sight!"
			), MSGBOX_NPC)	
		case TIME_NOONTIME:
			msgbox(format(
				"I'm waiting to pick up my children after school.\p"
				"With all the recent Pokémon attacks, I will not let them leave my sight!"
			), MSGBOX_NPC)
	}
}

script ChiiTown_EventScript_MomWaiting {
	speakername("Woman")

}

script ChiiTown_EventScript_SpawnBirds {
	// Random birds on roofs
	// Taillow
	setflag(FLAG_TAILLOW_1)
	setflag(FLAG_TAILLOW_2)
	// Fletchling
	setflag(FLAG_FLETCHLING_1)
	setflag(FLAG_FLETCHLING_2)

	gettime
	// Morning
	if (var(VAR_TIME_HOUR) >= 5 && var(VAR_TIME_HOUR) <= 8) {
		random(2)
		switch(var(VAR_RESULT)) {
			case 0:
				clearflag(FLAG_TAILLOW_1)
			case 1:
				clearflag(FLAG_TAILLOW_2)
		}
	// Day
	} elif (var(VAR_TIME_HOUR) >= 9 && var(VAR_TIME_HOUR) <= 17) {
		random(2)
		switch(var(VAR_RESULT)) {
			case 0:
				clearflag(FLAG_FLETCHLING_1)
			case 1:
				clearflag(FLAG_FLETCHLING_2)
		}
	}
}

script ChiiTown_EventScript_Shopper {
	speakername("Woman")
	msgbox(format(
		"The lady who keeps the town's market has started selling Pokémon medicine and Pokéballs!\p"
		"Be careful, though: the ointments she sells can't be used in the middle of a battle."
	), MSGBOX_NPC)
}

script ChiiTown_EventScript_Kecleon {
	lock
	faceplayer
	playmoncry(SPECIES_KECLEON, CRY_MODE_NORMAL)
	ismonseen(SPECIES_KECLEON)
    if (var(VAR_RESULT)) {
        speakername("Kecleon")
    } else {
        speakername("Weird lizard")
    }
	msgbox(format("Kleeeeh!"))
	waitmoncry
	closemessage
	release
}

script ChiiTown_EventScript_PlantingFlowers {
	speakername("Little girl")
	msgbox(format(
		"I'm planting flowers so that the wild Pokémon become happy again!"
	), MSGBOX_NPC)
}

script ChiiTown_EventScript_Mart {
	lock
	faceplayer
	speakername("Saleswoman")
	message(format("Welcome to Chii Town's market! How may I help you?"))
	waitmessage
	pokemart(ChiiTown_Mart)
	speakername("Saleswoman")
	msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
}

mart ChiiTown_Mart {
	ITEM_POKE_BALL
	ITEM_BERRY_JUICE
	ITEM_PARALYZE_HEAL
	ITEM_ANTIDOTE
	ITEM_AWAKENING
	ITEM_ESCAPE_ROPE
	ITEM_REPEL
}

script ChiiTown_EventScript_Samurai {
	speakername("Samurai")
	msgbox(format(
		"There's been reports of some shady men dressed in blue loitering around the area.\pBe careful when traveling!"
	), MSGBOX_NPC)
}

script ChiiTown_EventScript_TownSign {
	msgbox(format("Chii Town\n“The smallest town, with the biggest heart.”"), MSGBOX_SIGN)
}

script ChiiTown_EventScript_SetTrespassVar {
	setvar(VAR_CHII_TOWN_OLD_MAN_TRESPASS_STATE, 1)
}
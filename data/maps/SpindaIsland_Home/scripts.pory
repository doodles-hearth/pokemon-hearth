const LOCALID_POEM_GIRL = 4

mapscripts SpindaIsland_Home_MapScripts {
}

script SpindaIsland_Home_EventScript_ArchiesHouseSign {
	msgbox(format("The Aquas"), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_BobsHouseSign {
	msgbox(format("Bob"), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_ICanNeverGoHome {
	lock
	faceplayer
	speakername("Sad man")
	msgbox(format(
		"Curse the architect that built my house facing the wrong direction!!\p"
		"I can never go home!"
	), MSGBOX_NPC)
	release
}

script SpindaIsland_Home_EventScript_Dad {
	lock
	faceplayer
	if (flag(FLAG_OBTAINED_LEMONADE)) {
		speakername("Dad")
		msgbox(format(
			"Don't uh, pour that lemonade I gave you on the ground or anything.\p"
			"It'd probably kill all the grass and flowers."
		))
		closemessage
	} else {
		if (flag(FLAG_SPOKE_TO_DAD)) {
			speakername("Dad")
			msgbox(format(
				"Here, have some lemonade!"
			))
			closemessage
		} else {
			speakername("Dad")
			msgbox(format(
				"Hiya, sonny! going adventuring today, are we?\p"
				"Want some lemonade?"
			))
			closemessage
		}

		// TODO EVA comp
		giveitem(ITEM_SALTY_LEMONADE)
		removefieldmugshot
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
		speakername("Little Archie")
		msgbox(format(
			"Now we're talkin'!! I can bring it to mom, that should do it!\p"
			"Let's take a sip first, I am PARCHED!"
		))
		closemessage
		removefieldmugshot

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_DISGUSTED)
		speakername("Little Archie")
		msgbox(format(
			"BLEARGH!\p"
			"Why is it… so salty?!"
		))
		closemessage

		speakername("Dad")
		msgbox(format(
			"Why, because it's seawater, obviously!\p"
			"I take it you're not a fan of my recipe?\p"
			"You wouldn't be the only one…\p"
			"In any case, it's nice of you to come visit me at the office, haha!\p"
			"See you around, little Archie! Don't get into trouble!"
		))
		closemessage

		setflag(FLAG_OBTAINED_LEMONADE)
		setflag(FLAG_SPOKE_TO_DAD)
	}
	release
}

script SpindaIsland_Home_EventScript_Dad_FromSide {
	lock
	faceplayer
	if (flag(FLAG_OBTAINED_LEMONADE)) {
		speakername("Dad")
		msgbox(format(
			"Don't uh, pour that lemonade I gave you on the ground or anything.\p"
			"It'd probably kill all the grass and flowers."
		))
		closemessage
	} else {
		if (flag(FLAG_SPOKE_TO_DAD)) {
			speakername("Dad")
			msgbox(format(
				"Come round to the front of the lemonade stand!"
			))
			closemessage
		} else {
			speakername("Dad")
			msgbox(format(
				"Hiya, sonny! going adventuring today, are we?\p"
				"Want some lemonade?\p"
				"Come round to the front of the lemonade stand!"
			))
			closemessage
		}
		setflag(FLAG_SPOKE_TO_DAD)
	}
	release
}

script SpindaIsland_Home_EventScript_LemonadeStandSign {
	msgbox(format(
		"“ARCHIE'S LEMONADE\n"
		"A taste of the seaside!”"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_PoemGirl {
	lock
	faceplayer

	speakername("Maggie")
	msgbox(format(
		"Archie…\p"
		"I want to hear a poem…\p"
		"I want you to reveal your heart to me."
	))
	closemessage

	dynmultichoice(
		0,
		6,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Ocean waves swimming…",
		"Roses are red…",
		"Roses are blue…"
	)

	switch (var(VAR_RESULT)) {
		case 0:
			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"From the center of the sea…",
				"They are calling to my heart…"
			)

			switch (var(VAR_RESULT)) {
				case 0:
					dynmultichoice(
						0,
						0,
						TRUE,
						10,
						0,
						DYN_MULTICHOICE_CB_NONE,
						"With faith to reach land.",
						"Only to go back."
					)
				case 1:
					dynmultichoice(
						0,
						0,
						TRUE,
						10,
						0,
						DYN_MULTICHOICE_CB_NONE,
						"And my heart to theirs.",
						"Yearning for a touch."
					)
			}

			speakername("Maggie")
			msgbox(format(
				"That was very beautiful.\p"
				"I'm not into haiku, though."
			))
			closemessage
		case 1:
			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"Violets are blue…",
				"Just like our love…"
			)
			switch (var(VAR_RESULT)) {
				// Violets are blue…
				case 0:
					dynmultichoice(
						0,
						0,
						TRUE,
						10,
						0,
						DYN_MULTICHOICE_CB_NONE,
						"The sky is pretty…",
						"The Pincurchin are spiky…"
					)

					switch(var(VAR_RESULT)) {
						// The sky is pretty…
						case 0:
							dynmultichoice(
								0,
								0,
								TRUE,
								10,
								0,
								DYN_MULTICHOICE_CB_NONE,
								"And so are you.",
								"And so are you!"
							)

							speakername("Maggie")
							msgbox(format(
								"Oh, that's nice.\p"
								"You're not truly revealing your poet's heart, though."
							))
							closemessage

						// The Pincurchin are spiky…
						case 1:
							dynmultichoice(
								0,
								0,
								TRUE,
								10,
								0,
								DYN_MULTICHOICE_CB_NONE,
								"And so are you.",
								"And so are you!"
							)

							speakername("Maggie")
							msgbox(format(
								"That's… a weird comparison.\p"
								"I thought you were a better poet than that."
							))
							closemessage
					}

					// Just like our love…
				case 1:
					dynmultichoice(
						0,
						0,
						TRUE,
						10,
						0,
						DYN_MULTICHOICE_CB_NONE,
						"Which blooms like they do.",
						"Wait, I didn't mean to say that."
					)

					speakername("Maggie")
					msgbox(format(
						"Wow, Archie, uh…\p"
						"That's not really what I meant when I said you should reveal your heart.\p"
						"(Well, this is awkward.)\p"
						"(Let's pretend this never happened.)"
					))
					closemessage
			}
		case 2:
			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"Violets are red…",
				"Violets are blue…"
			)

			switch(var(VAR_RESULT)) {
				// Roses are blue, violets are red
				case 0:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"The sky isn't blue, it's purple instead.",
					"It doesn't make sense, but it's fun to be said."
				)

				// Roses are blue, violets are blue
				case 1:
					dynmultichoice(
						0,
						0,
						TRUE,
						10,
						0,
						DYN_MULTICHOICE_CB_NONE,
						"Orchids are blue, too.",
						"Everything is blue because it's my favorite color."
					)

					switch(var(VAR_RESULT)) {
						case 0:
							speakername("Maggie")
							msgbox(format(
								"Hmm…\p"
								"You're not revealing your heart right now.\p"
								"Plus, this didn't make any sense."
							))
							closemessage
						case 1:
							speakername("Maggie")
							msgbox(format(
								"… … … … …\p"
								"Wow, Archie, I…\p"
								"This was so touching…\p"
								"Thank you for revealing your heart to me like this.\p"
								""
							))
							closemessage
					}
			}
	}

	applymovement(LOCALID_POEM_GIRL, Common_Movement_FaceOriginalDirection)
	release
}
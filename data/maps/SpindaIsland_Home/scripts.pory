const LOCALID_POEM_GIRL = 4
const LOCALID_DAD = 2
const LOCALID_SLOWPOKE_MISTRESS = 6

mapscripts SpindaIsland_Home_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (flag(FLAG_SLOWPOKE_EVOLVED)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWBRO))
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWPOKE))
		}
	}
}

script SpindaIsland_Home_EventScript_ArchiesHouseSign {
	msgbox(format("The Hoenns"), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_BobsHouseSign {
	msgbox(format("Bob"), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_ICanNeverGoHome {
	lock
	faceplayer
	speakername("Sad man")
	msgbox(format(
		"Curse the architect that built my house facing the wrong direction!!\p"
		"I can never go home!"
	), MSGBOX_NPC)
	release
}

script SpindaIsland_Home_EventScript_Dad {
	lock
	turnobject(LOCALID_DAD, DIR_SOUTH)
	if (flag(FLAG_OBTAINED_LEMONADE)) {
		speakername("Dad")
		msgbox(format(
			"Don't uh, pour that lemonade I gave you on the ground or anything.\p"
			"It'd probably kill all the grass and flowers."
		))
		closemessage
	} else {
		if (flag(FLAG_SPOKE_TO_DAD)) {
			speakername("Dad")
			msgbox(format(
				"Here, have some lemonade!"
			))
			closemessage
		} else {
			speakername("Dad")
			msgbox(format(
				"Hiya, sonny! going adventuring today, are we?\p"
				"Want some lemonade?"
			))
			closemessage
		}

		giveitem(ITEM_SALTY_LEMONADE)

		removefieldmugshot
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
		speakername("Little Archie")
		msgbox(format(
			"Now we're talkin'!! I can bring this to mom, that should do it!\p"
			"Let's take a sip first, I am PARCHED!"
		))
		closemessage
		removefieldmugshot

		delay(10)
		playse(SE_BALL)
		waitse
		playse(SE_BALL)
		waitse
		playse(SE_BALL)
		waitse
		delay(20)

		playse(SE_PIN)
		applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
		waitmovement(0)
		delay(48)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_DISGUSTED)
		speakername("Little Archie")
		msgbox(format(
			"BLEARGH!\p"
			"Why is it… so salty?!"
		))
		closemessage
		removefieldmugshot

		speakername("Dad")
		msgbox(format(
			"Why, because it's seawater, obviously!\p"
			"I take it you're not a fan of my recipe?\p"
			"(You wouldn't be the only one…)\p"
			"In any case, it's nice of you to come visit me at the office, haha!\p"
			"See you around, little Archie! Don't get into trouble!"
		))
		closemessage

		setflag(FLAG_OBTAINED_LEMONADE)
		setflag(FLAG_SPOKE_TO_DAD)
	}
	release
}

script SpindaIsland_Home_EventScript_Dad_FromSide {
	lock
	faceplayer
	if (flag(FLAG_OBTAINED_LEMONADE)) {
		speakername("Dad")
		msgbox(format(
			"Don't uh, pour that lemonade I gave you on the ground or anything.\p"
			"It'd probably kill all the grass and flowers."
		))
		closemessage
	} else {
		if (flag(FLAG_SPOKE_TO_DAD)) {
			speakername("Dad")
			msgbox(format(
				"Come round to the front of the lemonade stand!"
			))
			closemessage
		} else {
			speakername("Dad")
			msgbox(format(
				"Hiya, sonny! going adventuring today, are we?\p"
				"Want some lemonade?\p"
				"Come round to the front of the lemonade stand!"
			))
			closemessage
		}
		setflag(FLAG_SPOKE_TO_DAD)
	}
	release
}

script SpindaIsland_Home_EventScript_LemonadeStandSign {
	msgbox(format(
		"“ARCHIE'S LEMONADE\n"
		"A taste of the seaside!”"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_MainStreetSign {
	msgbox(format(
		"{UP_ARROW} Main street"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_MadelineSign {
	msgbox(format(
		"Madeline"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_PoemGirl {
	lock

	playse(SE_M_GRASSWHISTLE)
	waitse

	faceplayer

	if (flag(FLAG_POEM_GIRL)) {
		speakername("Maggie")
		msgbox(format(
			"See you around, Archie…"
		))
		closemessage
	} else {
		speakername("Maggie")
		msgbox(format(
			"Archie…\p"
			"I want to hear a poem…\p"
			"I want you to reveal your heart to me."
		))
		closemessage

		dynmultichoice(
			0,
			0,
			TRUE,
			10,
			0,
			DYN_MULTICHOICE_CB_NONE,
			"Ocean waves swimming…",
			"Roses are red…",
			"Roses are blue…"
		)

		switch (var(VAR_RESULT)) {
			case 0:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"From the center of the sea…",
					"They are calling to my heart…"
				)

				switch (var(VAR_RESULT)) {
					case 0:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"With faith to reach land.",
							"Only to go back."
						)
					case 1:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"And my heart to theirs.",
							"Yearning for a touch."
						)
				}

				speakername("Maggie")
				msgbox(format(
					"That was very beautiful.\p"
					"I'm not really into haiku, though."
				))
				closemessage
			case 1:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"Violets are blue…",
					"Just like our love…"
				)
				switch (var(VAR_RESULT)) {
					// Violets are blue…
					case 0:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"The sky is pretty…",
							"The Pincurchin are spiky…"
						)

						switch(var(VAR_RESULT)) {
							// The sky is pretty…
							case 0:
								dynmultichoice(
									0,
									0,
									TRUE,
									10,
									0,
									DYN_MULTICHOICE_CB_NONE,
									"And so are you.",
									"And so are you!"
								)

								speakername("Maggie")
								msgbox(format(
									"Oh, that's nice.\p"
									"You're not truly revealing your poet's heart, though."
								))
								closemessage

							// The Pincurchin are spiky…
							case 1:
								dynmultichoice(
									0,
									0,
									TRUE,
									10,
									0,
									DYN_MULTICHOICE_CB_NONE,
									"And so are you.",
									"And so are you!"
								)

								speakername("Maggie")
								msgbox(format(
									"That's… a weird comparison.\p"
									"I thought you were a better poet than that."
								))
								closemessage
						}

						// Just like our love…
					case 1:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"Which blooms like they do.",
							"Wait, I didn't mean to say that."
						)

						speakername("Maggie")
						msgbox(format(
							"Wow, Archie, uh…\p"
							"That's not really what I meant when I said you should reveal your heart.\p"
							"(Well, this is awkward.)\p"
							"(Let's pretend this never happened.)"
						))
						closemessage
				}
			case 2:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"Violets are red…",
					"Violets are blue…"
				)

				switch(var(VAR_RESULT)) {
					// Roses are blue, violets are red
					case 0:
					dynmultichoice(
						0,
						0,
						TRUE,
						10,
						0,
						DYN_MULTICHOICE_CB_NONE,
						"The sky isn't blue, it's purple instead.",
						"Doesn't make sense, but fun to be said."
					)

					speakername("Maggie")
					msgbox(format(
						"Hmm…\p"
						"You're not revealing your heart right now.\p"
						"Plus, this didn't make any sense."
					))
					closemessage

					// Roses are blue, violets are blue
					case 1:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"Orchids are blue, too.",
							"Everything is blue…"
						)

						switch(var(VAR_RESULT)) {
							case 0:
								speakername("Maggie")
								msgbox(format(
									"Hmm…\p"
									"You're not revealing your heart right now.\p"
									"Plus, this didn't make any sense."
								))
								closemessage
							case 1:
								dynmultichoice(
									0,
									0,
									TRUE,
									10,
									0,
									DYN_MULTICHOICE_CB_NONE,
									"Because it's my favorite color."
								)
								speakername("Maggie")
								msgbox(format(
									"… … … … …\p"
									"Wow, Archie, I…\p"
									"This was so touching…\p"
									"Thank you for revealing your heart to me like this.\p"
									"Here, take this."
								))
								closemessage

								giveitem(ITEM_GRASS_BLADE)

								speakername("Maggie")
								msgbox(format(
									"It's a blade of grass large enough to whistle with."
								))
								closemessage

								setflag(FLAG_POEM_GIRL)
						}
				}
		}
	}
	release
}

script SpindaIsland_Home_Slowpoke {
	lock
	if (flag(FLAG_SLOWPOKE_EVOLVED)) {
		faceplayer
		playmoncry(SPECIES_SLOWBRO, CRY_MODE_NORMAL)
		speakername("Slowbro")
		msgbox(format(
			"Bruh…"
		))
		closemessage

	} else {
		faceplayer
		playmoncry(SPECIES_SLOWPOKE, CRY_MODE_NORMAL)
		speakername("Slowpoke")
		msgbox(format(
			"Slo…"
		))
		waitmoncry
		closemessage

		if (checkitem(ITEM_WHITE_SEASHELL) == TRUE) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_THINKING)
			speakername("Little Archie")
			msgbox(format(
				"… … …?\p"
				"He's looking at the white seashell I found…"
			))
			removefieldmugshot

			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"Give the seashell",
				"Don't give the seashell"
			)
			closemessage

			switch(var(VAR_RESULT)) {
				case 0:
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
					speakername("Little Archie")
					msgbox(format(
						"Sure, why the hell not!\p"
						"Here you go, buddy."
					))
					removefieldmugshot

					removeitem(ITEM_WHITE_SEASHELL)

					setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWBRO))
					removeobject(VAR_LAST_TALKED)
					addobject(VAR_LAST_TALKED)
					playse(SE_M_BITE)
					applymovement(VAR_LAST_TALKED, SpindaIsland_Home_Movement_Slowpoke)
					applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
					waitmovement(VAR_LAST_TALKED)
					waitmovement(OBJ_EVENT_ID_PLAYER)

					playmoncry(SPECIES_SLOWPOKE, CRY_MODE_NORMAL)
					speakername("Slowbro")
					msgbox(format(
						"Bruh!!"
					))
					waitmoncry
					closemessage
					
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SURPRISED)
					speakername("Little Archie")
					msgbox(format(
						"The heck?!\p"
						"My seashell bit him!\p"
						"Guess it was a Shellder all this time!"
					))
					removefieldmugshot
					closemessage

					setflag(FLAG_SLOWPOKE_EVOLVED)
				case 1:
					playmoncry(SPECIES_SLOWPOKE, CRY_MODE_FAINT)
					speakername("Slowpoke")
					msgbox(format(
						"…Slo…\p"
						":("
					))
					waitmoncry
					closemessage
			}
			
		}
	}
	release
}

movement SpindaIsland_Home_Movement_Slowpoke {
	lock_facing_direction
	jump_in_place_down
	unlock_facing_direction
	delay_16 * 3
	face_up
	delay_16
	face_down
	delay_16
	face_left
	delay_16
	face_up
	delay_16
	face_original_direction
	delay_16 * 3
	lock_facing_direction
	jump_in_place_down
	jump_in_place_down
	emote_heart
	delay_16 * 3
}

script SpindaIsland_Home_SlowpokeMistress {
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_SLOWPOKE_REWARD)) {
		speakername("Girl")
		msgbox(format(
			"Look at my super cool Slowbro!\p"
			"He just looks so happy!"
		))
		closemessage
	} elif (flag(FLAG_SLOWPOKE_EVOLVED)) {
		playse(SE_PIN)
		applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
		waitse
		waitmovement(0)
		delay(48)

		speakername("Girl")
		msgbox(format(
			"By Arceus!!\p"
			"How on earth did you convince him to evolve?!\p"
			"…A cool-looking seashell? What are you talking about?\p"
			"Well, thank you anyway! You can have this!\p"
			"I no longer need it."
		))
		closemessage

		giveitem(ITEM_WATER_STONE_2)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_THINKING)
		speakername("Little Archie")
		msgbox(format(
			"Hmm… A {COLOR BLUE}WATER{COLOR DARK_GRAY} Stone…\p"
			"Maybe I could try giving this to mom?"
		))
		closemessage
		removefieldmugshot

		setflag(FLAG_RECEIVED_SLOWPOKE_REWARD)
	} else {
		speakername("Girl")
		msgbox(format(
			"I wish my Pokémon would evolve…\p"
			"I keep trying to give him this Water Stone, but it just does nothing.\p"
			"Perhaps there's another way…"
		))
		closemessage
	}
	release
}
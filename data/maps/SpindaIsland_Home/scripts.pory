const LOCALID_DAD = 7
const LOCALID_SAD_MAN = 6
const LOCALID_CAR_GIRL = 15
const LOCAL_RAFT = 21

mapscripts SpindaIsland_Home_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (flag(FLAG_SLOWPOKE_EVOLVED)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWBRO))
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWPOKE))
		}
	}
	MAP_SCRIPT_ON_LOAD {
		// Faucet wheel
		if (flag(FLAG_REPLACED_WHEEL)) {
			setmetatile(27, 50, METATILE_SpindaIslandSecondary_FaucetWheel, TRUE)
		}

		if (flag(FLAG_CAMERA_EXPLODED)) {
			setmetatile(14, 27, METATILE_SpindaIsland_Grass, FALSE)
			setmetatile(14, 28, METATILE_SpindaIslandSecondary_RoofProfileTopRightCorner, FALSE)
		}

		if (flag(FLAG_OPENED_CHEST)) {
			setmetatile(53, 27, METATILE_SpindaIsland_EmptyChest, TRUE)
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SPINDA_ISLAND_INTRO_STATE, 2: SpindaIsland_Home_EventScript_MainQuestRecap
        VAR_REPAIRED_FAUCET, 2: SpindaIsland_Home_EventScript_GoToSail
    ]
}

script SpindaIsland_Home_EventScript_GoToSail {
	lock
	setvar(VAR_REPAIRED_FAUCET, 3)
	applymovement(OBJ_EVENT_ID_PLAYER, SpindaIsland_Home_Movement_GoToSail)
	waitmovement(0)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
	speakername("Little Archie")
	msgbox(format(
		"At last! I can finally set sail!!"
	))
	removefieldmugshot
	closemessage
	
	playse(SE_FU_ZAKU)
	turnobject(LOCAL_RAFT, DIR_SOUTH)
	removeitem(ITEM_BADASS_SAIL)
	waitse

	delay(48)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
	speakername("Little Archie")
	msgbox(format(
		"And voilà!"
	))
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	msgbox(format(
		"Goodbye, Spinda Island…\p"
		"Hello, adventure!"
	))
	removefieldmugshot
	closemessage

	applymovement(OBJ_EVENT_ID_PLAYER, SpindaIsland_Home_Movement_PlayerEntersRaft)
	waitmovement(0)

	special(SpawnCameraObject)
	applymovement(LOCAL_RAFT, SpindaIsland_Home_Movement_RaftGoesToIsland)
	applymovement(OBJ_EVENT_ID_CAMERA, SpindaIsland_Home_Movement_RaftGoesToIsland)
	delay(100)
	warpsilent(MAP_SPINDA_ISLAND_SHUPPET_HOUSE_ATTIC, 5, 0)

	release
}

movement SpindaIsland_Home_Movement_GoToSail {
	walk_fast_down * 5
	walk_fast_right * 5
	walk_fast_down * 3
}

script SpindaIsland_Home_EventScript_MainQuestRecap {
	lock
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
	speakername("Little Archie")
	msgbox(format(
		"Alright! The quicker mom gets her glass of {COLOR BLUE}WATER{COLOR DARK_GRAY}, the quicker Archie gets his super badass pirate {COLOR RED}sail{COLOR DARK_GRAY}!\p"
		"Let's get moving!"
	))
	closemessage
	removefieldmugshot
	setvar(VAR_SPINDA_ISLAND_INTRO_STATE, 3)
	release
}

script SpindaIsland_Home_EventScript_MetapodOtherSide {
	lock
	setvar(VAR_METAPOD_OTHER_SIDE, 1)
	release
}

script SpindaIsland_Home_EventScript_ArchiesHouseSign {
	// lock
	// callnative(StartCredits)
	msgbox(format("The Hoenns"), MSGBOX_SIGN)
	// TODO EVA 
	// release
}

script SpindaIsland_TryToBlindZiggy {
	lock
	playse(SE_SWITCH)
    waitse
    playse(SE_M_LEER)
    fadescreenspeed(FADE_TO_WHITE, 0)
    fadescreenspeed(FADE_FROM_WHITE, 0)
    waitse
	delay(30)

	speakername("Rabid Zigzagoon")
	msgbox(format("GrrrRRRRRHHH!!!!"))
	speakername("Little Archie")
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	msgbox(format("He looks even angrier now!"))
	removefieldmugshot
	closemessage
	release
}

script SpindaIsland_Home_EventScript_BobsHouseSign {
	msgbox(format("Mindy"), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_CynthiasHouseSign {
	msgbox(format("Cynthia"), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_SadManHome_Back {
	lock
	if (!flag(FLAG_HIDE_MAN_CANT_GO_HOME)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Door's locked."
		))
		removefieldmugshot
		closemessage
	} else {
		if (flag(FLAG_RECEIVED_RUSTY_BOLT)) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			speakername("Little Archie")
			msgbox(format(
				"I guess I should leave him alone now."
			))
			removefieldmugshot
			closemessage
		} else {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			speakername("Little Archie")
			msgbox(format(
				"Hello? Are you there?"
			))
			removefieldmugshot
			closemessage

			speakername("Stingy man")
			msgbox(format(
				"…\p"
				"My GOD you're annoying."
			))
			removefieldmugshot

			playse(SE_EXIT)

			clearflag(FLAG_HIDE_MAN_CANT_GO_HOME)
			setobjectxyperm(LOCALID_SAD_MAN, 42, 24)
			addobject(LOCALID_SAD_MAN)
			applymovement(LOCALID_SAD_MAN, SpindaIsland_Home_Movement_SadManExitsHouse)
			applymovement(OBJ_EVENT_ID_PLAYER, SpindaIsland_Home_Movement_PlayerMakesWay)
			waitmovement(0)
			waitmovement(LOCALID_SAD_MAN)
			waitse
			delay(48)
			createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
			speakername("Stingy man")
			msgbox(format(
				"Here's your reward. I hope you're happy."
			))
			removefieldmugshot
			closemessage
			giveitem(ITEM_RUSTY_BOLT)
			setflag(FLAG_RECEIVED_RUSTY_BOLT)
			applymovement(LOCALID_SAD_MAN, SpindaIsland_Home_Movement_SadManReEntersHouse)
			waitmovement(0)
			playse(SE_EXIT)
			removeobject(LOCALID_SAD_MAN)
			setflag(FLAG_HIDE_MAN_CANT_GO_HOME)
			waitse

			delay(48)

			if (flag(FLAG_FAUCET_QUEST_STARTED)) {
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
				speakername("Little Archie")
				msgbox(format(
					"An old and rusty bolt? What am I supposed to do with this?"
				))
				removefieldmugshot
				closemessage

				delay(48)

				playse(SE_PIN)
				applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
				waitmovement(0)
				delay(48)
				waitse

				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
				speakername("Little Archie")
				msgbox(format(
					"A {COLOR RED}bolt{COLOR DARK_GRAY}! Of course! Lucky me!\p"
					"I'm one step closer to getting that sweet sweet faucet replacement!!"
				))
				removefieldmugshot
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
				msgbox(format(
					"(Did I really say that? Is this how being a grownup feels like?)"
				))
				removefieldmugshot
				closemessage

				call(SpindaIsland_Common_CheckGotAllItemsForNewFaucet)
			} else {
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
				speakername("Little Archie")
				msgbox(format(
					"A bolt?\p"
					"It's all rusty…\p"
					"Guess it'll join my useless trash collection!"
				))
				removefieldmugshot
				closemessage
			}
		}
	}
	release
}

script SpindaIsland_Home_EventScript_ICanNeverGoHome {
	lock
	faceplayer
	createfieldmugshot(MUGSHOT_MAN, EMOTE_SAD)
	speakername("Sad man")
	msgbox(format(
		"Curse the architect that built my house facing the wrong direction!!\p"
		"I can never go home!"
	), MSGBOX_NPC)
	removefieldmugshot
	release
}

script SpindaIsland_Home_EventScript_Dad {
	lockall
	turnobject(LOCALID_DAD, DIR_SOUTH)
	if (flag(FLAG_OBTAINED_LEMONADE)) {
		createfieldmugshot(MUGSHOT_DAD, EMOTE_EMBARRASSED)
		speakername("Dad")
		msgbox(format(
			"Don't uh, pour that lemonade I gave you on the ground or anything.\p"
			"It'd probably kill all the grass and flowers."
		))
		removefieldmugshot
		closemessage
	} else {
		if (flag(FLAG_SPOKE_TO_DAD)) {
			createfieldmugshot(MUGSHOT_DAD, EMOTE_HAPPY)
			speakername("Dad")
			msgbox(format(
				"Here, have some lemonade!"
			))
			removefieldmugshot
			closemessage
		} else {
			createfieldmugshot(MUGSHOT_DAD, EMOTE_HAPPY)
			speakername("Dad")
			msgbox(format(
				"Hiya, sonny! going adventuring today, are we?\p"
				"Want some lemonade?"
			))
			removefieldmugshot
			closemessage
		}

		giveitem(ITEM_SALTY_LEMONADE)

		removefieldmugshot
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
		speakername("Little Archie")
		msgbox(format(
			"Now we're talkin'!! I can bring this to mom, that should do it!\p"
			"Let's take a sip first, I am PARCHED!"
		))
		closemessage
		removefieldmugshot

		delay(10)
		playse(SE_BALL)
		waitse
		playse(SE_BALL)
		waitse
		playse(SE_BALL)
		waitse
		delay(20)

		playse(SE_PIN)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
		waitmovement(0)
		waitse
		delay(48)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_DISGUSTED)
		speakername("Little Archie")
		msgbox(format(
			"BLEARGH!\p"
			"Why is it… so salty?!"
		))
		closemessage
		removefieldmugshot

		createfieldmugshot(MUGSHOT_DAD, EMOTE_GRINNING)
		speakername("Dad")
		msgbox(format(
			"Why, because it's seawater, obviously!"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_DAD, EMOTE_HAPPY)
		msgbox(format(
			"I take it you're not a fan of my recipe?"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_DAD, EMOTE_EMBARRASSED)
		msgbox(format(
			"(You wouldn't be the only one…)"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_DAD, EMOTE_HAPPY)
		msgbox(format(
			"In any case, it's nice of you to come visit me at the office, haha!\p"
			"See you around, little Archie! Don't get into trouble!"
		))
		removefieldmugshot
		closemessage

		setflag(FLAG_OBTAINED_LEMONADE)
		setflag(FLAG_SPOKE_TO_DAD)
	}
	releaseall
}

script SpindaIsland_Home_EventScript_Dad_FromSide {
	lock
	faceplayer
	if (flag(FLAG_OBTAINED_LEMONADE)) {
		createfieldmugshot(MUGSHOT_DAD, EMOTE_EMBARRASSED)
		speakername("Dad")
		msgbox(format(
			"Don't uh, pour that lemonade I gave you on the ground or anything.\p"
			"It'd probably kill all the grass and flowers."
		))
		removefieldmugshot
		closemessage
	} else {
		if (flag(FLAG_SPOKE_TO_DAD)) {
			createfieldmugshot(MUGSHOT_DAD, EMOTE_NORMAL)
			speakername("Dad")
			msgbox(format(
				"Come round to the front of the lemonade stand!"
			))
			removefieldmugshot
			closemessage
		} else {
			createfieldmugshot(MUGSHOT_DAD, EMOTE_HAPPY)
			speakername("Dad")
			msgbox(format(
				"Hiya, sonny! going adventuring today, are we?\p"
				"Want some lemonade?\p"
				"Come round to the front of the lemonade stand!"
			))
			removefieldmugshot
			closemessage
		}
		setflag(FLAG_SPOKE_TO_DAD)
	}
	release
}

script SpindaIsland_Home_EventScript_LemonadeStandSign {
	msgbox(format(
		"“ARCHIE'S LEMONADE\n"
		"A taste of the seaside!”"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_MainStreetSign {
	msgbox(format(
		"{UP_ARROW} Main street"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_MadelineSign {
	msgbox(format(
		"Bertha"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_PoemGirl {
	lock

	if (flag(FLAG_POEM_GIRL)) {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
		speakername("Maggie")
		msgbox(format(
			"See you around, Archie…"
		))
		removefieldmugshot
		closemessage

		applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
		waitmovement(0)
		playse(SE_M_GRASSWHISTLE)
	} else {
		playse(SE_M_GRASSWHISTLE)
		waitse

		faceplayer

		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
		speakername("Maggie")
		msgbox(format(
			"Archie…\p"
			"I want to hear a poem…\p"
			"I want you to reveal your heart to me."
		))
		removefieldmugshot
		closemessage

		dynmultichoice(
			0,
			0,
			TRUE,
			10,
			0,
			DYN_MULTICHOICE_CB_NONE,
			"Ocean waves swimming…",
			"Roses are red…",
			"Roses are blue…"
		)

		switch (var(VAR_RESULT)) {
			case 0:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"From the center of the sea…",
					"They are calling to my heart…"
				)

				switch (var(VAR_RESULT)) {
					case 0:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"With faith to reach land.",
							"Only to go back."
						)
					case 1:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"And my heart to theirs.",
							"Yearning for a touch."
						)
				}

				createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
				speakername("Maggie")
				msgbox(format(
					"That was very beautiful.\p"
					"I'm not really into haiku, though."
				))
				removefieldmugshot
				closemessage
			case 1:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"Violets are blue…",
					"Just like our love…"
				)
				switch (var(VAR_RESULT)) {
					// Violets are blue…
					case 0:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"The sky is pretty…",
							"The Pincurchin are spiky…"
						)

						switch(var(VAR_RESULT)) {
							// The sky is pretty…
							case 0:
								dynmultichoice(
									0,
									0,
									TRUE,
									10,
									0,
									DYN_MULTICHOICE_CB_NONE,
									"And so are you.",
									"And so are you!"
								)

								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"Oh, that's nice.\p"
									"You're not truly revealing your poet's heart, though."
								))
								removefieldmugshot
								closemessage

							// The Pincurchin are spiky…
							case 1:
								dynmultichoice(
									0,
									0,
									TRUE,
									10,
									0,
									DYN_MULTICHOICE_CB_NONE,
									"And so are you.",
									"And so are you!"
								)

								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"That's… a weird comparison.\p"
									"I thought you were a better poet than that."
								))
								removefieldmugshot
								closemessage
						}

						// Just like our love…
					case 1:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"Which blooms like they do.",
							"Wait, I didn't mean to say that."
						)

						createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
						speakername("Maggie")
						msgbox(format(
							"Wow, Archie, uh…\p"
							"That's not really what I meant when I said you should reveal your heart.\p"
							"(Well, this is awkward.)\p"
							"(Let's pretend this never happened.)"
						))
						removefieldmugshot
						closemessage
				}
			case 2:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"Violets are red…",
					"Violets are blue…"
				)

				switch(var(VAR_RESULT)) {
					// Roses are blue, violets are red
					case 0:
					dynmultichoice(
						0,
						0,
						TRUE,
						10,
						0,
						DYN_MULTICHOICE_CB_NONE,
						"The sky isn't blue, it's purple instead.",
						"Doesn't make sense, but fun to be said."
					)

					createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
					speakername("Maggie")
					msgbox(format(
						"Hmm…\p"
						"You're not revealing your heart right now.\p"
						"Plus, that didn't make any sense."
					))
					removefieldmugshot
					closemessage

					// Roses are blue, violets are blue
					case 1:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"Orchids are blue, too.",
							"Everything is blue…"
						)

						switch(var(VAR_RESULT)) {
							case 0:
								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"Hmm…\p"
									"You're not revealing your heart right now.\p"
									"Plus, that didn't make any sense."
								))
								removefieldmugshot
								closemessage
							case 1:
								dynmultichoice(
									0,
									0,
									TRUE,
									10,
									0,
									DYN_MULTICHOICE_CB_NONE,
									"Because it's my favorite color."
								)
								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"… … … … …\p"
									"Wow, Archie, I…\p"
									"This was so touching…\p"
									"Thank you for revealing your heart to me like this.\p"
									"Here, take this."
								))
								removefieldmugshot
								closemessage

								giveitem(ITEM_GRASS_BLADE)
								
								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"It's a blade of grass large enough to whistle with.\p"
									"Just don't listen to it too attentively, you might get sleepy."
								))
								removefieldmugshot
								closemessage

								setflag(FLAG_POEM_GIRL)
						}
				}
		}
	}
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
}

script SpindaIsland_Home_EventScript_Sunbather {
	lock
	createfieldmugshot(MUGSHOT_SUNBATHER, EMOTE_NORMAL)
	speakername("Sunbather")
	msgbox(format(
		"Ahh, it sure feels good to be sitting in the sun surrounded by all this {COLOR BLUE}WATER{COLOR DARK_GRAY}…\p"
		"…"
	))
	removefieldmugshot
	closemessage
	delay(48)
	faceplayer

	createfieldmugshot(MUGSHOT_SUNBATHER, EMOTE_PENSIVE)
	speakername("Sunbather")
	msgbox(format(
		"Could you leave? You're blocking the sun."
	))
	removefieldmugshot
	closemessage
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
}

script SpindaIsland_Home_Slowpoke {
	lock
	if (flag(FLAG_SLOWPOKE_EVOLVED)) {
		faceplayer
		playmoncry(SPECIES_SLOWBRO, CRY_MODE_NORMAL)
		speakername("Slowbro")
		msgbox(format(
			"Bruh…"
		))
		waitmoncry
		closemessage

	} else {
		faceplayer
		playmoncry(SPECIES_SLOWPOKE, CRY_MODE_NORMAL)
		speakername("Slowpoke")
		msgbox(format(
			"Slo…"
		))
		waitmoncry
		closemessage

		if (checkitem(ITEM_WHITE_SEASHELL) == TRUE) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_THINKING)
			speakername("Little Archie")
			msgbox(format(
				"… … …?\p"
				"He's looking at the white seashell I found…"
			))

			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"Give the seashell",
				"Don't give the seashell"
			)
			closemessage
			removefieldmugshot

			switch(var(VAR_RESULT)) {
				case 0:
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
					speakername("Little Archie")
					msgbox(format(
						"Sure, why the hell not!\p"
						"Here you go, buddy."
					))
					removefieldmugshot
					closemessage

					removeitem(ITEM_WHITE_SEASHELL)

					setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWBRO))
					playmoncry(SPECIES_SHELLDER, CRY_MODE_NORMAL)
					waitmoncry
					removeobject(VAR_LAST_TALKED)
					addobject(VAR_LAST_TALKED)
					playse(SE_M_BITE)
					applymovement(VAR_LAST_TALKED, SpindaIsland_Home_Movement_Slowpoke)
					applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
					waitmovement(VAR_LAST_TALKED)
					waitmovement(OBJ_EVENT_ID_PLAYER)

					playmoncry(SPECIES_SLOWBRO, CRY_MODE_NORMAL)
					speakername("Slowbro")
					msgbox(format(
						"Bruh!!"
					))
					waitmoncry
					closemessage
					
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SHOCKED)
					speakername("Little Archie")
					msgbox(format(
						"The heck?!\p"
						"My seashell bit him!"
					))
					removefieldmugshot
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
					msgbox(format(
					"Guess it was a Shellder all this time!"
					))
					removefieldmugshot
					closemessage

					setflag(FLAG_SLOWPOKE_EVOLVED)
				case 1:
					playmoncry(SPECIES_SLOWPOKE, CRY_MODE_FAINT)
					speakername("Slowpoke")
					msgbox(format(
						"…Slo…\p"
						":("
					))
					waitmoncry
					closemessage
			}
			
		}
	}
	release
}

script SpindaIsland_Home_EventScript_Metapod {
	lock
	speakername("Metapod")
	playmoncry(SPECIES_METAPOD, CRY_MODE_NORMAL)
	msgbox(format(
		"…"
	))
	waitmoncry
	closemessage
	release
}

script SpindaIsland_Home_EventScript_MetapodBoysFuckOff {
	faceplayer
	createfieldmugshot(MUGSHOT_TWIN, EMOTE_ANGRY)
	speakername("Twin")
	msgbox(format(
		"What is it?!"
	))

	createfieldmugshot(MUGSHOT_TWIN, EMOTE_NORMAL)
	msgbox(format(
		"Oh, I guess we are blocking the stairs a little…\p"
		"Fine, we'll take our epic battle somewhere else."
	))
	closemessage

	fadescreen(FADE_TO_BLACK)
	setflag(FLAG_HIDE_METAPOD_BATTLE)
	removeobject(1)
	removeobject(2)
	removeobject(3)
	removeobject(4)
	delay(60)
	fadescreen(FADE_FROM_BLACK)
	delay(40)
	release
}

script SpindaIsland_Home_EventScript_MetapodBoysFuckOff_Bussin {
	faceplayer
	createfieldmugshot(MUGSHOT_TWIN, EMOTE_ANGRY)
	speakername("Twin")
	msgbox(format(
		"On god why you gotta bust my balls like that fr?!"
	))

	createfieldmugshot(MUGSHOT_TWIN, EMOTE_NORMAL)
	msgbox(format(
		"Yikes, we lowkey do be blockin the stairs rn no cap…\p"
		"Aight word word, we gon vibe somewhere else."
	))
	removefieldmugshot
	closemessage

	fadescreen(FADE_TO_BLACK)
	setflag(FLAG_HIDE_METAPOD_BATTLE)
	removeobject(1)
	removeobject(2)
	removeobject(3)
	removeobject(4)
	delay(60)
	fadescreen(FADE_FROM_BLACK)
	delay(40)
	release
}

script SpindaIsland_Home_EventScript_MetapodBoy {
	lock
	if (var(VAR_METAPOD_OTHER_SIDE) == 1) {
		goto(SpindaIsland_Home_EventScript_MetapodBoysFuckOff)
	}
	createfieldmugshot(MUGSHOT_TWIN, EMOTE_ANGRY)
	speakername("Twin")
	msgbox(format(
		"I'm SO CLOSE to tasting that sweet, sweet victory!"
	))
	removefieldmugshot
	closemessage

	faceplayer

	createfieldmugshot(MUGSHOT_TWIN, EMOTE_FINGER)
	speakername("Twin")
	msgbox(format(
		"Can't you see we're in the midst of a fiery battle?"
	))
	removefieldmugshot
	closemessage

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	
	createfieldmugshot(MUGSHOT_TWIN, EMOTE_ANGRY)
	speakername("Twin")
	msgbox(format(
		"Metapod, use harden!!"
	))
	removefieldmugshot
	closemessage

	playse(SE_M_HARDEN)
	waitse
	playse(SE_M_HARDEN)
	waitse
	delay(48)
	release
}

script SpindaIsland_Home_EventScript_MetapodBoy_Bussin {
	lock
	if (var(VAR_METAPOD_OTHER_SIDE) == 1) {
		goto(SpindaIsland_Home_EventScript_MetapodBoysFuckOff_Bussin)
	}
	createfieldmugshot(MUGSHOT_TWIN, EMOTE_ANGRY)
	speakername("Twin")
	msgbox(format(
		"I'm deadass SO CLOSE to catchin that bussin drip fr!"
	))
	closemessage
	removefieldmugshot

	faceplayer

	createfieldmugshot(MUGSHOT_TWIN, EMOTE_FINGER)
	speakername("Twin")
	msgbox(format(
		"Can't you see we tryna catch some real heat rn bro? Sheesh!"
	))
	removefieldmugshot
	closemessage

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)

	createfieldmugshot(MUGSHOT_TWIN, EMOTE_ANGRY)
	speakername("Twin")
	msgbox(format(
		"Metapod, keep hittin that mid boi with harden, chief!!"
	))
	removefieldmugshot
	closemessage

	playse(SE_M_HARDEN)
	waitse
	playse(SE_M_HARDEN)
	waitse
	delay(48)
	release
}

script SpindaIsland_Home_PhotoCameraWrongDir {
	lock
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	speakername("Little Archie")
	msgbox(format(
		"Can't use that from this side."
	))
	closemessage
	removefieldmugshot
    release
}

script SpindaIsland_Home_EventScript_FaucetShopSign {
	msgbox(format(
		"“FAUCET WORKSHOP\n"
		"For all your faucet needs!\l"
		"WE ARE OPEN!!”"
	), MSGBOX_SIGN)
}

movement SpindaIsland_Home_Movement_SadManExitsHouse {
	walk_down
}

movement SpindaIsland_Home_Movement_SadManReEntersHouse {
	face_up
	delay_16
	delay_16
}

movement SpindaIsland_Home_Movement_PlayerMakesWay {
	lock_facing_direction
	walk_down
	unlock_facing_direction
}

movement SpindaIsland_Home_Movement_Slowpoke {
	lock_facing_direction
	jump_in_place_down
	unlock_facing_direction
	delay_16 * 3
	face_up
	delay_16
	face_down
	delay_16
	face_left
	delay_16
	face_up
	delay_16
	face_player
	delay_16 * 3
	lock_facing_direction
	jump_in_place_down
	jump_in_place_down
	unlock_facing_direction
	emote_heart
	delay_16 * 3
}

script SpindaIsland_Home_SlowpokeMistress {
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_SLOWPOKE_REWARD)) {
		createfieldmugshot(MUGSHOT_LASS, EMOTE_HAPPY)
		speakername("Girl")
		msgbox(format(
			"Look at my super cool Slowbro!\p"
			"He just looks so happy!"
		))
		removefieldmugshot
		closemessage
	} elif (flag(FLAG_SLOWPOKE_EVOLVED)) {
		playse(SE_PIN)
		applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
		waitse
		waitmovement(0)
		delay(48)

		createfieldmugshot(MUGSHOT_LASS, EMOTE_SURPRISED)
		speakername("Girl")
		msgbox(format(
			"By Arceus!!\p"
			"How on earth did you convince him to evolve?"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_LASS, EMOTE_NORMAL)
		msgbox(format(
			"…A cool-looking seashell? What are you talking about?"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_LASS, EMOTE_HAPPY)
		msgbox(format(
			"Well, thank you anyway! You can have this!"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_LASS, EMOTE_NORMAL)
		msgbox(format(
			"I no longer need it."
		))
		removefieldmugshot
		closemessage

		giveitem(ITEM_WATER_STONE_2)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_THINKING)
		speakername("Little Archie")
		msgbox(format(
			"Hmm… A {COLOR BLUE}WATER{COLOR DARK_GRAY} Stone…\p"
			"Maybe I could try giving this to mom?"
		))
		closemessage
		removefieldmugshot

		setflag(FLAG_RECEIVED_SLOWPOKE_REWARD)
	} else {
		createfieldmugshot(MUGSHOT_LASS, EMOTE_SWEAT)
		speakername("Girl")
		msgbox(format(
			"I wish my Pokémon would evolve…"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_LASS, EMOTE_NORMAL)
		msgbox(format(
			"I keep trying to give him this {COLOR BLUE}WATER{COLOR DARK_GRAY} Stone, but it just does nothing.\p"
			"Perhaps there's another way…"
		))
		removefieldmugshot
		closemessage
	}
	release
}

script SpindaIsland_Home_EventScript_Juan {
	lock
	faceplayer
	createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
	speakername("Juan")
	if (flag(FLAG_TALKED_TO_JUAN_ABOUT_PUBES)) {
		msgbox(format(
			"Hey, wanna hear more interesting facts about Pokémon?"
		))

		dynmultichoice(
			0,
			0,
			TRUE,
			10,
			0,
			DYN_MULTICHOICE_CB_NONE,
			"Nope",
			"No thanks"
		)

		createfieldmugshot(MUGSHOT_MAN, EMOTE_GRINNING)
		msgbox(format(
			"Bummer! Come back whenever, I have plenty!"
		))

		removefieldmugshot
		closemessage
	} else {
		msgbox(format(
			"I'm just taking my friend Machoke for a stroll!\p"
			"You know, I've always wondered where the cute little thong and belt come from.\p"
			"My Machoke was already wearing it when I caught him!\p"
			"Do they have like a Machoke thong shop somewhere and all the Machoke go there to get their thong?\p"
			"Is there a Machoke censor who travels across all lands, putting thongs on wild Machoke?\p"
			"Do they actually just grow their own thong naturally?\p"
			"Is it… {PAUSE 20}Is it their…\p"
			"… … …\p"
			"… …It is, isn't it?"
		))
		createfieldmugshot(MUGSHOT_MAN, EMOTE_SURPRISED)
		msgbox(format(
			"Oh boy, what a find! I need to write this discovery down in my PokéDex!!"
		))
		removefieldmugshot
		closemessage
		setflag(FLAG_TALKED_TO_JUAN_ABOUT_PUBES)
	}
	
	release
}

script SpindaIsland_Home_EventScript_Machoke {
	lock
	faceplayer
	playmoncry(SPECIES_MACHOKE, CRY_MODE_NORMAL)
	if (flag(FLAG_TALKED_TO_JUAN_ABOUT_PUBES)) {
		speakername("Sexy Machoke")
	} else {
		speakername("Machoke")
	}
	msgbox(format(
		"Hmmm…"
	))
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	closemessage
	release
}

script SpindaIsland_Home_EventScript_Lisa {
	lock
	faceplayer
	createfieldmugshot(MUGSHOT_LITTLE_GIRL_BLACK, EMOTE_NORMAL)
	speakername("Lisa")
	msgbox(format(
		"Hey Archie! Heard you were building a sick pirate ship!"
	))
	removefieldmugshot
	closemessage
	release
}

script SpindaIsland_Home_EventScript_OldWoman {
	lock
	faceplayer
	if (!flag(FLAG_SHUPPET_QUEST_STARTED)) {
		faceplayer
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"I think my house is haunted. There's something living in my attic.\p"
			"I don't know what to do…"
		))
		removefieldmugshot
		closemessage
		setflag(FLAG_SHUPPET_QUEST_STARTED)
	} elif (!flag(FLAG_HIDE_SHUPPETS)) {
		faceplayer
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"Oh, I'm too scared to go back inside…"
		))
		removefieldmugshot
		closemessage
	}

	if (flag(FLAG_HIDE_SHUPPETS)) {
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"Oh, I'm too scared to go back inside…"
		))
		removefieldmugshot
		closemessage

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
		speakername("Little Archie")
		msgbox(format(
			"Don't worry granny! There were some Shuppet in your attic, but I chased them away!"
		))
		removefieldmugshot
		closemessage

		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"Shuppet, you say?"
		))
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_HAPPY)
		msgbox(format(
			"How adorable of you to help out a poor old lady…\p"
			"How can I repay you?\p"
			"You look like you're hungry… Let me fix you a little snack!"
		))
		removefieldmugshot
		closemessage
		
		giveitem(ITEM_PB_AND_J)

		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_HAPPY)
		msgbox(format(
			"Have a good day, child."
		))
		removefieldmugshot
		closemessage

		turnobject(VAR_LAST_TALKED, DIR_NORTH)
		removeobject(VAR_LAST_TALKED)
		playse(SE_EXIT)
		waitse
		setflag(FLAG_HIDE_OLD_WOMAN)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Cool! Lemme pack this up for later.\p"
			"Let's hope it doesn't taste like old people or something."
		))
		removefieldmugshot
		closemessage

		call(SpindaIsland_Common_CheckGotAllItemsForNewFaucet)
	}
	release
}

script SpindaIsland_Home_EventScript_OldWomansDoor {
	lock

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
    speakername("Little Archie")
    msgbox(format(
		"I'm glad I was able to help!"
    ))
    closemessage
	removefieldmugshot
    
    release
}

script SpindaIsland_Home_EventScript_Chest {
	lock
	if (flag(FLAG_OPENED_CHEST)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Already opened it!"
		))
		closemessage
		removefieldmugshot
	} else {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
		speakername("Little Archie")
		msgbox(format(
			"Woah, a treasure chest?!\p"
			"Wonder who put it here!"
		))
		closemessage
		removefieldmugshot

		setmetatile(53, 27, METATILE_SpindaIsland_EmptyChest, TRUE)
		special(DrawWholeMapView)

		setflag(FLAG_OPENED_CHEST)
		giveitem(ITEM_PRECIOUS_BOLT)

		if (flag(FLAG_FAUCET_QUEST_STARTED)) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
			speakername("Little Archie")
			msgbox(format(
				"A {COLOR RED}bolt{COLOR DARK_GRAY}!!\p"
				"And it's gold, too!\p"
				"How lucky!"
			))
			closemessage
			removefieldmugshot

			call(SpindaIsland_Common_CheckGotAllItemsForNewFaucet)
		} else {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
			speakername("Little Archie")
			msgbox(format(
				"A gold coin?!"
			))
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			msgbox(format(
				"Ah, no, it's a {COLOR RED}bolt{COLOR DARK_GRAY}.\p"
				"Weird, I wonder who put it here."
			))
			closemessage
			removefieldmugshot
		}
	}
	release
}

script SpindaIsland_Home_CarBoy {
	lock
	faceplayer
	createfieldmugshot(MUGSHOT_LITTLE_BOY, EMOTE_NORMAL)
	speakername("Rudy")
	msgbox(format(
		"Vrrrbbbbbmmmmmmm!!!!!"
	))
	removefieldmugshot
	closemessage
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
}

script SpindaIsland_Home_CarGirl {
	lock
	if (flag(FLAG_RECEIVED_NICKEL)) {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_NORMAL)
		speakername("Katie")
		msgbox(format(
			"Thanks a bunch!"
		))
		removefieldmugshot
		closemessage
		
	} elif (flag(FLAG_REPLACED_WHEEL)) {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_NORMAL)
		speakername("Katie")
		msgbox(format(
			"Oh!\p"
			"You've got this super cool {COLOR RED}metal wheel{COLOR DARK_GRAY}…\p"
			"And you're just giving it to us?\p"
			"You're the best, Archie!!\p"
			"We don't have much, but please take this.\p"
			"It's my lucky coin! I hope it'll be lucky for you, too!"
		))
		removefieldmugshot
		closemessage

		giveitem(ITEM_RUSTY_NICKEL)
		setflag(FLAG_RECEIVED_NICKEL)
	} elif (flag(FLAG_LOOKING_FOR_WHEEL_REPLACEMENT)) {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
		speakername("Katie")
		msgbox(format(
			"Now what could we use as a wheel…"
		))
		removefieldmugshot
		closemessage
	} else {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_NORMAL)
		speakername("Katie")
		msgbox(format(
			"Hey Archie!\p"
			"As you can see we're almost done with our monster car.\p"
			"We're just missing one last wheel!\p"
			"We don't really need it to be an actual wheel, either.\p"
			"Anything round and spinny would do!"
		))
		removefieldmugshot
		closemessage

		setflag(FLAG_LOOKING_FOR_WHEEL_REPLACEMENT)
	}

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	
	release
}

script SpindaIsland_Home_EventScript_MissingWheel {
	lock
	if (flag(FLAG_REPLACED_WHEEL)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
		speakername("Little Archie")
		msgbox(format(
			"A shiny new wheel!"
		))
		removefieldmugshot
		closemessage
	} else {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"A wheel is missing.\p"
			"I'm sure I could find a replacement."
		))
		removefieldmugshot
		closemessage
	}
	release
}

script SpindaIsland_Home_EventScript_ReplaceWheelWithBrokenFaucet {
	lock

	if (flag(FLAG_FAUCET_QUEST_STARTED)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"What if…"
		))
		removefieldmugshot
		closemessage

		delay(48)

		playse(SE_CLICK)
		setmetatile(27, 50, METATILE_SpindaIslandSecondary_FaucetWheel, TRUE)
		special(DrawWholeMapView)
		waitse

		delay(48)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
		speakername("Little Archie")
		msgbox(format(
			"Fits perfectly! There ya go!"
		))
		removefieldmugshot
		closemessage

		removeitem(ITEM_BROKEN_FAUCET)
		setflag(FLAG_REPLACED_WHEEL)

		release
	} else {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"This'd be perfect, but I need to repair it!"
		))
		removefieldmugshot
		closemessage
	}
}

script SpindaIsland_Home_EventScript_SurveyedWheel {
	lock
	if (flag(FLAG_FAUCET_QUEST_STARTED) && !flag(FLAG_FOUND_TOUGH_BOLT)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Just a regular wheel."
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
		msgbox(format(
			"Wait, it's stuck to the car with bolts!\p"
			"If I could just take one…"
		))
		removefieldmugshot

		if (checkitem(ITEM_BORROWED_PLIERS)) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			msgbox(format(
				"Here, let me just…"
			))
			removefieldmugshot
			closemessage

			turnobject(LOCALID_CAR_GIRL, DIR_EAST)
			createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_NORMAL)
			speakername("Katie")
			msgbox(format(
				"Whatcha doing, Archie?"
			))
			removefieldmugshot
			closemessage

			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
			speakername("Little Archie")
			msgbox(format(
				"Ah, uh, nothing!"
			))
			removefieldmugshot
			closemessage

			turnobject(LOCALID_CAR_GIRL, DIR_NORTH)
			delay(48)

			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			speakername("Little Archie")
			msgbox(format(
				"(Darn, can't do this right under her nose.)"
			))
			removefieldmugshot
			closemessage
		} else {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			msgbox(format(
				"I'd need some pliers to get it out."
			))
			removefieldmugshot
			closemessage
		}
	} else {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Just a regular wheel."
		))
		removefieldmugshot
		closemessage
	}
	release
}

script SpindaIsland_Home_EventScript_Raft {
	lock
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	speakername("Little Archie")
	msgbox(format(
		"My super-awesome pirate ship!\p"
		"Only thing missing is the sail."
	))
	removefieldmugshot
	closemessage
	release
}

script SpindaIsland_Home_EventScript_ThisIsConvenient {
	lock

	if (flag(FLAG_FAUCET_BROKE)) {
		turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
		delay(20)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Well, this is eerily convenient!"
		))
		removefieldmugshot
		closemessage

		setvar(VAR_PASSED_IN_FRONT_OF_FAUCET_SHOP, 1)
	}
	
	release
}

script SpindaIsland_Home_EventScript_Pot {
	lock
	if (flag(FLAG_SALTED_DISH)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
		speakername("Little Archie")
		msgbox(format(
			"Much better!"
		))
		removefieldmugshot
		closemessage
	} else {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Slurp…"
		))
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_DISGUSTED)
		msgbox(format(
			"Ugh, it's tasteless…\p"
			"Have people never heard of salt?"
		))
		removefieldmugshot
		closemessage
	}
	release
}

script SpindaIsland_Home_EventScript_SaltDish {
	lock
	removeitem(ITEM_SALTY_LEMONADE)
	setflag(FLAG_SALTED_DISH)
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	speakername("Little Archie")
	msgbox(format(
		"Needs some salt, so maybe…"
	))
	removefieldmugshot
	closemessage

	playse(SE_M_POISON_POWDER)
	waitse
	playse(SE_M_POISON_POWDER)
	waitse
	playse(SE_M_POISON_POWDER)
	waitse

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
	speakername("Little Archie")
	msgbox(format(
		"There, fixed, probably!"
	))
	removefieldmugshot
	closemessage

	release
}

script SpindaIsland_Home_EventScript_FeedZiggy {
	lock
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	speakername("Little Archie")
	if (flag(FLAG_FAUCET_QUEST_STARTED)) {
		msgbox(format(
			"Good idea, except I need this sandwich for the faucet salesman!"
		))
	} else {
		msgbox(format(
			"I'd be afraid of this one biting off my hand!"
		))
	}
	removefieldmugshot
	closemessage
	release
}

script SpindaIsland_Home_EventScript_Cook {
	lock
	faceplayer

	if (flag(FLAG_GOT_OIL)) {
		createfieldmugshot(MUGSHOT_COOK, EMOTE_NORMAL)
		speakername("Cook")
		msgbox(format(
			"Thank you again.\p"
			"You will become a great chef when you grow up, I can feel it!"
		))
		removefieldmugshot
		closemessage

	} elif (flag(FLAG_SALTED_DISH)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Can you try your dish now?"
		))
		removefieldmugshot
		closemessage

		createfieldmugshot(MUGSHOT_COOK, EMOTE_SAD)
		speakername("Cook")
		msgbox(format(
			"It's not like it's gotten any tastier on its own, but sure."
		))
		removefieldmugshot
		closemessage

		// turnobject(VAR_LAST_TALKED, DIR_WEST)
		delay(20)

		createfieldmugshot(MUGSHOT_COOK, EMOTE_SAD)
		speakername("Cook")
		msgbox(format(
			"Slurp…"
		))
		removefieldmugshot
		closemessage

		playse(SE_PIN)
		applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
		waitmovement(0)
		waitse
		delay(48)

		createfieldmugshot(MUGSHOT_COOK, EMOTE_NORMAL)
		speakername("Cook")
		msgbox(format(
			"Oh, wow! It's… It's remarkable!!\p"
			"What arcane sorcery…\p"
			"What? You added some lemonade in the pot?\p"
			"Who would have thought!!\p"
			"I don't have much to thank you, but here is a bottle of my finest cooking oil!"
		))
		removefieldmugshot
		closemessage

		setflag(FLAG_COOK_SAD)
		setflag(FLAG_GOT_OIL)
		giveitem(ITEM_COOKING_OIL)

		call(SpindaIsland_Common_CheckGotAllItemsForNewFaucet)
	} else {
		createfieldmugshot(MUGSHOT_COOK, EMOTE_SAD)
		speakername("Cook")
		msgbox(format(
			"I am distraught.\p"
			"My dish isn't turning out how I wanted at all…\p"
			"How can I make it less… flavorless?\p"
			"Is there any hope at all?"
		))
		removefieldmugshot
		closemessage

		setflag(FLAG_COOK_SAD)
	}
	
	release
}

script SpindaIsland_Home_EventScript_Zigzagoon {
	lock
	if (flag(FLAG_ZIGGY_ASLEEP)) {
		playse(SE_M_SNORE)
		speakername("Sleepy Zigzagoon")
		msgbox(format(
			"ZzzzzZzZz…"
		))
		waitse
		closemessage
	} else {
		playmoncry(SPECIES_ZIGZAGOON, CRY_MODE_ENCOUNTER)
		speakername("Rabid Zigzagoon")
		msgbox(format(
			"GRRRRRRRR…"
		))
		waitmoncry
		closemessage
	}
	release
}

script SpindaIsland_Home_EventScript_Pliers {
	lock
	if (flag(FLAG_ZIGGY_ASLEEP)) {
		removeobject(VAR_LAST_TALKED)
		giveitem(ITEM_BORROWED_PLIERS)
		setflag(FLAG_PICKED_UP_PLIERS)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_DISGUSTED)
		speakername("Little Archie")
		msgbox(format(
			"Yuck, the Zigzagoon drooled all over them!\p"
			"At least I hope it's drool…"
		))
		removefieldmugshot
		closemessage
	} else {
		if (flag(FLAG_SAW_PLIERS)) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			speakername("Little Archie")
			msgbox(format(
				"Can't get to those {COLOR RED}pliers{COLOR DARK_GRAY} with that Zigzagoon guarding them…\p"
				"If only there was a way to distract or incapacitate it…"
			))
			removefieldmugshot
			closemessage
		} else {
			if (flag(FLAG_FAUCET_QUEST_STARTED)) {
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
				speakername("Little Archie")
				msgbox(format(
					"These must be the {COLOR RED}pliers{COLOR DARK_GRAY} the faucet salesman was talking about!"
				))

				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
				msgbox(format(
					"You're gonna give them to me, right little Ziggy?"
				))
				removefieldmugshot
				closemessage

				playmoncry(SPECIES_ZIGZAGOON, CRY_MODE_ENCOUNTER)
				speakername("Rabid Zigzagoon")
				msgbox(format(
					"GRRRRRRRR…"
				))
				waitmoncry
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
				speakername("Little Archie")
				msgbox(format(
					"Nope, not gonna risk a hand just to get these!"
				))
				removefieldmugshot
				closemessage

				setflag(FLAG_SAW_PLIERS)
			} else {
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
				speakername("Little Archie")
				msgbox(format(
					"Is this… a pair of {COLOR RED}pliers{COLOR DARK_GRAY}?\p"
					"I wonder what a Zigzagoon would use these for."
				))
			}
		}
	}
	
	release
}

script SpindaIsland_Home_EventScript_UseGrassBladeWithZiggy {
	lock
	playse(SE_M_GRASSWHISTLE)
	waitse
	
	playmoncry(SPECIES_ZIGZAGOON, CRY_MODE_ENCOUNTER)
	speakername("Rabid Zigzagoon")
	msgbox(format(
		"GRRRRRRRR!!"
	))
	waitmoncry
	closemessage

	delay(30)

	playmoncry(SPECIES_ZIGZAGOON, CRY_MODE_FAINT)
	speakername("Sleepy Zigzagoon")
	msgbox(format(
		"…GrrrzzZZzzzzZz…"
	))
	waitmoncry
	playse(SE_M_SNORE)
	msgbox(format(
		"ZzzzZzZzzZZzzz…"
	))
	waitse

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
	speakername("Little Archie")
	msgbox(format(
		"Awesome! It fell asleep!\p"
		"Let's get those {COLOR RED}pliers{COLOR DARK_GRAY}!"
	))
	removefieldmugshot
	closemessage

	setflag(FLAG_ZIGGY_ASLEEP)
	release
}

script SpindaIsland_Home_EventScript_UseGrassBlade {
	lock
	playse(SE_M_GRASSWHISTLE)
	waitse
	release
}

script SpindaIsland_Home_EventScript_BobsDoor {
	lock
	speakername("Mindy")
	msgbox(format(
		"Hey Archie! Wanna come in for a Pokémon trade?"
	))
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	speakername("Little Archie")
	msgbox(format(
		"Can't today, I'm busy!"
	))
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SAD)
	msgbox(format(
		"(Never again.)"
	))
	removefieldmugshot
	closemessage
	release
}

script SpindaIsland_Home_EventScript_CynthiasDoor {
	lock
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	speakername("Little Archie")
	msgbox(format(
		"I ain't going in there unprepared.\p"
		"Or at all."
	))
	removefieldmugshot
	closemessage
	release
}


movement SpindaIsland_Home_Movement_RaftGoesToIsland {
	face_left
	lock_facing_direction
	lock_anim
	walk_down * 7
}

movement SpindaIsland_Home_Movement_PlayerEntersRaft {
	walk_down
	set_invisible
}
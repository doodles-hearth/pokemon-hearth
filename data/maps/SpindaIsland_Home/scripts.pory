const LOCALID_DAD = 3
const LOCALID_SAD_MAN = 2
const LOCALID_CAR_GIRL = 15

mapscripts SpindaIsland_Home_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (flag(FLAG_SLOWPOKE_EVOLVED)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWBRO))
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWPOKE))
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SPINDA_ISLAND_INTRO_STATE, 2: SpindaIsland_Home_EventScript_MainQuestRecap
    ]
}
script SpindaIsland_Home_EventScript_MainQuestRecap {
	lock
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
	speakername("Little Archie")
	msgbox(format(
		"Alright! The quicker mom gets her glass of {COLOR BLUE}WATER{COLOR DARK_GRAY}, the quicker Archie gets his super badass pirate {COLOR RED}sail{COLOR DARK_GRAY}!\p"
		"Let's get moving!"
	))
	closemessage
	removefieldmugshot
	setvar(VAR_SPINDA_ISLAND_INTRO_STATE, 3)
	release
}

script SpindaIsland_Home_EventScript_MetapodOtherSide {
	lock
	setvar(VAR_METAPOD_OTHER_SIDE, 1)
	release
}

script SpindaIsland_Home_EventScript_ArchiesHouseSign {
	msgbox(format("The Hoenns"), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_BobsHouseSign {
	msgbox(format("Bob"), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_SadManHome_Back {
	lock
	if (!flag(FLAG_HIDE_MAN_CANT_GO_HOME)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Door's locked."
		))
		removefieldmugshot
		closemessage
	} else {
		if (flag(FLAG_RECEIVED_RUSTY_BOLT)) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			speakername("Little Archie")
			msgbox(format(
				"I guess I should leave him alone now."
			))
			removefieldmugshot
			closemessage
		} else {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			speakername("Little Archie")
			msgbox(format(
				"Hello? Are you there?"
			))
			removefieldmugshot
			closemessage

			speakername("Stingy man")
			msgbox(format(
				"…\p"
				"My GOD you're annoying."
			))
			removefieldmugshot

			playse(SE_EXIT)

			clearflag(FLAG_HIDE_MAN_CANT_GO_HOME)
			setobjectxyperm(LOCALID_SAD_MAN, 42, 24)
			addobject(LOCALID_SAD_MAN)
			applymovement(LOCALID_SAD_MAN, SpindaIsland_Home_Movement_SadManExitsHouse)
			applymovement(OBJ_EVENT_ID_PLAYER, SpindaIsland_Home_Movement_PlayerMakesWay)
			waitmovement(0)
			waitmovement(LOCALID_SAD_MAN)
			waitse
			delay(48)
			createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
			speakername("Stingy man")
			msgbox(format(
				"Here's your reward. I hope you're happy."
			))
			removefieldmugshot
			closemessage
			giveitem(ITEM_RUSTY_BOLT)
			setflag(FLAG_RECEIVED_RUSTY_BOLT)
			applymovement(LOCALID_SAD_MAN, SpindaIsland_Home_Movement_SadManReEntersHouse)
			waitmovement(0)
			playse(SE_EXIT)
			removeobject(LOCALID_SAD_MAN)
			setflag(FLAG_HIDE_MAN_CANT_GO_HOME)
			waitse

			delay(48)

			if (flag(FLAG_FAUCET_QUEST_STARTED)) {
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
				speakername("Little Archie")
				msgbox(format(
					"An old and rusty bolt? What am I supposed to do with this?"
				))
				removefieldmugshot
				closemessage

				delay(48)

				playse(SE_PIN)
				applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
				waitmovement(0)
				delay(48)
				waitse

				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
				speakername("Little Archie")
				msgbox(format(
					"A {COLOR RED}bolt{COLOR DARK_GRAY}! Of course! Lucky me!\p"
					"I'm one step closer to getting that sweet sweet faucet replacement!!"
				))
				removefieldmugshot
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
				msgbox(format(
					"(Did I really say that? Is this how being a grownup feels like?)"
				))
				removefieldmugshot
				closemessage

				call(SpindaIsland_Common_CheckGotAllItemsForNewFaucet)
			} else {
				createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
				speakername("Little Archie")
				msgbox(format(
					"A bolt?\p"
					"It's all rusty…\p"
					"Guess it'll join my useless trash collection!"
				))
				removefieldmugshot
				closemessage
			}
		}
	}
	release
}

script SpindaIsland_Home_EventScript_ICanNeverGoHome {
	lock
	faceplayer
	createfieldmugshot(MUGSHOT_MAN, EMOTE_SAD)
	speakername("Sad man")
	msgbox(format(
		"Curse the architect that built my house facing the wrong direction!!\p"
		"I can never go home!"
	), MSGBOX_NPC)
	removefieldmugshot
	release
}

script SpindaIsland_Home_EventScript_Dad {
	lock
	turnobject(LOCALID_DAD, DIR_SOUTH)
	if (flag(FLAG_OBTAINED_LEMONADE)) {
		speakername("Dad")
		msgbox(format(
			"Don't uh, pour that lemonade I gave you on the ground or anything.\p"
			"It'd probably kill all the grass and flowers."
		))
		closemessage
	} else {
		if (flag(FLAG_SPOKE_TO_DAD)) {
			speakername("Dad")
			msgbox(format(
				"Here, have some lemonade!"
			))
			closemessage
		} else {
			speakername("Dad")
			msgbox(format(
				"Hiya, sonny! going adventuring today, are we?\p"
				"Want some lemonade?"
			))
			closemessage
		}

		giveitem(ITEM_SALTY_LEMONADE)

		removefieldmugshot
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
		speakername("Little Archie")
		msgbox(format(
			"Now we're talkin'!! I can bring this to mom, that should do it!\p"
			"Let's take a sip first, I am PARCHED!"
		))
		closemessage
		removefieldmugshot

		delay(10)
		playse(SE_BALL)
		waitse
		playse(SE_BALL)
		waitse
		playse(SE_BALL)
		waitse
		delay(20)

		playse(SE_PIN)
		applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
		waitmovement(0)
		delay(48)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_DISGUSTED)
		speakername("Little Archie")
		msgbox(format(
			"BLEARGH!\p"
			"Why is it… so salty?!"
		))
		closemessage
		removefieldmugshot

		speakername("Dad")
		msgbox(format(
			"Why, because it's seawater, obviously!\p"
			"I take it you're not a fan of my recipe?\p"
			"(You wouldn't be the only one…)\p"
			"In any case, it's nice of you to come visit me at the office, haha!\p"
			"See you around, little Archie! Don't get into trouble!"
		))
		closemessage

		setflag(FLAG_OBTAINED_LEMONADE)
		setflag(FLAG_SPOKE_TO_DAD)
	}
	release
}

script SpindaIsland_Home_EventScript_Dad_FromSide {
	lock
	faceplayer
	if (flag(FLAG_OBTAINED_LEMONADE)) {
		speakername("Dad")
		msgbox(format(
			"Don't uh, pour that lemonade I gave you on the ground or anything.\p"
			"It'd probably kill all the grass and flowers."
		))
		closemessage
	} else {
		if (flag(FLAG_SPOKE_TO_DAD)) {
			speakername("Dad")
			msgbox(format(
				"Come round to the front of the lemonade stand!"
			))
			closemessage
		} else {
			speakername("Dad")
			msgbox(format(
				"Hiya, sonny! going adventuring today, are we?\p"
				"Want some lemonade?\p"
				"Come round to the front of the lemonade stand!"
			))
			closemessage
		}
		setflag(FLAG_SPOKE_TO_DAD)
	}
	release
}

script SpindaIsland_Home_EventScript_LemonadeStandSign {
	msgbox(format(
		"“ARCHIE'S LEMONADE\n"
		"A taste of the seaside!”"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_MainStreetSign {
	msgbox(format(
		"{UP_ARROW} Main street"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_MadelineSign {
	msgbox(format(
		"Madeline"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_PoemGirl {
	lock

	if (flag(FLAG_POEM_GIRL)) {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
		speakername("Maggie")
		msgbox(format(
			"See you around, Archie…"
		))
		removefieldmugshot
		closemessage

		applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
		waitmovement(0)
		playse(SE_M_GRASSWHISTLE)
	} else {
		playse(SE_M_GRASSWHISTLE)
		waitse

		faceplayer

		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
		speakername("Maggie")
		msgbox(format(
			"Archie…\p"
			"I want to hear a poem…\p"
			"I want you to reveal your heart to me."
		))
		removefieldmugshot
		closemessage

		dynmultichoice(
			0,
			0,
			TRUE,
			10,
			0,
			DYN_MULTICHOICE_CB_NONE,
			"Ocean waves swimming…",
			"Roses are red…",
			"Roses are blue…"
		)

		switch (var(VAR_RESULT)) {
			case 0:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"From the center of the sea…",
					"They are calling to my heart…"
				)

				switch (var(VAR_RESULT)) {
					case 0:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"With faith to reach land.",
							"Only to go back."
						)
					case 1:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"And my heart to theirs.",
							"Yearning for a touch."
						)
				}

				createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
				speakername("Maggie")
				msgbox(format(
					"That was very beautiful.\p"
					"I'm not really into haiku, though."
				))
				removefieldmugshot
				closemessage
			case 1:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"Violets are blue…",
					"Just like our love…"
				)
				switch (var(VAR_RESULT)) {
					// Violets are blue…
					case 0:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"The sky is pretty…",
							"The Pincurchin are spiky…"
						)

						switch(var(VAR_RESULT)) {
							// The sky is pretty…
							case 0:
								dynmultichoice(
									0,
									0,
									TRUE,
									10,
									0,
									DYN_MULTICHOICE_CB_NONE,
									"And so are you.",
									"And so are you!"
								)

								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"Oh, that's nice.\p"
									"You're not truly revealing your poet's heart, though."
								))
								removefieldmugshot
								closemessage

							// The Pincurchin are spiky…
							case 1:
								dynmultichoice(
									0,
									0,
									TRUE,
									10,
									0,
									DYN_MULTICHOICE_CB_NONE,
									"And so are you.",
									"And so are you!"
								)

								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"That's… a weird comparison.\p"
									"I thought you were a better poet than that."
								))
								removefieldmugshot
								closemessage
						}

						// Just like our love…
					case 1:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"Which blooms like they do.",
							"Wait, I didn't mean to say that."
						)

						createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
						speakername("Maggie")
						msgbox(format(
							"Wow, Archie, uh…\p"
							"That's not really what I meant when I said you should reveal your heart.\p"
							"(Well, this is awkward.)\p"
							"(Let's pretend this never happened.)"
						))
						removefieldmugshot
						closemessage
				}
			case 2:
				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"Violets are red…",
					"Violets are blue…"
				)

				switch(var(VAR_RESULT)) {
					// Roses are blue, violets are red
					case 0:
					dynmultichoice(
						0,
						0,
						TRUE,
						10,
						0,
						DYN_MULTICHOICE_CB_NONE,
						"The sky isn't blue, it's purple instead.",
						"Doesn't make sense, but fun to be said."
					)

					createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
					speakername("Maggie")
					msgbox(format(
						"Hmm…\p"
						"You're not revealing your heart right now.\p"
						"Plus, that didn't make any sense."
					))
					removefieldmugshot
					closemessage

					// Roses are blue, violets are blue
					case 1:
						dynmultichoice(
							0,
							0,
							TRUE,
							10,
							0,
							DYN_MULTICHOICE_CB_NONE,
							"Orchids are blue, too.",
							"Everything is blue…"
						)

						switch(var(VAR_RESULT)) {
							case 0:
								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"Hmm…\p"
									"You're not revealing your heart right now.\p"
									"Plus, that didn't make any sense."
								))
								removefieldmugshot
								closemessage
							case 1:
								dynmultichoice(
									0,
									0,
									TRUE,
									10,
									0,
									DYN_MULTICHOICE_CB_NONE,
									"Because it's my favorite color."
								)
								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"… … … … …\p"
									"Wow, Archie, I…\p"
									"This was so touching…\p"
									"Thank you for revealing your heart to me like this.\p"
									"Here, take this."
								))
								removefieldmugshot
								closemessage

								giveitem(ITEM_GRASS_BLADE)
								
								createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
								speakername("Maggie")
								msgbox(format(
									"It's a blade of grass large enough to whistle with."
								))
								removefieldmugshot
								closemessage

								setflag(FLAG_POEM_GIRL)
						}
				}
		}
	}
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
}

script SpindaIsland_Home_EventScript_Sunbather {
	lock
	createfieldmugshot(MUGSHOT_SUNBATHER, EMOTE_NORMAL)
	speakername("Sunbather")
	msgbox(format(
		"Ahh, it sure feels good to be sitting in the sun surrounded by all this {COLOR BLUE}WATER{COLOR DARK_GRAY}…\p"
		"…"
	))
	removefieldmugshot
	closemessage
	delay(48)
	faceplayer

	createfieldmugshot(MUGSHOT_SUNBATHER, EMOTE_PENSIVE)
	speakername("Sunbather")
	msgbox(format(
		"Could you leave? You're blocking the sun."
	))
	removefieldmugshot
	closemessage
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
}

script SpindaIsland_Home_Slowpoke {
	lock
	if (flag(FLAG_SLOWPOKE_EVOLVED)) {
		faceplayer
		playmoncry(SPECIES_SLOWBRO, CRY_MODE_NORMAL)
		speakername("Slowbro")
		msgbox(format(
			"Bruh…"
		))
		closemessage

	} else {
		faceplayer
		playmoncry(SPECIES_SLOWPOKE, CRY_MODE_NORMAL)
		speakername("Slowpoke")
		msgbox(format(
			"Slo…"
		))
		waitmoncry
		closemessage

		if (checkitem(ITEM_WHITE_SEASHELL) == TRUE) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_THINKING)
			speakername("Little Archie")
			msgbox(format(
				"… … …?\p"
				"He's looking at the white seashell I found…"
			))

			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"Give the seashell",
				"Don't give the seashell"
			)
			closemessage
			removefieldmugshot

			switch(var(VAR_RESULT)) {
				case 0:
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
					speakername("Little Archie")
					msgbox(format(
						"Sure, why the hell not!\p"
						"Here you go, buddy."
					))
					removefieldmugshot

					removeitem(ITEM_WHITE_SEASHELL)

					setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWBRO))
					removeobject(VAR_LAST_TALKED)
					addobject(VAR_LAST_TALKED)
					playse(SE_M_BITE)
					applymovement(VAR_LAST_TALKED, SpindaIsland_Home_Movement_Slowpoke)
					applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
					waitmovement(VAR_LAST_TALKED)
					waitmovement(OBJ_EVENT_ID_PLAYER)

					playmoncry(SPECIES_SLOWPOKE, CRY_MODE_NORMAL)
					speakername("Slowbro")
					msgbox(format(
						"Bruh!!"
					))
					waitmoncry
					closemessage
					
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SHOCKED)
					speakername("Little Archie")
					msgbox(format(
						"The heck?!\p"
						"My seashell bit him!"
					))
					removefieldmugshot
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
					msgbox(format(
					"Guess it was a Shellder all this time!"
					))
					removefieldmugshot
					closemessage

					setflag(FLAG_SLOWPOKE_EVOLVED)
				case 1:
					playmoncry(SPECIES_SLOWPOKE, CRY_MODE_FAINT)
					speakername("Slowpoke")
					msgbox(format(
						"…Slo…\p"
						":("
					))
					waitmoncry
					closemessage
			}
			
		}
	}
	release
}

script SpindaIsland_Home_EventScript_Metapod {
	lock
	speakername("Metapod")
	playmoncry(SPECIES_METAPOD, CRY_MODE_NORMAL)
	msgbox(format(
		"…"
	))
	waitmoncry
	closemessage
	release
}

script SpindaIsland_Home_EventScript_MetapodBoysFuckOff {
	faceplayer
	speakername("Twin")
	msgbox(format(
		"What is it?!\p"
		"Oh, I guess we are blocking the stairs a little…\p"
		"Fine, we'll take our epic battle somewhere else."
	))
	closemessage

	fadescreen(FADE_TO_BLACK)
	setflag(FLAG_HIDE_METAPOD_BATTLE)
	special(DrawWholeMapView)
	delay(60)
	fadescreen(FADE_FROM_BLACK)
	delay(40)
	release
}

script SpindaIsland_Home_EventScript_MetapodBoy1 {
	lock
	if (var(VAR_METAPOD_OTHER_SIDE) == 1) {
		goto(SpindaIsland_Home_EventScript_MetapodBoysFuckOff)
	}
	speakername("Twin")
	msgbox(format(
		"I'm SO CLOSE to winning!"
	))
	closemessage

	faceplayer

	speakername("Twin")
	msgbox(format(
		"What? You're distracting me!"
	))
	closemessage

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	
	speakername("Twin")
	msgbox(format(
		"Metapod, use harden!!"
	))
	closemessage

	playse(SE_M_HARDEN)
	waitse
	playse(SE_M_HARDEN)
	waitse
	delay(48)
	release
}

script SpindaIsland_Home_EventScript_MetapodBoy2 {
	lock
	if (var(VAR_METAPOD_OTHER_SIDE) == 1) {
		goto(SpindaIsland_Home_EventScript_MetapodBoysFuckOff)
	}
	faceplayer
	speakername("Twin")
	msgbox(format(
		"Can't you see we're in the midst of a fiery battle?"
	))
	closemessage
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	speakername("Twin")
	msgbox(format(
		"Metapod, use harden!!"
	))
	closemessage

	playse(SE_M_HARDEN)
	waitse
	playse(SE_M_HARDEN)
	waitse
	delay(48)
	release
}

script SpindaIsland_Home_PhotoCameraWrongDir {
	lock

	if (flag(FLAG_CAMERA_EXPLODED)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Nothing left but a pile of ashes…"
		))
		closemessage
		removefieldmugshot
	} else {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Can't use that from this side."
		))
		closemessage
		removefieldmugshot
	}
    release
}

script SpindaIsland_Home_EventScript_FaucetShopSign {
	msgbox(format(
		"“FAUCET WORKSHOP\n"
		"For all your faucet needs!\l"
		"WE ARE OPEN!!”"
	), MSGBOX_SIGN)
}

movement SpindaIsland_Home_Movement_SadManExitsHouse {
	walk_down
}

movement SpindaIsland_Home_Movement_SadManReEntersHouse {
	face_up
	delay_16
	delay_16
}

movement SpindaIsland_Home_Movement_PlayerMakesWay {
	lock_facing_direction
	walk_down
	unlock_facing_direction
}

movement SpindaIsland_Home_Movement_Slowpoke {
	lock_facing_direction
	jump_in_place_down
	unlock_facing_direction
	delay_16 * 3
	face_up
	delay_16
	face_down
	delay_16
	face_left
	delay_16
	face_up
	delay_16
	face_player
	delay_16 * 3
	lock_facing_direction
	jump_in_place_down
	jump_in_place_down
	unlock_facing_direction
	emote_heart
	delay_16 * 3
}

script SpindaIsland_Home_SlowpokeMistress {
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_SLOWPOKE_REWARD)) {
		createfieldmugshot(MUGSHOT_LASS, EMOTE_HAPPY)
		speakername("Girl")
		msgbox(format(
			"Look at my super cool Slowbro!\p"
			"He just looks so happy!"
		))
		removefieldmugshot
		closemessage
	} elif (flag(FLAG_SLOWPOKE_EVOLVED)) {
		playse(SE_PIN)
		applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
		waitse
		waitmovement(0)
		delay(48)

		createfieldmugshot(MUGSHOT_LASS, EMOTE_SURPRISED)
		speakername("Girl")
		msgbox(format(
			"By Arceus!!\p"
			"How on earth did you convince him to evolve?"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_LASS, EMOTE_NORMAL)
		msgbox(format(
			"…A cool-looking seashell? What are you talking about?"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_LASS, EMOTE_HAPPY)
		msgbox(format(
			"Well, thank you anyway! You can have this!"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_LASS, EMOTE_NORMAL)
		msgbox(format(
			"I no longer need it."
		))
		removefieldmugshot
		closemessage

		giveitem(ITEM_WATER_STONE_2)

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_THINKING)
		speakername("Little Archie")
		msgbox(format(
			"Hmm… A {COLOR BLUE}WATER{COLOR DARK_GRAY} Stone…\p"
			"Maybe I could try giving this to mom?"
		))
		closemessage
		removefieldmugshot

		setflag(FLAG_RECEIVED_SLOWPOKE_REWARD)
	} else {
		createfieldmugshot(MUGSHOT_LASS, EMOTE_SWEAT)
		speakername("Girl")
		msgbox(format(
			"I wish my Pokémon would evolve…"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_LASS, EMOTE_NORMAL)
		msgbox(format(
			"I keep trying to give him this Water Stone, but it just does nothing.\p"
			"Perhaps there's another way…"
		))
		removefieldmugshot
		closemessage
	}
	release
}

script SpindaIsland_Home_CarBoy {
	lock
	faceplayer
	createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_NORMAL)
	speakername("Rudy")
	msgbox(format(
		"Vrrrbbbbbmmmmmmm!!!!!"
	))
	removefieldmugshot
	closemessage
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
}

script SpindaIsland_Home_CarGirl {
	lock
	if (flag(FLAG_RECEIVED_NICKEL)) {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_NORMAL)
		speakername("Katie")
		msgbox(format(
			"Thanks a bunch!"
		))
		removefieldmugshot
		closemessage
		
	} elif (flag(FLAG_REPLACED_WHEEL)) {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_NORMAL)
		speakername("Katie")
		msgbox(format(
			"Oh!\p"
			"You've got this super cool {COLOR RED}metal wheel{COLOR DARK_GRAY}…\p"
			"And you're just giving it to us?\p"
			"You're the best, Archie!!\p"
			"We don't have much, but please take this.\p"
			"It's my lucky coin! I hope it'll be lucky for you, too!"
		))
		removefieldmugshot
		closemessage

		giveitem(ITEM_RUSTY_NICKEL)
		setflag(FLAG_RECEIVED_NICKEL)
	} elif (flag(FLAG_LOOKING_FOR_WHEEL_REPLACEMENT)) {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_PENSIVE)
		speakername("Katie")
		msgbox(format(
			"Now what could we use as a wheel…"
		))
		removefieldmugshot
		closemessage
	} else {
		faceplayer
		createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_NORMAL)
		speakername("Katie")
		msgbox(format(
			"Hey Archie!\p"
			"As you can see we're almost done with our monster car.\p"
			"We're just missing one last wheel!\p"
			"We don't really need it to be an actual wheel, either.\p"
			"Anything round and spinny would do!"
		))
		removefieldmugshot
		closemessage

		setflag(FLAG_LOOKING_FOR_WHEEL_REPLACEMENT)
	}

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	
	release
}

script SpindaIsland_Home_EventScript_MissingWheel {
	lock
	if (flag(FLAG_REPLACED_WHEEL)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
		speakername("Little Archie")
		msgbox(format(
			"A shiny new wheel!"
		))
		removefieldmugshot
		closemessage
	} else {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"A wheel is missing.\p"
			"I'm sure I could find a replacement."
		))
		removefieldmugshot
		closemessage
	}
	release
}

script SpindaIsland_Home_EventScript_ReplaceWheelWithBrokenFaucet {
	lock

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	speakername("Little Archie")
	msgbox(format(
		"What if…"
	))
	removefieldmugshot
	closemessage

	delay(48)

	playse(SE_CLICK)
	setmetatile(27, 50, METATILE_SpindaIslandSecondary_FaucetWheel, TRUE)
	special(DrawWholeMapView)
	waitse

	delay(48)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
	speakername("Little Archie")
	msgbox(format(
		"Fits perfectly! There ya go!"
	))
	removefieldmugshot
	closemessage

	release
}

script SpindaIsland_Home_EventScript_SurveyedWheel {
	lock
	if (flag(FLAG_FAUCET_QUEST_STARTED) && !flag(FLAG_FOUND_TOUGH_BOLT)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Just a regular wheel."
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
		msgbox(format(
			"Wait, it's stuck to the car with bolts!\p"
			"If I could just take one…"
		))
		removefieldmugshot

		if (checkitem(ITEM_BORROWED_PLIERS)) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			msgbox(format(
				"Here, let me just…"
			))
			removefieldmugshot
			closemessage

			turnobject(LOCALID_CAR_GIRL, DIR_EAST)
			createfieldmugshot(MUGSHOT_LITTLE_GIRL, EMOTE_NORMAL)
			speakername("Katie")
			msgbox(format(
				"Whatcha doing, Archie?"
			))
			removefieldmugshot
			closemessage

			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
			speakername("Little Archie")
			msgbox(format(
				"Ah, uh, nothing!"
			))
			removefieldmugshot
			closemessage

			turnobject(LOCALID_CAR_GIRL, DIR_NORTH)
			delay(48)

			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			speakername("Little Archie")
			msgbox(format(
				"(Darn, can't do this right under her nose.)"
			))
			removefieldmugshot
			closemessage
		} else {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			msgbox(format(
				"I'd need some pliers to get it out."
			))
			removefieldmugshot
			closemessage
		}
	} else {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Just a regular wheel."
		))
		removefieldmugshot
		closemessage
	}
	release
}
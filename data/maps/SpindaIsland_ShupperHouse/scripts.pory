mapscripts SpindaIsland_ShupperHouse_MapScripts {
	
}

script SpindaIsland_ShupperHouse_Shuppet {
	lock
	playmoncry(SPECIES_SHUPPET, CRY_MODE_NORMAL)
	msgbox(format("Kekhekheheheâ€¦"))
	waitmoncry
	closemessage

	if (flag(FLAG_SHUPPET_QUEST_STARTED) && !flag(FLAG_TEMP_2)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"That's what the old lady must have been hearing!\p"
			"Maybe if I spook them, they'll go away?\p"
			"What are ghosts afraid of?"
		))
		closemessage
		removefieldmugshot

		setflag(FLAG_TEMP_2)
	}

	release
}

script SpindaIsland_ShupperHouse_ChaseShuppets {
	lock

    playse(SE_SWITCH)
    waitse
    playse(SE_M_LEER)
    fadescreenspeed(FADE_TO_WHITE, 0)
    fadescreenspeed(FADE_FROM_WHITE, 0)
    waitse

	applymovement(1, Common_Movement_ExclamationMark)
	playmoncry(SPECIES_SHUPPET, CRY_MODE_NORMAL)
	waitmoncry
	applymovement(2, Common_Movement_ExclamationMark)
	playmoncry(SPECIES_SHUPPET, CRY_MODE_NORMAL)
	waitmoncry
	waitmovement(0)

	removeobject(1)
	delay(20)
	removeobject(2)
	delay(20)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
	speakername("Little Archie")
	msgbox(format(
		"This takes care of that!"
	))
	closemessage
	removefieldmugshot

	setflag(FLAG_HIDE_SHUPPETS)

    release
}

script SpindaIsland_ShupperHouse_Blinds {
	lock

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
    speakername("Little Archie")
	if (flag(FLAG_HIDE_SHUPPETS)) {
		msgbox(format(
			"No need to open them now."
    	))
	} else {
		msgbox(format(
			"The blinds are shut. They won't budge."
		))
	}
    closemessage
	removefieldmugshot
    
    release
}

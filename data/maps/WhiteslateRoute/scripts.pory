const LOCALID_SECRET_BASE_MAN = 7
const LOCALID_SECRET_BASE_POKEMON = 8
const LOCALID_SHAMISEN_PLAYER = 23
const LOCALID_SHAMISEN_PLAYER_POKEMON = 24
const VAR_TEMP_SHAMISEN_TRIGGER = VAR_TEMP_2
const FLAG_HIDE_SHAMISEN = FLAG_TEMP_1

mapscripts WhiteslateRoute_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		getroutechallengestate(ROUTE_CHALLENGE_WHITESLATE_YIFU)
		if (var(VAR_RESULT) != ROUTE_CHALLENGE_STATE_INITIAL && var(VAR_RESULT) != ROUTE_CHALLENGE_STATE_EXPLAINED) {
			setvar(VAR_TEMP_SHAMISEN_TRIGGER, 1)
			setflag(FLAG_HIDE_SHAMISEN)
		}
	}
}

script WhiteslateRoute_Trigger_ShamisenPlayerTop {
	lockall
	
	call(WhiteslateRoute_EventScript_ShamisenCallsOut)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_up, delay_16 * 2))
	waitmovement(OBJ_EVENT_ID_PLAYER)

	if (getroutechallengestate(ROUTE_CHALLENGE_WHITESLATE_YIFU) == ROUTE_CHALLENGE_STATE_EXPLAINED) {
		call(WhiteslateRoute_EventScript_ShamisenChallengeAreYouReady)
	} else {
		call(WhiteslateRoute_EventScript_ShamisenChallenge)
	}

	releaseall
}

script WhiteslateRoute_Trigger_ShamisenPlayerBottom {
	lockall
	
	call(WhiteslateRoute_EventScript_ShamisenCallsOut)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_up, delay_16 * 2, walk_up))
	waitmovement(OBJ_EVENT_ID_PLAYER)

	if (getroutechallengestate(ROUTE_CHALLENGE_WHITESLATE_YIFU) == ROUTE_CHALLENGE_STATE_EXPLAINED) {
		call(WhiteslateRoute_EventScript_ShamisenChallengeAreYouReady)
	} else {
		call(WhiteslateRoute_EventScript_ShamisenChallenge)
	}

	releaseall
}

script WhiteslateRoute_EventScript_ShamisenChallengeAreYouReady {
	setspeaker(SP_NAME_SHAMISEN_PLAYER)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SHAMISEN_RED EMOTE_NORMAL}So, are you ready?"
	))

	call(WhiteslateRoute_EventScript_ShamisenPlayerChoice)
}

script WhiteslateRoute_EventScript_ShamisenChallenge {
	setspeaker(SP_NAME_SHAMISEN_PLAYER)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SHAMISEN_RED EMOTE_NORMAL}I am a traveling blind musician.\nI was arrested by the aura of a brave and promising young Wielder…\p"
		"Are you them? Why, you must be!\nThere's no other explanation!\p"
		"We Shamisen Players sing songs about brave souls we meet on our travels.\p"
		"To test your worth, how about I submit you to a little challenge?\p"
		"If you manage to reach the next city's Refuge in one go, without running back to the previous towns', I will meet you there and give you a little something.\p"
		"I know we only just met, but I believe you will succeed!\p"
		"I can let you prepare before considering this challenge as having begun, if you wish."
	))

	call(WhiteslateRoute_EventScript_ShamisenPlayerChoice)
}

script WhiteslateRoute_EventScript_ShamisenCallsOut {
	applymovement(LOCALID_SHAMISEN_PLAYER, moves(lock_facing_direction, walk_in_place_fast_down, unlock_facing_direction))
	waitmovement(LOCALID_SHAMISEN_PLAYER)

	setspeaker(SP_NAME_SHAMISEN_PLAYER)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SHAMISEN_RED EMOTE_NORMAL}… … …!\p"
		"Excuse me! Yes, you!"
	))
	closemessage
}

script WhiteslateRoute_EventScript_ShamisenPlayer {
	lock

	applymovement(LOCALID_SHAMISEN_PLAYER, moves(walk_in_place_down))
	waitmovement(LOCALID_SHAMISEN_PLAYER)

	setspeaker(SP_NAME_SHAMISEN_PLAYER)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SHAMISEN_RED EMOTE_NORMAL}I am a traveling blind musician.\nI was arrested by the aura of a brave and promising young Wielder…\p"
		"Are you them? Why, you must be!\nThere's no other explanation!\p"
		"We Shamisen Players sing songs about brave souls we meet on our travels.\p"
		"To test your worth, how about I submit you to a little challenge?\p"
		"If you manage to reach the next city's Refuge in one go, without running back to the previous towns', I will meet you there and give you a little something.\p"
		"I know we only just met, but I believe you will succeed!\p"
		"I can let you prepare before considering this challenge as having begun, if you wish."
	))

	call(WhiteslateRoute_EventScript_ShamisenPlayerChoice)

	release
}

script WhiteslateRoute_EventScript_ShamisenPlayerChoice {
	mcdialogue(
		"I'm ready!",
		"I need to prepare first."
	)
	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			setspeaker(SP_NAME_SHAMISEN_PLAYER)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SHAMISEN_RED EMOTE_NORMAL}Go forth, bold young soul!\nMay luck echo your steps!"
			))
			closemessage

			applymovement(LOCALID_SHAMISEN_PLAYER_POKEMON, moves(walk_left * 5, walk_down * 9))
			applymovement(LOCALID_SHAMISEN_PLAYER, moves(walk_left * 4, walk_down * 10))
			delay(16)
			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_left))
			waitmovement(LOCALID_SHAMISEN_PLAYER)
			waitmovement(LOCALID_SHAMISEN_PLAYER_POKEMON)
			removeobject(LOCALID_SHAMISEN_PLAYER)
			removeobject(LOCALID_SHAMISEN_PLAYER_POKEMON)
			
			setroutechallengestate(ROUTE_CHALLENGE_WHITESLATE_YIFU, ROUTE_CHALLENGE_STATE_STARTED)
			setvar(VAR_TEMP_SHAMISEN_TRIGGER, 1)
			setflag(FLAG_HIDE_SHAMISEN)
		case 1:
			setspeaker(SP_NAME_SHAMISEN_PLAYER)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SHAMISEN_RED EMOTE_NORMAL}Understood. Come back when you are done preparing."
			))
			closemessage

			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right))
			waitmovement(OBJ_EVENT_ID_PLAYER)

			setroutechallengestate(ROUTE_CHALLENGE_WHITESLATE_YIFU, ROUTE_CHALLENGE_STATE_EXPLAINED)
	}
}

script WhiteslateRoute_EventScript_ShamisenPlayerPokemon {
	lock
    faceplayer
	setseenmon(SPECIES_GROWLITHE)
	setspeakertomonname(SPECIES_GROWLITHE)
	playmoncry(SPECIES_GROWLITHE, CRY_MODE_NORMAL)
    msgbox(format(
        "Arf!"
    ))
    waitmoncry
    release
}

script WhiteslateRoute_EventScript_Itsumi {
	trainerbattle_single(
		TRAINER_ITSUMI,
		format("{CREATE_MUGSHOT MUGSHOT_BREEDER_F EMOTE_NORMAL}Do you nurture your Pokémon with love?"),
		format("Oh, you do!")
	)
	setspeaker(SP_NAME_BREEDER)
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_BREEDER_F EMOTE_NORMAL}I am glad we met! Your Pokémon seem so happy."), MSGBOX_AUTOCLOSE)
	end
}

script WhiteslateRoute_EventScript_Ryosuke {
	trainerbattle_single(
		TRAINER_RYOSUKE,
		format("{CREATE_MUGSHOT MUGSHOT_BREEDER_M EMOTE_NORMAL}I massage my Pokémon after each battle!"),
		format("Can somebody give me a massage?")
	)
	setspeaker(SP_NAME_BREEDER)
	// TODO EVA
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_BREEDER_M EMOTE_NORMAL}I heard there was a very talented masseur in Saberside Town.\pI want to see for myself!"), MSGBOX_AUTOCLOSE)
	end
}

script WhiteslateRoute_EventScript_Riku {
	trainerbattle_single(
		TRAINER_RIKU,
		format("{CREATE_MUGSHOT MUGSHOT_NINJA_BOY EMOTE_NORMAL}BOO!!\p… … …Are you surprised?"),
		format("My loyal Pokémon!")
	)
	setspeaker(SP_NAME_NINJA_BOY)
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_NINJA_BOY EMOTE_NORMAL}I have to work on my element of surprise."), MSGBOX_AUTOCLOSE)
	end
}

script WhiteslateRoute_EventScript_Tomomi {
	trainerbattle_single(
		TRAINER_TOMOMI,
		format("Attack, my loyal servants!"),
		format("Retreat! Retreat!")
	)
	setspeaker(SP_NAME_BEAUTY)
	msgbox(format("Deer Pokémon are my very favorite. Such strength! Such elegance!"), MSGBOX_AUTOCLOSE)
	end
}

script WhiteslateRoute_EventScript_Yono {
	trainerbattle_single(
		TRAINER_YONO,
		format("Huff… Puff… I walk several hours everyday!"),
		format("Let an old woman catch her breath!…")
	)
	setspeaker(SP_NAME_OLD_LADY)
	msgbox(format("Walking is good for the joints. It keeps me young!"), MSGBOX_AUTOCLOSE)
	end
}

script WhiteslateRoute_EventScript_Sumi {
	trainerbattle_single(
		TRAINER_SUMI,
		format("{CREATE_MUGSHOT MUGSHOT_HEX_MANIAC EMOTE_NORMAL}Excuse me… What's with that white hand on your shoulder?"),
		format("Heeeeeee!")
	)
	setspeaker(SP_NAME_HEX_MANIAC)
	applymovement(VAR_LAST_TALKED, moves(emote_angry))
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_HEX_MANIAC EMOTE_NORMAL}You don't scare easily, do you?"), MSGBOX_AUTOCLOSE)
	end
}

script WhiteslateRoute_EventScript_Tomiko {
	trainerbattle_single(
		TRAINER_TOMIKO,
		format("{CREATE_MUGSHOT MUGSHOT_LASS EMOTE_NORMAL}Weird Pokémon are my favorite!"),
		format("I don't care if they're not the strongest!")
	)

	setspeakertomonname(SPECIES_SPINDA)
	setspeaker(SP_NAME_LASS)
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_LASS EMOTE_NORMAL}I love Spinda. Doesn't it look silly and adorable?"), MSGBOX_AUTOCLOSE)
	end
}

script WhiteslateRoute_EventScript_Utaro {
	trainerbattle_single(
		TRAINER_UTARO,
		format("{CREATE_MUGSHOT MUGSHOT_YOUNGSTER EMOTE_NORMAL}I love cool Pokémon!"),
		format("At least I lost with style!")
	)
	setspeakertomonname(SPECIES_SEVIPER)
	setspeaker(SP_NAME_YOUNGSTER)
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_YOUNGSTER EMOTE_NORMAL}Seviper is so darn cool! I mean, it's got a sword on its tail!"), MSGBOX_AUTOCLOSE)
	end
}

script WhiteslateRoute_EventScript_ShuckleGirl {
	setnamedmon(SPECIES_SHUCKLE)
	setspeaker(SP_NAME_WOMAN)
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_UUME_GIRL_1 EMOTE_NORMAL}This Shuckle lives around here. I feed him fresh berries every morning!"), MSGBOX_AUTOCLOSE)
	end
}

script WhiteslateRoute_EventScript_Deerling {
	lock
	faceplayer
	setseenmon(SPECIES_DEERLING_AUTUMN)
	setspeakertomonname(SPECIES_DEERLING_AUTUMN)
	playmoncry(SPECIES_DEERLING_AUTUMN, CRY_MODE_NORMAL)
	msgbox(format(
		"Uhnn!"
	))
	waitmoncry
	closemessage
	release
}

script WhiteslateRoute_EventScript_Shuckle {
	lock
	faceplayer
	setseenmon(SPECIES_SHUCKLE)
	setspeakertomonname(SPECIES_SHUCKLE)
	playmoncry(SPECIES_SHUCKLE, CRY_MODE_NORMAL)
	msgbox(format(
		"Cklckl!"
	))
	waitmoncry
	closemessage

	// Gives you berry juice everyday
	if (!flag(FLAG_DAILY_SHUCKLE_BERRY_JUICE)) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_question_mark))
		msgbox(format(
			"…Oh? The Pokémon is giving you something."
		))
		closemessage

		giveitem(ITEM_BERRY_JUICE)

		setflag(FLAG_DAILY_SHUCKLE_BERRY_JUICE)
	}
	release
}

script WhiteslateRoute_Trigger_SecretBase {
	lockall
	setspeaker(SP_NAME_MAN)
	msgbox(format(
		"Hehe… Looks like the perfect spot!"
	))
	closemessage

	applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_right, face_left))
	waitmovement(LOCALID_SECRET_BASE_MAN)

	delay(24)
	
	playse(SE_BALL_OPEN)
	applymovement(LOCALID_SECRET_BASE_POKEMON, moves(exit_pokeball, face_up))
	waitmovement(LOCALID_SECRET_BASE_POKEMON)
	waitse
	playmoncry(SPECIES_GROVYLE, CRY_MODE_NORMAL)
	applymovement(LOCALID_SECRET_BASE_POKEMON, moves(
		jump_in_place_up,
		delay_16 * 2
	))
	waitmovement(LOCALID_SECRET_BASE_POKEMON)

	setseenmon(SPECIES_GROVYLE)
	setnamedmon(SPECIES_GROVYLE)

	playse(SE_M_ROCK_THROW)
	
	applymovement(LOCALID_SECRET_BASE_POKEMON, moves(
		lock_anim,
		lock_facing_direction,
		walk_faster_up,
		walk_faster_down,
		unlock_anim,
		unlock_facing_direction
	))
	waitmovement(LOCALID_SECRET_BASE_POKEMON)
	setmetatile(26, 34, METATILE_PorytilesPrimaryTutorial_CaveEntrance, TRUE)
	special(DrawWholeMapView)
	waitse

	setspeaker(SP_NAME_MAN)
	msgbox(format(
		"Good job, Grovyle!"
	))
	closemessage

	playmoncry(SPECIES_GROVYLE, CRY_MODE_NORMAL)
	applymovement(LOCALID_SECRET_BASE_POKEMON, moves(
		jump_in_place_right,
		delay_16 * 2,
		enter_pokeball
	))
	waitmovement(LOCALID_SECRET_BASE_POKEMON)
	playse(SE_BALL_OPEN)
	waitmoncry
	waitse

	delay(24)

	playse(SE_PIN)
	applymovement(LOCALID_SECRET_BASE_MAN, moves(face_down, emote_exclamation_mark))
	waitmovement(LOCALID_SECRET_BASE_MAN)
	waitse
	delay(48)

	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	switch(var(VAR_TEMP_0)) {
		case 25:
			applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_down * 3, walk_left))
			waitmovement(LOCALID_SECRET_BASE_MAN)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
		case 26:
			applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_down * 2, walk_left, face_down))
			waitmovement(LOCALID_SECRET_BASE_MAN)
		case 27:
			applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_down * 2))
			waitmovement(LOCALID_SECRET_BASE_MAN)
	}

	applymovement(LOCALID_SECRET_BASE_MAN, moves(emote_sad))
	setspeaker(SP_NAME_MAN)
	msgbox(format(
		"Oh, man! You were here this whole time?\NMy secret base is no longer secret!"
	))
	
	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"What were you doing?",
		"Sorry for interrupting!",
		"Are you a criminal?"
	)
	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			setspeaker(SP_NAME_MAN)
			msgbox(format(
				"Alright, you seem interested, so I'll let you in on the secret!\p"
				"My Grovyle knows a technique to open passages to hidden natural caves!\p"
				"Not only that! Trees with low-hanging vines, bushes…\p"
				"Those kinds of places are perfect to build a secret base!\p"
				"I wrote the technique down just in case. Here, teach your Pokémon too!"
			))
		case 1:
			setspeaker(SP_NAME_MAN)
			msgbox(format(
				"Ah, it's alright.\nYou seem like a good kid.\p"
				"I'm sure you have what it takes to become a secret base master.\p"
				"I'll let you in on the secret!\p"
				"My Grovyle knows a technique to open passages to hidden natural caves!\p"
				"Not only that! Trees with low-hanging vines, bushes…\p"
				"That kind of place is perfect to build a secret base!\p"
				"I wrote the technique down just in case. Here, teach your Pokémon too!"
			))
		case 2:
			setspeaker(SP_NAME_MAN)
			msgbox(format(
				"Of course not, this is perfectly legal! You know what?\lI'll let you in on the secret!\p"
				"My Grovyle knows a technique to open passages to hidden natural caves!\p"
				"Not only that! Trees with low-hanging vines, bushes…\p"
				"That kind of place is perfect to build a secret base!\p"
				"I wrote the technique down just in case. Here, teach your Pokémon too!"
			))
	}
	closemessage

	giveitem(ITEM_TM_SECRET_POWER)

	setspeaker(SP_NAME_MAN)
	msgbox(format(
		"I'll keep looking for the perfect secret base spot!\NMaybe I'll see you around!"
	))
	closemessage

	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)

	switch(var(VAR_TEMP_0)) {
		case 25:
			applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_up * 3))
			waitmovement(LOCALID_SECRET_BASE_MAN)
		case 26:
			applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_up * 2))
			waitmovement(LOCALID_SECRET_BASE_MAN)
		case 27:
			applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_left, walk_up * 2))
			waitmovement(LOCALID_SECRET_BASE_MAN)
	}

	playse(SE_M_ROCK_THROW)
	setmetatile(26, 34, METATILE_PorytilesPrimaryTutorial_SecretBaseCaveIndentBrown, TRUE)
	special(DrawWholeMapView)
	waitse

	switch(var(VAR_TEMP_0)) {
		case 25:
			applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_down * 10))
			waitmovement(LOCALID_SECRET_BASE_MAN)
		case 26:
			applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_down * 2, walk_left, walk_down * 8))
			waitmovement(LOCALID_SECRET_BASE_MAN)
		case 27:
			applymovement(LOCALID_SECRET_BASE_MAN, moves(walk_down * 10))
			waitmovement(LOCALID_SECRET_BASE_MAN)
	}
	removeobject(LOCALID_SECRET_BASE_MAN)
	removeobject(LOCALID_SECRET_BASE_POKEMON)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)

	setflag(FLAG_RECEIVED_SECRET_POWER)

	releaseall
}

script WhiteslateRoute_EventScript_MetHariko {
	lock
	faceplayer
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, moves(emote_sweat))
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_2 EMOTE_NORMAL}{SPEAKER NAME_MAN}Some girl with a Slowpoke just talked my head off about Deerling…\l"
		"I'm still dizzy!\p"
		"Did you know its location determines its fur color?\p"
		"Did you know Uume Forest Deerling are orange and smell like ripe fruit?\p"
		"Most importantly, do you care?\nYeah, I'm not sure I do either…"
	))
	closemessage
	setnamedmon(SPECIES_SLOWPOKE)
	setnamedmon(SPECIES_DEERLING_AUTUMN)
	setdescribedmon(SPECIES_DEERLING_AUTUMN)
	release
}
const FLAG_HIDE_KRABBY = FLAG_TEMP_1

mapscripts SunriseCave_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (var(VAR_TEMP_0) != 0) {
			setflag(FLAG_HIDE_KRABBY)
		}
	}
}

script SunriseCave_Trigger_ScaringKrabby {
	lockall

	setseenmon(SPECIES_KRABBY)
	
	setvar(VAR_TEMP_0, 1)

	playmoncry(SPECIES_KRABBY, CRY_MODE_NORMAL)
	applymovement(LOCALID_SUNRISE_KRABBY_1, moves(emote_exclamation_mark, delay_16 * 3, walk_fast_up))
	delay(16)
	playmoncry(SPECIES_KRABBY, CRY_MODE_NORMAL)
	applymovement(LOCALID_SUNRISE_KRABBY_2, moves(emote_exclamation_mark, delay_16 * 3, walk_fast_up * 2))
	// SE_BALL? SE_M_DIVE?

	waitmovement(LOCALID_SUNRISE_KRABBY_1)
	playse(SE_LEDGE)
	applymovement(LOCALID_SUNRISE_KRABBY_1, moves(jump_up, set_invisible))
	waitse
	playse(SE_BALL)
	
	waitmovement(LOCALID_SUNRISE_KRABBY_2)
	playse(SE_LEDGE)
	applymovement(LOCALID_SUNRISE_KRABBY_2, moves(jump_up, set_invisible))
	waitse
	playse(SE_BALL)

	waitmovement(LOCALID_SUNRISE_KRABBY_1)
	waitmovement(LOCALID_SUNRISE_KRABBY_2)

	removeobject(LOCALID_SUNRISE_KRABBY_1)
	removeobject(LOCALID_SUNRISE_KRABBY_2)
	setflag(FLAG_HIDE_KRABBY)

	releaseall
}
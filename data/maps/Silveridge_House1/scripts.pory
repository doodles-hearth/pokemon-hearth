const FLAG_HIDE_FIREBRINGER_FANGIRL = FLAG_TEMP_1

mapscripts Silveridge_House1_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
		getweekday
		switch(var(VAR_RESULT)) {
			case WEEKDAY_TUE:
			case WEEKDAY_THU:
			case WEEKDAY_SAT:
				setflag(FLAG_HIDE_FIREBRINGER_FANGIRL)
		}
	}
}

script Silveridge_House1_EventScript_Woman {
    lock
    faceplayer
    setspeaker("Woman")
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_MOUNTAIN_WOMAN_1 EMOTE_NORMAL}Oh, you came all the way from Kura Village? You have good legs!"
    ))
    closemessage
    release
}

script Silveridge_House1_EventScript_FirebringerFangirl {
    lock
    faceplayer
    setspeaker("Girl")
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_MOUNTAIN_GIRL_1 EMOTE_NORMAL}This place gets pretty cold, especially when the winds blow strong into the valley.\p"
        "I don't know what we would do without those brave Firebringers.\p"
        "They bring us live Slugma, carrying them on their back all the way down Mt Kazan!\p"
        "Slugma cools and hardens up pretty fast, so they basically run the whole way down! I could never!\p"
        "Sigh… I hope a handsome Firebringer notices me one day…"
    ))
    closemessage
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)

    setnamedmon(SPECIES_SLUGMA)
    setdescribedmon(SPECIES_SLUGMA)

    release
}

script Silveridge_House1_EventScript_Sandslash {
    lock
    faceplayer
    setseenmon(SPECIES_SANDSLASH)
    playmoncry(SPECIES_SANDSLASH, CRY_MODE_NORMAL)
    setspeaker("Sandslash")
    msgbox(format(
        "Sahaaah!"
    ))
    waitmoncry
    closemessage
    release
}

script Silveridge_House1_EventScript_Man {
    lock
    faceplayer
    setspeaker("Miner")
    if (flag(FLAG_OBTAINED_EVERSTONE)) {
        msgbox(format(
            "You think some Pokémon prefer to remain small forever?"
        ))
        closemessage
    } else {
        msgbox(format(
            "My job is to mine ore in Silver Tunnel, all day long.\p"
            "I love it! I wouldn't trade it for the world. I'm fine with being a simple miner forever.\p"
            "Speaking of… I often find interesting rocks in there. Look at this!"
        ))
        closemessage

        giveitem(ITEM_EVERSTONE)
        setflag(FLAG_OBTAINED_EVERSTONE)

        setspeaker("Miner")
        msgbox(format(
            "Apparently as long as a Pokémon holds on to this stone, it'll stop growing.\p"
            "My helper Sandslash is already fully grown, so I don't need it."
        ))
        closemessage

        setnamedmon(SPECIES_SANDSLASH)
    }
    release
}
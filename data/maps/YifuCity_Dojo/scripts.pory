mapscripts YifuCity_Dojo_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		if (var(VAR_YIFU_DOJO_STATE) >= 1) {
			call(YifuCity_Dojo_SetDoor1Metatiles)
		}
		if (var(VAR_YIFU_DOJO_STATE) >= 2) {
			call(YifuCity_Dojo_SetDoor2Metatiles)
		}
		if (var(VAR_YIFU_DOJO_STATE) >= 3) {
			call(YifuCity_Dojo_SetDoor3Metatiles)
		}
		if (flag(FLAG_BADGE03_GET)) {
			call(YifuCity_Dojo_SetShortcutMetatiles)
		}
	}
    MAP_SCRIPT_ON_TRANSITION: YifuCity_Dojo_SetupNpcs
}

script YifuCity_Dojo_SetupNpcs {
    if (var(VAR_YIFU_DOJO_STATE) >= 1) {
        setobjectxyperm(LOCALID_YIFU_GEISHA_2, 15, 33)
        setobjectxyperm(LOCALID_YIFU_GEISHA_3, 18, 33)
        removeobject(LOCALID_YIFU_GEISHA_1)
    }
    if (var(VAR_YIFU_DOJO_STATE) >= 2) {
        setobjectxyperm(LOCALID_YIFU_GEISHA_4, 42, 33)
        setobjectxyperm(LOCALID_YIFU_GEISHA_6, 45, 33)
    }
    if (var(VAR_YIFU_DOJO_STATE) >= 3) {
        setobjectxyperm(LOCALID_YIFU_GEISHA_4, 42, 33)
        setobjectxyperm(LOCALID_YIFU_GEISHA_6, 45, 33)
    }
}

script YifuCity_Dojo_EventScript_Dance2_FromUp {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 5, face_up, delay_16))
    waitmovement(0)
    goto(YifuCity_Dojo_EventScript_Dance2)
}

script YifuCity_Dojo_EventScript_Dance2_FromDown {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 5, walk_up, delay_16))
    waitmovement(0)
    goto(YifuCity_Dojo_EventScript_Dance2)
}

script YifuCity_Dojo_EventScript_Dance3_FromUp {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 6, walk_up, delay_16))
    waitmovement(0)
    goto(YifuCity_Dojo_EventScript_Dance3)
}

script YifuCity_Dojo_EventScript_Dance3_FromDown {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 6, walk_up * 2, delay_16))
    waitmovement(0)
    goto(YifuCity_Dojo_EventScript_Dance3)
}

script YifuCity_Dojo_EventScript_Dance1_FromUp {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 5, face_up, delay_16))
    waitmovement(0)
    goto(YifuCity_Dojo_EventScript_Dance1)
}

script YifuCity_Dojo_EventScript_Dance1_FromDown {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 5, walk_up, delay_16))
    waitmovement(0)
    goto(YifuCity_Dojo_EventScript_Dance1)
}

script YifuCity_Dojo_EventScript_Dance1 {
    setspeaker(SP_NAME_KIMONO_GIRLS)
    msgbox((
        "Welcome to the Yifu City Dojo.\p"
        "You will have the pleasure to watch\nthree different dances, at the end of\lwhich you will be asked a question.\p"
        "The first dance will soon commence!\nSee the dancer on the left.\lKeep your eyes on her!\p"
        "If you know where she is by the end of\nour dance, we shall let you through.\p"
        "If you guess wrong, you will have to\nbattle us.\lAre you ready? One, two…"
    ))
    closemessage

    special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 2, delay_16 * 2))
	waitmovement(0)

    call(YifuCity_Dojo_Dance1)

    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 2, delay_16 * 2))
	waitmovement(0)
    special(RemoveCameraObject)

    setspeaker(SP_NAME_KIMONO_GIRLS)
    msgbox(format(
        "Well? Which dancer is the one who was originally on the left?"
    ))

    dynmultichoice(
        0,
        0,
        TRUE,
        10,
        0,
        DYN_MULTICHOICE_CB_NONE,
        "The left one.",
        "The middle one.",
        "The right one."
    )
    closemessage

    call(YifuCity_Dojo_Dance1_Jump)

    if (var(VAR_RESULT) == 1) {
        playse(SE_SUCCESS)

        setspeaker(SP_NAME_KIMONO_GIRLS)
        msgbox(format(
            "You are correct!\p"
            "Very well.\nYou may proceed to the next room."
        ))
        closemessage

        setvar(VAR_YIFU_DOJO_STATE, 1)

        call(YifuCity_Dojo_Dance1_GeishasGoDirectlyToDoor)
    } else {
        playse(SE_FAILURE)

        setspeaker(SP_NAME_KIMONO_GIRLS)
        msgbox(format(
            "You are incorrect!\p"
            "We shall face you in battle before you proceed."
        ))
        closemessage

        call(YifuCity_Dojo_Dance1_GeishasSurroundPlayerToBattle)

        trainerbattle_two_trainers(
            TRAINER_AKARI,
            format("Oh!"),
            TRAINER_YUKA,
            format("I see.")
        )

        setvar(VAR_YIFU_DOJO_STATE, 1)

        setspeaker(SP_NAME_KIMONO_GIRLS)
        msgbox(format(
            "Very well! You may now proceed."
        ))
        closemessage

        // They walk up to the door
        applymovement(LOCALID_YIFU_GEISHA_2, moves(
            walk_down
            walk_right * 7
            walk_up
            face_right
        ))
        applymovement(LOCALID_YIFU_GEISHA_3, moves(
            walk_right * 8
            face_left
        ))

        // Camera follows
        special(SpawnCameraObject)
        applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right * 4))

        waitmovement(LOCALID_YIFU_GEISHA_2)
        waitmovement(LOCALID_YIFU_GEISHA_3)
	    waitmovement(OBJ_EVENT_ID_CAMERA)
    }

    delay(32)

    // Door appears
    call(YifuCity_Dojo_SetDoor1Metatiles)

    call(YifuCity_Dojo_DoorAppears_Screenshake)

    applymovement(LOCALID_YIFU_GEISHA_2, Common_Movement_WalkInPlaceFasterDown)
    applymovement(LOCALID_YIFU_GEISHA_3, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(LOCALID_YIFU_GEISHA_2)
    waitmovement(LOCALID_YIFU_GEISHA_3)
    waitse

    delay(32)
    
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left * 4))
    waitmovement(OBJ_EVENT_ID_CAMERA)
    special(RemoveCameraObject)

    release
}

script YifuCity_Dojo_EventScript_Dance2 {
    setspeaker(SP_NAME_KIMONO_GIRLS)
    msgbox((
        "This time, we won't tell you which\ndancer you need to keep track of.\p"
        "Are you ready for the second dance?\nFollow each of our moves…\lOne, two, three!"
    ))
    closemessage

    special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 2, delay_16 * 2))
	waitmovement(0)

    call(YifuCity_Dojo_Dance2)

    delay(48)

    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 2, delay_16 * 2))
	waitmovement(0)
    special(RemoveCameraObject)

    setspeaker(SP_NAME_KIMONO_GIRLS)
    msgbox(format(
        "The dancer on the right…\nWhere was she originally?"
    ))

    mcdialogue(
        "She was on the left.",
        "She was in the middle.",
        "She was on the right."
    )
    closemessage

    call(YifuCity_Dojo_Dance2_Jump)

    if (var(VAR_RESULT) == 2) {
        playse(SE_SUCCESS)

        setspeaker(SP_NAME_KIMONO_GIRLS)
        msgbox(format(
            "You are correct!\p"
            "Very well.\nYou may proceed to the next room."
        ))
        closemessage

        setvar(VAR_YIFU_DOJO_STATE, 2)

        call(YifuCity_Dojo_Dance2_GeishasGoDirectlyToDoor)
    } else {
        playse(SE_FAILURE)

        setspeaker(SP_NAME_KIMONO_GIRLS)
        msgbox(format(
            "You are incorrect!\p"
            "We shall face you in battle before you proceed."
        ))
        closemessage

        call(YifuCity_Dojo_Dance2_GeishasSurroundPlayerToBattle)

        trainerbattle_two_trainers(
            TRAINER_KARIN,
            format("I liked the dancing part better…"),
            TRAINER_MIIKA,
            format("Oh, well.")
        )

        setvar(VAR_YIFU_DOJO_STATE, 2)

        setspeaker(SP_NAME_KIMONO_GIRLS)
        msgbox(format(
            "Very well! You may now proceed."
        ))
        closemessage

        // They walk up to the door
        applymovement(LOCALID_YIFU_GEISHA_5, moves(
            walk_down
            walk_right * 7
            walk_up
            face_right
        ))
        applymovement(LOCALID_YIFU_GEISHA_6, moves(
            walk_right * 8
            face_left
        ))

        // Camera follows
        special(SpawnCameraObject)
        applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right * 4))

        waitmovement(LOCALID_YIFU_GEISHA_5)
        waitmovement(LOCALID_YIFU_GEISHA_6)
	    waitmovement(OBJ_EVENT_ID_CAMERA)
    }

    delay(32)

    // Door appears
    call(YifuCity_Dojo_SetDoor2Metatiles)

    call(YifuCity_Dojo_DoorAppears_Screenshake)

    applymovement(LOCALID_YIFU_GEISHA_5, Common_Movement_WalkInPlaceFasterDown)
    applymovement(LOCALID_YIFU_GEISHA_6, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(LOCALID_YIFU_GEISHA_5)
    waitmovement(LOCALID_YIFU_GEISHA_6)
    waitse

    delay(32)
    
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left * 4))
    waitmovement(OBJ_EVENT_ID_CAMERA)
    special(RemoveCameraObject)

    release
}

script YifuCity_Dojo_EventScript_Dance3 {
    setspeaker(SP_NAME_KIMONO_GIRLS)
    msgbox((
        "For the third and final dance, we won't\nask you about a dancer's position.\l"
        "Just pay attention to our actions!\p"
        "One, two, three, four."
    ))
    closemessage

    special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 2, delay_16 * 2))
	waitmovement(0)

    call(YifuCity_Dojo_Dance3)

    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 2, delay_16 * 2))
	waitmovement(0)
    special(RemoveCameraObject)

    setspeaker(SP_NAME_KIMONO_GIRLS)
    msgbox(format(
        "Can you tell us how many jumps there were in this dance?"
    ))

    dynmultichoice(
        0,
        0,
        TRUE,
        10,
        0,
        DYN_MULTICHOICE_CB_NONE,
        "Five.",
        "Six.",
        "Seven."
    )
    closemessage

    call(YifuCity_Dojo_Dance3_Jump)

    if (var(VAR_RESULT) == 0) {
        playse(SE_SUCCESS)

        setspeaker(SP_NAME_KIMONO_GIRLS)
        msgbox(format(
            "You are correct!\p"
            "Very well.\nYou may proceed to the last room."
        ))
        closemessage

        setvar(VAR_YIFU_DOJO_STATE, 3)

        call(YifuCity_Dojo_Dance3_GeishasGoDirectlyToDoor)
    } else {
        playse(SE_FAILURE)

        setspeaker(SP_NAME_KIMONO_GIRLS)
        msgbox(format(
            "You are incorrect!\p"
            "We shall face you in battle before you proceed."
        ))
        closemessage

        call(YifuCity_Dojo_Dance3_GeishasSurroundPlayerToBattle)

        trainerbattle_two_trainers(
            TRAINER_NANAMI,
            format("Pant… Hey, let me catch my breath!"),
            TRAINER_KANAE,
            format("You didn't see me trip over my own foot, did you?")
        )

        setvar(VAR_YIFU_DOJO_STATE, 3)

        setspeaker(SP_NAME_KIMONO_GIRLS)
        msgbox(format(
            "Very well! You may now proceed."
        ))
        closemessage

        // They walk up to the door
        applymovement(LOCALID_YIFU_GEISHA_7, moves(walk_up, face_left))
        applymovement(LOCALID_YIFU_GEISHA_10, moves(walk_up, face_right))
        playse(SE_LEDGE)
        applymovement(LOCALID_YIFU_GEISHA_9, moves(
            jump_2_up,
            walk_up,
            face_right
        ))
        applymovement(LOCALID_YIFU_GEISHA_8, moves(
            jump_2_up,
            walk_right,
            walk_up,
            face_left
        ))

        // Camera follows
        special(SpawnCameraObject)
        applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 2))

        waitmovement(LOCALID_YIFU_GEISHA_10)
        applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 2))
	    waitmovement(OBJ_EVENT_ID_CAMERA)
    }

    delay(32)

    // Door appears
    call(YifuCity_Dojo_SetDoor3Metatiles)

    call(YifuCity_Dojo_DoorAppears_Screenshake)

    applymovement(LOCALID_YIFU_GEISHA_4, Common_Movement_WalkInPlaceFasterDown)
    applymovement(LOCALID_YIFU_GEISHA_6, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(LOCALID_YIFU_GEISHA_4)
    waitmovement(LOCALID_YIFU_GEISHA_6)
    waitse

    delay(32)
    
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 2))
    waitmovement(OBJ_EVENT_ID_CAMERA)
    special(RemoveCameraObject)

    release
}

script YifuCity_Dojo_Dance1_GeishasSurroundPlayerToBattle {
    applymovement(LOCALID_YIFU_GEISHA_1, Common_Movement_TeleportAway)
    applymovement(LOCALID_YIFU_GEISHA_2, moves(walk_down))
    applymovement(LOCALID_YIFU_GEISHA_3, moves(walk_down))
    waitmovement(LOCALID_YIFU_GEISHA_2)
    waitmovement(LOCALID_YIFU_GEISHA_3)

    playse(SE_LEDGE)

    applymovement(LOCALID_YIFU_GEISHA_2, moves(jump_2_down, delay_16, walk_right))
    applymovement(LOCALID_YIFU_GEISHA_3, moves(jump_2_down, delay_16, walk_left))
    waitmovement(LOCALID_YIFU_GEISHA_2)
    waitmovement(LOCALID_YIFU_GEISHA_3)
}

script YifuCity_Dojo_Dance2_GeishasSurroundPlayerToBattle {
    applymovement(LOCALID_YIFU_GEISHA_5, moves(walk_down))
    applymovement(LOCALID_YIFU_GEISHA_6, moves(walk_down))
    waitmovement(LOCALID_YIFU_GEISHA_5)
    waitmovement(LOCALID_YIFU_GEISHA_6)

    playse(SE_LEDGE)

    applymovement(LOCALID_YIFU_GEISHA_5, moves(jump_2_down, delay_16, walk_right))
    applymovement(LOCALID_YIFU_GEISHA_6, moves(jump_2_down, delay_16, walk_left))
    waitmovement(LOCALID_YIFU_GEISHA_5)
    waitmovement(LOCALID_YIFU_GEISHA_6)
}

script YifuCity_Dojo_Dance3_GeishasSurroundPlayerToBattle {
    applymovement(LOCALID_YIFU_GEISHA_7, moves(walk_left * 2, face_down))
    applymovement(LOCALID_YIFU_GEISHA_8, moves(walk_down))
    applymovement(LOCALID_YIFU_GEISHA_9, moves(walk_down))
    applymovement(LOCALID_YIFU_GEISHA_10, moves(walk_right * 2, face_down))
    waitmovement(LOCALID_YIFU_GEISHA_7)
    waitmovement(LOCALID_YIFU_GEISHA_8)
    waitmovement(LOCALID_YIFU_GEISHA_9)
    waitmovement(LOCALID_YIFU_GEISHA_10)

    playse(SE_LEDGE)

    applymovement(LOCALID_YIFU_GEISHA_8, moves(jump_2_down, delay_16, walk_left))
    applymovement(LOCALID_YIFU_GEISHA_9, moves(jump_2_down, delay_16, face_right))
    waitmovement(LOCALID_YIFU_GEISHA_8)
    waitmovement(LOCALID_YIFU_GEISHA_9)
}

script YifuCity_Dojo_Dance1_GeishasGoDirectlyToDoor {
    applymovement(LOCALID_YIFU_GEISHA_1, Common_Movement_TeleportAway)
    applymovement(LOCALID_YIFU_GEISHA_2, moves(walk_down))
    applymovement(LOCALID_YIFU_GEISHA_3, moves(walk_down))
    waitmovement(LOCALID_YIFU_GEISHA_2)
    waitmovement(LOCALID_YIFU_GEISHA_3)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right * 4))

    playse(SE_LEDGE)
    applymovement(LOCALID_YIFU_GEISHA_2, moves(jump_2_down, delay_16, walk_down, walk_right * 8, walk_up, face_right))
    applymovement(LOCALID_YIFU_GEISHA_3, moves(jump_2_down, delay_16, walk_right * 7, face_left))
    waitmovement(LOCALID_YIFU_GEISHA_2)
    waitmovement(LOCALID_YIFU_GEISHA_3)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    waitse
}

script YifuCity_Dojo_Dance2_GeishasGoDirectlyToDoor {
    applymovement(LOCALID_YIFU_GEISHA_5, moves(walk_down))
    applymovement(LOCALID_YIFU_GEISHA_6, moves(walk_down))
    waitmovement(LOCALID_YIFU_GEISHA_5)
    waitmovement(LOCALID_YIFU_GEISHA_6)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right * 4))

    playse(SE_LEDGE)
    applymovement(LOCALID_YIFU_GEISHA_5, moves(jump_2_down, delay_16, walk_down, walk_right * 8, walk_up, face_right))
    applymovement(LOCALID_YIFU_GEISHA_6, moves(jump_2_down, delay_16, walk_right * 7, face_left))
    waitmovement(LOCALID_YIFU_GEISHA_5)
    waitmovement(LOCALID_YIFU_GEISHA_6)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    waitse
}

script YifuCity_Dojo_Dance3_GeishasGoDirectlyToDoor {
    applymovement(LOCALID_YIFU_GEISHA_8, moves(walk_up, face_left))
    applymovement(LOCALID_YIFU_GEISHA_9, moves(walk_up, face_right))
    applymovement(LOCALID_YIFU_GEISHA_7, moves(walk_left * 2))
    applymovement(LOCALID_YIFU_GEISHA_10, moves(walk_right * 2))
    waitmovement(LOCALID_YIFU_GEISHA_7)
    waitmovement(LOCALID_YIFU_GEISHA_8)
    waitmovement(LOCALID_YIFU_GEISHA_9)
    waitmovement(LOCALID_YIFU_GEISHA_10)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 2))
    waitmovement(OBJ_EVENT_ID_CAMERA)
    waitse
}

script YifuCity_Dojo_DoorAppears_Screenshake {
    setvar(VAR_0x8004, 1)  // vertical pan
    setvar(VAR_0x8005, 1)  // horizontal pan
    setvar(VAR_0x8006, 2)  // num shakes
    setvar(VAR_0x8007, 4)  // shake delay
    special(ShakeCamera)
    playse(SE_UNLOCK)
}

script YifuCity_Dojo_SetDoor1Metatiles {
    setmetatile(16, 31, METATILE_DojoYifu_DoorTopLeft, TRUE)
    setmetatile(17, 31, METATILE_DojoYifu_DoorTopRight, TRUE)
    setmetatile(16, 32, METATILE_DojoYifu_DoorBottomLeft, TRUE)
    setmetatile(17, 32, METATILE_DojoYifu_DoorBottomRight, TRUE)
    special(DrawWholeMapView)
}

script YifuCity_Dojo_SetDoor2Metatiles {
    setmetatile(43, 31, METATILE_DojoYifu_DoorTopLeft, TRUE)
    setmetatile(44, 31, METATILE_DojoYifu_DoorTopRight, TRUE)
    setmetatile(43, 32, METATILE_DojoYifu_DoorBottomLeft, TRUE)
    setmetatile(44, 32, METATILE_DojoYifu_DoorBottomRight, TRUE)
    special(DrawWholeMapView)
}

script YifuCity_Dojo_SetDoor3Metatiles {
    setmetatile(22, 13, METATILE_DojoYifu_PassageTopLeft, TRUE)
    setmetatile(25, 13, METATILE_DojoYifu_PassageTopRight, TRUE)
    setmetatile(22, 14, METATILE_DojoYifu_PassageBottomLeft, TRUE)
    setmetatile(25, 14, METATILE_DojoYifu_PassageBottomRight, TRUE)

    setmetatile(23, 10, METATILE_DojoYifu_WoodFloorDark, FALSE)
    setmetatile(24, 10, METATILE_DojoYifu_WoodFloor, FALSE)
    setmetatile(23, 11, METATILE_DojoYifu_WoodFloorDark, FALSE)
    setmetatile(24, 11, METATILE_DojoYifu_WoodFloor, FALSE)
    setmetatile(23, 12, METATILE_DojoYifu_WoodFloorDark, FALSE)
    setmetatile(24, 12, METATILE_DojoYifu_WoodFloor, FALSE)
    setmetatile(23, 13, METATILE_DojoYifu_WoodFloorDark, FALSE)
    setmetatile(23, 14, METATILE_DojoYifu_WoodFloorDark, FALSE)
    setmetatile(24, 13, METATILE_DojoYifu_WoodFloor, FALSE)
    setmetatile(24, 14, METATILE_DojoYifu_WoodFloor, FALSE)
    setmetatile(24, 15, METATILE_DojoYifu_WoodFloor, FALSE)

    setmetatile(23, 18, METATILE_DojoYifu_Stairs, FALSE)
    setmetatile(24, 18, METATILE_DojoYifu_Stairs, FALSE)

    special(DrawWholeMapView)
}

script YifuCity_Dojo_SetShortcutMetatiles {
    setmetatile(23, 0, METATILE_DojoYifu_DoorTopLeft, TRUE)
    setmetatile(24, 0, METATILE_DojoYifu_DoorTopRight, TRUE)
    setmetatile(23, 1, METATILE_DojoYifu_DoorBottomLeft, TRUE)
    setmetatile(24, 1, METATILE_DojoYifu_DoorBottomRight, TRUE)
    special(DrawWholeMapView)
}

script YifuCity_Dojo_Dance1_Jump {
    playse(SE_BANG)
    applymovement(LOCALID_YIFU_GEISHA_1, YifuCity_Dojo_Movement_JumpInPlaceDown)
    applymovement(LOCALID_YIFU_GEISHA_2, YifuCity_Dojo_Movement_JumpInPlaceDown)
    applymovement(LOCALID_YIFU_GEISHA_3, YifuCity_Dojo_Movement_JumpInPlaceDown)
    waitmovement(LOCALID_YIFU_GEISHA_1)
    waitmovement(LOCALID_YIFU_GEISHA_2)
    waitmovement(LOCALID_YIFU_GEISHA_3)
    waitse

    delay(16)
}

script YifuCity_Dojo_Dance2_Jump {
    playse(SE_BANG)
    applymovement(LOCALID_YIFU_GEISHA_4, YifuCity_Dojo_Movement_JumpInPlaceDown)
    applymovement(LOCALID_YIFU_GEISHA_5, YifuCity_Dojo_Movement_JumpInPlaceDown)
    applymovement(LOCALID_YIFU_GEISHA_6, YifuCity_Dojo_Movement_JumpInPlaceDown)
    waitmovement(LOCALID_YIFU_GEISHA_4)
    waitmovement(LOCALID_YIFU_GEISHA_5)
    waitmovement(LOCALID_YIFU_GEISHA_6)
    waitse

    delay(16)
}

script YifuCity_Dojo_Dance3_Jump {
    playse(SE_BANG)
    applymovement(LOCALID_YIFU_GEISHA_7, YifuCity_Dojo_Movement_JumpInPlaceDown)
    applymovement(LOCALID_YIFU_GEISHA_8, YifuCity_Dojo_Movement_JumpInPlaceDown)
    applymovement(LOCALID_YIFU_GEISHA_9, YifuCity_Dojo_Movement_JumpInPlaceDown)
    applymovement(LOCALID_YIFU_GEISHA_10, YifuCity_Dojo_Movement_JumpInPlaceDown)
    waitmovement(LOCALID_YIFU_GEISHA_7)
    waitmovement(LOCALID_YIFU_GEISHA_8)
    waitmovement(LOCALID_YIFU_GEISHA_9)
    waitmovement(LOCALID_YIFU_GEISHA_10)
    waitse

    delay(16)
}

movement YifuCity_Dojo_Movement_JumpInPlaceDown {
    jump_in_place_down
}

script YifuCity_Dojo_Dance1 {
    playbgm(MUS_LILYCOVE, FALSE)

    applymovement(
        LOCALID_YIFU_GEISHA_1,
        // Start left, end mid
        moves( // 13
            walk_down,
            walk_right * 4
            walk_up,
            walk_left,
            jump_2_left,
            walk_left,
            walk_down,
            walk_right * 2,
            walk_up
            face_down
        )
    )
    applymovement(
        LOCALID_YIFU_GEISHA_2,
        // Start mid, end left
        moves(
            walk_left * 2,
            walk_down,
            walk_right * 4
            walk_up,
            walk_left * 4,
            face_down
        ) // 12
    )
    applymovement(
        LOCALID_YIFU_GEISHA_3,
        moves( // 13
            walk_up,
            walk_left * 4
            walk_down,
            walk_right * 2
            walk_down,
            walk_right * 2,
            walk_up,
            face_down
        )
    )

    // 7 * 16
    delay(112)
    playse(SE_LEDGE)

    waitmovement(LOCALID_YIFU_GEISHA_1)
    waitmovement(LOCALID_YIFU_GEISHA_2)
    waitmovement(LOCALID_YIFU_GEISHA_3)

    fadedefaultbgm
}

script YifuCity_Dojo_Dance2 {
    playbgm(MUS_LILYCOVE, FALSE)

    applymovement(
        LOCALID_YIFU_GEISHA_4,
        moves(
            walk_right,   // 1
            jump_2_right, // 2, 3
            jump_2_left,  // 4, 5
            jump_2_right, // 6, 7
            walk_right,   // 8
            walk_left,    // 9
            walk_left,    // 10

            face_down
        )
    )
    applymovement(
        LOCALID_YIFU_GEISHA_5,
        moves(
            face_left,  // 1
            delay_16,   // 1

            jump_2_left,  // 2, 3
            jump_2_right, // 4, 5
            jump_2_left,  // 6, 7

            face_down
        )
    )
    applymovement(
        LOCALID_YIFU_GEISHA_6,
        moves(
            walk_down,  // 1
            walk_left,  // 2
            walk_left,  // 3
            walk_left,  // 4
            walk_left,  // 5
            walk_up,    // 6
            jump_2_right, // 7, 8
            jump_2_right, // 9, 10

            face_down
        )
    )

    delay(16)
    playse(SE_LEDGE)
    delay(32)
    playse(SE_LEDGE)
    delay(32)
    playse(SE_LEDGE)
    delay(32)
    playse(SE_LEDGE)

    waitmovement(LOCALID_YIFU_GEISHA_4)
    waitmovement(LOCALID_YIFU_GEISHA_5)
    waitmovement(LOCALID_YIFU_GEISHA_6)

    fadedefaultbgm
}

script YifuCity_Dojo_Dance3 {
    playbgm(MUS_LILYCOVE, FALSE)

    applymovement(LOCALID_YIFU_GEISHA_7, moves(
        face_left,
        delay_16,
        face_up,
        delay_16,
        face_right,
        delay_16,
        face_down,
        delay_16,

        jump_2_right, // 1, 2
        walk_right,   // 3
        jump_2_right, // 4, 5
        walk_right,   // 6
        walk_right,   // 7
        
        face_left,
        delay_16,
        face_up,
        delay_16,
        face_right,
        delay_16,
        face_down,
        delay_16,
    ))
    
    applymovement(LOCALID_YIFU_GEISHA_8, moves(
        face_left,
        delay_16,
        face_up,
        delay_16,
        face_right,
        delay_16,
        face_down,
        delay_16,

        walk_left,    // 1
        walk_down,    // 2
        walk_right,   // 3
        walk_right,   // 4
        walk_up,      // 5
        jump_2_right, // 6, 7
        
        face_left,
        delay_16,
        face_up,
        delay_16,
        face_right,
        delay_16,
        face_down,
        delay_16,
    ))
    
    applymovement(LOCALID_YIFU_GEISHA_9, moves(
        face_left,
        delay_16,
        face_up,
        delay_16,
        face_right,
        delay_16,
        face_down,
        delay_16,

        walk_right,  // 1
        walk_up,     // 2
        walk_left,   // 3
        walk_left,   // 4
        walk_down,   // 5
        walk_left,   // 6
        walk_left,   // 7
        
        face_left,
        delay_16,
        face_up,
        delay_16,
        face_right,
        delay_16,
        face_down,
        delay_16,
    ))
    
    applymovement(LOCALID_YIFU_GEISHA_10, moves(
        face_left,
        delay_16,
        face_up,
        delay_16,
        face_right,
        delay_16,
        face_down,
        delay_16,

        jump_2_left, // 1, 2
        walk_left,   // 3
        jump_2_left, // 4, 5
        walk_left,   // 6
        walk_left,   // 7
        
        face_left,
        delay_16,
        face_up,
        delay_16,
        face_right,
        delay_16,
        face_down,
        delay_16,
    ))

    // 4 * 16
    delay(64)
    playse(SE_LEDGE)
    // 3 * 16
    delay(48)
    playse(SE_LEDGE)
    delay(32)
    playse(SE_LEDGE)

    waitmovement(LOCALID_YIFU_GEISHA_7)
    waitmovement(LOCALID_YIFU_GEISHA_8)
    waitmovement(LOCALID_YIFU_GEISHA_9)
    waitmovement(LOCALID_YIFU_GEISHA_10)

    fadedefaultbgm
}

script YifuCity_Dojo_EventScript_Advice {
    lock
	faceplayer
	if (flag(FLAG_BADGE03_GET)) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_UUME_MAN_2 EMOTE_NORMAL}Such grace! Such swiftness!\nI am wowed!\p"
            "{PLAYER}…\nThat's a name to be remembered!"
		))
	} else {
		msgbox((
            "{CREATE_MUGSHOT MUGSHOT_UUME_MAN_2 EMOTE_NORMAL}Hello, young challenger, and welcome\nto the Dance Hall Dojo!\p"
            "Got what it takes to see through the\ndancers' intoxicating moves?\p"
            "Master Yutaka favors Dance Moves.\nA battle with her is like partaking in\la dizzying dancing showdown!"
            "Watch out though, she can be as\nabrasive as she is beautiful.\p"
            "She probably won't appreciate you\nwalking around her Dojo with your\nclothes looking like… this…\p"
            "Uh, nevermind my babbling!\nGo get 'em, champ!"
		))
	}
	release
}

script YifuCity_Dojo_EventScript_DojoStatue {
	lockall
	if (flag(FLAG_BADGE03_GET)) {
		msgbox(
			"Dojo of Yifu City\p"
			"certified Wielders:\n"
			"Natsuki,\l"
			"{PLAYER}",
			MSGBOX_SIGN
		)
	} else {
		msgbox(
			"Dojo of Kura Village\p"
			"certified Wielders:\n"
			"Natsuki",
			MSGBOX_SIGN
		)
	}
	releaseall
}

script YifuCity_Dojo_EventScript_Akari {
    trainerbattle_single(
		TRAINER_AKARI,
		format("You still wish to fight?\nVery well!"),
		format("Oh!")
	)
	setspeaker(SP_NAME_KIMONO_GIRL)
	msgbox(format("You are very graceful.\nAre you sure you're not a dancer?"), MSGBOX_AUTOCLOSE)
	end
}

script YifuCity_Dojo_EventScript_Yuka {
    trainerbattle_single(
		TRAINER_YUKA,
		format("You have a good eye!\nLet's see if that applies to Pokémon, as well!"),
		format("You have many talents, I see!")
	)
	setspeaker(SP_NAME_KIMONO_GIRL)
	msgbox(format("The next dance should be more of a challenge."), MSGBOX_AUTOCLOSE)
	end
}

script YifuCity_Dojo_EventScript_Nanami {
    trainerbattle_single(
		TRAINER_NANAMI,
		format("Pant… I'm impressed… Pant…\nThis last dance was… intense!"),
		format("Pant… Hey, let me catch my breath!")
	)
	setspeaker(SP_NAME_KIMONO_GIRL)
	msgbox(format("I really need to exercise more…"), MSGBOX_AUTOCLOSE)
	end
}

script YifuCity_Dojo_EventScript_Kanae {
    trainerbattle_single(
		TRAINER_KANAE,
		format("You didn't see me trip over my own foot, did you?"),
        format("My dance wasn't perfect!\nI deserve this!")
	)
	setspeaker(SP_NAME_KIMONO_GIRL)
	msgbox(format("Next time my dance will be perfect, you'll see!"), MSGBOX_AUTOCLOSE)
	end
}

script YifuCity_Dojo_EventScript_Karin {
    trainerbattle_single(
		TRAINER_KARIN,
		format("Oh good! A battle!\nI was getting bored."),
        format("I am regretting this…")
	)
	setspeaker(SP_NAME_KIMONO_GIRL)
	msgbox(("Miss Yutaka holds us to high standards\nboth in battle and onstage!"), MSGBOX_AUTOCLOSE)
	end
}

script YifuCity_Dojo_EventScript_Miika {
    trainerbattle_single(
		TRAINER_MIIKA,
		format("Please admire my brand new designer kimono!"),
        format("You don't look impressed at all!")
	)
	setspeaker(SP_NAME_KIMONO_GIRL)
	msgbox(format("Working here has its perks.\nWe get fine silk clothes for free!"), MSGBOX_AUTOCLOSE)
	end
}

script YifuCity_Dojo_EventScript_Dancer {
    lock
    faceplayer
    setspeaker(SP_NAME_KIMONO_GIRL)
    msgbox(format(
        "I have no Pokémon!\nI'm only a dancer."
    ))
    release
}

script YifuCity_Dojo_EventScript_Dancer2 {
    lock
    faceplayer
    setspeaker(SP_NAME_KIMONO_GIRL)
    msgbox(format(
        "Come back to watch us dance sometimes!"
    ))
    release
}

script YifuCity_Dojo_EventScript_ImpossibleDancer {
    lock
    faceplayer
    setspeaker(SP_NAME_KIMONO_GIRL)
    msgbox(format(
        "Hey! How did you get up here?!"
    ))
    release
}

script YifuCity_Dojo_EventScript_Yutaka {
    lock
    faceplayer
    if (!flag(FLAG_BADGE03_GET)) {
        setspeaker(SP_NAME_YUTAKA)
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_YUTAKA EMOTE_NORMAL}Well done! Welcome to Yifu City's Doj-"
        ))

        applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
        playse(SE_PIN)
        waitmovement(0)
        waitse
        delay(48)
    }

	trainerbattle_single(
		TRAINER_YUTAKA_1,
		format(
            "Excuse me, what is this?\p"
            "Is there a reason for the\n"
            "accoutrement you're donning?\p"
            "A carnival, perhaps?\nOr have you lost a bet?\p"
            "Or do you just lack a basic sense of style to such an extreme degree?\p"
            "Here you are, parading around\n"
            "in your peasant's clothes…\p"
            "(…Yutaka, you must not let this\n"
            "offend you so!\l"
            "You are stronger than that!)\p"
            "Ahem… Let us move on…\p"
            "You shall be fighting my exquisite assortment of Pokémon,\l"
            "in the hopes of obtaining\l"
            "an official Token from me.\p"
            "Try not to be too overwhelmed by my strength and, most of all, by my impeccable sense of fashion.\p"
            "Let this battle begin!"
		),
		format("How dare you!"),
		YifuCity_Dojo_EventScript_YutakaDefeated,
		NO_MUSIC
	)
    
	setspeaker(SP_NAME_YUTAKA)
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_YUTAKA EMOTE_NORMAL}You shall achieve great things,\ndespite your fashion faux-pas.\p"
        "I hope to battle you again."
	))

    release
	end
}

script YifuCity_Dojo_EventScript_YutakaDefeated {
    // removespeakername
	message(format(
		"{PLAYER} received the Silk Token from Yutaka."
	))
	waitmessage
	call(Common_EventScript_PlayGymBadgeFanfare)

    setspeaker(SP_NAME_YUTAKA)
	msgbox(
		format(
            "{CREATE_MUGSHOT MUGSHOT_YUTAKA EMOTE_NORMAL}I am outrée! Your techniques are rough and vulgar, yet…\p"
            "Victory is yours…\nThat is undeniable.\p"
			"I guess what you lack in aesthetic values, you compensate in strength.\p"
			"Do accept this, as well."
		),
		MSGBOX_DEFAULT
	)
    closemessage
	
	giveitem(ITEM_TM_X_SCISSOR)

    setspeaker(SP_NAME_YUTAKA)
	msgbox(
		format(
			"{CREATE_MUGSHOT MUGSHOT_YUTAKA EMOTE_NORMAL}This scroll contains my famous X-Scissor technique.\p"
            "I perfected it back when I worked as a seamstress.\p"
			"About that, uh… We should go clothes-shopping together sometime.\p"
            "I cannot bear to see your image suffer so."
		)
	)
	closemessage

	setflag(FLAG_BADGE03_GET)
	setvar(VAR_0x8008, 3)
	call(Common_EventScript_SetGymTrainers)
	addvar(VAR_PLAYER_REPUTATION, 5)

    // Door appears
    call(YifuCity_Dojo_SetShortcutMetatiles)

    call(YifuCity_Dojo_DoorAppears_Screenshake)

    delay(48)

	release
	end
}
const FLAG_HIDE_LITTLE_BOY = FLAG_TEMP_1

mapscripts SunriseVillage_Beach_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (!flag(FLAG_RECEIVED_STARTER)) {
			setflag(FLAG_HIDE_LITTLE_BOY)
		}
	}
}

const LOCALID_RIVAL = 1

script SunriseVillage_Beach_EventScript_RivalSpeech {
	lock
	setvar(VAR_TEMP_0, 0) //VAR_TEMP_0 used as a way to effectively make a for loop out of a while loop
	if (var(VAR_FACING) == DIR_EAST){
		waitse
		playse(SE_PIN)
		applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
		waitmovement(0)
		delay(48)
		setspeaker(SP_NAME_NATSUKI)
		msgbox("{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Oy, {PLAYER}!\nOut of the way!\p"
				"I've got three reps left!")
		closemessage
		applymovement(LOCALID_PLAYER, moves(walk_up, face_down))
		waitmovement(0)
		delay(60)
		setspeaker(SP_NAME_NATSUKI)
		while(var(VAR_TEMP_0) != 3){ call(SunriseVillage_Beach_EventScript_NatsukiTraining) }
		closemessage
		delay(48)
		applymovement(LOCALID_PLAYER, moves(walk_down, face_right))
		waitmovement(0)
		setspeaker(SP_NAME_NATSUKI)
		msgbox(format("{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Hoo boy, there's nothing like workin' up a sweat on a bright, sunny morning!\pNow, what did you wanna-"))
		closemessage
	}
	else {
		delay(40)
		setspeaker(SP_NAME_NATSUKI)
		while(var(VAR_TEMP_0) != 2){ call(SunriseVillage_Beach_EventScript_NatsukiTraining) }
		setspeaker(SP_NAME_NATSUKI)
		message("{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}One… {PAUSE 40}Two…{PAUSE 40}") //Neutral face mugshot
		waitmessage
		dynmultichoice(
			0,
			0,
			TRUE,
			10,
			0,
			DYN_MULTICHOICE_CB_NONE,
			"HEY!",
			"NATSUKI!",
			"BOO!"
		)
		closemessage
		waitse
		switch(var(VAR_FACING)){
			case DIR_SOUTH:
				turnobject(LOCALID_RIVAL, DIR_NORTH)
			case DIR_NORTH:
				turnobject(LOCALID_RIVAL, DIR_SOUTH)
			case DIR_WEST:
				turnobject(LOCALID_RIVAL, DIR_EAST)
		}
		waitmovement(LOCALID_RIVAL)
		//playse(SE_PIN)
		//applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
		//waitse
		setspeaker(SP_NAME_NATSUKI)
		message("{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}AAAAAAAH!")
		waitmovement(0)
		playse(SE_LEDGE)
		switch(var(VAR_FACING)){
			case DIR_SOUTH:
				applymovement(LOCALID_RIVAL, moves(face_up, lock_facing_direction, jump_2_down, unlock_facing_direction))
			case DIR_NORTH:
				applymovement(LOCALID_RIVAL, moves(face_down, lock_facing_direction, jump_2_up, unlock_facing_direction))
			case DIR_WEST:
				applymovement(LOCALID_RIVAL, moves(face_right, lock_facing_direction, jump_2_left, unlock_facing_direction))
		}
		waitse
		waitmovement(0)
		delay(20)
		closemessage
		delay(40)
		switch(var(VAR_FACING))
		{
			case DIR_SOUTH:
				applymovement(LOCALID_RIVAL, moves(walk_up * 2))
			case DIR_NORTH:
				applymovement(LOCALID_RIVAL, moves(walk_down * 2))
			case DIR_WEST:
				applymovement(LOCALID_RIVAL, moves(walk_right * 2))
		}
		waitmovement(0)
		setspeaker(SP_NAME_NATSUKI)
		msgbox("{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}{PLAYER}!\pI've told you dozens of times\nNOT to sneak up on me during training!\p"
				"I almost whacked you upside the head!\p"
				"…{PAUSE 30} …{PAUSE 30} …{PAUSE 30}\p"
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}(You shoulda seen your face though…!)") //Smiling mugshot. Doing the mugshot call a second time right now to indicate a facial expression change once there's more than just the one mugshot
		closemessage
		delay(48)
	}
	fadeoutbgm(0)
	// Volcano quake!
	setvar(VAR_0x8004, 3)  // vertical pan
	setvar(VAR_0x8005, 0)  // horizontal pan
	setvar(VAR_0x8006, 20)  // num shakes
	setvar(VAR_0x8007, 5)  // shake delay
	special(ShakeCamera)
	delay(16)
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSurprisedByQuake)
	delay(16)
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_PlayerSurprisedByQuake)
	waitstate
	waitmovement(OBJ_EVENT_ID_PLAYER)
	waitmovement(LOCALID_RIVAL)
	delay(48)
	fadeinbgm(0)
	switch(var(VAR_FACING)) {
		case DIR_SOUTH:
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
			applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)
		case DIR_NORTH:
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
			applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterDown)
		case DIR_WEST:
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
			applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterRight)
		case DIR_EAST:
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
			applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterLeft)
	}
	setspeaker(SP_NAME_NATSUKI)
	msgbox("{CREATE_MUGSHOT MUGSHOT_NATSUKI 0} Oh jeez, the quakes are\nstarting up again…\pI'm feeling really off-balance…\pLet's sit down…") //Feeling bad Mugshot
	closemessage
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSitsOnBench)
	// Player sits next to rival
	switch(var(VAR_FACING)) {
		case DIR_SOUTH:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromTop)
		case DIR_NORTH:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromBottom)
		case DIR_WEST:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromRight)
		case DIR_EAST:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromLeft)
	}
	waitmovement(LOCALID_RIVAL)
	waitmovement(LOCALID_PLAYER)

	delay(72)
	setspeaker(SP_NAME_NATSUKI)
	msgbox(
		"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Life's been crazy lately, huh…\p"
		"Between the Pokémon in Ginko Woods\ngoing rabid…\pAnd the volcano rumbling after being\ndormant all our lives…\p"
		"All the adults are so anxious.\p"
		"We haven't been allowed to leave the\nvillage for weeks now…!"
	)
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, moves(emote_pensive))
	msgbox(format(
		"… … … … … …\n… … … … … …"
	))

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Are you worried?",
		"What are you thinking about?"
	)
	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			setspeaker(SP_NAME_NATSUKI)
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Me? {PAUSE 20}Worried!?\nNo WAY!\p"
				"Theres no point to worrying.{PAUSE 20}\nEverything turns out fine eventually.\p"
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Just… {PAUSE 20}don't think too much about\nhow things are now."
			)
			closemessage

			delay(72)

			setspeaker(SP_NAME_NATSUKI)
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Hey, look.\p"
				"You can see Yifu City and the Shogunate\nover the horizon today.\pIsn't it incredible?"
			)
			closemessage
			playse(SE_PIN)
			applymovement(LOCALID_RIVAL, moves(emote_pensive))

			delay(60)
			// They face each other
			applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)

			setspeaker(SP_NAME_NATSUKI)
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Don't worry about me, {PLAYER}.\nWhat about you?\p"
				"You've got anxiety written all\nover your face!\p"
				"Like, aren't you at least excited\nto become a Pokémon Wielder?"
			)
		case 1:
			setspeaker(SP_NAME_NATSUKI)
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}…{PAUSE 30}Huh?…\p"
				"…Oh, it's nothing…"
			)
			closemessage
			turnobject(LOCALID_PLAYER, DIR_SOUTH)
			delay(72)
			turnobject(LOCALID_RIVAL, DIR_NORTH)
			delay(90)
			turnobject(LOCALID_RIVAL, DIR_SOUTH)
			setspeaker(SP_NAME_NATSUKI)
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Hey, don't go looking all worried\nabout me like that!\p"
				"I'm NOT nervous, if that's what\nyou're thinking!"
			)
			closemessage
			turnobject(LOCALID_RIVAL, DIR_WEST)
			applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
			waitmovement(0)

			setspeaker(SP_NAME_NATSUKI)
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Hey, look.\p"
				"You can see Yifu City and the Shogunate\nover the horizon today."
			)
			turnobject(LOCALID_PLAYER, DIR_WEST)
			msgbox("Isn't it incredible?") //Not sure why nametag is redrawn here, would be nice if it wasnt
			closemessage
			playse(SE_PIN)
			applymovement(LOCALID_RIVAL, moves(emote_pensive))
			delay(60)
			// They face each other
			applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)

			setspeaker(SP_NAME_NATSUKI)
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}ANYWAYS…\p"
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}You excited about finally\nbecoming a Pokémon Wielder?"
			)
	}

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"I'm thrilled!",
		"I'm terrified!",
		"It's whatever."
	)
	closemessage

	setspeaker(SP_NAME_NATSUKI)
	switch (var(VAR_RESULT)) {
		case 0:
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Hell yeah!\p"
				"After we get our new buds,\nyou GOTTA train with me!\p"
				"I don't want you falling\nTOO far behind, now!"
			)
		case 1:
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Heh, getting cold feet right as your\ndream is coming true?\p"
				"It's gonna be awesome!\p"
				"We'll finally get to go out into the\nworld and blaze our own trails!\p"
				"I can even become a sa-"
				
			)
			closemessage
			waitse
			playse(SE_PIN)
			applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
			waitmovement(0)
			delay(48)
			setspeaker(SP_NAME_NATSUKI)
			msgbox(format("{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Ahhhh, I've already rattled your ear off about that enough this past week!\p"
			"I'm getting ahead of myself now."
			))
		case 2:
			msgbox(
				"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Oh c'mon, {PLAYER}!\nYou've raved about wanting your\lown Pokémon for HOW many years now!?\p"
				"Don't give me that Trubbish now!"
			)
	}
	closemessage
	delay(48)

	// Volcano quake!
	setvar(VAR_0x8004, 3)  // vertical pan
	setvar(VAR_0x8005, 0)  // horizontal pan
	setvar(VAR_0x8006, 20)  // num shakes
	setvar(VAR_0x8007, 3)  // shake delay
	special(ShakeCamera)
	delay(16)
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSurprisedByQuake)
	delay(16)
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_PlayerSurprisedByQuake)
	waitstate
	waitmovement(OBJ_EVENT_ID_PLAYER)
	waitmovement(LOCALID_RIVAL)
	delay(48)

	// They face each other
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)

	setspeaker(SP_NAME_NATSUKI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Woah, there it goes again!\n"
		"You good, {PLAYER}?\p"
		"Those quakes are no joke, huh.\p"
		"Maybe the volcano's telling us\nto get to Elder Kaba's house already…?"
	))
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)

	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSeesDistribution)
	waitmovement(0)

	setspeaker(SP_NAME_NATSUKI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}MUK! The distribution's already started!\p"
		"Let's go, {PLAYER}!!"
	))
	closemessage

	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	// Rival runs towards the village
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalRaces)
	waitmovement(0)
	delay(60)
	setflag(FLAG_HIDE_SUNRISE_BEACH_RIVAL)
	clearflag(FLAG_HIDE_SUNRISE_VILLAGE_STANDARD_NPC_POSITIONS)
	clearflag(FLAG_HIDE_BIGSIS_HOME)
	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 4)

	removeobject(LOCALID_RIVAL)
    release
    end
}

script SunriseVillage_Beach_EventScript_NatsukiTraining {
	setspeaker(SP_NAME_NATSUKI)
	message("{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}One… {PAUSE 40}Two… {PAUSE 40}THREE!")
	waitmessage
	//closemessage
	playse(SE_M_CUT)
	applymovement(LOCALID_RIVAL, moves(walk_fast_left, delay_16 * 3, lock_facing_direction, walk_slow_right, unlock_facing_direction))
	waitmovement(0)
	waitse
	delay(48)
	addvar(VAR_TEMP_0, 1)
	return
}

movement SunriseVillage_Beach_Movement_PlayerSurprisedByQuake {
	emote_exclamation_mark
	delay_16
	face_right
	delay_16
	face_up
	delay_16
	face_right
	delay_16
	face_down
	delay_16
	face_left
	delay_16
	face_up
}

movement SunriseVillage_Beach_Movement_RivalSurprisedByQuake {
	emote_exclamation_mark
	delay_16
	face_down
	delay_16
	face_up
	delay_16
	face_left
	delay_16
	face_right
	delay_16
	face_up
	delay_16
	face_down
}

// Corsola
script SunriseVillage_Beach_EventScript_Corsola {
	lock
    faceplayer
	setseenmon(SPECIES_CORSOLA)
	setspeakertomonname(SPECIES_CORSOLA)
    playmoncry(SPECIES_CORSOLA, CRY_MODE_NORMAL)
    msgbox(format(
        "Krr!"
    ))
    waitmoncry
    release
    end
}

script SunriseVillage_Beach_EventScript_Corsola2 {
	lock
    faceplayer
	setseenmon(SPECIES_CORSOLA)
	setspeakertomonname(SPECIES_CORSOLA)
    playmoncry(SPECIES_CORSOLA, CRY_MODE_WEAK)
    msgbox(format(
        "Laaa!"
    ))
    waitmoncry
    release
    end
}

script SunriseVillage_Event_LookingAtYifu {
	lock
	faceplayer
	setspeaker(SP_NAME_LITTLE_BOY)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SUNRISE_LITTLE_BOY 0}My best friend lives all the way over there in Yifu City!\p"
		"I don't get to see her often. I miss her…"
	))
	closemessage
	applymovement(VAR_LAST_TALKED, moves(face_original_direction))
	waitmovement(0)
	release
	end
}

script SunriseVillage_Event_PokemonWontComeOut {
	lock
	faceplayer
	setspeaker(SP_NAME_LITTLE_BOY)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SUNRISE_LITTLE_BOY 0}I'm going to catch my first Pokémon myself, all on my own!"
	))
	closemessage
	applymovement(VAR_LAST_TALKED, moves(face_original_direction))
	waitmovement(0)

	setspeaker(SP_NAME_LITTLE_BOY)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SUNRISE_LITTLE_BOY 0}Pspspsps… Come on out, Pokémon… Pspsps…"
	))
	closemessage

	release
	end
}

script SunriseVillage_Event_ChildsMon {
	lock
    faceplayer
	setseenmon(SPECIES_DWEBBLE)
	setspeakertomonname(SPECIES_DWEBBLE)
    playmoncry(SPECIES_DWEBBLE, CRY_MODE_NORMAL)
    msgbox(format(
        "Mrrb!"
    ))
    waitmoncry
    release
    end
}

movement SunriseVillage_Beach_Movement_RivalSitsOnBench {
	walk_left * 4
	walk_in_place_faster_left
	delay_16 * 3
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromLeft {
	walk_up
	face_down
	delay_8
	face_left
	delay_16
	delay_8
	walk_left * 3
	walk_in_place_faster_left
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromTop {
	face_left
	delay_16 * 2
	walk_left * 4
	walk_in_place_faster_left
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromRight {
	walk_left * 4
	walk_up
	walk_left
	walk_in_place_faster_left
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromBottom {
	delay_16
	walk_up
	walk_left * 3
	walk_up
	walk_left
	walk_in_place_faster_left
	step_end
}

movement SunriseVillage_Beach_Movement_RivalSeesDistribution {
	face_right
	delay_16
	delay_16
	delay_16
	face_player
	step_end
}

movement SunriseVillage_Beach_Movement_RivalRaces {
	walk_faster_right * 9
	step_end
}
mapscripts MaguroHarbor_House1_MapScripts {
	
}

script MaguroHarbor_House1_EventScript_PokedexRiddler {
	lock
	faceplayer

	if(flag(FLAG_DAILY_MON_DEX_RIDDLE))
	{
		setspeaker(SP_NAME_PKMN_NERD)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_FAT_GUY 0}Come back tomorrow for more Pokémon riddles… Hehe…"
		))
		closemessage
		release
		end
	}

	setspeaker(SP_NAME_PKMN_NERD)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SEASIDE_FAT_GUY 0}Hehe… Hullo, kid…\nThey call me the Pokémon Nerd.\p"
		"(They're just jealous…)\p"
		"I know a whole lot about Pokémon… Wanna play a game?\p"
		"Guess what Pokémon I'm describing,\nand I'll share my knowledge with you.\p"
		"Hehe… Think you can solve my riddle? Ahem…"
	))
	closemessage

	randomdexmsgbox

	setspeaker(SP_NAME_PKMN_NERD)
	msgbox("{CREATE_MUGSHOT MUGSHOT_SEASIDE_FAT_GUY 0}Well? What Pokémon am I talking about?")
	closemessage

	special(EnterDexRiddleGuess)
	waitstate
	special(GetDexRiddleFeedback)

	if(var(VAR_RESULT) == 1)
	{
		playse(SE_PIN)
		applymovement(LOCALID_MAGURO_NERD, moves(emote_exclamation_mark, delay_16 * 6))
		waitse
		waitmovement(0)
		setspeaker(SP_NAME_PKMN_NERD)
		waitse
		playse(SE_SUCCESS)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_FAT_GUY 0}Woah, you… You solved my riddle!\p"
			"Could you… be a true Pokémon Nerd?\p"
			"You have bested me today. Feel the rush of pure knowledge coursing through your veins?\p"
			"Your Pokémon… They will gain more experience today.\p"
			"Knowledge is power, after all, hehe…\p"
			"Come back another time for another riddle…"
		))
		closemessage

		incrementgamestat(GAME_STAT_SOLVED_RIDDLE_DEX_DESC)

		setflag(FLAG_DAILY_MON_DEX_RIDDLE_WON)
	}
	else
	{
		waitse
		playse(SE_FAILURE)
		applymovement(VAR_LAST_TALKED, moves(emote_upset))
		setspeaker(SP_NAME_PKMN_NERD)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_FAT_GUY 0}Wrong, wrong, WRONG!\nYou are wrong!\p"
			"I'm honestly disappointed.\p"
			"Come back tomorrow, or when you've accumulated more Pokémon knowledge."
		))
	}

	setflag(FLAG_DAILY_MON_DEX_RIDDLE)

	release
	end
}

script MaguroHarbor_House1_EventScript_Clefairy {
	lock
	setseenmon(SPECIES_CLEFAIRY)
	playmoncry(SPECIES_CLEFAIRY, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_CLEFAIRY)
	applymovement(VAR_LAST_TALKED, moves(face_player, emote_music))
	msgbox(format(
		"Teehee!"
	))
	waitmoncry
	release
}

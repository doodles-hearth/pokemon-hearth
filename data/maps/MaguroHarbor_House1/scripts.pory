mapscripts MaguroHarbor_House1_MapScripts {
	
}

script MaguroHarbor_House1_EventScript_PokedexRiddler {
	lock
	faceplayer

	if(flag(FLAG_DAILY_MON_DEX_RIDDLE))
	{
		speakername("Pokémon Nerd")
		msgbox(format(
			"Come back tomorrow for more Pokémon riddles… Hehe…"
		))
		closemessage
		release
		end
	}

	speakername("Pokémon Nerd")
	msgbox(format(
		"Hehe… Hullo, kid…\nPeople call me the Pokémon Nerd.\p"
		"(They're just jealous…)\p"
		"I know a whole lot about Pokémon… Wanna play a game?\p"
		"If you can guess what Pokémon I'm describing, I'll share my knowledge with you.\p"
		"Hehe… Think you can solve my riddle? Ahem…"
	))
	closemessage

	randomdexmsgbox

	speakername("Pokémon Nerd")
	msgbox("Well? What Pokémon am I talking about?")
	closemessage

	special(EnterDexRiddleGuess)
	waitstate
	special(GetDexRiddleFeedback)

	if(var(VAR_RESULT) == 1)
	{
		speakername("Pokémon Nerd")
		msgbox(format(
			"Woah, you… You solved my riddle!\p"
			"Could you… be a true Pokémon Nerd?\p"
			"You have bested me today. Can you feel the rush of pure knowledge that's coursing through your veins?\p"
			"Your Pokémon… They will gain more experience today.\p"
			"Knowledge is power, after all, hehe…\p"
			"Come back another time for another riddle…"
		))
		closemessage

		setflag(FLAG_DAILY_MON_DEX_RIDDLE_WON)
	}
	else
	{
		speakername("Pokémon Nerd")
		msgbox(format(
			"Wrong, wrong, WRONG!\nYou are wrong!\p"
			"I'm honestly disappointed.\p"
			"Come back tomorrow, or when you've accumulated more Pokémon knowledge."
		))
	}

	setflag(FLAG_DAILY_MON_DEX_RIDDLE)

	release
	end
}

script MaguroHarbor_House1_EventScript_Clefairy {
	lock
	faceplayer
	setseenmon(SPECIES_CLEFAIRY)
	playmoncry(SPECIES_CLEFAIRY, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_CLEFAIRY)
	msgbox(format(
		"Teehee!"
	))
	waitmoncry
	release
}

const LOCALID_PHOTO_MAN = 13

mapscripts SpindaIsland_Home_Clock1_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (flag(FLAG_SLOWPOKE_EVOLVED)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWBRO))
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWPOKE))
		}
	}
	MAP_SCRIPT_ON_LOAD {
		// Broken faucet
		if (flag(FLAG_FAUCET_BROKE)) {
			setmetatile(18, 13, METATILE_SpindaIslandSecondary_BrokenFaucetWall, TRUE)
		}

		if (flag(FLAG_CAMERA_EXPLODED)) {
			setmetatile(31, 13, METATILE_SpindaIsland_Grass, FALSE)
			setmetatile(31, 14, METATILE_SpindaIsland_Grass, FALSE)
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SPINDA_ISLAND_INTRO_STATE, 2: SpindaIsland_Home_EventScript_MainQuestRecap
    ]
}

script SpindaIsland_Home_Clock1_HidingBoy {
	lock

	faceplayer

	createfieldmugshot(MUGSHOT_LITTLE_BOY, EMOTE_NORMAL)
	speakername("Timmy")
	msgbox(format(
		"Darnit! You found me!"
	), MSGBOX_AUTOCLOSE)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
    speakername("Little Archie")
    msgbox(format(
		"Wazzup, Timmy?"
    ))
    closemessage
	removefieldmugshot

	release
}

script SpindaIsland_Home_Clock1_Faucet {
	lock
	// Faucet already broke
	if (flag(FLAG_FAUCET_BROKE)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Faucet's broken…"
		))
		closemessage
		removefieldmugshot
		release
	} else {
		hidefollower
		setflag(FLAG_TEMP_HIDE_FOLLOWER)
		callnative(UpdateFollowingPokemon)
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Alrighty, time to turn the big old faucet back on!\p"
			"Hnng…\p"
			"… … … … …"
		))
		closemessage
		removefieldmugshot

		delay(48)

		setmetatile(18, 13, METATILE_SpindaIslandSecondary_BrokenFaucetWall, TRUE)
		setmetatile(18, 14, METATILE_SpindaIsland_BrokenFaucetGround, FALSE)
		special(DrawWholeMapView)

		applymovement(OBJ_EVENT_ID_PLAYER, SpindaIsland_Home_Clock1_FaucetBreaks)

		playse(SE_M_BRICK_BREAK)
		waitmovement(OBJ_EVENT_ID_PLAYER)
		waitse

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SHOCKED)
		speakername("Little Archie")
		msgbox(format(
			"Aw, Shuckles! It broke!\p"
			"Mom is totally gonna kill me!!"
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		msgbox(format(
			"Let's, uh, not tell her anything and fix this ourselves!\p"
			"I better pocket this, too."
		))
		closemessage
		removefieldmugshot

		setmetatile(18, 14, METATILE_SpindaIsland_Grass, FALSE)
		special(DrawWholeMapView)
		giveitem(ITEM_BROKEN_FAUCET)

		setflag(FLAG_FAUCET_BROKE)
		clearflag(FLAG_TEMP_HIDE_FOLLOWER)
		callnative(UpdateFollowingPokemon)

		release
	}
}

script SpindaIsland_Home_Clock1_PhotoCamera {
	lock

	if (flag(FLAG_CAMERA_EXPLODED)) {
		speakername("Little Archie")
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		msgbox(format(
			"Nothing left but a pile of ashes…"
		))
		removefieldmugshot
		closemessage
		return
	}

	if (var(VAR_FACING) != DIR_NORTH) {
		speakername("Little Archie")
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		msgbox(format(
			"I should probably face the lens if I want to take a picture of myself."
		))
		removefieldmugshot
		closemessage
	} else {
			if (checkitem(ITEM_RUSTY_NICKEL)) {
				if (flag(FLAG_REALIZED_I_HAVE_NICKEL)) {
					speakername("Little Archie")
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
					msgbox(format(
						"I still have that nickel from earlier."
					))
				} else {
					msgbox(format(
						"“GET A PHOTO FOR A NICKEL!”"
					))

					speakername("Little Archie")
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SAD)
					msgbox(format(
						"Ugh, Rattatas… Don't they know kids don't have money?"
					))
					removefieldmugshot
					closemessage

					playse(SE_PIN)
					applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
					waitse
					delay(48)

					speakername("Little Archie")
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
					msgbox(format(
						"Wait! I do have a nickel, don't I?\p"
						"Those kids gave it to me earlier!\p"
						"I'm basically rich!"
					))

					setflag(FLAG_REALIZED_I_HAVE_NICKEL)
				}

				dynmultichoice(
					0,
					0,
					TRUE,
					10,
					0,
					DYN_MULTICHOICE_CB_NONE,
					"Use nickel",
					"Save nickel"
				)

				removefieldmugshot
				closemessage
				
				if (var(VAR_RESULT) == 0) {
					removeitem(ITEM_RUSTY_NICKEL)
					setflag(FLAG_CAMERA_EXPLODED)

					speakername("Little Archie")
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
					msgbox(format(
						"It's kinda rusty and misshapen…"
					))
					removefieldmugshot
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
					msgbox(format(
						"Oh well, what's the worst that can happen?\p"
						"A coin's a coin, right?"
					))
					closemessage
					removefieldmugshot

					playse(SE_VEND)
					msgbox(format(
						"…clunk!"
					))
					waitse
					closemessage

					speakername("Little Archie")
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
					msgbox(format(
						"Cheese!"
					))
					closemessage
					removefieldmugshot

					delay(48)

					playse(SE_SWITCH)
					waitse
					playse(SE_SWITCH)
					waitse
					playse(SE_SWITCH)
					waitse
					delay(20)
					playse(SE_FAILURE)
					waitse
					delay(20)
					playse(SE_M_MEGA_KICK)
					waitse
					playse(SE_M_EXPLOSION)

					setmetatile(31, 13, METATILE_SpindaIsland_Grass, FALSE)
					setmetatile(31, 14, METATILE_SpindaIsland_Grass, FALSE)
					special(DrawWholeMapView)

					setvar(VAR_0x8004, 1)  // vertical pan
					setvar(VAR_0x8005, 1)  // horizontal pan
					setvar(VAR_0x8006, 8)  // num shakes
					setvar(VAR_0x8007, 5)  // shake delay
					special(ShakeCamera)

					applymovement(OBJ_EVENT_ID_PLAYER, SpindaIsland_Home_Clock1_Movement_ArchieJumps)

					dofieldeffect(FLDEFF_EXPLOSION)

					waitfieldeffect(FLDEFF_EXPLOSION)
					waitmovement(0)
					waitse
					waitstate

					speakername("Little Archie")
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SHOCKED)
					msgbox(format(
						"The hell?!\p"
						"The camera… exploded?!"
					))
					closemessage
					removefieldmugshot

					playse(SE_EXIT)
					clearflag(FLAG_HIDE_PHOTO_MAN)
					addobject(LOCALID_PHOTO_MAN)
					waitse

					turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
					applymovement(LOCALID_PHOTO_MAN, SpindaIsland_Home_Clock1_Movement_PhotoManNotice)
					waitmovement(LOCALID_PHOTO_MAN)

					applymovement(LOCALID_PHOTO_MAN, SpindaIsland_Home_Clock1_Movement_PhotoManJump)
					playse(SE_BIKE_HOP)
					waitmovement(LOCALID_PHOTO_MAN)
					waitse

					createfieldmugshot(MUGSHOT_MAN, EMOTE_SAD)
					speakername("Camera owner")
					msgbox(format(
						"What the hell happened?\p"
						"Why did you destroy my automatic camera?!"
					))

					dynmultichoice(
						0,
						0,
						TRUE,
						10,
						0,
						DYN_MULTICHOICE_CB_NONE,
						"Where's my photograph?",
						"It didn't like my nickel!"
					)
					removefieldmugshot
					closemessage

					createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
					speakername("Camera owner")
					msgbox(format(
						"… …You were just trying to have your photograph taken?\p"
						"Hmm…"
					))
					turnobject(LOCALID_PHOTO_MAN, DIR_WEST)
					msgbox(format(
						"I guess maybe using Voltorb as electrical components wasn't such a great idea after all…"
					))
					applymovement(LOCALID_PHOTO_MAN, Common_Movement_FacePlayer)
					waitmovement(0)
					msgbox(format(
						"Alright, alright, I'm sorry about this whole exploding business.\p"
						"Let me give you a brand-new camera as an apology."
					))
					closemessage
					removefieldmugshot

					giveitem(ITEM_TOY_CAMERA)

					createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
					speakername("Camera owner")
					msgbox(format(
						"You're welcome, kid.\p"
						"Please uh, don't tell your parents about this, alright?"
					))
					closemessage

					applymovement(LOCALID_PHOTO_MAN, SpindaIsland_Home_Clock1_Movement_PhotoManLeave)
					waitmovement(0)
					playse(SE_EXIT)
					removeobject(LOCALID_PHOTO_MAN)
					setflag(FLAG_HIDE_PHOTO_MAN)
					waitse

					delay(60)
					turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)

					speakername("Little Archie")
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
					msgbox(format(
						"Hey, at least I got a free camera out of this!\p"
						"…Wait a minute…"
					))
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SAD)
					msgbox(format(
						"Oh, man, this is just a toy! It doesn't take any pictures!"
					))
					removefieldmugshot
					closemessage

					playse(SE_SWITCH)
					waitse
					playse(SE_M_LEER)
					fadescreenspeed(FADE_TO_WHITE, 0)
					fadescreenspeed(FADE_FROM_WHITE, 0)
					waitse

					speakername("Little Archie")
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_BLIND)
					msgbox(format(
						"Ugh, the flash works though…\p"
						"My eyes… I'm blind…"
					))
					removefieldmugshot
					closemessage
				}
			} else {
				msgbox(format(
						"“GET A PHOTO FOR A NICKEL!”"
					))

					speakername("Little Archie")
					createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_SAD)
					msgbox(format(
						"Ugh, Rattatas… Don't they know kids don't have money?"
					))
					removefieldmugshot
					closemessage
			}
			
			closemessage
	}
	
	release
}

script SpindaIsland_Home_Clock1_PhotoHouseDoor {
	lock
	speakername("Little Archie")
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_THINKING)
	msgbox(format("…I can hear tiny explosions through the door…"))
	removefieldmugshot
	release
}

script SpindaIsland_Home_Clock1_PhotoHouseSign {
	msgbox(format("Louis"), MSGBOX_SIGN)
}

movement SpindaIsland_Home_Clock1_Movement_PhotoManLeave {
	walk_left * 3
	face_up
}

movement SpindaIsland_Home_Clock1_Movement_PhotoManNotice {
	face_right
	delay_16
	walk_right * 3
	delay_16
}

movement SpindaIsland_Home_Clock1_Movement_PhotoManJump {
	lock_facing_direction
	jump_in_place_down
	unlock_facing_direction
}

movement SpindaIsland_Home_Clock1_Movement_ArchieJumps {
	lock_facing_direction
	jump_in_place_up
	unlock_facing_direction
	emote_exclamation_mark
}

movement SpindaIsland_Home_Clock1_FaucetBreaks {
	lock_facing_direction
	player_run_down
}

script SpindaIsland_Home_Clock3_EventScript_OldWoman {
	lock
	faceplayer
	if (!flag(FLAG_SHUPPET_QUEST_STARTED)) {
		faceplayer
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"I think my house is haunted. There's something living in my attic.\p"
			"I don't know what to do…"
		))
		removefieldmugshot
		closemessage
		setflag(FLAG_SHUPPET_QUEST_STARTED)
	} elif (!flag(FLAG_HIDE_SHUPPETS)) {
		faceplayer
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"Oh, I'm too scared to go back inside…"
		))
		removefieldmugshot
		closemessage
	}

	if (flag(FLAG_HIDE_SHUPPETS)) {
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"Oh, I'm too scared to go back inside…"
		))
		removefieldmugshot
		closemessage

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
		speakername("Little Archie")
		msgbox(format(
			"Don't worry granny! There were some Shuppet in your attic, but I chased them away!"
		))
		removefieldmugshot
		closemessage

		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_NORMAL)
		speakername("Old woman")
		msgbox(format(
			"Shuppet, you say?"
		))
		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_HAPPY)
		msgbox(format(
			"How adorable of you to help out a poor old lady…\p"
			"Please take this, for your trouble!"
		))
		removefieldmugshot
		closemessage

		// TODO EVA COMP giveitem

		createfieldmugshot(MUGSHOT_GRANDMA, EMOTE_HAPPY)
		msgbox(format(
			"Have a good day, child."
		))
		removefieldmugshot
		closemessage

		applymovement(VAR_LAST_TALKED, Common_Movement_FaceRight)
		waitmovement(0)
		removeobject(VAR_LAST_TALKED)
		playse(SE_EXIT)
		waitse
		setflag(FLAG_HIDE_OLD_WOMAN)
	}
	release
}
mapscripts SpindaIsland_Home_Clock1_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		// Broken faucet
		if (flag(FLAG_FAUCET_BROKE)) {
			setmetatile(20, 8, METATILE_SpindaIslandSecondary_BrokenFaucetWall, TRUE)
		}
	}
}

script SpindaIsland_Home_Clock1_HidingBoy {
	lock

	faceplayer
	speakername("Timmy")
	msgbox(format(
		"Darnit! You found me!"
	), MSGBOX_AUTOCLOSE)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_GRINNING)
    speakername("Little Archie")
    msgbox(format(
		"Wazzup, Timmy?"
    ))
    closemessage
	removefieldmugshot

	release
}

script SpindaIsland_Home_Clock1_Faucet {
	// Faucet already broke
	if (var(VAR_SPINDA_ISLAND_INTRO_STATE) > 2) {
		lock
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Faucet's broken…"
		))
		closemessage
		removefieldmugshot
		release
	} elif (var(VAR_SPINDA_ISLAND_INTRO_STATE) == 2) {
		hidefollower
		lock
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Alrighty, time to turn the big old faucet back on!\p"
			"Hnng…\p"
			"… … … … …"
		))
		closemessage
		removefieldmugshot

		delay(48)

		setmetatile(18, 13, METATILE_SpindaIslandSecondary_BrokenFaucetWall, TRUE)
		setmetatile(18, 14, METATILE_SpindaIslandSecondary_BrokenFaucetGround, FALSE)
		special(DrawWholeMapView)

		applymovement(OBJ_EVENT_ID_PLAYER, SpindaIsland_Home_Clock1_FaucetBreaks)

		playse(SE_M_BRICK_BREAK)
		// playse(SE_UNLOCK)
		// playse(SE_BANG)
		// playse(SE_ICE_BREAK)
		waitmovement(OBJ_EVENT_ID_PLAYER)
		waitse

		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Aw, Shuckles! It broke!\p"
			"Mom is totally gonna kill me!!\p"
			"Let's, uh, not tell her anything and fix this ourselves!\p"
			"I better pocket this, too."
		))
		closemessage
		removefieldmugshot

		setmetatile(18, 14, METATILE_SpindaIsland_Grass, FALSE)
		special(DrawWholeMapView)
		giveitem(ITEM_BROKEN_FAUCET)

		setvar(VAR_SPINDA_ISLAND_INTRO_STATE, 3)
		setflag(FLAG_FAUCET_BROKE)

		release
	}
}

movement SpindaIsland_Home_Clock1_FaucetBreaks {
	lock_facing_direction
	player_run_down
}
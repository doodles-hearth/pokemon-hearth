const LOCALID_SAD_MAN = 3

mapscripts SpindaIsland_Home_Clock2_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (flag(FLAG_SLOWPOKE_EVOLVED)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWBRO))
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(SLOWPOKE))
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SPINDA_ISLAND_INTRO_STATE, 2: SpindaIsland_Home_EventScript_MainQuestRecap
    ]
}

script SpindaIsland_Home_Clock2_EventScript_SadManHome_Front {
	lock
	if (!flag(FLAG_HIDE_MAN_CANT_GO_HOME)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Door's locked."
		))
		removefieldmugshot
		closemessage
	} else {
		if (checkitem(ITEM_RUSTY_BOLT)) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			speakername("Little Archie")
			msgbox(format(
				"I guess I should leave him alone now."
			))
			removefieldmugshot
			closemessage
		} else {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			speakername("Little Archie")
			msgbox(format(
				"Mister? Can you hear me?"
			))
			removefieldmugshot
			closemessage

			speakername("Stingy man")
			msgbox(format(
				"Go away!"
			))
			removefieldmugshot
		}
	}
	release
}

script SpindaIsland_Home_Clock2_EventScript_ICanNeverGoHome_GoHome {
	lock
	faceplayer
	createfieldmugshot(MUGSHOT_MAN, EMOTE_SAD)
	speakername("Sad man")
	msgbox(format(
		"Curse the architect that built my house facing the wrong direction!!\p"
		"I can never go home!\p"
		"… …"
	))
	removefieldmugshot
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_SAD_MAN, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)

	applymovement(LOCALID_SAD_MAN, SpindaIsland_Home_Clock2_Movement_SadManSurprise)
	waitmovement(0)

	createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
	speakername("Fulfilled man")
	msgbox(format(
		"My house! It's facing the right direction!\p"
		"But what… how…"
	))
	removefieldmugshot
	closemessage

	faceplayer
	createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
	speakername("Fulfilled man")
	msgbox(format(
		"Did… did you do something?"
	))
	
	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"I didn't do anything special…",
		"I… turned around?"
	)
	closemessage

	speakername("Fulfilled man")
	msgbox(format(
		"Don't be modest, kid!\p"
		"I don't know how you did it, but you saved me from a life of endless waiting and suffering!\p"
		"I can finally go home.\p"
		"Well, goodbye."
	))
	removefieldmugshot
	closemessage

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
	speakername("Little Archie")
	msgbox(format(
		"…"
	))
	removefieldmugshot
	closemessage

	createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
	speakername("Fulfilled man")
	msgbox(format(
		"… …"
	))
	removefieldmugshot
	closemessage

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
	speakername("Little Archie")
	msgbox(format(
		"… … …"
	))
	removefieldmugshot
	closemessage

	createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
	speakername("Fulfilled man")
	msgbox(format(
		"… … … …\p"
		"… … …{PAUSE 20}What?"
	))
	removefieldmugshot
	closemessage

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
	speakername("Little Archie")
	msgbox(format(
		"Well… Don't I get a reward or something?"
	))
	removefieldmugshot
	closemessage

	createfieldmugshot(MUGSHOT_MAN, EMOTE_NORMAL)
	speakername("Stingy man")
	msgbox(format(
		"No. I don't owe you anything.\p"
		"Don't follow me."
	))
	removefieldmugshot
	closemessage

	switch(var(VAR_FACING)) {
		case DIR_NORTH:
			applymovement(LOCALID_SAD_MAN, SpindaIsland_Home_Clock2_Movement_SadManEntersHome)
		case DIR_SOUTH:
			applymovement(LOCALID_SAD_MAN, SpindaIsland_Home_Clock2_Movement_SadManEntersHome)
		case DIR_WEST:
			applymovement(LOCALID_SAD_MAN, SpindaIsland_Home_Clock2_Movement_SadManEntersHome)
		case DIR_EAST:
			applymovement(LOCALID_SAD_MAN, SpindaIsland_Home_Clock2_Movement_SadManEntersHomeAroundPlayer)
	}
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)

	playse(SE_EXIT)
	waitse
	removeobject(LOCALID_SAD_MAN)
	setflag(FLAG_HIDE_MAN_CANT_GO_HOME)

	delay(48)

	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
	speakername("Little Archie")
	msgbox(format(
		"He's probably just in shock.\p"
		"I'm sure I can make him change his mind."
	))
	removefieldmugshot
	closemessage

	release
}

script SpindaIsland_Home_Clock2_EventScript_FindSeashell {
	lock
	createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
    speakername("Little Archie")
    msgbox(format(
		"Woah, what's this?\p"
		"This seashell's completely white! So cool!!"
    ))
	removefieldmugshot

	giveitem(ITEM_WHITE_SEASHELL)
	setflag(FLAG_FOUND_WHITE_SEASHELL)
	removeobject(VAR_LAST_TALKED)

	release
}

script SpindaIsland_Home_Clock2_EventScript_NoteBehindFaucetSign {
	msgbox(format(
		"“look down”"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_Clock2_EventScript_ExplanationSign {
	msgbox((
		"“This world is a little strange.\n"
		"When an object is turned to the side,\l"
		"you cannot interact with it.\l"
		"It has to face {COLOR RED}you{COLOR DARK_GRAY}.\l"
		"Not your body, but your very soul.\l"
		"What could be called the {COLOR RED}true you{COLOR DARK_GRAY}.”"
	), MSGBOX_SIGN)
}

script SpindaIsland_Home_EventScript_StealWheel {
	lock
	if (flag(FLAG_FAUCET_QUEST_STARTED) && !flag(FLAG_FOUND_TOUGH_BOLT)) {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Just a regular wheel."
		))
		removefieldmugshot
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_EXCITED)
		msgbox(format(
			"Wait, it's stuck to the car with bolts!\p"
			"If I could just take one…"
		))
		removefieldmugshot

		if (checkitem(ITEM_BORROWED_PLIERS)) {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			msgbox(format(
				"Here, let me just…"
			))
			removefieldmugshot
			closemessage
			
			playse(SE_CLICK)
			waitse
			playse(SE_CLICK)
			waitse

			delay(48)

			giveitem(ITEM_TOUGH_BOLT)
			setflag(FLAG_FOUND_TOUGH_BOLT)
			call(SpindaIsland_Common_CheckGotAllItemsForNewFaucet)
		} else {
			createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
			msgbox(format(
				"I'd need some pliers to get it out."
			))
			removefieldmugshot
			closemessage
		}
	} else {
		createfieldmugshot(MUGSHOT_ARCHIE, EMOTE_NORMAL)
		speakername("Little Archie")
		msgbox(format(
			"Just a regular wheel."
		))
		removefieldmugshot
		closemessage
	}
	release
}

movement SpindaIsland_Home_Clock2_Movement_SadManEntersHome {
	walk_left * 3
	walk_up * 2
}

movement SpindaIsland_Home_Clock2_Movement_SadManEntersHomeAroundPlayer {
	walk_up
	walk_left * 3
	walk_up
}

movement SpindaIsland_Home_Clock2_Movement_SadManSurprise {
	delay_16 * 3
	face_right
	delay_16 * 2
	face_down
	delay_16 * 2
	face_up
	delay_16 * 2
	face_left
	delay_16 * 2
	jump_in_place_left
	delay_16 * 3
}
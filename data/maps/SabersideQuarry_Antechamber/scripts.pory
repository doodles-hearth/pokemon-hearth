const FLAG_HIDE_PLAYER_IN_MINE_CART = FLAG_TEMP_1
const FLAG_HIDE_EMPTY_MINE_CART = FLAG_TEMP_2
const VAR_ENTERING_QUARRY = VAR_TEMP_0

mapscripts SabersideQuarry_Antechamber_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (flag(FLAG_RIDING_MINECART)) {
            setflag(FLAG_HIDE_EMPTY_MINE_CART)
            setobjectxyperm(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, 13, 14)
            setobjectmovementtype(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, MOVEMENT_TYPE_FACE_UP)
            setvar(VAR_ENTERING_QUARRY, 1)
        } else {
            setflag(FLAG_HIDE_PLAYER_IN_MINE_CART)
            setobjectmovementtype(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, MOVEMENT_TYPE_FACE_DOWN)
        }

        if (checkplayergender == MALE) {
            setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_BRENDAN_MINE_CART)
        } else {
            setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_MAY_MINE_CART)
        }
    }
	
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_ENTERING_QUARRY, 1 {
            hideobjectat(LOCALID_PLAYER, MAP_SABERSIDE_QUARRY_ANTECHAMBER)
		}
	]

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ENTERING_QUARRY, 1: SabersideQuarry_Antechamber_EventScript_EnterQuarry
    ]
}

script SabersideQuarry_Antechamber_EventScript_EnterQuarry {
    lockall
    setvar(VAR_ENTERING_QUARRY, 0)
    clearflag(FLAG_RIDING_MINECART)

    applymovement(LOCALID_PLAYER, SabersideQuarry_Antechamber_Movement_EnterQuarry)
    applymovement(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, SabersideQuarry_Antechamber_Movement_EnterQuarry)
    waitmovement(LOCALID_PLAYER)

    setobjectsubpriority(LOCALID_PLAYER, MAP_SABERSIDE_QUARRY_ANTECHAMBER, 0)
    setobjectsubpriority(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, MAP_SABERSIDE_QUARRY_ANTECHAMBER, 1)
    setobjectsubpriority(LOCALID_QUARRY_INTERIOR_MINE_CART_EMPTY, MAP_SABERSIDE_QUARRY_ANTECHAMBER, 1)

    removeobject(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER)
    addobject(LOCALID_QUARRY_INTERIOR_MINE_CART_EMPTY)

    applymovement(LOCALID_PLAYER, moves(set_visible, jump_up))
    playse(SE_LEDGE)
    waitmovement(LOCALID_PLAYER)

    setobjectxyperm(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, 13, 9)

    
    resetobjectsubpriority(LOCALID_PLAYER, MAP_SABERSIDE_QUARRY_ANTECHAMBER)
    resetobjectsubpriority(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, MAP_SABERSIDE_QUARRY_ANTECHAMBER)
    resetobjectsubpriority(LOCALID_QUARRY_INTERIOR_MINE_CART_EMPTY, MAP_SABERSIDE_QUARRY_ANTECHAMBER)

    releaseall
}

movement SabersideQuarry_Antechamber_Movement_EnterQuarry {
    walk_faster_up * 2,
    walk_fast_up * 2,
    walk_up
}

script SabersideQuarry_Antechamber_EventScript_RideMineCart {
    lockall

	msgbox(format("Jump on the mine cart?"))

	mcdialogueB("Yes", "No")

	closemessage

	if (var(VAR_RESULT) == 0) {
        setobjectsubpriority(LOCALID_PLAYER, MAP_SABERSIDE_QUARRY_ANTECHAMBER, 0)
        setobjectsubpriority(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, MAP_SABERSIDE_QUARRY_ANTECHAMBER, 1)
        setobjectsubpriority(LOCALID_QUARRY_INTERIOR_MINE_CART_EMPTY, MAP_SABERSIDE_QUARRY_ANTECHAMBER, 1)
        
        switch (var(VAR_FACING)) {
            case DIR_SOUTH:
                applymovement(LOCALID_PLAYER, moves(jump_down, set_invisible))
            case DIR_WEST:
                applymovement(LOCALID_PLAYER, moves(jump_left, set_invisible))
            case DIR_EAST:
                applymovement(LOCALID_PLAYER, moves(jump_right, set_invisible))
        }
        waitse
        playse(SE_LEDGE)
        waitmovement(LOCALID_PLAYER)
        removeobject(LOCALID_QUARRY_INTERIOR_MINE_CART_EMPTY)
        addobject(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER)

        waitse
        playbgm(MUS_CYCLING, FALSE)
        playse(SE_TRUCK_MOVE)

        applymovement(LOCALID_PLAYER, SabersideQuarry_Antechamber_Movement_RideMineCart)
        applymovement(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, SabersideQuarry_Antechamber_Movement_RideMineCart)
        waitmovement(LOCALID_PLAYER)

        
        resetobjectsubpriority(LOCALID_PLAYER, MAP_SABERSIDE_QUARRY_ANTECHAMBER)
        resetobjectsubpriority(LOCALID_QUARRY_INTERIOR_MINE_CART_PLAYER, MAP_SABERSIDE_QUARRY_ANTECHAMBER)
        resetobjectsubpriority(LOCALID_QUARRY_INTERIOR_MINE_CART_EMPTY, MAP_SABERSIDE_QUARRY_ANTECHAMBER)

        setflag(FLAG_RIDING_MINECART)
        warp(MAP_SABERSIDE_QUARRY_EXTERIOR, WARP_ID_SABERSIDE_QUARRY_ENTRANCE)
	}

	releaseall
}

movement SabersideQuarry_Antechamber_Movement_RideMineCart {
    walk_fast_down * 5
}

script SabersideQuarry_Antechamber_EventScript_FlashSign {
    msgbox(format(
        "Don't forget your torch, lantern or Pokémon!\p"
        "Getting lost in the galleries is no joke."
    ), MSGBOX_SIGN)
}

script SabersideTown_EventScript_GlowingMon {
    lock
    faceplayer

    if (flag(FLAG_RECEIVED_QUARRY_SURVIVAL_KIT)) {
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_MINER 0}{SPEAKER NAME_MINER}Don't get lost now!"
        ))
        closemessage
    } else {
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_MINER 0}{SPEAKER NAME_MINER}Hunting for treasure, are you?\nI see you're a Wielder.\p"
            "I assume you brought a Pokémon that glows in the dark?\p"
            "Here, take this, in case it faints."
        ))
        closemessage

        giveitem(ITEM_REVIVAL_BALM)

        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_MINER 0}{SPEAKER NAME_MINER}And take this, in case it faints twice!"
        ))
        closemessage

        giveitem(ITEM_ESCAPE_ROPE)

        setflag(FLAG_RECEIVED_QUARRY_SURVIVAL_KIT)
    }

    release
}

script SabersideTown_EventScript_GlowingMon2 {
    lock
    faceplayer
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_MINER 0}{SPEAKER NAME_MINER}You've got to keep a glowing Pokémon outside its Poké Ball.\p"
        "It'll naturally glow as it walks by your side!"
    ))
    closemessage

    release
}

script SabersideTown_EventScript_Lunatone {
    lock
    faceplayer
	setseenmon(SPECIES_LUNATONE)
	setspeakertomonname(SPECIES_LUNATONE)
	playmoncry(SPECIES_LUNATONE, CRY_MODE_NORMAL)
    msgbox(format(
        "Hooon!"
    ))
    waitmoncry
    release
    end
}
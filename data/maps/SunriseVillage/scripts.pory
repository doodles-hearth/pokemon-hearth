const LOCALID_FATGUY = 3
const LOCALID_GUARD = 6

const FLAG_HIDE_BIGSIS_MAP = FLAG_TEMP_5

mapscripts SunriseVillage_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        // Random birds on roofs
        setflag(FLAG_TEMP_1)
        setflag(FLAG_TEMP_2)
        setflag(FLAG_TEMP_3)
        setflag(FLAG_TEMP_4)

        gettime
        if (var(VAR_0x8000) >= 5 && var(VAR_0x8000) <= 17) {
            random(4)
            switch(var(VAR_RESULT)) {
                case 0:
                    clearflag(FLAG_TEMP_1)
                case 1:
                    clearflag(FLAG_TEMP_2)
                case 2:
                    clearflag(FLAG_TEMP_4)
                case 3:
                    clearflag(FLAG_TEMP_1)
                    clearflag(FLAG_TEMP_3)
            }
        }
    }
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_VISITED_SUNRISE_VILLAGE)
        setflag(FLAG_HIDE_BIGSIS_MAP)
    }
}

// "Science is amazing" fat guy
script SunriseVillage_Event_FatGuy {
    lock
    faceplayer

    setspeaker(SP_NAME_NEIGHBOR)
    if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) >= 4) {
        applymovement(VAR_LAST_TALKED, moves(emote_heart))
        playse(SE_PIN)
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_SUNRISE_FAT_GUY 0}My new Pokémon is so cute, I don't feel like leaving it in its Pokéball!"
        ))
        closemessage
        waitmovement(VAR_LAST_TALKED)
        applymovement(LOCALID_FATGUY, Common_Movement_FaceOriginalDirection)
    }
    else {
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_SUNRISE_FAT_GUY 0}Someone managed to fit entire Pokémon"
            "inside these tiny balls.\p"
            "The power of science is staggering!"
        ))
        closemessage
    }
    release
    end
}

script SunriseVillage_Event_BigSisMap_Wait {
    setspeaker("???")
    msgbox(format(
        "{PLAYER}, wait!"
    ))
    closemessage

    applymovement(LOCALID_PLAYER, moves(walk_in_place_fast_down))
    waitmovement(0)

    clearflag(FLAG_HIDE_BIGSIS_MAP)
    addobject(LOCALID_BIGSIS_MAP)
}

script SunriseVillage_Event_BigSisMap_Left {
    lockall

    call(SunriseVillage_Event_BigSisMap_Wait)

    applymovement(LOCALID_BIGSIS_MAP, moves(walk_up * 6))
    waitmovement(0)

    call(SunriseVillage_Event_BigSisMap_GiveMap)

    applymovement(LOCALID_BIGSIS_MAP, moves(walk_down * 6))
    waitmovement(0)

    removeobject(LOCALID_BIGSIS_MAP)
    setflag(FLAG_HIDE_BIGSIS_MAP)

    releaseall
}

script SunriseVillage_Event_BigSisMap_Middle {
    lockall

    call(SunriseVillage_Event_BigSisMap_Wait)

    applymovement(LOCALID_BIGSIS_MAP, moves(walk_up * 4, walk_right, walk_up * 2))
    waitmovement(0)

    call(SunriseVillage_Event_BigSisMap_GiveMap)

    applymovement(LOCALID_BIGSIS_MAP, moves(walk_down * 2, walk_left, walk_down * 4))
    waitmovement(0)

    removeobject(LOCALID_BIGSIS_MAP)
    setflag(FLAG_HIDE_BIGSIS_MAP)

    releaseall
}

script SunriseVillage_Event_BigSisMap_Right {
    lockall

    call(SunriseVillage_Event_BigSisMap_Wait)

    applymovement(LOCALID_BIGSIS_MAP, moves(walk_up * 7, walk_right))
    applymovement(LOCALID_PLAYER, moves(delay_16 * 6, face_left))
    waitmovement(LOCALID_BIGSIS_MAP)

    call(SunriseVillage_Event_BigSisMap_GiveMap)

    applymovement(LOCALID_BIGSIS_MAP, moves(walk_left, walk_down * 7))
    applymovement(LOCALID_PLAYER, moves(delay_16 * 2, face_down))
    waitmovement(LOCALID_BIGSIS_MAP)

    removeobject(LOCALID_BIGSIS_MAP)
    setflag(FLAG_HIDE_BIGSIS_MAP)

    releaseall
}

script SunriseVillage_Event_BigSisMap_GiveMap {
    bufferspeciesname(STR_VAR_2, VAR_STARTER_MON)

    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_BIG_SIS 0}{SPEAKER NAME_BIG_SIS}Were you trying to leave without saying goodbye? Shame on you!\p"
        "Oh, who is that lovely little thing with you?\N"
        "{STR_VAR_2}, huh?\p"
        "Looks like it already likes you a lot! You're going to make a great Wielder,\lI know it!\p"
        "… … … … … …\n… … … … … …\p"
        "So you're going on a journey…\p"
        "That's great! I'm proud of you, {PLAYER}.\p"
        "I know you'll get lost though, so I'll lend you my map of Toku!"
    ))
    closemessage
    
    giveitem(ITEM_TOKU_MAP)

    special(GetPlayerBrotherSisterString)
    setspeaker(SP_NAME_BIG_SIS)
    msgbox((
        "{CREATE_MUGSHOT MUGSHOT_BIG_SIS 0}Alrighty! You all set? Have a safe\njourney, little {STR_VAR_1}.\p"
        "Maybe I'll see you around!\p"
        "Come home sometimes though, will you?\p"
        "See you soon!\nI'll send you Chatot messages!"
    ))
    closemessage

    setnamedmon(SPECIES_CHATOT)

    setflag(FLAG_HIDE_BIGSIS_MAP)
    setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 8)
}

// Man guarding the forest while you have no Pokémon
script SunriseVillage_Event_GuardForest {
    lockall
    applymovement(LOCALID_GUARD, Common_Movement_WalkInPlaceFasterLeft)
    if (flag(FLAG_RECEIVED_STARTER)) {
        setspeaker(SP_NAME_NEIGHBOR)
        if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) >= 8) {
            msgbox(format(
                "{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_2 0}Oh, I see you received a Pokémon!\p"
                "You'll probably be able to manage the woods on your own, then.\p"
                "Be careful!"
            ))
            closemessage
        } else {
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
            msgbox(format(
                "{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_2 0}Hey, kid, a friend of yours was looking for you."
            ))
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
            waitmovement(0)
        }
    }
    else {
        setspeaker(SP_NAME_NEIGHBOR)
        msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_2 0}Stop right there, kid!"
        ))
        closemessage
        turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
        setspeaker(SP_NAME_NEIGHBOR)
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_2 0}Wild Pokémon have been lashing out recently.\p"
            "I know you're used to walking around on your own, but the woods have become a dangerous place lately!\p"
            "I wouldn't go alone if I were you.\p"
            "Or, you know, if I were me."
        ))
        closemessage
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
        waitmovement(0)
    }
    releaseall
    end
}

script SunriseVillage_Event_GuardForest_Interact {
    lock
    faceplayer
    setspeaker(SP_NAME_NEIGHBOR)
    if (flag(FLAG_RECEIVED_STARTER)) {
        if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) >= 8) {
            msgbox(format(
                "{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_2 0}Oh, I see you received a Pokémon!\p"
                "You'll probably be able to manage the woods on your own, then.\p"
                "Be careful!"
            ))
            closemessage
        } elif (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) == 7) {
            msgbox(format(
                "{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_2 0}Hey, kid, heard you were going off on an adventure!\p"
                "Did you say bye to your sister?"
            ))
            applymovement(LOCALID_GUARD, moves(emote_angry))
            msgbox(format(
                "No? Thought so. Go see her before you run off, you ungrateful sibling!"
            ))
            closemessage
        } else {
            msgbox(format(
                "{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_2 0}Hey, kid, a friend of yours was looking for you."
            ))
            closemessage
        }
    }
    else {
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_2 0}Wild Pokémon have been lashing out recently.\p"
            "Even the Ginko woods have become a dangerous place lately!"
        ))
        closemessage
    }
    release
    end
}

// Little running girl
script SunriseVillage_Event_LittleGirl {
    lock
    faceplayer
    applymovement(VAR_LAST_TALKED, moves(emote_happy))
    setspeaker(SP_NAME_LITTLE_GIRL)
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_SUNRISE_LITTLE_GIRL 0}I can't wait I can't wait I can't wait!!!\p"
        "AHHHHH I CAN'T WAIT!!!!"
    ))
    closemessage
    release
}

// Boy
script SunriseVillage_Event_Boy {
    lock
    faceplayer
    setspeaker(SP_NAME_BOY)
    if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) >= 4) {
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_SUNRISE_BOY_1 0}My Happiny is the strongest of all! See how fierce it looks?"
        ))
        closemessage

        setnamedmon(SPECIES_HAPPINY)
    }
    else {
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_SUNRISE_BOY_1 0}Hey {PLAYER}!\nYou think we'll get to choose?"
        ))
        closemessage
    }
    release
    end
}

// Woman
script SunriseVillage_Event_Woman {
    lock
    faceplayer
    setspeaker(SP_NAME_NEIGHBOR)
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_SUNRISE_WOMAN_2 0}I can see the Poké Balls from here… I've never seen so many in my life!"
    ))
    release
    end
}

// Farmer
script SunriseVillage_Event_Farmer {
    lock
    faceplayer
    playse(SE_PIN)
    applymovement(VAR_LAST_TALKED, moves(emote_upset))
    setspeaker(SP_NAME_NEIGHBOR)
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_SUNRISE_MAN_1 0}I've been waiting for hours!"
    ))
    release
    end
}

// Old man
script SunriseVillage_Event_OldMan {
    lock
    faceplayer
    setspeaker(SP_NAME_NEIGHBOR)
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_SUNRISE_OLD_MAN 0}The distribution is about to start."
    ))
    release
}

// Old woman
script SunriseVillage_Event_OldWoman {
    lock
    faceplayer
    playse(SE_PIN)
    applymovement(VAR_LAST_TALKED, moves(emote_angry))
    setspeaker(SP_NAME_NEIGHBOR)
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_SUNRISE_OLD_WOMAN 0}I'm tired of waiting!"
    ))
    release
}

// Happiny
script SunriseVillage_Event_Happiny {
    lock
    faceplayer
    playmoncry(SPECIES_HAPPINY, CRY_MODE_NORMAL)
    setseenmon(SPECIES_HAPPINY)
    applymovement(VAR_LAST_TALKED, moves(emote_music))
    setspeakertomonname(SPECIES_HAPPINY)
    msgbox(format(
        "Happi!"
    ))
    waitmoncry
    release
    end
}

// Munchlax
script SunriseVillage_Event_Munchlax {
    lock
    faceplayer
    setseenmon(SPECIES_MUNCHLAX)
    playmoncry(SPECIES_MUNCHLAX, CRY_MODE_NORMAL)
    setspeakertomonname(SPECIES_MUNCHLAX)
    msgbox(format(
        "Munch munch munch…"
    ))
    waitmoncry
    release
    end
}

// Kirlia
script SunriseVillage_Event_Kirlia {
    lock
    faceplayer
    applymovement(VAR_LAST_TALKED, moves(emote_pensive))
    setspeakertomonname(SPECIES_KIRLIA)
    msgbox(format(
        "…"
    ))
    release
    end
}

script SunriseVillage_Event_TownSign {
    msgbox(format("Sunrise Village\n“A village bathing in soft light.”"), MSGBOX_SIGN)
}

script SunriseVillage_Event_PlayersHouseSign {
    msgbox(format("{PLAYER}'s house"), MSGBOX_SIGN)
}

script SunriseVillage_Event_RivalsHouseSign {
    msgbox(format("Natsuki's house"), MSGBOX_SIGN)
}

script SunriseVillage_Event_EldersHouseSign {
    msgbox(format("Elder Kaba's house"), MSGBOX_SIGN)
}


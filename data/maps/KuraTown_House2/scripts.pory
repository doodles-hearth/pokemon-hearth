const LOCALID_ASHII = 1

mapscripts KuraTown_House2_MapScripts {
	
}

script KuraTown_House2_EventScript_Ashii {
	lock
	switch(var(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE)){
		case 0: // First encounter
			call(AshiiScript1)
			break
		case 1: // Accepted the quest
			call(AshiiScript2)
			break
		case 2: //After talking to Tama
			call(AshiiScript3)
		default:
	}
	release
}

script(local) AshiiScript1 {
	speakername("Girl")
	msgbox(format(
		"Sigh… Oh, Tama… Why must life be so complicated?"
	))
	closemessage
	delay(30)

	playse(SE_PIN)
	applymovement(LOCALID_ASHII, Common_Movement_FacePlayer)
	waitmovement(0)
	applymovement(LOCALID_ASHII, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_ASHII, Common_Movement_Delay48)
	waitmovement(0)

	speakername("Girl")
	msgbox(format(
		"Hey, what are you doing in my house? Were you eavesdropping?\p"
		"…It doesn't matter anyway. The boy I really like lives in Saku Village, but our two villages hate each other, so I can't see him anymore.\p"
		"If only there was a way for him to know my feelings…"
	))
	closemessage
	delay(30)

	playse(SE_PIN)
	applymovement(LOCALID_ASHII, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_ASHII, Common_Movement_Delay48)
	waitmovement(0)

	msgbox(format(
		"Wait, you're not from here, are you?\p"
	))
	closemessage
	speakername("Ashii")
	msgbox(format(	
		"My name is Ashii. I need your help."
		"The guard won't let anyone from Kura Village enter Saku Village. But you should be fine!\p"
		"Please, go to Saku Village and give him this. He'll know it's from me."
	))
	closemessage

	giveitem(ITEM_ASHIIS_SCARF)
	setvar(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE, 1)
	startquest(QUEST_SAKURALOVERS)
}

script(local) AshiiScript2 {
	faceplayer
	speakername("Ashii")
	msgbox(format(
		"The guard won't let anyone from Kura Village enter Saku Village. But you should be fine!\p"
		"Please, go to Saku Village and give him this. He'll know it's from me."
	))
	closemessage
}

script(local) AshiiScript3 {
	faceplayer
	speakername("Ashii")
	msgbox(format(
		"Were able to give the scarf to Tama yet? Did he get my message?"
	))
	closemessage
	dynmultichoice(0,0,TRUE,10,0,DYN_MULTICHOICE_CB_NONE, "Yep!","He sure did!","Loud and clear.")
	playse(SE_PIN)
	applymovement(LOCALID_ASHII, moves(emote_exclamation_mark, delay_16 * 2, face_away_player, delay_16 * 3, emote_heart, face_player))
	waitmovement(0)
	speakername("Ashii")
	msgbox(format(
		"I'm sorry. You've been an incredible help and I haven't even gotten your name yet. May i ask what it is?\p
		{PLAYER}… I see…\p {PLAYER}, I know this is a big thing to ask, but…\pI want to be sure that my meeting with Tama will be safe, especially if it might be our last. If so, can you please come with me when we go see each other again?"
	))
	closemessage
	dynmultichoice(0,0,TRUE,10,0,DYN_MULTICHOICE_CB_NONE, "Of course.","Who wouldn't?","You both deserve it.")
	applymovement(LOCALID_ASHII, moves(emote_exclamation_mark, delay_16 *3, jump_in_place_down, delay_4, jump_in_place_down, delay_4, jump_in_place_down, delay_4, step_end))
	waitmovement(0)
	faceplayer
	gettimeofday
	speakername("Ashii")
	if(var(VAR_RESULT) == TIME_DEAD_NIGHT) {
		msgbox(format("Thank you! Thank you! Thank you! Let's make haste, then. Before the moon takes her slumber in a few hours."))
		fadescreen(FADE_TO_BLACK)
		warp(MAP_KURA_TOWN, 48, 29)
		setvar(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE, 3)
	} else {
		msgbox(format("Thank you! Thank you! Thank you! Please come see me later tonight."))
		closemessage
		fadescreen(FADE_TO_BLACK)
		msgbox("Late that night…")
		waitmessage
		closemessage
		fadescreen(FADE_FROM_BLACK)
		pausefakertc
		fwdtime(0, 0)
		warp(MAP_KURA_TOWN, 48, 29)
		setvar(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE, 3)
	} 
	
	
	
	closemessage
}
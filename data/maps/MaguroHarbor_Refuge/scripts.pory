mapscripts MaguroHarbor_Refuge_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setrespawn(HEAL_LOCATION_MAGURO_HARBOR)
	}
}

script MaguroHarbor_Refuge_EventScript_Nurse {
	setvar(VAR_0x800A, LOCALID_CHANSEY_MAGURO)
	setvar(VAR_0x800B, LOCALID_NURSE_MAGURO)
	setvar(VAR_0x8009, MUGSHOT_SEASIDE_NURSE)
	call(Common_EventScript_PkmnCenterNurse_CutTheBullshit)
	waitmessage
	waitbuttonpress
	release
	end
}

script MaguroHarbor_Refuge_EventScript_Hariko {
	lock
	faceplayer
	if (flag(FLAG_BEAT_HARIKO_IN_MAGURO_REFUGE)) {
		setspeaker(SP_NAME_HARIKO)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HARIKO EMOTE_NORMAL}Where are you headed after getting the Maguro Dojo's Token?\p"
			"Silveridge? I heard there are enormous rock snakes burrowing through Silver Tunnel…\p"
			"If you see one, tell me about it the next time we meet."
		))
		closemessage
	}
	elif (flag(FLAG_TALKED_TO_HARIKO_IN_MAGURO_REFUGE)) {
		setspeaker(SP_NAME_HARIKO)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HARIKO EMOTE_NORMAL}Hello again, {PLAYER}. So, do you want to battle?"
		))
		closemessage

		goto(MaguroHarbor_Refuge_BattleHariko)
	} else {
		setspeaker(SP_NAME_HARIKO)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HARIKO EMOTE_NORMAL}Oh, hello there, {PLAYER}.\nFancy meeting you here, as they say.\p"
			"I assume you are here to challenge the Maguro Harbor Dojo?\p"
			"Me? I am not particularly interested in becoming a great Wielder.\p"
			"I travel to learn as much as I can about Pokémon!\p"
			"You've not met my companion yet, have you? Meet my Slowpoke, Bean!"
		))
		closemessage

		setseenmon(SPECIES_SLOWPOKE)
		setnamedmon(SPECIES_SLOWPOKE)
		setflag(FLAG_TALKED_TO_HARIKO_IN_MAGURO_REFUGE)

		applymovement(LOCALID_BEAN, moves(walk_in_place_fast_left))
		waitmovement(LOCALID_BEAN)
		delay(60)
		playse(SE_LEDGE)
		applymovement(LOCALID_SLOWPOKE, moves(jump_in_place_down))
		waitmovement(LOCALID_SLOWPOKE)
		playmoncry(SPECIES_SLOWPOKE, CRY_MODE_NORMAL)

		setspeaker(SP_NAME_BEAN)
		msgbox(format(
			"Hurr…"
		))
		closemessage

		applymovement(LOCALID_BEAN, moves(walk_in_place_fast_down))
		waitmovement(LOCALID_BEAN)

		setspeaker(SP_NAME_HARIKO)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HARIKO EMOTE_NORMAL}Bean is a little slow, but he is happy to meet you!\p"
			"He may not look like a fighter, but he enjoys a good battle, too.\p"
			"Would you like to have a friendly bout, Bean against your Pokémon?"
		))

		goto(MaguroHarbor_Refuge_BattleHariko)
	}
}

script MaguroHarbor_Refuge_BattleHariko {
	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Yes!",
		"Maybe later."
	)
	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			// TODO EVA do something like ChooseHalfPartyForBattle but for 1 mon
			trainerbattle_no_intro(
				TRAINER_HARIKO_MAGURO,
				format("Very interesting.")
			)

			setflag(FLAG_BEAT_HARIKO_IN_MAGURO_REFUGE)

			setspeaker(SP_NAME_HARIKO)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_HARIKO EMOTE_NORMAL}We lost, but Bean tells me he had fun!\nI am glad.\p"
				"Everyday we learn something new about Pokémon, be it through battling or observation.\p"
				"I brought snacks for my journey.\nIf I give you some, will you feel rewarded for this fight?"
			))
			closemessage

			giveitem(ITEM_UUMEGIRI, 4)

			setspeaker(SP_NAME_HARIKO)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_HARIKO EMOTE_NORMAL}These can be fed to your Pokémon during battle to save you from a pickle.\p"
				"Farewell, {PLAYER}."
			))
			closemessage
		case 1:
			setspeaker(SP_NAME_HARIKO)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_HARIKO EMOTE_NORMAL}No problem. I will be here a while before I continue my journey."
			))
			closemessage
	}

	release
}

script MaguroHarbor_Refuge_EventScript_NurseSleep {
	setvar(VAR_0x8009, MUGSHOT_SEASIDE_NURSE)
	call(Common_EventScript_PkmnCenterNurse_Sleep)
	waitmessage
	waitbuttonpress
	release
	end
}

script MaguroHarbor_Refuge_EventScript_Slowpoke {
	lock
	faceplayer
	setseenmon(SPECIES_SLOWPOKE)
	playmoncry(SPECIES_SLOWPOKE, CRY_MODE_NORMAL)
	if (flag(FLAG_TALKED_TO_HARIKO_IN_MAGURO_REFUGE)) {
		setspeaker(SP_NAME_BEAN)
	} else {
		setspeakertomonname(SPECIES_SLOWPOKE)
	}
	applymovement(VAR_LAST_TALKED, moves(emote_sleep))
	msgbox(format("Duh…"))
	waitmoncry
	closemessage
	release
}

script MaguroHarbor_Refuge_EventScript_DescribedMons {
	lock
	faceplayer
	setspeaker(SP_NAME_MAN)
	if (getdescribedmon(SPECIES_KRABBY)) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_BREEDER_F EMOTE_NORMAL}So, have you written many entries in your encyclopedia?"
		))
		closemessage
	} else {
		applymovement(VAR_LAST_TALKED, moves(emote_happy))
		msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_BREEDER_F EMOTE_NORMAL}Oh! You're writing a Pokémon encyclopedia? That's amazing!\p"
		"I bet you're wondering how to fill up the description of every Pokémon.\l"
		"Well, asking around definitely helps!\p"
		"For instance, Krabby sleeps burrowed inside holes in the sand, and did you know it can grow its pincers back if they fall during battle?\p"
		"Go ahead, you can write that down about it!"
	))
	closemessage
	}

	setnamedmon(SPECIES_KRABBY)
	setdescribedmon(SPECIES_KRABBY)
	release
}

script MaguroHarbor_Refuge_EventScript_Krabby {
	lock
	faceplayer
	setseenmon(SPECIES_KRABBY)
	playmoncry(SPECIES_KRABBY, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_KRABBY)
	msgbox(format("Krblblblbl…"))
	waitmoncry
	closemessage
	release
}
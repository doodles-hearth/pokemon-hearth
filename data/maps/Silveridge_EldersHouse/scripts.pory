const LOCALID_ELDER = 1
const LOCALID_OLD_MAN = 2
const LOCALID_OLD_WOMAN = 3
const LOCALID_MESSENGER = 4
const FLAG_HIDE_MESSENGER = FLAG_TEMP_1

mapscripts Silveridge_EldersHouse_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_HIDE_MESSENGER)
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_DECAY_BEACHBOUND_ROUTE, 4: Silveridge_EldersHouse_EventScript_Conversation
    ]
}

script Silveridge_EldersHouse_EventScript_EldersMon {
	lock
    faceplayer
	setseenmon(SPECIES_LAIRON)
    playmoncry(SPECIES_LAIRON, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_LAIRON)
    msgbox(format(
        "Ghurrr!"
    ))
    waitmoncry
    release
}

script Silveridge_EldersHouse_EventScript_Conversation {
	lock
	setvar(VAR_DECAY_BEACHBOUND_ROUTE, 5)

	applymovement(LOCALID_OLD_MAN, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)

	speakername("Old man")
	msgbox(format(
		"Yet another area is decaying on Beachbound Route…"
	))
	closemessage

	applymovement(LOCALID_OLD_WOMAN, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)

	speakername("Old woman")
	msgbox(format(
		"We have yet to receive tidings from the Elder in Sunrise Village…"
	))
	closemessage

	applymovement(LOCALID_ELDER, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)

	speakername("Elder Iwa")
	msgbox(format(
		"Let us not surrender to panic."
	))
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_ELDER, Common_Movement_ExclamationMark)
	waitse
	waitmovement(0)

	speakername("Elder Iwa")
	msgbox(format(
		"Excuse me… Can I help you?"
	))
	closemessage

	applymovement(LOCALID_OLD_WOMAN, Common_Movement_FaceDown)
	applymovement(LOCALID_OLD_MAN, Common_Movement_FaceDown)
	waitmovement(LOCALID_OLD_WOMAN)
	waitmovement(LOCALID_OLD_MAN)
	delay(48)

	delay(48)
	applymovement(LOCALID_OLD_WOMAN, moves(walk_down * 2))
	applymovement(LOCALID_OLD_MAN, moves(walk_right, walk_down * 3))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(delay_16, lock_facing_direction, walk_left, unlock_facing_direction))
	waitmovement(LOCALID_OLD_WOMAN)
	removeobject(LOCALID_OLD_WOMAN)
	playse(SE_EXIT)
	waitmovement(LOCALID_OLD_MAN)
	removeobject(LOCALID_OLD_MAN)
	playse(SE_EXIT)
	waitse

	setflag(FLAG_GAVE_LETTER_ELDER_SILVERIDGE)

	delay(48)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right, walk_up * 2))
	waitmovement(OBJ_EVENT_ID_PLAYER)

	speakername("Elder Iwa")
	msgbox(format(
		"Yes? I am Iwa, the Elder of Silveridge. What brings you to me?"
	))

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Give the letter"
	)
	closemessage

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up, delay_16, walk_down, face_up))
	waitmovement(OBJ_EVENT_ID_PLAYER)

	removeitem(ITEM_LETTER)

	speakername("Elder Iwa")
	msgbox(format(
		"Ah! The letter I was expecting from Kaba…\p"
		"… … … … … …\n… … … … … …\p"
		"I see…\p"
		"The wild Pokémon are in a frenzy all the way down to the Ginko Woods as well…\p"
		"The phenomenon has thus been confirmed to be happening across the whole Toku region!\p"
		"It must be somehow connected to the spreading Decay - and to the rumbling of Mt Kazan!\p"
		"The situation is more dire than we thought, and our strongest Wielders are already stretched thin.\p"
		"What to do, what to do…"
	))
	closemessage

	applymovement(LOCALID_ELDER, moves(walk_slow_left, walk_slow_right * 2, walk_slow_left, delay_16))
	waitmovement(0)

	playse(SE_EXIT)
	clearflag(FLAG_HIDE_MESSENGER)
	addobject(LOCALID_MESSENGER)
	waitse

	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
	applymovement(LOCALID_ELDER, moves(face_down, emote_exclamation_mark, delay_16 * 3))
	waitmovement(LOCALID_ELDER)
	waitse

	speakername("Girl")
	msgbox(format(
		"Elder Iwa! Elder Iwa!\p"
		"A Chatot just got in! Some Decay was discovered in Uume Forest!"
	))
	closemessage

	applymovement(LOCALID_MESSENGER, moves(delay_16, walk_in_place_fast_down))
	waitmovement(LOCALID_MESSENGER)
	playse(SE_EXIT)
	waitse
	removeobject(LOCALID_MESSENGER)

	applymovement(LOCALID_ELDER, moves(walk_in_place_fast_down))
	waitmovement(LOCALID_ELDER)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_up))

	speakername("Elder Iwa")
	msgbox(format(
		"We cannot lose a second! We need someone to go investigate Uume Forest right now!\p"
		"… … … … … …\n… … … … … …\p"
		"Young one from Sunrise Village.\nWhat is your name? {PLAYER}?\p"
		"You came all the way up here\non foot…\lYou already have a team of capable Pokémon…\lKaba has trusted you with this delivery…\p"
		"It is decided! You shall travel to Uume Forest, southeast of Tranquil Route!\p"
		"Our fate may well be in your hands, {PLAYER}.\p"
		"Before you leave, let me give you something to make your journey easier."
	))
	closemessage

	applymovement(LOCALID_ELDER, moves(walk_slow_right * 2, walk_in_place_fast_up, delay_16 * 2, walk_slow_left * 2, walk_in_place_fast_down, delay_16))
	waitmovement(LOCALID_ELDER)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up))
	waitmovement(OBJ_EVENT_ID_PLAYER)

	giveitem(ITEM_EXP_SHARE)

	applymovement(LOCALID_ELDER, moves(walk_in_place_fast_down))
	waitmovement(LOCALID_ELDER)

	speakername("Elder Iwa")
	msgbox(format(
		"I have been keeping this mysterious ancient relic for years.\p"
		"We aren't sure where nor when it originated, but it lets one Pokémon mirror the experience of another.\p"
		"It should be immensely useful in the hands of a promising young Wielder such as yourself.\p"
		"Now go, and be quick!"
	))
	closemessage

	clearflag(FLAG_KURA_DOJO_CLOSED)
	setflag(FLAG_HIDE_BONSAI_SHOP_OWNER)

	release
}

script Silveridge_EldersHouse_EventScript_Elder {
	lock
	faceplayer
	speakername("Elder Iwa")
	msgbox(format(
		"Go forth, young one."
	))
	closemessage
	release
}
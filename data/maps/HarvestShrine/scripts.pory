const FLAG_SET_RAIN = FLAG_TEMP_1

mapscripts HarvestShrine_MapScripts {
	
}

script HarvestShrine_EventScript_MonkBronzong {
	lock
	faceplayer
	setspeaker(SP_NAME_MONK)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_MONK EMOTE_NORMAL}The Bronzong you have before your eyes is a thousand years old.\p"
		"It has been bringing us rain and plentiful harvests for generations.\p"
		"Can you believe this magnificent statue is a living, breathing Pokémon?\p"
		"Actually, I'm not sure Bronzong breathe, but you get the idea."
	))
	closemessage

	setseenmon(SPECIES_BRONZONG)
	setnamedmon(SPECIES_BRONZONG)
	setdescribedmon(SPECIES_BRONZONG)

	release
}

script HarvestShrine_EventScript_Kouga {
	trainerbattle_single(
		TRAINER_KOUGA,
		format("{CREATE_MUGSHOT MUGSHOT_MONK EMOTE_NORMAL}Have you come here to honor the venerable Bronzong?\NOr are you here to stir trouble?"),
		format("By a thousand gods!"),
		HarvestShrine_EventScript_SetMonFlags
	)
	setspeaker(SP_NAME_MONK)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_MONK EMOTE_NORMAL}Fine, you may approach the ancient Bronzong. Please be respectful."
	))
	closemessage
	end
}

script HarvestShrine_EventScript_SetMonFlags {
	setnamedmon(SPECIES_BRONZONG)
}

script HarvestShrine_EventScript_MonkRain {
	lock
	faceplayer
	setspeaker(SP_NAME_MONK)
	if (checkitem(ITEM_TM_RAIN_DANCE)) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_MONK EMOTE_NORMAL}I do enjoy the sounds of the rain…"
		))
	} elif (flag(FLAG_SET_RAIN)) {
		setnamedmon(SPECIES_BRONZONG)

		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_MONK EMOTE_NORMAL}You prayed to the venerable Bronzong for rain, and He heeded your prayer!\p"
			"He doesn't listen to just anyone,\nyou know?\lThere's something about you, kid…\lTake this! I insist!"
		))
		closemessage

		giveitem(ITEM_TM_RAIN_DANCE)
	} else {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_MONK EMOTE_NORMAL}Impressive, isn't He?"
		))
	}
	closemessage
	release
}

script HarvestShrine_EventScript_Bronzong {
	lock

	if (flag(FLAG_SET_RAIN)) {
		msgbox(format(
			"… … …The statue is back to silence."
		))
	} else {
		setseenmon(SPECIES_BRONZONG)
		delay(24)

		fadeoutbgm(1)

		setvar(VAR_0x8004, 0) // vertical pan
		setvar(VAR_0x8005, 2) // horizontal pan
		setvar(VAR_0x8006, 100) // num shakes
		setvar(VAR_0x8007, 1) // shake delay
		special(ShakeCamera)

		playmoncry(SPECIES_BRONZONG, CRY_MODE_GROWL_1)
		waitmoncry
		playmoncry(SPECIES_BRONZONG, CRY_MODE_GROWL_2)
		delay(16)
		playmoncry(SPECIES_BRONZONG, CRY_MODE_GROWL_2)
		waitmoncry

		delay(48)

		setweather(WEATHER_RAIN)
		doweather

		delay(64)

		applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
		waitmovement(0)

		delay(48)

		msgbox(format(
			"… … …It started to rain."
		))
		closemessage

		fadeinbgm(1)
		setflag(FLAG_TEMP_1)
	}

	release
}
const FLAG_SET_RAIN = FLAG_TEMP_1

mapscripts HarvestShrine_MapScripts {
	
}

script HarvestShrine_EventScript_MonkBronzong {
	lock
	faceplayer
	speakername("Monk")
	msgbox(format(
		"The Bronzong you have before your eyes is a thousand years old.\p"
		"It has been bringing us rain and plentiful harvests for generations.\p"
		"Can you believe this magnificent statue is actually a living, breathing Pokémon?\p"
		"Actually, I'm not sure Bronzong breathe, but you get the idea."
	))
	closemessage

	setseenmon(SPECIES_BRONZONG)
	setnamedmon(SPECIES_BRONZONG)
	setdescribedmon(SPECIES_BRONZONG)

	release
}

script HarvestShrine_EventScript_Kouga {
	trainerbattle_single(
		TRAINER_KOUGA,
		format("Have you come here to honor the venerable Bronzong? Or are you here to stir trouble?"),
		format("By a thousand gods!"),
		HarvestShrine_EventScript_SetMonFlags
	)
	speakername("Kouga")
	msgbox(format(
		"Fine, you may approach the ancient Bronzong. Please be respectful."
	))
	closemessage
	end
}

script HarvestShrine_EventScript_SetMonFlags {
	setnamedmon(SPECIES_BRONZONG)
}

script HarvestShrine_EventScript_MonkRain {
	lock
	faceplayer
	speakername("Monk")
	if (checkitem(ITEM_TM_RAIN_DANCE)) {
		msgbox(format(
			"I do enjoy the sounds of the rain…"
		))
	} elif (flag(FLAG_SET_RAIN)) {
		setnamedmon(SPECIES_BRONZONG)

		msgbox(format(
			"You prayed to the venerable Bronzong for rain, and He heeded your prayer!\p"
			"He doesn't listen to just anyone,\nyou know?\lThere's something about you, kid…\lTake this! I insist!"
		))

		giveitem(ITEM_TM_RAIN_DANCE)
	} else {
		msgbox(format(
			"Impressive, isn't He?"
		))
	}
	release
}

script HarvestShrine_EventScript_Bronzong {
	lock

	if (flag(FLAG_SET_RAIN)) {
		msgbox(format(
			"… … …The statue is back to silence."
		))
	} else {
		setseenmon(SPECIES_BRONZONG)
		delay(24)

		fadeoutbgm(1)

		setvar(VAR_0x8004, 0) // vertical pan
		setvar(VAR_0x8005, 2) // horizontal pan
		setvar(VAR_0x8006, 100) // num shakes
		setvar(VAR_0x8007, 1) // shake delay
		special(ShakeCamera)

		playmoncry(SPECIES_BRONZONG, CRY_MODE_GROWL_1)
		waitmoncry
		playmoncry(SPECIES_BRONZONG, CRY_MODE_GROWL_2)
		delay(16)
		playmoncry(SPECIES_BRONZONG, CRY_MODE_GROWL_2)
		waitmoncry

		delay(48)

		setweather(WEATHER_RAIN)
		doweather

		delay(64)

		applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
		waitmovement(0)

		delay(48)

		msgbox(format(
			"… … …It started to rain."
		))
		closemessage

		fadeinbgm(1)
		setflag(FLAG_TEMP_1)
	}

	release
}
mapscripts KuraTown_Dojo_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		// All the trees are cut from the start if the player has already beaten the dojo
		if (flag(FLAG_BADGE02_GET)) {
			setflag(FLAG_TEMP_1)
			setflag(FLAG_TEMP_2)
			setflag(FLAG_TEMP_3)
			setflag(FLAG_TEMP_5)
			setflag(FLAG_TEMP_6)
			setflag(FLAG_TEMP_8)
			setflag(FLAG_TEMP_9)
		}
	}
}

script KuraTown_Dojo_EventScript_Matsu {
	trainerbattle_single(
		TRAINER_MATSU,
		format("Ho ho ho… It seems you have taken a wrong turn…"),
		format("Very impressive skills!")
	)

	speakername("Matsu")
	msgbox(
		format("Not all paths lead to the master."),
		MSGBOX_AUTOCLOSE
	)
	end
}

script KuraTown_Dojo_EventScript_Kiku {
	trainerbattle_single(
		TRAINER_KIKU,
		format("Is the maze giving you trouble?"),
		format("At least I didn't give you any trouble…")
	)

	speakername("Kiku")
	msgbox(format(
		"You're wondering why you can't just cut down all the trees?\p"
		"Uhhh… It's a secret!"
	), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Laura {
	trainerbattle_single(
		TRAINER_AYAME,
		format("No! Don't cut down that tree! It's my favorite tree!"),
		format("Sniffle…")
	)
	msgbox(format("It's okay, go ahead. They're all my favorite tree anyway."), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Brenden {
	trainerbattle_single(
		TRAINER_TSUBAKI,
		format("Take a deep breath! The air is so rich and flowery here!"),
		format("A-tchoo!")
	)

	speakername("Tsubaki")
	msgbox(format("Sniff… By zeazonal allergiez are agdig up agaid…"), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Cristian {
	trainerbattle_single(
		TRAINER_AJISAI,
		format("Did you know any tree can be bonsaified by pruning its leaves regularly?"),
		format("You're not here because you're particularly interested in bonsai, are you?")
	)
	msgbox(format("I feel bad sometimes, not letting the tree grow the way it wants."), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Lilith {
	trainerbattle_single(
		TRAINER_SUMIRE,
		format("Hana may look frail, but she's actually way tougher than you!"),
		format("Stop it! I'm a delicate flower!")
	)

	speakername("Sumire")
	if (flag(FLAG_BADGE02_GET)) {
		msgbox(format(
			"I saw your fight peeking through the leaves! That was amazing!\p"
			"I'm officially a fan!"
		))
		closemessage
		addvar(VAR_PLAYER_REPUTATION, 1)
	} else {
		msgbox(format("I wish I could see you two fight! Good luck!"), MSGBOX_AUTOCLOSE)
	}
	end
}

script KuraTown_Dojo_EventScript_Hana {
	speakername("Hana")
	trainerbattle_single(
		TRAINER_HANA_1,
		// TODO different dialogue if you already met
		format(
			"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Welcome to my humble Dojo. I am Hana. Did you enjoy my little maze?\p"
			"Tending to my bonsai and fighting alongside my Pokémon are my favorite pastimes. With this Dojo, I can do both!\p"
			"A Pokémon battle can be graceful like a budding flower, strong like the deepest roots, powerful like a windswept pine tree, all at once.\p"
			"I want you to show me all the grace, strength and power you are capable of!"
		),
		format("Ho ho ho…"),
		KuraTown_Dojo_EventScript_HanaDefeated,
		NO_MUSIC
	)
	// specialvar(VAR_RESULT, ShouldTryRematchBattle)
	// goto_if_eq(VAR_RESULT, TRUE, KuraTown_Dojo_EventScript_HanaRematch)

	speakername("Hana")
	msgbox(
		format(
			"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}The slow-growing tree becomes the sturdiest. Take your time on your journey, {PLAYER}."
		),
		MSGBOX_DEFAULT
	)

	release
	end
}

script KuraTown_Dojo_EventScript_HanaDefeated {
	message(format(
		"{PLAYER} received the Carved Token\n"
		"from Hana."
	))
	waitmessage
	call(Common_EventScript_PlayGymBadgeFanfare)
	speakername("Hana")
	msgbox(
		format(
			// TODO EVA boost atk?
			"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Thanks to this Token, your Pokémon will obey you without question, even those you trade with others.\p"
			"If you are interested in submitting to the Toku Council challenge, you will need to collect a Token from each of the eight Dojos scattered throughout the region.\p"
			"It is not an easy task, but I can feel determination taking root in your heart.\p"
			"Here, let me also give you this."
		),
		MSGBOX_DEFAULT
	)
	setflag(FLAG_BADGE02_GET)
	setvar(VAR_0x8008, 2)
	call(Common_EventScript_SetGymTrainers)
	call(KuraTown_Dojo_EventScript_GiveBulldoze)
	closemessage
	// TODO rematch
	// delay(30)
	// playfanfare(MUS_REGISTER_MATCH_CALL)
	// msgbox(format("Registered Hana\nin the address log."), MSGBOX_DEFAULT)
	// waitfanfare
	// closemessage
	release
	end
}

script KuraTown_Dojo_EventScript_GiveBulldoze {
	giveitem(ITEM_TM_WEATHER_BALL)
	goto_if_eq(VAR_RESULT, 0, Common_EventScript_BagIsFull)
	speakername("Hana")
	msgbox(
		format(
			"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}This scroll contains Weather Ball. Just like a tree, this technique is as strong as it is weather-dependent."
		),
		MSGBOX_DEFAULT
	)
	return
}

script KuraTown_Dojo_EventScript_Advice {
	lock
	faceplayer
	if (flag(FLAG_BADGE02_GET)) {
		speakername("Man")
		msgbox(format(
			"That was a beautiful fight! You're going to blossom into an excellent Pokémon Wielder, I can just feel it!"
		))
	} else {
		speakername("Man")
		if (isfieldmoveunlocked(FIELD_MOVE_CUT)) {
			msgbox(format(
				"Hello, young challenger, and welcome to the official Dojo of Kura Village!\p"
				"Lady Hana, the Dojo Master, is an expert in all things bonsai. She designed this place and grew these trees, of course!\p"
				"She tends to favor grass-type Pokémon, but she's got other tricks up her sleeve, too. Be on your guard!\p"
				"Now, pay attention: only some of these pretty trees can be cut down.\p"
				"Figure out which, and you'll reach the Master!"
			))
		} else {
			speakername("Man")
			if (flag(FLAG_BADGE01_GET)) {
				msgbox(format(
					"Hello, young challenger, and welcome to the official Dojo of Kura Village!\p"
					"All these trees are blocking your way, aren't they?\p"
					"You should come back once you've figured out a way to cut them!"
				))
			} else {
				speakername("Man")
				msgbox(format(
					"Hello, young challenger, and welcome to the official Dojo of Kura Village!\p"
					"All these trees are blocking your way, aren't they?\p"
					"You should come back once you've figured out a way to cut them!\p"
					"If you're itching for a challenge, why not try your skills at the Dojo in Maguro Harbor first?\p"
					"It's not very far east from here!"
				))
			}
		}
	}
	release
}

script KuraTown_Dojo_EventScript_DojoStatue {
	lockall
	if (flag(FLAG_BADGE02_GET)) {
		msgbox(
			"Dojo of Kura Village\p"
			"certified Wielders:\n"
			"Natsuki,\l"
			"{PLAYER}",
			MSGBOX_SIGN
		)
	} else {
		msgbox(
			"Dojo of Kura Village\p"
			"certified Wielders:\n"
			"Natsuki",
			MSGBOX_SIGN
		)
	}
	releaseall
}

script KuraTown_Dojo_EventScript_KuraDojoTree_CanCut {
	lock
	if (checkfieldmove(FIELD_MOVE_CUT) != PARTY_SIZE) {
		msgbox("…Oh? This tree can be cut down!", MSGBOX_DEFAULT)
		closemessage
		playse(SE_M_CUT)
		applymovement(VAR_LAST_TALKED, Movement_CutTreeDown)
		waitse
		waitmovement(0)
		removeobject(VAR_LAST_TALKED)
	} else {
		call(KuraTown_Dojo_EventScript_KuraDojoTree_CantCut)
	}
	release
}

script KuraTown_Dojo_EventScript_KuraDojoTree_CantCut {
	msgbox("A gorgeous and well-nurtured tree.", MSGBOX_SIGN)
}
mapscripts KuraTown_Dojo_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		// All the trees are cut from the start if the player has reached Hana
		if (flag(FLAG_MET_HANA_GYM)) {
			setflag(FLAG_TEMP_1)
			setflag(FLAG_TEMP_2)
			setflag(FLAG_TEMP_3)
			setflag(FLAG_TEMP_5)
			setflag(FLAG_TEMP_6)
			setflag(FLAG_TEMP_8)
			setflag(FLAG_TEMP_9)
		}
	}
}

script KuraTown_Dojo_EventScript_Matsu {
	trainerbattle_single(
		TRAINER_MATSU,
		format("{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_MAN EMOTE_NORMAL}Ho ho ho… It seems you have taken a wrong turn…"),
		format("Very impressive skills!")
	)

	setspeaker(SP_NAME_OLD_MAN)
	msgbox(
		format("{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_MAN EMOTE_NORMAL}Not all paths lead to the master."),
		MSGBOX_AUTOCLOSE
	)
	end
}

script KuraTown_Dojo_EventScript_Kiku {
	trainerbattle_single(
		TRAINER_KIKU,
		format("{CREATE_MUGSHOT MUGSHOT_AROMA_LADY EMOTE_NORMAL}Is the maze giving you trouble?"),
		format("At least I didn't give you any trouble…")
	)

	setspeaker(SP_NAME_AROMA_LADY)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_AROMA_LADY EMOTE_NORMAL}You're wondering why you can't just cut down all the trees?\l"
		"Uhhh… It's a secret!"
	), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Laura {
	trainerbattle_single(
		TRAINER_AYAME,
		format("{CREATE_MUGSHOT MUGSHOT_GARDENER EMOTE_NORMAL}No! Don't cut down that tree! It's my favorite tree!"),
		format("Sniffle…")
	)

	setspeaker(SP_NAME_GARDENER)
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_GARDENER EMOTE_NORMAL}It's okay, go ahead. They're all my favorite tree anyway."), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Brenden {
	trainerbattle_single(
		TRAINER_TSUBAKI,
		format("{CREATE_MUGSHOT MUGSHOT_LASS EMOTE_NORMAL}Take a deep breath! The air is so rich and flowery here!"),
		format("A-tchoo!")
	)

	setspeaker(SP_NAME_LASS)
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_LASS EMOTE_NORMAL}Sniff… By zeazonal allergiez are agdig up agaid…"), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Cristian {
	trainerbattle_single(
		TRAINER_AJISAI,
		format("{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_WOMAN EMOTE_NORMAL}Did you know any tree can be bonsaified by pruning its leaves regularly?"),
		format("You're not here because you're interested in bonsai, are you?")
	)

	setspeaker(SP_NAME_OLD_LADY)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAKURA_OLD_WOMAN EMOTE_NORMAL}I feel bad sometimes, not letting the tree grow the way it wants."
	), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Lilith {
	trainerbattle_single(
		TRAINER_SUMIRE,
		format("{CREATE_MUGSHOT MUGSHOT_GARDENER EMOTE_NORMAL}Hana may look frail, but she's actually way tougher than you!"),
		format("Stop it! I'm a delicate flower!")
	)

	setspeaker(SP_NAME_GARDENER)
	if (flag(FLAG_BADGE02_GET)) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_GARDENER EMOTE_NORMAL}I saw your fight peeking through the leaves! That was amazing!\l"
			"I'm officially a fan!"
		))
		closemessage
		addvar(VAR_PLAYER_REPUTATION, 1)
	} else {
		msgbox(format("{CREATE_MUGSHOT MUGSHOT_GARDENER EMOTE_NORMAL}I wish I could see you two fight!\nGood luck!"), MSGBOX_AUTOCLOSE)
	}
	end
}

script KuraTown_Dojo_EventScript_Hana {
	setflag(FLAG_MET_HANA_GYM)
	trysetchatotpostactive(POST_NATSUKI_RARE_CANDY)

	// Zomg, the bonsai shop owner is ALSO the dojo master?!
	if (flag(FLAG_MET_HANA_BONSAI_SHOP)) {
		setspeaker(SP_NAME_HANA)
		trainerbattle_single(
			TRAINER_HANA_1,
			format(
				"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Welcome to my humble Dojo. I am Hana.\nDid you enjoy my little maze?\p"
				"You must be wondering what a Bonsai seller from Saku Village is doing in the Kura Village Dojo.\p"
				"I'd prefer you don't tell anyone.\nSee, the two towns have a beef with one another.\p"
				"A Pokémon battle can be graceful like a budding flower, strong like the deepest roots, powerful like a windswept pine tree, all at once.\p"
				"Show me all the grace, strength and power you are capable of!"
			),
			format("Ho ho ho…"),
			KuraTown_Dojo_EventScript_HanaDefeated,
			NO_MUSIC
		)
		// Player doesn't know she's also the bonsai shop owner yet
	} else {
		setspeaker(SP_NAME_HANA)
		trainerbattle_single(
			TRAINER_HANA_1,
			(
				"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Welcome to my humble Dojo. I am Hana.\nDid you enjoy my little maze?\p"
				"Growing bonsai and battling with my\nPokémon are my favorite pastimes.\lWith this Dojo, I can do both!\p"
				"A Pokémon battle can be graceful like a\nbudding flower, strong like the\ldeepest roots, powerful like a\lwindswept pine tree, all at once.\p"
				"Show me all the grace, strength and\npower you are capable of!"
			),
			format("Ho ho ho…"),
			KuraTown_Dojo_EventScript_HanaDefeated,
			NO_MUSIC
		)
	}
	
	// specialvar(VAR_RESULT, ShouldTryRematchBattle)
	// goto_if_eq(VAR_RESULT, TRUE, KuraTown_Dojo_EventScript_HanaRematch)

	setspeaker(SP_NAME_HANA)
	msgbox(
		format(
			"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}The slow-growing tree becomes the sturdiest.\p"
			"Take your time on your journey, {PLAYER}."
		),
		MSGBOX_DEFAULT
	)

	release
	end
}

script KuraTown_Dojo_EventScript_HanaDefeated {
	// removespeakername
	message(format(
		"{PLAYER} received the Carved Token\n"
		"from Hana."
	))
	waitmessage
	call(Common_EventScript_PlayGymBadgeFanfare)
	setspeaker(SP_NAME_HANA)
	msgbox(
		(
			"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}If you want to submit to the Toku\nCouncil challenge, you will need a\lToken from the region's eight Dojos.\p"
			"Arduous task! But I feel determination\ntaking root in your heart.\p"
			"Here, let me also give you this."
		),
		MSGBOX_DEFAULT
	)
	closemessage
	setflag(FLAG_BADGE02_GET)
	setvar(VAR_0x8008, 2)
	call(Common_EventScript_SetGymTrainers)
	call(KuraTown_Dojo_EventScript_GiveTm)
	closemessage
	// TODO rematch
	// delay(30)
	// playfanfare(MUS_REGISTER_MATCH_CALL)
	// msgbox(format("Registered Hana\nin the address log."), MSGBOX_DEFAULT)
	// waitfanfare
	// closemessage
	release
	end
}

script(local) KuraTown_Dojo_EventScript_GiveTm {
	giveitem(ITEM_TM_WEATHER_BALL)
	setspeaker(SP_NAME_HANA)
	msgbox(
		format(
			"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}This scroll contains Weather Ball.\p"
			"Just like a tree, this technique is as strong as it is weather-dependent."
		),
		MSGBOX_DEFAULT
	)
	return
}

script KuraTown_Dojo_EventScript_Advice {
	lock
	faceplayer
	if (flag(FLAG_BADGE02_GET)) {
		setspeaker(SP_NAME_MAN)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SAKURA_MAN_2 EMOTE_NORMAL}That was a beautiful fight!\nYou'll blossom into an excellent Pokémon Wielder, I can just feel it!"
		))
	} else {
		setspeaker(SP_NAME_MAN)
		if (isfieldmoveunlocked(FIELD_MOVE_CUT)) {
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAKURA_MAN_2 EMOTE_NORMAL}Hello, young challenger, and welcome to the Kura Village Dojo!\p"
				"Lady Hana, the Dojo Master, is an expert in all things bonsai.\p"
				"She designed this place and, of course, grew these trees herself!\p"
				"Now, pay attention: only some of these noble trees can be cut down.\p"
				"Figure out which, and you'll reach the Master!\p"
				"By the way - Hana's a weather expert, and a formidable adversary.\p"
				"Be on your guard!\nMaybe get an umbrella, I don't know!"
			))
		} else {
			setspeaker(SP_NAME_MAN)
			if (flag(FLAG_BADGE01_GET)) {
				msgbox(format(
					"{CREATE_MUGSHOT MUGSHOT_SAKURA_MAN_2 EMOTE_NORMAL}Hello, young challenger, and welcome to the Kura Village Dojo!\p"
					"All these trees are blocking your way, aren't they?\p"
					"You should come back once you've figured out a way to cut them!"
				))
			} else {
				msgbox(format(
					"{CREATE_MUGSHOT MUGSHOT_SAKURA_MAN_2 EMOTE_NORMAL}Hello, young challenger, and welcome to the Kura Village Dojo!\p"
					"All these trees are blocking your way, aren't they?\p"
					"You should come back once you've figured out a way to cut them!\p"
					"If you're itching for a challenge, why not try your skills at the Dojo in Maguro Harbor first?\p"
					"It's not very far east from here!"
				))
			}
		}
	}
	release
}

script KuraTown_Dojo_EventScript_DojoStatue {
	lockall
	if (flag(FLAG_BADGE02_GET)) {
		msgbox(
			"Kura Village Dojo\p"
			"Certified Wielders:\n"
			"Natsuki,\l"
			"{PLAYER}",
			MSGBOX_SIGN
		)
	} else {
		msgbox(
			"Kura Village Dojo\p"
			"Certified Wielders:\n"
			"Natsuki",
			MSGBOX_SIGN
		)
	}
	releaseall
}

script KuraTown_Dojo_EventScript_KuraDojoTree_CanCut {
	lock
	if (checkfieldmove(FIELD_MOVE_CUT, TRUE) != PARTY_SIZE) {
		msgbox("…Oh? This tree can be cut down!", MSGBOX_DEFAULT)
		closemessage
		playse(SE_M_CUT)
		applymovement(VAR_LAST_TALKED, Movement_CutTreeDown)
		waitse
		waitmovement(0)
		removeobject(VAR_LAST_TALKED)
	} else {
		call(KuraTown_Dojo_EventScript_KuraDojoTree_CantCut)
	}
	release
}

script KuraTown_Dojo_EventScript_KuraDojoTree_CantCut {
	msgbox("A gorgeous and well-nurtured tree.", MSGBOX_SIGN)
}
const FLAG_HIDE_NATSUKI_HARIKO = FLAG_TEMP_1

mapscripts Beachbound_Route_Decay_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_DECAY_BEACHBOUND_ROUTE, 1: Beachbound_Route_Decay_SeeDecay
    ]
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_HIDE_NATSUKI_HARIKO)
	}
}

script Beachbound_Route_Decay_SeeDecay {
	lock
	setvar(VAR_DECAY_BEACHBOUND_ROUTE, 2)

	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitse
	waitmovement(0)
	delay(48)

	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left * 20, delay_16 * 5))
	waitmovement(0)

	// TODO fadescreen fucks with DNS tint
	fadescreenswapbuffers(FADE_TO_BLACK)

	setvar(VAR_0x8004, 73)
    setvar(VAR_0x8005, 17)
    special(TeleportCamera)

	special(RemoveCameraObject)

	special(DrawWholeMapView)

	fadescreenswapbuffers(FADE_FROM_BLACK)

	clearflag(FLAG_HIDE_NATSUKI_HARIKO)
	addobject(LOCALID_BEACHBOUND_DECAY_NATSUKI)
	addobject(LOCALID_BEACHBOUND_DECAY_HARIKO)

	release
}

script BeachboundRoute_Decay_EventScript_Shepherd {
	setspeaker(SP_NAME_SHEPHERD)
	msgbox(format(
		"What's goin' on? Why are all the plants rotting?"
	), MSGBOX_NPC)
}

script BeachboundRoute_Decay_EventScript_Karu {
	setspeaker(SP_NAME_LASS)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_LASS 0}I'm scared…"
	), MSGBOX_NPC)
}

script BeachboundRoute_Decay_EventScript_Kin {
	setspeaker(SP_NAME_BATTLE_GIRL)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_BATTLE_GIRL 0}What's happening?"
	), MSGBOX_NPC)
}

script BeachboundRoute_Decay_EventScript_BeanDialogue {
	setvar(VAR_DECAY_BEACHBOUND_ROUTE, 4)
	setspeaker(SP_NAME_HARIKO)
	if (flag(FLAG_TALKED_TO_HARIKO_IN_MAGURO_REFUGE)) {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HARIKO 0}Oh! Hello again, {PLAYER}!\n"
			"As you can see, I am investigating."
		))
	} else {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HARIKO 0}Oh! Hello.\n{PLAYER}, right?\p"
			"I am Hariko, remember me? We had a chat in the Ginko Woods.\p"
			"As you can see, I am investigating."
		))
	}

	mcdialogue(
		"What's happening?",
		"Investigating?"
	)
	closemessage

	setspeaker(SP_NAME_HARIKO)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_HARIKO 0}Well, look around you! This whole area is decaying.\p"
		"Whatever this is, it is bad.\nI need to learn more about this.\p"
		"Did you also come here to investigate, {PLAYER}?\lWe should team up!"
	))
	closemessage

	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(walk_right * 4))
	waitmovement(0)

	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, Common_Movement_ExclamationMark)
	waitse
	waitmovement(0)
	delay(48)

	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(walk_right * 5, walk_in_place_fast_down))
	waitmovement(0)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_up))

	setspeaker(SP_NAME_NATSUKI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}{PLAYER}!\nWhat the heck is going on here?\lEverything is rotting!"
	))
	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(emote_sad))
	msgbox(format(
		"Stepping in that slimy black stuff made me awfully nauseous…"
	))
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(emote_question_mark, delay_16 * 6))
	waitmovement(LOCALID_BEACHBOUND_DECAY_NATSUKI)

	setspeaker(SP_NAME_NATSUKI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Wait, who are you?"
	))

	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(walk_in_place_fast_up))
	waitmovement(LOCALID_BEACHBOUND_DECAY_HARIKO)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_left))
	waitmovement(OBJ_EVENT_ID_PLAYER)

	setspeaker(SP_NAME_HARIKO)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_HARIKO 0}I am Hariko.\n{PLAYER} and I are friends.\pTogether, we are going to find the cause of this Decay!"
	))

	mcdialogue(
		"That's right!",
		"We are?"
	)

	delay(24)

	switch(var(VAR_RESULT)) {
		case 1:
			applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(walk_in_place_fast_right))
			waitmovement(LOCALID_BEACHBOUND_DECAY_HARIKO)

			setspeaker(SP_NAME_HARIKO)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_HARIKO 0}I may… have gotten ahead of myself.\p"
				"You agree though, right?\nGreat!"
			))
	}

	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(walk_in_place_fast_up))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
	waitmovement(OBJ_EVENT_ID_PLAYER)

	setspeaker(SP_NAME_HARIKO)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_HARIKO 0}Your name is Natsuki?\nYou are a friend of {PLAYER}'s too…\p"
		"That makes us friends as well!"
	))

	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(walk_in_place_fast_down))
	waitmovement(LOCALID_BEACHBOUND_DECAY_NATSUKI)

	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(emote_sweat))
	setspeaker(SP_NAME_NATSUKI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_NATSUKI 0}Is… Is that so?"
	))
	closemessage

	applymovement(LOCALID_BEACHBOUND_DECAY_SAMURAI_COLLEAGUE, moves(face_down, delay_16 * 2))
	waitmovement(LOCALID_BEACHBOUND_DECAY_SAMURAI_COLLEAGUE)
	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_SAMURAI, moves(face_down, delay_16, emote_exclamation_mark, delay_16 * 3))
	waitmovement(LOCALID_BEACHBOUND_DECAY_SAMURAI)
	waitse

	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, Common_Movement_FaceUp)
	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, Common_Movement_FaceUp)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)

	applymovement(LOCALID_BEACHBOUND_DECAY_SAMURAI, moves(walk_down * 2))

	delay(16)

	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(walk_right, walk_down, face_up))

	waitmovement(LOCALID_BEACHBOUND_DECAY_SAMURAI)

	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}What's a bunch of children doing here?\p"
		"You really shouldn't stay here.\nThe situation is under control,\lbut this is no place for you kids!"
	))

	mcdialogue(
		"What's going on?",
		"We can help!"
	)
	closemessage

	switch (var(VAR_RESULT)) {
		case 0:
			waitse
			playse(SE_PIN)
			applymovement(LOCALID_BEACHBOUND_DECAY_SAMURAI, moves(emote_upset))
			setspeaker(SP_NAME_SAMURAI)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}Nothing that concerns you!\n"
				"Let the grownups handle this!\p"
				"On you go!"
			))
			closemessage
		case 1:
			setspeaker(SP_NAME_SAMURAI)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}Help? That's adorable."
			))
			closemessage

			applymovement(LOCALID_BEACHBOUND_DECAY_SAMURAI, moves(walk_in_place_fast_up))
			waitmovement(LOCALID_BEACHBOUND_DECAY_SAMURAI)

			setspeaker(SP_NAME_SAMURAI)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}Hear that?\nKids here are saying they can help!\p"
				"Hahaha!"
			))
			closemessage

			applymovement(LOCALID_BEACHBOUND_DECAY_SAMURAI, moves(walk_in_place_fast_down, delay_16 * 2))
			waitmovement(LOCALID_BEACHBOUND_DECAY_SAMURAI)

			setspeaker(SP_NAME_SAMURAI)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}In all seriousness though, move along and let the grownups handle this!\p"
				"On you go!"
			))
			closemessage
	}

	applymovement(LOCALID_BEACHBOUND_DECAY_SAMURAI, moves(walk_up * 2, delay_16))
	waitmovement(LOCALID_BEACHBOUND_DECAY_SAMURAI)

	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{FONT_SMALL}Mumble mumble…{PAUSE 32}\n"
		"Psst psst…{PAUSE 32} Mumble mumble…{PAUSE 32}"
	))

	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(walk_in_place_fast_right))
	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(walk_in_place_fast_left))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_left, delay_16 * 2, walk_in_place_fast_right, delay_16 * 2))
	waitmovement(OBJ_EVENT_ID_PLAYER)
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_music))
	waitmovement(OBJ_EVENT_ID_PLAYER)

	delay(48)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(lock_facing_direction, walk_up, delay_16 * 2, walk_up, unlock_facing_direction))
	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(delay_16, lock_facing_direction, walk_up, delay_16 * 2, walk_up, unlock_facing_direction))
	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(delay_8, lock_facing_direction, walk_up, delay_16 * 2, walk_up, unlock_facing_direction))

	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{FONT_SMALL}Mumble mumble…{PAUSE 32}\n"
		"Psst psst…{PAUSE 32}{FONT_NORMAL} Mumble mumble…{PAUSE 32}"
	))

	waitmovement(0)

	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}…need more Samurai to secure the perimeter…\p"
		"…started less than a week ago…\p"
		"…spreading over several areas…"
	))
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_SAMURAI, moves(emote_exclamation_mark, delay_16 * 6, face_down))
	waitmovement(LOCALID_BEACHBOUND_DECAY_SAMURAI)

	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}Are you kidding me?"
	))

	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(face_up))
	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(face_up))
	waitmovement(0)

	delay(32)

	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_SAMURAI, moves(emote_angry))
	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}Have your parents never taught you\nnot to eavesdrop?\p"
		"Move along!"
	))
	closemessage
	
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_fast_left * 8))
	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(walk_fast_left * 8))
	applymovement(LOCALID_BEACHBOUND_DECAY_NATSUKI, moves(walk_fast_left * 8))
	
	delay(48)

	warpsilent(MAP_KURA_TOWN, 52, 14)

	release
}

script BeachboundRoute_Decay_EventScript_BeanTrigger2StepsAway {
	lock
	
	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(face_right, emote_exclamation_mark, delay_16 * 3))

	delay(24)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))

	waitse
	waitmovement(LOCALID_BEACHBOUND_DECAY_HARIKO)

	delay(48)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 2))
	waitmovement(0)

	call(BeachboundRoute_Decay_EventScript_BeanDialogue)
}

script BeachboundRoute_Decay_EventScript_BeanTrigger1StepAway {
	lock
	
	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(face_right, emote_exclamation_mark, delay_16 * 3))

	delay(24)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))

	waitse
	waitmovement(LOCALID_BEACHBOUND_DECAY_HARIKO)

	delay(48)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left))
	waitmovement(0)

	call(BeachboundRoute_Decay_EventScript_BeanDialogue)
}

script BeachboundRoute_Decay_EventScript_BeanTrigger {
	lock

	playse(SE_PIN)
	applymovement(LOCALID_BEACHBOUND_DECAY_HARIKO, moves(face_right, emote_exclamation_mark, delay_16 * 3))

	delay(24)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))

	waitse
	waitmovement(LOCALID_BEACHBOUND_DECAY_HARIKO)

	delay(48)

	call(BeachboundRoute_Decay_EventScript_BeanDialogue)
}

script BeachboundRoute_Decay_EventScript_Bean {
	lock
	waitse
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, moves(emote_pensive))
	setspeaker(SP_NAME_HARIKO)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_HARIKO 0}… …"
	))
	closemessage

	release
}

script BeachboundRoute_Decay_EventScript_Koishi {
	lock
	faceplayer
	waitse
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, moves(emote_sad))
	setspeaker(SP_NAME_KOISHI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_KOISHI 0}This is very worrying…\p"
		"Please don't get into trouble, we're already spread thin as it is!"
	))
	closemessage

	release
}

script BeachboundRoute_Decay_EventScript_SamuraiBeCarefulTrigger {
	lock
	setvar(VAR_DECAY_BEACHBOUND_ROUTE, 3)

	applymovement(LOCALID_BEACHBOUND_DECAY_FIRST_SAMURAI, Common_Movement_WalkInPlaceFasterDown)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(LOCALID_BEACHBOUND_DECAY_FIRST_SAMURAI)
	waitmovement(OBJ_EVENT_ID_PLAYER)

	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}Please be careful if you're crossing over the {COLOR RED}decayed{COLOR DARK_GRAY} area.\p"
		"We don't want anyone getting hurt!"
	))
	closemessage
	release
}

script BeachboundRoute_Decay_EventScript_SamuraiBeCareful {
	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}Please be careful!"
	), MSGBOX_NPC)
}

script BeachboundRoute_Decay_EventScript_Samurai {
	lock
	faceplayer
	waitse
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, moves(emote_sad))
	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}Don't panic!\nDon't touch the Decay!"
	))
	closemessage
	release
}

script BeachboundRoute_Decay_EventScript_SamuraiWhatCouldBe {
	lock
	faceplayer
	waitse
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, moves(emote_upset))
	setspeaker(SP_NAME_SAMURAI)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAMURAI 0}What could be the cause of this?!"
	))
	closemessage
	release
}
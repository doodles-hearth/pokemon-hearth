const LOCALID_ASSISTANT = 1

mapscripts SakuTown_BonsaiShop_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: SakuTown_EventScript_SetupNpcs
}

script SakuTown_EventScript_SetupNpcs {
	// When the owner is behind the counter, her assistant wanders around
	if (!flag(FLAG_HIDE_BONSAI_SHOP_OWNER)) {
		setobjectmovementtype(LOCALID_ASSISTANT, MOVEMENT_TYPE_WANDER_AROUND)
		setobjectxyperm(LOCALID_ASSISTANT, 9, 3)
	}
}

script SakuTown_BonsaiShop_EventScript_ClayPots {
	msgbox(format(
		"Some clay pots, neatly stacked."
	), MSGBOX_AUTOCLOSE)
}

script SakuTown_BonsaiShop_EventScript_Bonsai {
	msgbox(format(
		"It's a bonsai. It has obviously been nurtured with love."
	), MSGBOX_AUTOCLOSE)
}

script SakuTown_BonsaiShop_EventScript_Water {
	msgbox(format(
		"It's filled to the brim with rainwater."
	), MSGBOX_AUTOCLOSE)
}

script SakuTown_BonsaiShop_EventScript_FlowerBasket {
	msgbox(format(
		"A basket of freshly picked flowers."
	), MSGBOX_AUTOCLOSE)
}

script SakuTown_BonsaiShop_EventScript_WateringCan {
	if (flag(FLAG_RECEIVED_WAILMER_PAIL)) {
		msgbox(format(
			"A cute Wailmer-shaped watering can.\nYou have one exactly like it!"
		), MSGBOX_AUTOCLOSE)
	} else {
		if (getseenmon(SPECIES_WAILMER)) {
			msgbox(format(
				"A cute Wailmer-shaped watering can."
			), MSGBOX_AUTOCLOSE)
		} else {
			msgbox(format(
				"A cute watering can shaped like a Pokémon."
			), MSGBOX_AUTOCLOSE)
		}
	}
}

script SakuTown_BonsaiShop_EventScript_Painting {
	msgbox(format(
		"A dainty painting of a flower."
	), MSGBOX_AUTOCLOSE)
}

script SakuTown_BonsaiShop_EventScript_LittleSisterGiveBerry {
	lock
	faceplayer
	dotimebasedevents
	if (flag(FLAG_DAILY_FLOWER_SHOP_RECEIVED_BERRY)) {
		setspeaker(SP_NAME_LITTLE_GIRL)
		msgbox(format("{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}Go plant the berry I gave you!"))
		closemessage
	} else {
		setspeaker(SP_NAME_LITTLE_GIRL)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}Have you met my twin sister?\nVery “Type A”, right?\p"
			"You should try beating her at her own game!\p"
			"Here, start by planting this!"
		))
		closemessage
		random(8)
		addvar(VAR_RESULT, FIRST_BERRY_INDEX)
		giveitem(VAR_RESULT)
		setflag(FLAG_DAILY_FLOWER_SHOP_RECEIVED_BERRY)

		setspeaker(SP_NAME_LITTLE_GIRL)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}Go for them berries!\nEyes on the prize!"
		))
		closemessage
	}
	release
}

script SakuTown_BonsaiShop_EventScript_LittleSister {
	lock
	faceplayer

	getgamestat(GAME_STAT_PLANTED_BERRIES)

	if (flag(FLAG_RECEIVED_TM_ACROBATICS)) {
		buffernumberstring(STR_VAR_2, VAR_RESULT)
		setspeaker(SP_NAME_LITTLE_GIRL)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}What's up, berry master?\p"
			"Didja know you've planted {STR_VAR_2} berries so far?"
		))
	} else {
		special(GetPlayerMrMrsString)
		setspeaker(SP_NAME_LITTLE_GIRL)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}Hi there {STR_VAR_1} Grownup!\pHave you planted many berries on your travels?"
		))

		if (var(VAR_RESULT) < 10) {
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}Hmm…\p"
				"Your face still has “berry amateur” written all over it.\p"
				"Come back when you're less of an embarrassment."
			))
			closemessage
		} elif (var(VAR_RESULT) < 20) {
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}Hmm…\p"
				"I'd say it's less terrible than before.\p"
				"Still not great though. You need to plant way more berries."
			))
			closemessage
		} elif (var(VAR_RESULT) < 30) {
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}Hmm…\p"
				"Hey, I'm actually impressed at the berry skills I'm sensing!\p"
				"… …"
				"Kidding!\p"
				"You're getting better at it though."
			))
			closemessage
		} elif (var(VAR_RESULT) < 40) {
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}Hmm…\p"
				"I'd say you're almost there…\p"
				"There's a fine line between being “almost good” at berries, and being “actually good” at berries.\p"
				"So, you know, keep it up, or whatever."
			))
			closemessage
		} elif (var(VAR_RESULT) >= 40) {
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}Hmm…\p"
				"Oh?\p"
				"Now that's what I'm talking about!\p"
				"Congrats, {STR_VAR_1} Grownup. By achieving berry mastery, you have earned my respect.\p"
				"Take this!"
			))
			closemessage

			giveitem(ITEM_TM_ACROBATICS)
			goto_if_eq(VAR_RESULT, 0, Common_EventScript_BagIsFull)
			setspeaker(SP_NAME_LITTLE_GIRL)
			msgbox(
				format(
					"{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_GIRL EMOTE_NORMAL}This scroll contains Acrobatics.\p"
					"Your Pokémon will strike harder if he isn't holding an item.\p"
					"Good match for a Pokémon holding a berry, right?\p"
					"First eat a little snack, then mercilessly crush your opponent!"
				)
			)
			closemessage
			setflag(FLAG_RECEIVED_TM_ACROBATICS)
		}
	}

	release
}

script SakuTown_BonsaiShop_EventScript_Assistant {
	lock
	faceplayer

	// Already visited shop
	if (var(VAR_BONSAI_SHOP_STATE) == 1) {
		// Assistant is alone
		if (flag(FLAG_HIDE_BONSAI_SHOP_OWNER)) {
			setspeaker(SP_NAME_WOMAN)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_AROMA_LADY EMOTE_NORMAL}Hi, {PLAYER}!\p"
				"The shop owner is currently away. She's very busy.\p"
				"Busy with what, you ask?\p"
				"Um, I'm actually not sure."
			))
			closemessage
			// Owner is there too
		} else {
			setspeaker(SP_NAME_WOMAN)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_AROMA_LADY EMOTE_NORMAL}Hi, {PLAYER}! Don't mind my sisters. They're quite the handful!"
			))
			closemessage
		}
	}
	// First time visiting shop
	else {
		// Already met on Tranquil route
		if (flag(FLAG_RECEIVED_WAILMER_PAIL)) {
			setspeaker(SP_NAME_WOMAN)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_AROMA_LADY EMOTE_NORMAL}Hello, welcome to our bonsai shop…"
			))
			playse(SE_PIN)
			applymovement(1, Common_Movement_ExclamationMark)
			waitmovement(0)
			applymovement(1, Common_Movement_Delay48)
			waitmovement(0)
			setspeaker(SP_NAME_WOMAN)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_AROMA_LADY EMOTE_NORMAL}Oh, but I know you! We met on Tranquil route, remember?\p"
				"Have you used the watering can I gave you yet?\p"
				"Oh, yes, I work here. We sell bonsai and other potted flowers!\p"
				"I'm the owner's assistant! I also manage the shop whenever she's away."
			))
			closemessage
		}
		// Very first encounter (missed her on Tranquil route)
		else {
			setspeaker(SP_NAME_WOMAN)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_AROMA_LADY EMOTE_NORMAL}Hello, welcome to our bonsai shop! I'm the owner's assistant.\p"
				"We sell bonsai and other potted flowers! Are you interested in plants?\p"
				"Have you ever seen strange patches of soil while on your journey?\p"
				"Those soil patches are incredibly fertile, which means any berry you plant in that soil will thrive and grow into a tree, ready to be harvested!\p"
				"I see you're a Wielder. Well, different berries have different effects when they're fed to Pokémon.\p"
				"I love berries, hopefully you'll soon learn to love them too!\p"
				"Here, try planting a few!"
			))
			closemessage
			giveitem(ITEM_ORAN_BERRY, 3)
			goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)

			setspeaker(SP_NAME_WOMAN)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_AROMA_LADY EMOTE_NORMAL}Oh, right, I was almost forgetting!\p"
				"You must remember to water them regularly, or they won't grow!\p"
				"I like you. Take this!"
			))
			closemessage
			giveitem(ITEM_WAILMER_PAIL)
			goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)

			setspeaker(SP_NAME_WOMAN)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_AROMA_LADY EMOTE_NORMAL}Isn't it cute? It's friend-shaped!\p"
				"Don't worry about it, I have a spare one.\p"
				"Have fun gardening!"
			))
			setflag(FLAG_RECEIVED_WAILMER_PAIL)
		}
		setvar(VAR_BONSAI_SHOP_STATE, 1)
	}

	release
}

script(local) SakuTown_BonsaiShop_EventScript_Owner_RegularDialogue {
	if (flag(FLAG_RECEIVED_SECRET_POWER)) {
			setspeaker(SP_NAME_HANA)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Hello, {PLAYER}. Can I interest you in a little greenery?"
			))
			closemessage
			
			pokemartdecoration(BonsaiShop)

			setspeaker(SP_NAME_HANA)
			msgbox("{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Thank you for your business!", MSGBOX_DEFAULT)
			closemessage
		} else {
			setspeaker(SP_NAME_HANA)
			msgbox(format(
				"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Hello, {PLAYER}. Can I interest you in a little greenery?\p"
				"Hmm… It looks like you wouldn't have anywhere to put bonsai. Come back when you do."
			))
			closemessage
		}
}

script(local) SakuTown_BonsaiShop_EventScript_Owner_DiscoverGymLeader {
	setflag(FLAG_MET_HANA_BONSAI_SHOP)
	setspeaker(SP_NAME_HANA)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Hello, young {STR_VAR_1}. Can I interest you in a little greenery?"
	))

	mcdialogue(
		"Hana?"
		"Have we met?"
	)
	closemessage

	setspeaker(SP_NAME_HANA)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Ho ho, I have been discovered…\p"
		"You must be wondering what the Kura Village Dojo Master is doing in a Bonsai shop in Saku Village.\p"
		"I'd prefer you don't tell anyone.\nSee, the two towns have a beef with one another.\p"
		"This senseless enmity pains me.\nI wish something could be done.\p"
		"If only the villagers saw they are more similar than they are different…"
	))

	closemessage
}

script SakuTown_BonsaiShop_EventScript_Owner {
	lock
	faceplayer

	// Never met as gym leader
	if (!flag(FLAG_MET_HANA_GYM)) {
		setflag(FLAG_MET_HANA_BONSAI_SHOP)

		special(GetPlayerBoyGirlString)
		setspeaker(SP_NAME_SHOP_OWNER)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_HANA EMOTE_NORMAL}Hello, young {STR_VAR_1}. Can I interest you in a little greenery?\p"
			"Hmm… It looks like you wouldn't have anywhere to put bonsai. Come back when you do."
		))
		closemessage

		// Already met as gym leader
	} else {
		// Player already knows she works both in Saku and Kura
		if (flag(FLAG_MET_HANA_BONSAI_SHOP)) {
			call(SakuTown_BonsaiShop_EventScript_Owner_RegularDialogue)
		// Player discovers the Kura gym leader is ALSO the Saku bonsai shop owner :o
		} else {
			call(SakuTown_BonsaiShop_EventScript_Owner_DiscoverGymLeader)
		}
	}

	release
}

mart BonsaiShop {
	DECOR_RED_PLANT
	DECOR_BIG_PLANT
	DECOR_GORGEOUS_PLANT
}

script SakuTown_EventScript_Man {
	lock
	faceplayer
	setspeaker(SP_NAME_CUSTOMER)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SAKURA_MAN_2 EMOTE_NORMAL}I'm choosing flowers for my wife.\p"
		"I, uh… I didn't realize there'd be so many different types.\p"
		"Guess I'll be here a while…"
	))
	closemessage
	release
}
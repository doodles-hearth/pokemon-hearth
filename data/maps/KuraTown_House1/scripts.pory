mapscripts KuraTown_House1_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setrespawn(HEAL_LOCATION_KURA_TOWN)
	}
}

script KuraTown_House1_OldWoman {
	lock
	faceplayer
	speakername("Old woman")
	if(var(VAR_SAKU_KURA_QUEST_SCYTHER_STATE) == 2) {
		call(OldWomanQuestScript)
	} else {
		call(OldWomanRegularScript)
	}
	release
}

script(local) OldWomanRegularScript {
	msgbox(format(
		"You look like you're tired, young one. Please rest a while."
	))
	closemessage
	call(Common_EventScript_OutOfCenterPartyHeal)
	speakername("Old woman")
	msgbox(format(
		"I wish you strength on your journey."
	))
	closemessage
	return
}

script(local) OldWomanQuestScript {
	msgbox(format("Hello, dearie. Do you need to rest yourself and your Pokémon?\p…\pI see… Can you take me to the ill Pokémon?"))
	waitmessage
	closemessage
	return
}

script KuraTown_House1_OldWomansSon {
	speakername("Guy")
	msgbox(format(
		"The Pokémon league organization didn't see fit to build a Pokémon refuge in our village, but those jerks over at Saku Village won't let us use theirs.\p"
		"So my mother has to work hard to heal all the villager's Pokémon.\p"
		"It's not fair!"
	), MSGBOX_NPC)
}

script KuraTown_House1_Growlithe {
	lock
	faceplayer
	setseenmon(SPECIES_GROWLITHE)
	setspeakertomonname(SPECIES_GROWLITHE)
	playmoncry(SPECIES_GROWLITHE, CRY_MODE_NORMAL)
	msgbox(format(
		"Wah!"
	))
	waitmoncry
	release
}
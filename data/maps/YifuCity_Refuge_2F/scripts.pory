const FLAG_HIDE_HERACROSS = FLAG_TEMP_1
const FLAG_HIDE_OKADA = FLAG_TEMP_2
const FLAG_HIDE_CITIZENS_UNSAFE_CITY = FLAG_TEMP_4
const FLAG_HIDE_NUMEL = FLAG_TEMP_3

mapscripts YifuCity_Refuge_2F_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (flag(FLAG_TRADE_NUMEL_COMPLETED)) {
			setflag(FLAG_HIDE_NUMEL)
		} else {
			setflag(FLAG_HIDE_HERACROSS)
		}

		if (!flag(FLAG_CLEARED_CROBAT_HIDEOUT)) {
			setflag(FLAG_HIDE_CITIZENS_UNSAFE_CITY)
			setflag(FLAG_HIDE_NUMEL)
		}

		if (!flag(FLAG_MET_TRAVELING_MERCHANT_2ND_TIME)) {
			setflag(FLAG_HIDE_OKADA)
		}
	}
}

script YifuCity_Refuge_2F_EventScript_Sleep {
	lockall
	msgbox(format(
		"It's a soft feather bed…\nRest a while?"
	))

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Sleep until morning.",
		"Sleep until nighttime.",
		"I'm not tired."
	)

	closemessage
	switch(var(VAR_RESULT)) {
		case 0:
			fadescreenswapbuffers(FADE_TO_BLACK)
			playfanfare(MUS_HEAL)
			waitfanfare
			special(HealPlayerParty)
			callnative(UpdateFollowingPokemon)
			fwdtime(8, 0)
			fadescreenswapbuffers(FADE_FROM_BLACK)
		case 1:
			fadescreenswapbuffers(FADE_TO_BLACK)
			playfanfare(MUS_HEAL)
			waitfanfare
			special(HealPlayerParty)
			callnative(UpdateFollowingPokemon)
			fwdtime(22, 0)
			fadescreenswapbuffers(FADE_FROM_BLACK)
	}

	releaseall
}

script YifuCity_Refuge_2F_EventScript_NumelTrade {
	lock
	faceplayer

	if (flag(FLAG_TRADE_NUMEL_COMPLETED)) {
        setspeaker(SP_NAME_GIRL)
		// TODO EVA setdescribedmon
        msgbox(format("Heracross is so fascinating!"))
        closemessage
    } else {
        setvar(VAR_0x8008, INGAME_TRADE_NUMEL)
        copyvar(VAR_0x8004, VAR_0x8008)
        specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
        copyvar(VAR_0x8009, VAR_RESULT)

        msgbox(
            format(
				"{CREATE_MUGSHOT MUGSHOT_HOT_GIRL_2 EMOTE_NORMAL}{SPEAKER NAME_GIRL}"
                "A visitor, hi, pleased to meet you!!\nYou have such pretty Pokémon!\p"
				"I brought Numel from my home, hoping to trade it for a local Heracross.\p"
				"What do you say? Want to trade a Heracross for my Numel?"
            )
        )

		mcdialogue(
			"With pleasure!",
			"Not interested."
		)

	    if (var(VAR_RESULT) != 0) {
			playse(SE_PIN)
			applymovement(VAR_LAST_TALKED, moves(emote_happy))
            msgbox(format(
				
				"{CREATE_MUGSHOT MUGSHOT_HOT_GIRL_2 EMOTE_NORMAL}{SPEAKER NAME_GIRL}"
				"It's okay. I'll hang out with Numel for a while!"
			))
            closemessage
        } else {
	        special(ChoosePartyMon)
            waitstate
            copyvar(VAR_0x800A, VAR_0x8004)
            if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
				playse(SE_PIN)
				applymovement(VAR_LAST_TALKED, moves(emote_happy))
                msgbox(format(
					"{CREATE_MUGSHOT MUGSHOT_HOT_GIRL_2 EMOTE_NORMAL}{SPEAKER NAME_GIRL}"
					"It's okay. I'll hang out with Numel for a while!"
				))
                closemessage
            } else {
                copyvar(VAR_0x8005, VAR_0x800A)
                specialvar(VAR_RESULT, GetTradeSpecies)
                if (var(VAR_RESULT) != VAR_0x8009) {
                    msgbox(format(
						"{CREATE_MUGSHOT MUGSHOT_HOT_GIRL_2 EMOTE_NORMAL}{SPEAKER NAME_GIRL}"
						"I'm sure your Pokémon is very nice, but I'm looking for a Heracross!"
					))
                    closemessage
                } else {
                    copyvar(VAR_0x8004, VAR_0x8008)
                    copyvar(VAR_0x8005, VAR_0x800A)
                    special(CreateInGameTradePokemon)
                    special(DoInGameTradeScene)

                    setflag(FLAG_TRADE_NUMEL_COMPLETED)
                    clearflag(FLAG_HIDE_HERACROSS)
                    removeobject(LOCALID_TRADE_NUMEL)

                    waitstate

                    addobject(LOCALID_TRADE_HERACROSS)

					playse(SE_PIN)
					applymovement(VAR_LAST_TALKED, moves(emote_happy))
                    msgbox(format(
						"{CREATE_MUGSHOT MUGSHOT_HOT_GIRL_2 EMOTE_NORMAL}{SPEAKER NAME_GIRL}"
						"Woah!\nIt's bigger than I thought! Amazing!!\p"
						"Please, take good care of my girl Luo Luo, okay?"
                    ), MSGBOX_DEFAULT)
                    closemessage
                }
            }
        }
    }
    release
}

script YifuCity_Refuge_2F_EventScript_Numel {
	lock
    faceplayer
	setseenmon(SPECIES_NUMEL)
    setspeakertomonname(SPECIES_NUMEL)
    playmoncry(SPECIES_NUMEL, CRY_MODE_NORMAL)
    msgbox(format(
        "Whoop!"
    ))
    waitmoncry
    release
}

script YifuCity_Refuge_2F_EventScript_Heracross {
	lock
    faceplayer
	setseenmon(SPECIES_HERACROSS)
    setspeakertomonname(SPECIES_HERACROSS)
    playmoncry(SPECIES_HERACROSS, CRY_MODE_NORMAL)
    msgbox(format(
        "Aaay!"
    ))
    waitmoncry
    release
}

script YifuCity_Refuge_2F_EventScript_TravelingMerchant {
	lock
	faceplayer
	setspeaker(SP_NAME_OKADA)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_OKADA EMOTE_NORMAL}Oh, good evening, {PLAYER}! I'm exhausted! I've been walking since morning, I'm calling it a day.\p"
		"Never underestimate a good night's sleep!"
	))
	closemessage
	release
}

script YifuCity_Refuge_2F_EventScript_Tourist {
	lock
	faceplayer
	setspeaker(SP_NAME_WOMAN)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SEASIDE_WOMAN_1 EMOTE_NORMAL}I'm so glad we're able to spend our vacation here."
	))
	closemessage
	release
}

script YifuCity_Refuge_2F_EventScript_TouristKid {
	lock
	faceplayer
	setspeaker(SP_NAME_LITTLE_BOY)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SEASIDE_LITTLE_BOY EMOTE_NORMAL}We went to see the Dojo's dancers today!"
	))
	closemessage
	release
}

script YifuCity_Refuge_2F_EventScript_TouristGirl {
	lock
	faceplayer
	waitse
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, moves(emote_angry))
	setspeaker(SP_NAME_GIRL)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SEASIDE_GIRL_1 EMOTE_NORMAL}Ughh, I have to share the bed with my baby brother…\p"
		"Life is SO unfair!"
	))
	closemessage
	release
}

script YifuCity_Refuge_2F_EventScript_Castform {
	lock
    faceplayer
	setseenmon(SPECIES_CASTFORM)
    setspeakertomonname(SPECIES_CASTFORM)
    playmoncry(SPECIES_CASTFORM, CRY_MODE_NORMAL)
    msgbox(format(
        "Blrblblbl…"
    ))
    waitmoncry
    release
}

script YifuCity_Refuge_2F_EventScript_SameRoom {
	trainerbattle_single(
		TRAINER_YAMATO,
		format("{CREATE_MUGSHOT MUGSHOT_UUME_OLD_MAN EMOTE_NORMAL}Hey! What are you doing in my room?"),
		format("This is outrageous!"),
		YifuCity_Refuge_2F_EventScript_SameRoomPostBattle
	)
	setspeaker(SP_NAME_OLD_MAN)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_UUME_OLD_MAN EMOTE_NORMAL}I'd complain at the lobby, but that valet seems like a bad type."
	), MSGBOX_AUTOCLOSE)
	end
}

script YifuCity_Refuge_2F_EventScript_SameRoomPostBattle {
	lockall

	applymovement(LOCALID_YIFU_REFUGE_SAME_ROOM, moves(emote_pensive, delay_16 * 6))
	waitmovement(LOCALID_YIFU_REFUGE_SAME_ROOM)

	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_UUME_OLD_MAN EMOTE_NORMAL}{SPEAKER NAME_OLD_MAN}"
		"You're saying we both got the same room?\p"
		"Bah… I guess we're going to have to share…\p"
		"The service here used to be cheaper and a whole lot better."
	))
	closemessage

	releaseall
}
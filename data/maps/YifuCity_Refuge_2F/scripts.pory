const FLAG_HIDE_HERACROSS = FLAG_TEMP_1

mapscripts YifuCity_Refuge_2F_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (!flag(FLAG_TRADE_NUMEL_COMPLETED)) {
			setflag(FLAG_HIDE_HERACROSS)
		}
	}
}

script YifuCity_Refuge_2F_EventScript_Sleep {
	lockall
	msgbox(format(
		"It's a soft feather bed…\nRest a while?"
	))

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Sleep until morning.",
		"Sleep until nighttime.",
		"I'm not tired."
	)

	closemessage
	switch(var(VAR_RESULT)) {
		case 0:
			fadescreenswapbuffers(FADE_TO_BLACK)
			playfanfare(MUS_HEAL)
			waitfanfare
			special(HealPlayerParty)
			callnative(UpdateFollowingPokemon)
			fwdtime(8, 0)
			fadescreenswapbuffers(FADE_FROM_BLACK)
		case 1:
			fadescreenswapbuffers(FADE_TO_BLACK)
			playfanfare(MUS_HEAL)
			waitfanfare
			special(HealPlayerParty)
			callnative(UpdateFollowingPokemon)
			fwdtime(22, 0)
			fadescreenswapbuffers(FADE_FROM_BLACK)
	}

	releaseall
}

script YifuCity_Refuge_2F_EventScript_NumelTrade {
	lock
	faceplayer

	if (flag(FLAG_TRADE_NUMEL_COMPLETED)) {
        speakername("Girl")
		// TODO EVA setdescribedmon
        msgbox(format("Heracross is so fascinating!"))
        closemessage
    } else {
        setvar(VAR_0x8008, INGAME_TRADE_NUMEL)
        copyvar(VAR_0x8004, VAR_0x8008)
        specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
        copyvar(VAR_0x8009, VAR_RESULT)

        speakername("Girl")
        msgbox(
            format(
                "A visitor, hi, pleased to meet you!!\p"
				"You have such pretty Pokémon!\nI brought a Numel from my home, hoping to trade it for a local Heracross.\p"
				"What do you say? Want to trade a Heracross for my Numel?"
            ), MSGBOX_YESNO
        )

	    if (var(VAR_RESULT) == NO || var(VAR_RESULT) == MULTI_B_PRESSED) {
            speakername("Girl")
            msgbox(format("It's okay. I'll hang out with my Numel for a while!"))
            closemessage
        } else {
	        special(ChoosePartyMon)
            waitstate
            copyvar(VAR_0x800A, VAR_0x8004)
            if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
                speakername("Girl")
                msgbox(format("It's okay. I'll hang out with my Numel for a while!"))
                closemessage
            } else {
                copyvar(VAR_0x8005, VAR_0x800A)
                specialvar(VAR_RESULT, GetTradeSpecies)
                if (var(VAR_RESULT) != VAR_0x8009) {
                    speakername("Girl")
                    msgbox(format("I'm sure your Pokémon is very nice, but I'm looking for a Heracross!"))
                    closemessage
                } else {
                    copyvar(VAR_0x8004, VAR_0x8008)
                    copyvar(VAR_0x8005, VAR_0x800A)
                    special(CreateInGameTradePokemon)
                    special(DoInGameTradeScene)

                    setflag(FLAG_TRADE_NUMEL_COMPLETED)
                    clearflag(FLAG_HIDE_HERACROSS)
                    removeobject(LOCALID_TRADE_NUMEL)

                    waitstate

                    addobject(LOCALID_TRADE_HERACROSS)

                    msgbox(format(
						"Woah!\nIt's bigger than I thought! Amazing!!\p"
						"Please, take good care of my girl Luo Luo, okay?"
                    ), MSGBOX_DEFAULT)
                    closemessage
                }
            }
        }
    }
    release
}

script YifuCity_Refuge_2F_EventScript_Numel {
	lock
    faceplayer
	setseenmon(SPECIES_NUMEL)
    setspeakertomonname(SPECIES_NUMEL)
    playmoncry(SPECIES_NUMEL, CRY_MODE_NORMAL)
    msgbox(format(
        "Whoop!"
    ))
    waitmoncry
    release
}

script YifuCity_Refuge_2F_EventScript_Heracross {
	lock
    faceplayer
	setseenmon(SPECIES_HERACROSS)
    setspeakertomonname(SPECIES_HERACROSS)
    playmoncry(SPECIES_HERACROSS, CRY_MODE_NORMAL)
    msgbox(format(
        "Aaay!"
    ))
    waitmoncry
    release
}

script YifuCity_Refuge_2F_EventScript_TravelingMerchant {
	lock
	faceplayer
	speakername("Okada")
	msgbox(format(
		"Oh, good evening, {PLAYER}! I'm exhausted! I've been walking since morning, I'm calling it a day.\p"
		"Never underestimate a good night's sleep!"
	))
	closemessage
	release
}

script YifuCity_Refuge_2F_EventScript_Tourist {
	lock
	faceplayer
	speakername("Woman")
	msgbox(format(
		"I'm so glad we're able to spend our vacation here."
	))
	closemessage
	release
}

script YifuCity_Refuge_2F_EventScript_TouristKid {
	lock
	faceplayer
	speakername("Little Boy")
	msgbox(format(
		"We went to see the Dojo's dancers today!"
	))
	closemessage
	release
}

script YifuCity_Refuge_2F_EventScript_TouristGirl {
	lock
	faceplayer
	speakername("Girl")
	msgbox(format(
		"Ughh, I have to share the bed with my baby brother…\p"
		"Life is SO unfair!"
	))
	closemessage
	release
}

script YifuCity_Refuge_2F_EventScript_Castform {
	lock
    faceplayer
	setseenmon(SPECIES_CASTFORM)
    setspeakertomonname(SPECIES_CASTFORM)
    playmoncry(SPECIES_CASTFORM, CRY_MODE_NORMAL)
    msgbox(format(
        "Blrblblbl…"
    ))
    waitmoncry
    release
}

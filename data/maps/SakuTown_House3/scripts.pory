const FLAG_HIDE_HOOTHOOT = FLAG_TEMP_1
const LOCALID_PSYDUCK = 3
const LOCALID_HOOTHOOT = 4

mapscripts SakuTown_House3_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: SakuTown_House3_EventScript_SetUpPokemon
}

script SakuTown_House3_EventScript_SetUpPokemon {
    if (!flag(FLAG_FORTREE_NPC_TRADE_COMPLETED)) {
        setflag(FLAG_HIDE_HOOTHOOT)
    }
}

script SakuTown_House3_EventScript_Psyduck {
    lock
    faceplayer
    setseenmon(SPECIES_PSYDUCK_TOKUAN)
    setspeakertomonname(SPECIES_PSYDUCK)
    playmoncry(SPECIES_PSYDUCK, CRY_MODE_NORMAL)
    msgbox(format(
        "Psaïaïaï…"
    ))
    waitmoncry
    release
    end
}

script SakuTown_House3_EventScript_Hoothoot {
    lock
    faceplayer
    setspeakertomonname(SPECIES_HOOTHOOT)
    playmoncry(SPECIES_HOOTHOOT, CRY_MODE_NORMAL)
    msgbox(format(
        "Hooo…"
    ))
    waitmoncry
    release
    end
}

script SakuTown_House3_EventScript_TradeMe {
    lock
	faceplayer
    if (flag(FLAG_FORTREE_NPC_TRADE_COMPLETED)) {
        setspeaker(SP_NAME_LITTLE_BOY)
        msgbox(format("{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_BOY EMOTE_NORMAL}How's Bread doing?"))
        closemessage
    } else {
        setvar(VAR_0x8008, INGAME_TRADE_PSYDUCK)
        copyvar(VAR_0x8004, VAR_0x8008)
        specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
        copyvar(VAR_0x8009, VAR_RESULT)

        setspeaker(SP_NAME_LITTLE_BOY)
        msgbox(
            format(
                "{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_BOY EMOTE_NORMAL}The best thing about Pokémon is trading them around!\p"
                "I want a Hoothoot, but my pa doesn't let me go out at night.\p"
                "I'll trade you a super dope Psyduck for it!"
            )
        )
        setnamedmon(SPECIES_PSYDUCK)
        setnamedmon(SPECIES_HOOTHOOT)

        mcdialogueB("Yes!", "No thanks.")

	    if (var(VAR_RESULT) != 0) {
            setspeaker(SP_NAME_LITTLE_BOY)
            msgbox(format("{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_BOY EMOTE_NORMAL}Keep your Hoothoot, I don't want it anyway…"))
            closemessage
        } else {
	        special(ChoosePartyMon)
            waitstate
            copyvar(VAR_0x800A, VAR_0x8004)
            if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
                setspeaker(SP_NAME_LITTLE_BOY)
                msgbox(format("{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_BOY EMOTE_NORMAL}Keep your Hoothoot, I don't want it anyway…"))
                closemessage
            } else {
                copyvar(VAR_0x8005, VAR_0x800A)
                specialvar(VAR_RESULT, GetTradeSpecies)
                if (var(VAR_RESULT) != VAR_0x8009) {
                    setspeaker(SP_NAME_LITTLE_BOY)
                    msgbox(format("{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_BOY EMOTE_NORMAL}I don't want your weird Pokémon! I want a Hoothoot!"))
                    closemessage
                } else {
                    copyvar(VAR_0x8004, VAR_0x8008)
                    copyvar(VAR_0x8005, VAR_0x800A)

                    applymovement(VAR_LAST_TALKED, moves(face_left))
                    waitmovement(0)
                    applymovement(LOCALID_PSYDUCK, moves(enter_pokeball, delay_16 * 2))
                    waitmovement(0)
                    applymovement(VAR_LAST_TALKED, moves(face_down, delay_16 * 2))
                    waitmovement(0)

                    special(CreateInGameTradePokemon)
                    special(DoInGameTradeScene)

                    setflag(FLAG_FORTREE_NPC_TRADE_COMPLETED)
                    clearflag(FLAG_HIDE_HOOTHOOT)
                    removeobject(LOCALID_PSYDUCK)

                    waitstate

                    addobject(LOCALID_HOOTHOOT)
                    applymovement(LOCALID_HOOTHOOT, moves(exit_pokeball, set_visible, delay_16 * 3))

                    bufferspeciesname(STR_VAR_1, VAR_0x8009)
                    msgbox(format(
                        "{CREATE_MUGSHOT MUGSHOT_SAKURA_LITTLE_BOY EMOTE_NORMAL}Yeahhh!! I have a {STR_VAR_1} now!\p"
                        "Take good care of Bread for me, okay?"
                    ), MSGBOX_DEFAULT)
                    closemessage
                }
            }
        }
    }
    release
}

script SakuTown_House3_EventScript_Man {
    lock
    faceplayer
    setspeaker(SP_NAME_MAN)
    if (flag(FLAG_FORTREE_NPC_TRADE_COMPLETED)) {
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_SAKURA_MAN_2 EMOTE_NORMAL}Oh great, he's got yet another Pokémon…"
        ))
    } else {
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_SAKURA_MAN_2 EMOTE_NORMAL}My son is really into this whole Pokémon trading business, huh…"
        ))
    }
    closemessage
    release
}
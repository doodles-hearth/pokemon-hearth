const LOCALID_KARATEKA = 6
const LOCALID_GUARD = 8
const LOCALID_RIVAL = 10
const LOCALID_MAMA = 1
const LOCALID_KUKU = 2
const LOCALID_TAMA = 20
const LOCALID_TAMA2 = OBJ_EVENT_ID_NPC_FOLLOWER
const LOCALID_TAMASDAD = 21
const LOCALID_ASHII = 22
const FLAG_HIDE_NATSUKI = FLAG_TEMP_1

const VAR_GET_QUEST_LOG_STATE_SAKURA_LOVERS = VAR_TEMP_0
const VAR_GET_QUEST_LOG_STATE_SAKURA_ESPEON = VAR_TEMP_1
const VAR_GET_QUEST_LOG_STATE_SAKURA_SCYTHER = VAR_TEMP_2

mapscripts SakuTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_VISITED_SAKU_TOWN)

		if (flag(FLAG_SAKU_KURA_REUNITED)) {
			// Once reunited, the guards from the two villages play a board game together
			setmetatile(19, 2, METATILE_SakuKura_BoardGame, TRUE)
			setobjectmovementtype(LOCALID_GUARD, MOVEMENT_TYPE_FACE_RIGHT)
		}

		if (var(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE) == 4) { //hides rocksmash rocks
			setflag(FLAG_TEMP_2)
			setflag(FLAG_TEMP_3)
		}

		if (var(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE) != 4) {
			setflag(FLAG_HIDE_TAMASDAD)
			setflag(FLAG_HIDE_DEADNIGHT_TAMA)
		} elif (var(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE) == 4){
			clearflag(FLAG_HIDE_TAMASDAD)
			clearflag(FLAG_HIDE_DEADNIGHT_TAMA)
		}

		if (var(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE) < 5){
			setflag(FLAG_HIDE_DEADNIGHT_ASHII)
		}

		returnqueststate(QUEST_SAKURALOVERS)
		copyvar(VAR_GET_QUEST_LOG_STATE_SAKURA_LOVERS, VAR_RESULT)
		returnqueststate(QUEST_SAKURAESPEON)
		copyvar(VAR_GET_QUEST_LOG_STATE_SAKURA_ESPEON, VAR_RESULT)
		returnqueststate(QUEST_SAKURASCYTHER)
		copyvar(VAR_GET_QUEST_LOG_STATE_SAKURA_SCYTHER, VAR_RESULT)

		if (
			var(VAR_GET_QUEST_LOG_STATE_SAKURA_LOVERS) == 4
			&& var(VAR_GET_QUEST_LOG_STATE_SAKURA_ESPEON) == 4
			&& var(VAR_GET_QUEST_LOG_STATE_SAKURA_SCYTHER) == 1
			&& var(VAR_SAKU_KURA_QUEST_SCYTHER_STATE) == 0
		) { 
			setflag(FLAG_HIDE_SAKU_FARMERS)
			setvar(VAR_SAKU_KURA_QUEST_SCYTHER_STATE, 1)

		} elif (var(VAR_SAKU_KURA_QUEST_SCYTHER_STATE) == 7) {
			clearflag(FLAG_HIDE_SAKU_FARMERS)
		}
	}
	MAP_SCRIPT_ON_LOAD {
		setflag(FLAG_HIDE_NATSUKI)
		gettimeofday
		switch(var(VAR_0x8000)){
			case TIME_DEAD_NIGHT:
				setmetatile(35, 31, METATILE_SakuKura_ClosedDoorBottom, TRUE)
				setmetatile(18, 32, METATILE_SakuKura_ClosedDoorTop, TRUE)
				setmetatile(18, 33, METATILE_SakuKura_ClosedDoorBottom, TRUE)
			case TIME_EARLY_MORNING:
			case TIME_MORNING:
			case TIME_LUNCHTIME:
			case TIME_AFTERNOON:
			case TIME_EVENING:
			case TIME_NIGHT:
			default:
		end
		}
	}
}

// Rival encounter right before the Saku wall
script SakuTown_EventScript_RivalAtSakuWall {
	lock
	speakername("???")
	msgbox(format("Wait up, {PLAYER}!"))
	closemessage

	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	getplayerxy(VAR_TEMP_2, VAR_TEMP_3)

	clearflag(FLAG_HIDE_NATSUKI)
	addobject(LOCALID_RIVAL)
	if (var(VAR_TEMP_2) == 16) {
		setobjectxy(LOCALID_RIVAL, 16, 10)
	} else {
		setobjectxy(LOCALID_RIVAL, 17, 10)
	}
	applymovement(LOCALID_RIVAL, moves(walk_up * 6))
	waitmovement(0)

	speakername("Natsuki")
	msgbox(format(
		"I was wondering if you'd reached Saku Village by now. Looks like we almost missed each other!\p"
		"I saw several patrolling Samurai on my way here. They look so cool, no wonder they're so admired!\p"
		"Oh, {PLAYER}, have you caught any Pokémon on your way here?\p"
		"Me?\nBoy, I'm so glad you asked…\p"
		"Let me show you!"
	))

	// Fight
	switch(var(VAR_STARTER_MON)) {
		case SPECIES_SNIVY:
			trainerbattle_no_intro(TRAINER_RIVAL_SAKU_SNIVY, SakuTown_Text_RivalDefeat)
		case SPECIES_FROAKIE:
			trainerbattle_no_intro(TRAINER_RIVAL_SAKU_FROAKIE, SakuTown_Text_RivalDefeat)
		case SPECIES_CYNDAQUIL:
			trainerbattle_no_intro(TRAINER_RIVAL_SAKU_CYNDAQUIL, SakuTown_Text_RivalDefeat)
	}

	speakername("Natsuki")
	msgbox(format(
		"Woah! I… I didn't think you'd go that hard, honestly!\p"
		"Clearly I underestimated you. That's alright. I won't make that mistake next time!\p"
		"I can see your Pokémon are really growing to like you, it's great!\p"
		"Are you doing the Dojo challenge?\nYou might just stand a chance against the Dojo that's in the next village!\p"
		"Me? Actually, I just got my first official Token!\p"
		"You'll have to hurry if you wanna catch up!\p"
		"It's fun to run into each other like this. Until next time then.\p"
		"I bet you I'll get to Silveridge first!\p"
		"See you later, Feraligatr!"
	))

	mcdialogue(
		"In a while, Totodile?",
		"Where's the Feraligatr?"
	)

	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			speakername("Natsuki")
			msgbox(format(
				"You got it!"
			))
		case 1:
			speakername("Natsuki")
			msgbox(format(
				"Right behind you!"
			))
			closemessage

			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_down, delay_16 * 3, walk_in_place_fast_up))
			waitmovement(0)

			speakername("Natsuki")
			msgbox(format(
				"Bahaha! Got you!"
			))
			closemessage
	}

	// Left
	if (var(VAR_TEMP_2) == 16) {
		applymovement(LOCALID_RIVAL, moves(walk_right, walk_up * 5))
		// Right
	} else {
		applymovement(LOCALID_RIVAL, moves(walk_left, walk_up * 5))
	}
	waitmovement(0)

	removeobject(LOCALID_RIVAL)
	setflag(FLAG_HIDE_NATSUKI)

	setvar(VAR_RIVAL_ENCOUNTER_STATE, 1)

	release
}

text SakuTown_Text_RivalDefeat {
	format("Oh, wow!")
}

script SakuTown_EventScript_Farmer {
	lock
	faceplayer
	speakername("Farmer")
	returnqueststate(QUEST_SAKURASCYTHER)
	if(var(VAR_RESULT) == 3){
		msgbox(format(
			"Wielder! It's you!\nI can't thank ya enough for helping us heal our girl.\p"
			"Been doin' some thinkin' too, after you told us the one who helped 'er was from Kura.\p"
			"That ain't here nor there though.\nAs thanks, ya can have these."
		))
		giveitem(ITEM_CHERI_BERRY, 20)
		completequest(QUEST_SAKURASCYTHER)
	} else {
		msgbox(format(
			"We're growin' Cheri berries.\nSaku Village exports its berries to many towns."
		))
	}
	release
}

script SakuTown_EventScript_Farmer2 {
	lock
	speakername("Farmer")
	msgbox(format(
		"Whew…\n"
		"Working in the fields sure is tirin'…"
	))
	release
}

script SakuTown_EventScript_Scyther {
	lock
	faceplayer
	setseenmon(SPECIES_SCYTHER)
	playmoncry(SPECIES_SCYTHER, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_SCYTHER)
	msgbox(format("Gyooo!"))
	waitmoncry
	closemessage
	release
}

script SakuTown_EventScript_Woman {
	lock
	faceplayer
	speakername("Woman")
	msgbox(format(
		"Those thugs in Kura Village may have their own Dojo, but their town is nothing but an ugly pile of rocks!\p"
		"They don't even have a Pokémon refuge!"
	))
	release
}

script SakuTown_EventScript_Karateka {
	lock
	faceplayer
	speakername("Man")
	msgbox(format(
		"I let the Toku Council borrow my super-strong Pokémon to help build the new Pokémon refuges throughout the region."
	))
	closemessage

	playse(SE_LEDGE)
	applymovement(LOCALID_KARATEKA, moves(jump_in_place_up))
	waitmovement(0)
	waitse

	speakername("Man")
	msgbox(format(
		"Come on, Mama, Kuku! Daddy loves you!!"
	))
	closemessage

	applymovement(LOCALID_MAMA, Common_Movement_FaceDown)
	applymovement(LOCALID_KUKU, Common_Movement_FaceDown)
	waitmovement(LOCALID_MAMA)
	waitmovement(LOCALID_KUKU)

	delay(24)

	playmoncry(SPECIES_MAKUHITA, CRY_MODE_NORMAL)
	applymovement(LOCALID_MAMA, moves(jump_in_place_down))
	waitmovement(LOCALID_MAMA)
	waitmoncry
	playmoncry(SPECIES_MAKUHITA, CRY_MODE_NORMAL)
	applymovement(LOCALID_KUKU, moves(jump_in_place_down))
	waitmovement(LOCALID_KUKU)
	waitmoncry

	setseenmon(SPECIES_MAKUHITA)

	release
}

script SakuTown_EventScript_Guard {
	lock
	if (flag(FLAG_SAKU_KURA_REUNITED)) {
		speakername("Guard")
		msgbox(format(
			"Damn, where did that Mareep come from?!"
		))
		closemessage
	} else {
		faceplayer
		speakername("Guard")
		msgbox(format(
			"One can never be too careful.\p"
			"The safety of the whole village rests on my shoulders."
		))
		closemessage
		applymovement(LOCALID_GUARD, Common_Movement_FaceOriginalDirection)
		waitmovement(0)
	}
	release
}

script SakuTown_EventScript_KuraGuard {
	lock
	speakername("Guard")
	msgbox(format(
		"Hah, got two of your Tauros in my herd!"
	))
	closemessage
	release
}

script SakuTown_EventScript_GuardSeesYou {
	lock

	specialvar(VAR_RESULT, GetPlayerFacingDirection)

	// First time going to Kura
	if (var(VAR_SAKU_KURA_VILLAGES_STATE) == 0) {
		call(SakuTown_EventScript_GuardStopsYou)
		// First time coming down from Kura
	} elif (var(VAR_SAKU_KURA_VILLAGES_STATE) == 2 && var(VAR_RESULT) == DIR_SOUTH) {
		call(SakuTown_EventScript_GuardStopsYou)
	}

	release
}

script SakuTown_EventScript_GuardStopsYou {
	specialvar(VAR_RESULT, GetPlayerFacingDirection)
	applymovement(LOCALID_GUARD, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement(LOCALID_GUARD)
	speakername("Guard")
	msgbox(format("Hey!"))
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	waitmovement(OBJ_EVENT_ID_PLAYER)
	delay(20)

	if (var(VAR_RESULT) == DIR_SOUTH) {
		speakername("Guard")
		msgbox(format(
			"Are you trying to sneak in from Kura Village?\p"
			"…Oh, you're that traveler from before. You can go through.\p"
			"How was it? Awful, right? Welcome back to Saku Village!"
		))
		closemessage
		setvar(VAR_SAKU_KURA_VILLAGES_STATE, 3)
	} else {
		speakername("Guard")
		msgbox(format(
			"Are you trying to sneak into Kura Village?\p"
			"…Oh, you're just a traveler.\nNevermind then, you can go through.\p"
			"Though I wouldn't recommend visiting the next village, there's really nothing interesting there."
		))
		closemessage
		setvar(VAR_SAKU_KURA_VILLAGES_STATE, 1)
	}

	applymovement(LOCALID_GUARD, Common_Movement_FaceDown)

}

script SakuTown_EventScript_TownSign {
	msgbox(format("Saku Village\n“A village of tender flower petals.”"), MSGBOX_SIGN)
}

script SakuTown_EventScript_TrainerTips {
	msgbox(format("Did you know?\pSaku Village is in all ways superior to Kura Village!"), MSGBOX_SIGN)
}

script SakuTown_EventScript_TrainerTips2 {
	msgbox(format("Did you know?\pThe folks from Kura Village are a bunch of mean jerks!"), MSGBOX_SIGN)
}

script SakuTown_EventScript_FloristSign {
	msgbox(format("Humble Blossom\nBonsai & flower shop"), MSGBOX_SIGN)
}

script SakuTown_EventScript_BoardGame {
	if (flag(FLAG_SAKU_KURA_REUNITED)) {
		lock
		msgbox(format("They're playing Tauros Herder, a popular board game!"), MSGBOX_DEFAULT)
		closemessage
		release
	}
}

script SakuTown_EventScript_Mart {
	lock
	faceplayer
	speakername("Shopkeeper")
	message(gText_HowMayIServeYou)
	waitmessage
	pokemart(SakuTown_Mart)
	speakername("Shopkeeper")
	msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
}

mart SakuTown_Mart {
	ITEM_POKE_BALL
	ITEM_OINTMENT
	ITEM_REVIVAL_POWDER
	ITEM_PARALYZE_HEAL
	ITEM_ANTIDOTE
	ITEM_AWAKENING
	ITEM_BURN_HEAL
	ITEM_ESCAPE_ROPE
}

script SakuTown_EventScript_Makuhita {
	lock
	faceplayer
	waitse
	setseenmon(SPECIES_MAKUHITA)
	playmoncry(SPECIES_MAKUHITA, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_MAKUHITA)
	msgbox(format("Kuhi!"))
	waitmoncry
	release
}

script SakuTown_EventScript_MonHues {
	lock
	faceplayer
	speakername("Man")
	msgbox(format(
		"Have you ever paid close attention to the fur and feather colors of the wild Pokémon?\p"
		"They may all look the same at first…\nBut each one has its own totally unique colors!\p"
		"Doesn't noticing that make your heart sing?"
	))
	closemessage
	release
}

script SakuTown_EventScript_Cherrim {
	lock
	faceplayer
	setseenmon(SPECIES_CHERRIM)
	playmoncry(SPECIES_CHERRIM, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_CHERRIM)
	msgbox(format("Weee!"))
	waitmoncry
	release
}

script SakuTown_EventScript_TamaArgument {
	lock
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_down, emote_exclamation_mark))
	waitmovement(0)
	waitse

	speakername("???")
	msgbox(format("NO! I forbid you from going!"))
	closemessage

	if (var(VAR_TEMP_0) == 1) {
		applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_fast_down * 4))
	} else {
		applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_fast_down * 3))
	}
	waitmovement(0)

	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 3, delay_16 * 2))
	waitmovement(0)

	applymovement(LOCALID_TAMA, moves(walk_in_place_fast_right))
	speakername("Tama")
	msgbox(format("The love of my life is waiting for me\non the other side!\lYou can't do this to me!"))
	closemessage

	applymovement(LOCALID_TAMASDAD, moves(walk_in_place_fast_right))
	speakername("Old man")
	msgbox(format("Over my dead body will my son be in love with someone from Kura Village!"))
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_TAMA, moves(emote_exclamation_mark, face_up))
	applymovement(LOCALID_TAMASDAD, moves(emote_exclamation_mark, face_up))
	waitmovement(LOCALID_TAMA)
	waitmovement(LOCALID_TAMASDAD)

	delay(60)
	playse(SE_LEDGE)
	applymovement(LOCALID_TAMA, moves(jump_in_place_up))
	waitmovement(LOCALID_TAMA)
	speakername("Tama")
	msgbox(format("It's you!\nYou need to help me get to Ashii,\lplease!"))
	waitmessage
	closemessage
	playse(MUS_VS_RAYQUAZA)
	delay(240)

	applymovement(LOCALID_TAMASDAD, moves(walk_in_place_fast_up))
	waitmovement(LOCALID_TAMASDAD)

	speakername("Old Man")
	msgbox(format(
		"So you're the whippersnapper that's making my son collude with the enemy, eh.\p"
		"You outta be taught a lesson in respect, kiddo!"
	))
	closemessage

	playse(MUS_DUMMY)
	waitse

	applymovement(LOCALID_TAMASDAD, moves(walk_slower_up))
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_slower_up))
	waitmovement(LOCALID_TAMASDAD)
	waitmovement(OBJ_EVENT_ID_CAMERA)

	speakername("Old Man")
	msgbox(format("{FONT_SMALL_NARROW}Huff… wheeze…"))
	closemessage

	applymovement(LOCALID_TAMASDAD, moves(walk_slower_up))
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_slow_up * 2))
	waitmovement(LOCALID_TAMASDAD)
	waitmovement(OBJ_EVENT_ID_CAMERA)
	special(RemoveCameraObject)

	trainerbattle_no_intro_no_whiteout(TRAINER_TAMAS_DAD, "Dangnabit!")

	specialvar(VAR_RESULT, GetBattleOutcome)
	if (var(VAR_RESULT) != B_OUTCOME_WON) {
		speakername("Kiyosumi")
		msgbox(format("That'll teach you!!"))
		closemessage
	}

	applymovement(LOCALID_TAMASDAD, moves(walk_in_place_fast_down, delay_16 * 3))
	waitmovement(LOCALID_TAMASDAD)

	speakername("Kiyosumi")
	msgbox(format("{FONT_SMALL_NARROW}Huff… puff… Definitely not in as good a shape anymore…"))
	closemessage

	applymovement(LOCALID_TAMA, moves(walk_up * 2, walk_in_place_fast_right))
	waitmovement(LOCALID_TAMA)

	speakername("Tama")
	msgbox(format(
		"Father, I know you don't approve of us, but please understand.\p"
		"My mind is made up and you can't convince me otherwise.\p"
		"What would you have done if someone had come between you and Mother?"
	))
	closemessage

	applymovement(LOCALID_TAMASDAD, moves(walk_in_place_fast_left))
	waitmovement(0)

	speakername("Kiyosumi")
	msgbox(format(
		"… … … … … …\n… … … … … …"
	))
	closemessage
	delay(60)

	applymovement(LOCALID_TAMASDAD, moves(walk_in_place_fast_down))
	waitmovement(0)

	delay(60)

	speakername("Kiyosumi")
	msgbox(format(
		"If your love is as true as you say, then neither I nor anyone else can stop you.\p"
		"Do what you must."
	))
	closemessage

	applymovement(LOCALID_TAMASDAD, moves(walk_slow_down * 3, walk_slow_right * 3, walk_slow_down * 4))
	waitmovement(0)
	removeobject(LOCALID_TAMASDAD)
	applymovement(LOCALID_TAMA, moves(walk_up * 1, face_player))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
	waitmovement(LOCALID_TAMA)
	waitmovement(OBJ_EVENT_ID_PLAYER)

	speakername("Tama")
	msgbox(format(
		"I can't thank you enough.\nNow, let's make haste!\lAshii must be waiting for us!\p"
		"Let me heal your Pokémon as thanks."
	))

	closemessage

	special(HealPlayerParty)
	playse(SE_USE_ITEM)

	destroyfollowernpc

	clearflag(FLAG_HIDE_DEADNIGHT_ASHII)
	addobject(LOCALID_ASHII)

	setvar(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE, 5)
}

script SakuTown_TopTamaTrigger {
	call(SakuTown_EventScript_LoversSeeEachOther)

	applymovement(LOCALID_TAMA2, moves(walk_fast_down, walk_fast_right * 3, walk_fast_up * 2, face_right))
	waitmovement(0)

	setvar(VAR_TEMP_0, 1)
	call(SakuTown_EventScript_LoversReunite)

	applymovement(LOCALID_TAMA2, moves(walk_down, walk_left))
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down, walk_left * 3))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
	waitmovement(LOCALID_TAMA2)
	waitmovement(OBJ_EVENT_ID_CAMERA)
	waitmovement(OBJ_EVENT_ID_PLAYER)

	call(SakuTown_EventScript_TamaTalksToPlayerFinal)

	applymovement(LOCALID_TAMA2, moves(walk_fast_down, walk_fast_left * 5, walk_fast_down, walk_fast_left, walk_fast_down * 4))
	waitmovement(0)

	destroyfollowernpc

	setvar(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE, 6)

	questmenu(QUEST_MENU_SET_REWARD, QUEST_SAKURALOVERS)
}

script SakuTown_BottomTamaTrigger {
	call(SakuTown_EventScript_LoversSeeEachOther)

	applymovement(LOCALID_TAMA2, moves(walk_fast_up, walk_fast_right * 3, walk_fast_up * 1, face_right))
	waitmovement(0)

	setvar(VAR_TEMP_0, 2)
	call(SakuTown_EventScript_LoversReunite)

	applymovement(LOCALID_TAMA2, moves(walk_down * 2, walk_left))
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down, walk_left * 3))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
	waitmovement(LOCALID_TAMA2)
	waitmovement(OBJ_EVENT_ID_CAMERA)
	waitmovement(OBJ_EVENT_ID_PLAYER)

	call(SakuTown_EventScript_TamaTalksToPlayerFinal)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_fast_up, face_down))
	applymovement(LOCALID_TAMA2, moves(walk_fast_left * 5, walk_fast_down, walk_fast_left, walk_fast_down * 4))
	waitmovement(OBJ_EVENT_ID_PLAYER)
	waitmovement(LOCALID_TAMA2)

	destroyfollowernpc

	setvar(VAR_SAKU_KURA_QUEST_TAMA_ASHII_STATE, 6)

	questmenu(QUEST_MENU_SET_REWARD, QUEST_SAKURALOVERS)
	end
}

script SakuTown_EventScript_LoversSeeEachOther {
	playse(SE_PIN)
	applymovement(LOCALID_TAMA2, moves(face_right, emote_exclamation_mark))
	applymovement(LOCALID_ASHII, moves(emote_exclamation_mark))
	waitmovement(LOCALID_TAMA2)
	waitmovement(LOCALID_ASHII)

	// TODO: PUT SOME LOVEYDOVEY MUSIC HERE
	speakername("Ashii")
	msgbox("Tama!")
	closemessage

	speakername("Tama")
	msgbox("Ashii!")
	closemessage

	return
}

script SakuTown_EventScript_LoversReunite {
	applymovement(LOCALID_ASHII, moves(walk_slow_left, walk_slow_down, face_left))
	waitmovement(0)

	speakername("Ashii")
	msgbox(format("… … … … … …\n… … … … … …"))

	special(SpawnCameraObject)	
	switch(var(VAR_TEMP_0))
	{
		case 1:
			applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_slow_right * 3, walk_slow_up))
			waitmovement(0)
		case 2:
			applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_slow_right * 3, walk_slow_up * 2))
			waitmovement(0)
	}
	delay(60)

	applymovement(LOCALID_TAMA2, moves(walk_in_place_fast_right))

	speakername("Tama")
	msgbox("…Hi.")
	closemessage

	applymovement(LOCALID_ASHII, moves(walk_in_place_fast_left))

	speakername("Ashii")
	msgbox(format(
		"Hehehe… Hi.\p"
		"It's nice seeing you."
	))
	closemessage

	delay(60)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_left))

	applymovement(LOCALID_TAMA2, moves(walk_in_place_fast_right))

	speakername("Tama")
	msgbox(format(
		"I didn't think I'd ever see you again.\p"
		"I… I have so much to say…"
	))
	closemessage

	applymovement(LOCALID_ASHII, moves(walk_in_place_fast_left))

	speakername("Ashii")
	msgbox(format(
		"So do I.\nI felt like an insect that could do nothing but stare at the impossibly tall barricade between us.\p"
		"And yet… Even fierce seperatism couldn't keep us apart tonight.\p"
		"Tonight, we can have a midsummer dream of mirth and joy."
	))
	closemessage

	applymovement(LOCALID_TAMA2, moves(walk_in_place_fast_right))

	speakername("Tama")
	msgbox(format("You always were so poetic with your words."))
	waitmessage
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_TAMA2, moves(emote_heart, delay_16 * 3))
	applymovement(LOCALID_ASHII, moves(emote_heart, delay_16 * 3))
	waitmovement(LOCALID_TAMA2)
	waitmovement(LOCALID_ASHII)
	waitse

	applymovement(LOCALID_TAMA2, moves(walk_in_place_fast_right))

	speakername("Tama")
	msgbox(format(
		"We can't stay here for long, Ashii.\N"
		"My father begrundingly let me go, but there's no telling the wrath our villages will wreak upon us if they see us here in the morning."
	))
	closemessage

	applymovement(LOCALID_ASHII, moves(walk_in_place_fast_left))

	speakername("Ashii")
	msgbox(format("I know. But even so, I'm glad to have spent this time with you."))
	closemessage

	applymovement(LOCALID_TAMA2, moves(walk_in_place_fast_right))

	speakername("Tama")
	msgbox(format(
		"Will…\p"
		"Will we see each other again?\nTomorrow?"
	))
	closemessage

	applymovement(LOCALID_ASHII, moves(walk_in_place_fast_left))

	speakername("Ashii")
	msgbox(format(
		"…Tomorrow and tomorrow and tomorrow…\p"
		"It creeps in a petty pace from day to day, but I would gladly wait a thousand of them to see you again."
	))
	closemessage

	delay(60)

	applymovement(LOCALID_TAMA2, moves(face_down, delay_16 * 3, face_right))

	speakername("Tama")
	msgbox(format("{FONT_SMALL_NARROW}What?"))
	closemessage

	applymovement(LOCALID_ASHII, moves(walk_in_place_fast_left))

	speakername("Ashii")
	msgbox(format("Hehe…\nI'll see you tomorrow, my love."))
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_TAMA2, moves(emote_exclamation_mark, delay_16 * 3))
	waitmovement(0)
	waitse
	speakername("Tama")
	msgbox(format("OH! Yes, haha!\nWell… I'll see you tomorrow, then."))
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_TAMA2, moves(emote_heart, delay_16 * 3))
	applymovement(LOCALID_ASHII, moves(emote_heart, delay_16 * 3))
	waitmovement(LOCALID_TAMA2)
	waitmovement(LOCALID_ASHII)
	waitse

	speakername("Ashii")
	msgbox(format("Goodbye, love! May the gods give you a restful slumber!"))
	closemessage

	applymovement(LOCALID_TAMA2, moves(walk_in_place_fast_right))

	speakername("Tama")
	msgbox(format("B-bye…"))
	closemessage

	applymovement(LOCALID_ASHII, moves(walk_slow_up, walk_slow_right * 2, walk_slow_up * 5))
	waitmovement(0)
	removeobject(LOCALID_ASHII)
	setflag(FLAG_HIDE_DEADNIGHT_ASHII)

	return
}

script SakuTown_EventScript_TamaTalksToPlayerFinal {
	special(RemoveCameraObject)

	speakername("Tama")
	msgbox(format(
		"I can't believe it, it finally happened!\p"
		"Wielder… thank you…\nIt's so good to see her again!"
	))
	closemessage

	return
}

script SakuTown_EventScript_TamaArgument_MoveDown1 {
	setvar(VAR_TEMP_0, 1)
	goto(SakuTown_EventScript_TamaArgument)
}

script SakuTown_EventScript_TamaArgument_MoveDown2 {
	setvar(VAR_TEMP_0, 2)
	goto(SakuTown_EventScript_TamaArgument)
}

script SakuTown_EventScript_TamasAnxious {
	faceplayer
	speakername("Tama")
	msgbox(format("My heart is about to burst out of my chest…"))
	closemessage
}

script SakuTown_EventScript_NoLeavingAshii {
	applymovement(LOCALID_TAMA2, moves(face_player))
	waitmovement(0)

	speakername("Tama")
	msgbox(format("W-where are you going?!\nAshii's waiting for us!"))
	closemessage

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up))
	waitmovement(0)
}
mapscripts SunriseVillage_PlayersHouse_Bedroom_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) < 1) {
			setrespawn(HEAL_LOCATION_SUNRISE_VILLAGE)
			setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 1)
			// Fake RTC will resume once the adventure starts!
			pausefakertc
		}
	}
	MAP_SCRIPT_ON_TRANSITION {
		call(SecretBase_EventScript_SetDecorationFlags)
	}
}

script SunriseVillage_PlayersHouse_Bedroom_EventScript_Doll {
	lock
	setnamedmon(SPECIES_MAREEP)
	msgbox(format(
		"“100% real Mareep wool”!"
	))
	release
}

// Big Sis calls player when he gets out of bed
script SunriseVillage_PlayersHouse_Bedroom_Event_BigSisCalling {
    lock
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	speakername("Big sis")
	msgbox(format(
		"{PLAYER}! {PLAYER}!\nAre you still sleeping?"
	))
	
	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"…Yes?",
		"Coming!",
		"…"
	)

	closemessage

	speakername("Big sis")

	if (var(VAR_RESULT) == 0) {
		delay(30)
		msgbox(format(
				"Very funny! Now get up and get ready!"
			))
	} else {
		delay(48)

		msgbox(format(
				"… … … … … …\p"
				"…{PLAYER}!! ARE YOU UP?!"
			))

		dynmultichoice(
			16,
			8,
			TRUE,
			10,
			0,
			DYN_MULTICHOICE_CB_NONE,
			"COMIIIIING!!",
			"COMIIIIIIIIIIING!!!"
		)

		closemessage

		delay(30)

		speakername("Big sis")
		msgbox(format(
			"No need to yell, I can hear you just fine!\p"
			"Now get up and get ready!"
		))
	}

	closemessage

	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 2)
	setflag(FLAG_HIDE_SUNRISE_VILLAGE_STANDARD_NPC_POSITIONS)
    release
    end
}

script SunriseVillage_PlayersHouse_Bedroom_PlayersChest {
	if(flag(FLAG_SYS_QUEST_MENU_GET) == FALSE){
	msgbox(format(
		"It's your chest full of items.\nYou should probably just bring these though."
	), MSGBOX_SIGN)
	waitmessage
	giveitem(ITEM_OINTMENT, 5)
	msgbox(format("You also fished your journal from the bottom! It might come in handy."), MSGBOX_DEFAULT)
	setflag(FLAG_SYS_QUEST_MENU_GET)
	questmenu(QUEST_MENU_SET_ACTIVE, QUEST_HEARTH_MAIN_CAMPAIGN)
	closemessage
	} else {
		playfanfare(MUS_OBTAIN_ITEM)
		msgbox(format(
			"You got the lint from the bottom of the chest!"
		), MSGBOX_SIGN)
		waitfanfare
		waitmessage
		closemessage
	}
}

// TODO EVA
script SunriseVillage_EventScript_PlayerPc {
	lockall
	special(BedroomPC)
	waitstate
}

script SunriseVillage_TurnOffPlayerPC {
	releaseall
}
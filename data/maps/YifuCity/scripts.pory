const LOCALID_GRUNT_PASSWORD_TOP = 9
const LOCALID_GRUNT_PASSWORD_BOTTOM = 10
const LOCALID_GRUNT_PASSWORD_ZUBAT = 11

const FLAG_IS_MON_SENT_TO_PC = FLAG_TEMP_1
const FLAG_HIDE_SAMURAI_IN_FRONT_OF_SHOP = FLAG_TEMP_2
const FLAG_HIDE_ROBBERY_VICTIM = FLAG_TEMP_3
const FLAG_HIDE_CITIZENS_UNSAFE_CITY = FLAG_TEMP_4

const LOCALID_SAMURAI_HIDEOUT = 38
const LOCALID_LOCKDOWN_SAMURAI = 33
const LOCALID_LOCKDOWN_SAMURAI_NORTH = 34
const LOCALID_HARIKO = 35

const VAR_HOUR_OF_DAY = VAR_0x8000

mapscripts YifuCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_YIFU_CITY)

		if (var(VAR_CROBAT_HIDEOUT_STATE) != 3) {
			setflag(FLAG_HIDE_SAMURAI_IN_FRONT_OF_SHOP)
		}

		if (!flag(FLAG_CLEARED_CROBAT_HIDEOUT)) {
			setflag(FLAG_HIDE_CITIZENS_UNSAFE_CITY)
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_CROBAT_HIDEOUT_STATE, 3: YifuCity_EscapedCrobatHideoutWithSamurai
    ]
}

script YifuCity_EscapedCrobatHideoutWithSamurai {
	lockall
	setvar(VAR_CROBAT_HIDEOUT_STATE, 4)
	applymovement(LOCALID_SAMURAI_HIDEOUT, moves(walk_in_place_fast_right))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_left))

	speakername("Samurai")
	msgbox(format(
		"Well, well…\nThey slipped through our fingers,\lbut they finally left the city!\p"
		"Their hideout is now useless!\p"
		"Thank you for giving the Samurai a hand.\lThis operation was a success!\p"
		"You might be young, but you wouldn't make a half bad Samurai yourself,\lyou know…\p"
		"Ha ha ha ha!\p"
		"I have to go report to my superiors.\p"
		"{PLAYER}!\nI am sure we will meet again!\lUntil then, farewell!"
	))
	closemessage

	applymovement(LOCALID_SAMURAI_HIDEOUT, moves(walk_down, walk_left * 7))
	waitmovement(LOCALID_SAMURAI_HIDEOUT)
	removeobject(LOCALID_SAMURAI_HIDEOUT)
	setflag(FLAG_HIDE_LOCKDOWN_SAMURAI)

	releaseall
}

script YifuCity_EventScript_StoleMyMon {
	lock
	faceplayer
	speakername("Man")

	if (flag(FLAG_CLEARED_CROBAT_HIDEOUT)) {
		msgbox(format(
			"The Refuge's nurse was stealing our Pokémon?\p"
			"That… explains a lot!"
		))
	} else {
		msgbox(format(
			"When my friend got his Shiftry back from the nurse, it had somehow changed into a Pidgey…\p"
			"Each day, Pokémon never cease to amaze me!"
		))
	}

	closemessage
	release
}

script YifuCity_EventScript_LostMyDoll {
	lock
	faceplayer
	speakername("Little Girl")
	msgbox(format(
		"I lost my doll while I was busy playing!\p"
		"It's okay though, Cleffa is cuter than any doll!"
	))
	closemessage

	setnamedmon(SPECIES_CLEFFA)
	release
}

script YifuCity_EventScript_Cleffa {
	lock
    faceplayer
	setspeakertomonname(SPECIES_CLEFFA)
    msgbox(format(
        "Faa!"
    ))
	setseenmon(SPECIES_CLEFFA)
    playmoncry(SPECIES_CLEFFA, CRY_MODE_NORMAL)
    waitmoncry
    release
    end
}

script YifuCity_EventScript_IDontFeelSafe {
	lock
	faceplayer
	speakername("Man")

	if (flag(FLAG_CLEARED_CROBAT_HIDEOUT)) {
		msgbox(format(
			"I heard a young Wielder made those weird Crobat dudes go away!\p"
			"They must be a very brave young person."
		))
	} else {
		msgbox(format(
			"I don't feel safe with those Crobat weirdos creeping around the city…\p"
			"I can't imagine how much worse it'd be were it not for the patrolling Samurai!"
		))
	}
	closemessage
	release
}

script YifuCity_EventScript_MissingChild {
	lock
	faceplayer
	speakername("Man")
	msgbox(format(
		"Have you seen a young child, small, red hair?\p"
		"My son told me he was going to the beach, but I don't see him anywhere…"
	))
	closemessage
	release
}

script YifuCity_EventScript_Samurai {
	lock
	faceplayer
	speakername("Samurai")
	msgbox(format(
		"The Crobat Shadows' reign of terror will cease!"
	))
	closemessage
	release
}

script YifuCity_EventScript_Samurai2 {
	lock
	faceplayer
	speakername("Samurai")
	msgbox(format(
		"Please be vigilant! Should you witness any suspicious situation, report it immediately!"
	))
	closemessage
	release
}

script YifuCity_EventScript_SamuraiTalkToRobberyVictim {
	lock
	applymovement(VAR_LAST_TALKED, moves(walk_in_place_fast_left))
	waitmovement(0)
	speakername("Samurai")
	msgbox(format(
		"Ma'am, can you describe what the thiefs looked like?"
	))
	closemessage
	release
}

script YifuCity_EventScript_RobberyVictim {
	lock
	
	speakername("Woman")
	if (flag(FLAG_CLEARED_CROBAT_HIDEOUT)) {
		faceplayer
		msgbox(format(
			"You and the Samurai scared those crooks off? That's good…\p"
			"Although I wonder where my stolen Pokémon are…"
		))

		if (checkitem(ITEM_STOLEN_MONS)) {
			dynmultichoice(
				0,
				0,
				TRUE,
				10,
				0,
				DYN_MULTICHOICE_CB_NONE,
				"Here are your Pokémon!",
				"…"
			)
			closemessage

			switch(var(VAR_RESULT)) {
				case 0:
					playfanfare(MUS_OBTAIN_TMHM)
					message(format("{PLAYER} returned the stolen Pokémon to their rightful owner."))
					waitmessage
					waitfanfare
					removeitem(ITEM_STOLEN_MONS)

					playse(SE_PIN)
					applymovement(VAR_LAST_TALKED, moves(emote_exclamation_mark, delay_16 * 3))
					waitmovement(0)
					waitse
					speakername("Woman")
					msgbox(format(
						"Oh my! You even got my precious Pokémon back!\p"
						"You're a true hero, you know that?\p"
						"I'm finally at peace again! Here, take this, as a thanks!"
					))
					closemessage

					giveitem(ITEM_TM_CALM_MIND)
					waitmessage
					setflag(FLAG_RECEIVED_TM_CALM_MIND)
					addvar(VAR_PLAYER_REPUTATION, 1)

					fadescreenswapbuffers(FADE_TO_BLACK)
					removeobject(VAR_LAST_TALKED)
					delay(60)
					fadescreenswapbuffers(FADE_FROM_BLACK)
					// TODO EVA put her somewhere in the city hanging out with her mons
				case 1:
					speakername("Woman")
					msgbox(format(
						"Sigh… I hope they're okay…"
					))
					closemessage
			}
		}
	} else {
		applymovement(VAR_LAST_TALKED, moves(walk_in_place_fast_right))
		waitmovement(0)
		msgbox(format(
			"A couple of crooks threatened me and stole all my Pokémon, right in front of the Refuge!\p"
			"Oh, what am I ever going to do…?"
		))
	}
	
	closemessage
	release
}

script YifuCity_EventScript_ShinySeller {
	lock
	faceplayer
	speakername("Shady man")
	msgbox(format(
		"Hey, uh… Wanna buy this Pokémon 'ere?\p"
		"'Parently it's super rare or somethin', but I don't really need it, so…"
	))

	setseenmon(SPECIES_CATERPIE)
	showmonpic(SPECIES_CATERPIE, 10, 3, TRUE)
	playmoncry(SPECIES_CATERPIE, CRY_MODE_NORMAL)
	waitmoncry

	waitbuttonpress
	hidemonpic
	
	speakername("Shady man")
	msgbox(format(
		"So whaddyasay?\n"
		"It's yours for only 10 000¥."
	))

	goto(YifuCity_ShinySellerChoice)

	release
}

script YifuCity_ShinySellerChoice {
	showmoneybox(18, 0)

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"I'll buy it!",
		"No, thanks.",
		"Are you a criminal?"
	)

	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			if (checkmoney(10000)) {
				speakername("Shady man")
				msgbox(format(
					"Alright yeah, gimme the cash."
				))

				setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_CATERPIE)
				givemon(SPECIES_CATERPIE, 5, isShiny=FALSE)

				if (var(VAR_RESULT) == MON_CANT_GIVE) {
					goto(Common_EventScript_NoMoreRoomForPokemon)
				}

				removemoney(10000)
				updatemoneybox
				playse(SE_SHOP)

				closemessage

				playfanfare(MUS_OBTAIN_ITEM)
				message(format("{PLAYER} received the Pokémon Caterpie!"))
				waitmessage
				waitfanfare

				hidemoneybox

				bufferspeciesname(STR_VAR_1, SPECIES_CATERPIE)
				if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY) {
					msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
					if (var(VAR_RESULT) != NO) {
						call(Common_EventScript_GetGiftMonPartySlot)
						call(Common_EventScript_NameReceivedPartyMon)
					}
				} elif (var(VAR_RESULT) == MON_GIVEN_TO_PC) {
					msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
					if (var(VAR_RESULT) != NO) {
						call(Common_EventScript_NameReceivedBoxMon)
					}
					setflag(FLAG_IS_MON_SENT_TO_PC)
				}

				speakername("Shady man")
				msgbox(format(
					"Heheheehe… I'm rich! Finally rich!"
				))
				closemessage

				if (var(VAR_FACING) == DIR_EAST) {
					applymovement(VAR_LAST_TALKED, moves(walk_faster_down, walk_faster_left * 8, walk_faster_up * 11))
				} else {
					applymovement(VAR_LAST_TALKED, moves(walk_faster_left * 8, walk_faster_up * 10))
				}
				delay(24)
				applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
				waitmovement(VAR_LAST_TALKED)
				removeobject(VAR_LAST_TALKED)

				delay(48)
				applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
				waitmovement(0)

				msgbox(format(
					"… … … … … …\n… … … … … …"
				))
				closemessage

				playse(SE_PIN)
				applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
				waitmovement(0)
				waitse
				delay(48)

				showmonpic(SPECIES_CATERPIE, 10, 3, TRUE)
				playmoncry(SPECIES_CATERPIE, CRY_MODE_NORMAL)
				waitmoncry

				msgbox(format(
					"Some gold paint came off the Caterpie…"
				))

				playse(SE_M_MIST)
				hidemonpic
				showmonpic(SPECIES_CATERPIE, 10, 3)
				waitse
				
				waitbuttonpress
				closemessage
				hidemonpic

				if (flag(FLAG_IS_MON_SENT_TO_PC)) {
					call(Common_EventScript_TransferredToPC)
				}

				setflag(FLAG_BOUGHT_FAKE_SHINY)

			} else {
				speakername("Shady man")
				msgbox(format(
					"You, uh… You don't got enough cash, kid."
				))
				closemessage
			}
		case 1:
			hidemoneybox
			speakername("Shady man")
			msgbox(format(
				"Then why are ya botherin' me? Scram, kid!"
			))
			closemessage
		case 2:
			hidemoneybox
			speakername("Shady man")
			msgbox(format(
				"What? No! Why woudja even say that? Do I not look respectable to ya?\p"
				"So uh… You gonna buy it or not?"
			))
			goto(YifuCity_ShinySellerChoice)
	}

	hidemoneybox
	release
}

script YifuCity_Trigger_CrobatGruntsScram {
	lockall
	applymovement(LOCALID_GRUNT_PASSWORD_TOP, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)

	speakername("Shady man")
	msgbox(format(
		"I tell ya man, damn password keeps changin'…"
	))
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_GRUNT_PASSWORD_TOP, Common_Movement_ExclamationMark)
	delay(16)
	playse(SE_PIN)
	applymovement(LOCALID_GRUNT_PASSWORD_BOTTOM, Common_Movement_ExclamationMark)
	waitmovement(0)
	waitse
	delay(48)
	turnobject(LOCALID_GRUNT_PASSWORD_TOP, DIR_WEST)
	turnobject(LOCALID_GRUNT_PASSWORD_BOTTOM, DIR_WEST)
	delay(48)

	playse(SE_LEDGE)
	applymovement(LOCALID_GRUNT_PASSWORD_TOP, moves(jump_in_place_left))
	waitmovement(0)
	waitse

	speakername("Shady man")
	msgbox(format(
		"Are ya kiddin' me? Lookaddis eavesdroppin' moron!\p"
		"Want a taste of this?"
	))
	closemessage

	playse(SE_BALL_OPEN)
	addobject(LOCALID_GRUNT_PASSWORD_ZUBAT)
	playmoncry(SPECIES_ZUBAT, CRY_MODE_NORMAL)
	setseenmon(SPECIES_ZUBAT)
	applymovement(
		LOCALID_GRUNT_PASSWORD_ZUBAT,
		moves(
			exit_pokeball,
			face_left,
			lock_anim,
			lock_facing_direction,
			walk_faster_left,
			walk_faster_right
			unlock_anim,
			unlock_facing_direction
		)
	)
	waitmoncry
	waitse
	waitmovement(0)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(lock_facing_direction, walk_left * 2, unlock_facing_direction))
	waitmovement(0)

	speakername("Shady man")
	msgbox(format(
		"Yeah, that's right, hightail it outta here!"
	))
	closemessage

	delay(24)
	addobject(LOCALID_GRUNT_PASSWORD_ZUBAT)
	applymovement(LOCALID_GRUNT_PASSWORD_ZUBAT, moves(enter_pokeball))
	waitmovement(0)

	turnobject(LOCALID_GRUNT_PASSWORD_TOP, DIR_SOUTH)
	turnobject(LOCALID_GRUNT_PASSWORD_BOTTOM, DIR_NORTH)

	releaseall
}

script YifuCity_Trigger_CrobatGruntsEavesdropPassword {
	lockall

	speakername("???")
	msgbox(format(
		"I tell ya man, damn password keeps changin'…"
	))
	closemessage

	delay(24)

	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_up))
	
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 3))
	waitmovement(0)
	delay(48)

	applymovement(LOCALID_GRUNT_PASSWORD_TOP, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)

	speakername("Shady man")
	msgbox(format(
		"Last time, I got it wrong and he didn' let me through!\p"
		"Like, brother, why I gotta tell ya some password for ya to let me in, we wen' to school together!"
	))
	closemessage

	applymovement(LOCALID_GRUNT_PASSWORD_BOTTOM, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)

	speakername("Shady man")
	msgbox(format(
		"Wait, ya didn't go to school though, didja?"
	))
	closemessage

	applymovement(LOCALID_GRUNT_PASSWORD_TOP, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)

	speakername("Shady man")
	msgbox(format(
		"Who cares! It's a figger of speech! What's the newest password anyway?"
	))
	closemessage

	applymovement(LOCALID_GRUNT_PASSWORD_BOTTOM, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)

	speakername("Shady man")
	msgbox(format(
		"It's uh…\nSomethin' with Crobat…\l"
		"“{COLOR RED}Crobat is{COLOR DARK_GRAY}”… Uh…"
	))

	playse(SE_LEDGE)
	applymovement(LOCALID_GRUNT_PASSWORD_BOTTOM, moves(jump_in_place_up))
	speakername("Shady man")
	msgbox(format(
		"Aw man! I forgot it too!"
	))
	waitmovement(0)
	waitse
	closemessage

	playse(SE_LEDGE)
	applymovement(LOCALID_GRUNT_PASSWORD_TOP, moves(jump_in_place_down))
	speakername("Shady man")
	msgbox(format(
		"Ya idjet! How're we gunna get back in now??\p"
		"Now we gotta find another Zubat an' hope he's got enough brains to remember the full sent'nce!\p"
		"C'mon, lessgo!"
	))
	waitmovement(0)
	waitse
	closemessage

	applymovement(LOCALID_GRUNT_PASSWORD_TOP, moves(walk_left * 10))
	applymovement(LOCALID_GRUNT_PASSWORD_BOTTOM, moves(walk_up, walk_left * 9))
	waitmovement(0)

	removeobject(LOCALID_GRUNT_PASSWORD_TOP)
	removeobject(LOCALID_GRUNT_PASSWORD_BOTTOM)
	removeobject(LOCALID_GRUNT_PASSWORD_ZUBAT)
	setflag(FLAG_GOT_CROBAT_PASSWORD_FIRST_HALF)

	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 3))
	waitmovement(0)
	special(RemoveCameraObject)

	releaseall
}

script YifuCity_EventScript_HearEcho {
	lock
	faceplayer
	speakername("Boy")
	msgbox(format(
		"There's a ghost haunting the sewers!\p"
		"I swear I heard a voice calling when I was walking along the canal!"
	))
	closemessage
	release
}

script YifuCity_Trigger_CrobatGruntStuckInHole {
	lockall

	if (flag(FLAG_GOT_CROBAT_PASSWORD_SECOND_HALF)) {
		speakername("???")

		random(5)
		switch(var(VAR_RESULT)) {
		case 0:
			msgbox(format(
				"Yawn… I'm bored…"
			))
		case 1:
			setnamedmon(SPECIES_SHARPEDO)
			msgbox(format(
				"Sure hope there ain't no Sharpedo in here…"
			))
		case 2:
		case 3:
			msgbox(format(
				"Echo! … … …\n… … …{FONT_SMALL}echo{FONT_NORMAL}…\p"
				"Hehe…\p"
				"…Sigh…"
			))
		case 4:
			msgbox(format(
				"“{COLOR RED}Crime is awesome{COLOR DARK_GRAY}”!\nHmm…\p"
				"“{COLOR RED}Stealing is awesome{COLOR DARK_GRAY}”?\nNah… That's not it."
			))
		}

		closemessage

	} else {
		speakername("???")
		msgbox(format(
			"Heyyy!! Help!"
		))
		closemessage

		delay(24)

		applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left, delay_16, face_up, delay_16, face_down, delay_16, face_right, delay_16, face_up, delay_16 * 3))
		waitmovement(0)

		delay(24)

		playse(SE_PIN)
		applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_exclamation_mark, face_down))
		speakername("???")
		msgbox(format(
			"I fell down the canal! I'm stuck down 'ere!\p"
			"Damn me, couldn't remember the first half of the damn password…\l"
			"“Something something… {COLOR RED}is awesome{COLOR DARK_GRAY}”! But what?!\l"
			"And why'd I think it was a good idea to try gettin' in through the canal?\l"
			"I can't swim! It's a dead end!"
		))
		waitmovement(0)
		waitse
		closemessage

		setflag(FLAG_GOT_CROBAT_PASSWORD_SECOND_HALF)
	}

	releaseall
}

script YifuCity_EventScript_Surskit {
	lock
    faceplayer
	setspeakertomonname(SPECIES_SURSKIT)
    msgbox(format(
        "Sksksksk…"
    ))
	setseenmon(SPECIES_SURSKIT)
    playmoncry(SPECIES_SURSKIT, CRY_MODE_NORMAL)
    waitmoncry
    release
    end
}

script YifuCity_EventScript_SafeCity {
	lock
	faceplayer
	speakername("Girl")
	msgbox(format(
		"Yifu City is finally safe again!\nI can't believe it!"
	))
	closemessage

	release
}


script YifuCity_EventScript_Tourist {
	lock
	faceplayer
	speakername("Woman")
	msgbox(format(
		"We came all the way from Windycape to see the great capital.\p"
		"The city's beautiful!\nSo many tourists, though…"
	))
	closemessage

	release
}

script YifuCity_EventScript_TouristKid {
	lock
	faceplayer
	speakername("Little Boy")
	msgbox(format(
		"We're on vacation!\nI wanna see the big Sawsbuck!"
	))
	closemessage

	setnamedmon(SPECIES_SAWSBUCK)

	release
}

script YifuCity_EventScript_TouristGirl {
	lock
	faceplayer
	speakername("Girl")
	msgbox(format(
		"I'm tired… I wish mom'd take us to the hot springs…"
	))
	closemessage

	setnamedmon(SPECIES_SAWSBUCK)

	release
}

script YifuCity_EventScript_Zubat1 {
	lock
	faceplayer
	speakername("Zubat Grunt")
	msgbox(format(
		"Whatchu lookin' at?"
	))
	closemessage

	release
	end
}

script YifuCity_EventScript_Zubat2 {
	lock
	faceplayer
	speakername("Zubat Grunt")
	msgbox(format(
		"Scram, kid, I got no time for the likes of ya."
	))
	closemessage

	release
	end
}

script YifuCity_EventScript_Zubat3 {
	lock
	faceplayer
	speakername("Zubat Grunt")
	msgbox(format(
		"I'm just a regular, non-suspect, law-abiding citizen, mindin' my own business."
	))
	closemessage

	release
	end
}

script YifuCity_EventScript_Zubat4 {
	lock
	faceplayer
	speakername("Zubat Grunt")
	msgbox(format(
		"I sure enjoy quaint day-to-day activities like strolling, shopping, thieving, fostering a positive environment within my community…"
	))
	closemessage

	release
	end
}

script YifuCity_Trigger_Lockdown {
	lockall
	// First time
	if (!flag(FLAG_TRIGGERED_LOCKDOWN)) {
		setflag(FLAG_TRIGGERED_LOCKDOWN)
		playse(SE_PIN)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
		msgbox(format(
			"ATTENTION! ATTENTION!\n"
			"THE CITY IS NOW UNDER LOCKDOWN!"
		))
		closemessage
		delay(48)
		waitmovement(0)
		waitse

		getplayerxy(VAR_TEMP_2, VAR_TEMP_3)

		clearflag(FLAG_HIDE_LOCKDOWN_SAMURAI)

		setobjectxyperm(LOCALID_LOCKDOWN_SAMURAI, 13, 39)
		addobject(LOCALID_LOCKDOWN_SAMURAI)

		applymovement(LOCALID_LOCKDOWN_SAMURAI, moves(walk_fast_left * 10, walk_in_place_fast_right))

		switch(var(VAR_TEMP_3)) {
			case 36:
				applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down * 3, face_left))
			case 37:
				applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down * 2, face_left))
			case 38:
				applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down, face_left))
			case 40:
				applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up, face_left))
			case 41:
				applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 2, face_left))
			case 42:
				applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 3, face_left))
			case 43:
				applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 4, face_left))
		}
		waitmovement(LOCALID_LOCKDOWN_SAMURAI)
		waitmovement(OBJ_EVENT_ID_PLAYER)

		special(GetPlayerSirMissString)
		speakername("Samurai")
		msgbox(format(
			"I'm sorry, {STR_VAR_1}, all exits are temporarily blocked. You are asked to remain inside the city until further notice."
		))

		mcdialogue(
			"Why?",
			"What's happening?"
		)
		closemessage

		speakername("Samurai")
		msgbox(format(
			"A robbery has taken place near the Pokémon Refuge!\p"
			"We Samurai are on the cusp of cracking down on the infamous Crobat Shadows!\p"
			"We cannot risk this operation to go awry, so we've locked down the premises for the time being!\p"
			"Please remain calm, I'm sure everything will be over before you know it."
		))
		closemessage

		applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right))
		waitmovement(OBJ_EVENT_ID_PLAYER)

	} else {
		applymovement(LOCALID_LOCKDOWN_SAMURAI, moves(walk_in_place_fast_right))
		waitmovement(0)
		special(GetPlayerSirMissString)
		speakername("Samurai")
		msgbox(format(
			"I'm sorry, {STR_VAR_1}, all exits are temporarily blocked. Nobody in or out.\p"
			"You are asked to remain inside the city until further notice."
		))
		closemessage
		
		applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right))
		waitmovement(OBJ_EVENT_ID_PLAYER)
	}

	releaseall
}

script YifuCity_Trigger_CantGoThruNorth {
	lockall
	applymovement(LOCALID_LOCKDOWN_SAMURAI_NORTH, moves(walk_in_place_fast_down))
	waitmovement(0)
	special(GetPlayerSirMissString)
	speakername("Samurai")
	msgbox(format(
		"I'm sorry, {STR_VAR_1}, all exits are temporarily blocked. Nobody in or out.\p"
		"You are asked to remain inside the city until further notice."
	))
	closemessage
	
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
	waitmovement(OBJ_EVENT_ID_PLAYER)

	releaseall
}

script YifuCity_Trigger_HarikoCriminals {
	lockall

	if (getroutechallengestate(ROUTE_CHALLENGE_WHITESLATE_YIFU) == ROUTE_CHALLENGE_STATE_STARTED) {
		setroutechallengestate(ROUTE_CHALLENGE_WHITESLATE_YIFU, ROUTE_CHALLENGE_STATE_SUCCESS)
	}

	playse(SE_PIN)

	getplayerxy(VAR_TEMP_2, VAR_TEMP_3)
	switch(var(VAR_TEMP_2)) {
		case 38:
			applymovement(LOCALID_HARIKO, moves(face_right, emote_exclamation_mark, delay_16 * 3, walk_right * 2, face_up))
		case 39:
			applymovement(LOCALID_HARIKO, moves(face_right, emote_exclamation_mark, delay_16 * 3, walk_right * 3, face_up))
		case 40:
			applymovement(LOCALID_HARIKO, moves(face_right, emote_exclamation_mark, delay_16 * 3, walk_right * 4, face_up))
	}
	waitmovement(0)

	speakername("Hariko")
	msgbox(format(
		"Hello, {PLAYER}.\nAre you well, since last time?\p"
		"Did you hear?\nThere was a robbery right here in Yifu City! Some people's Pokémon were stolen!\p"
		"I heard there was a second decaying area west of here.\lI was planning on going there, but a Samurai was blocking the way, so I am waiting here.\p"
		"Let us have lunch together before you leave. Yes? Good."
	))
	closemessage

	applymovement(LOCALID_HARIKO, moves(walk_left * 10))
	waitmovement(LOCALID_HARIKO)

	removeobject(LOCALID_HARIKO)
	setflag(FLAG_HIDE_HARIKO_YIFU_CITY)
	releaseall
}

script YifuCity_EventScript_DojoSign {
	msgbox(
		format(
			"Yifu City Pokémon Dojo\n"
			"Leader: Yutaka\p"
			"“The poetry in the rustling of a kimono.”"
		),
		MSGBOX_SIGN
	)
}

script YifuCity_EventScript_GirlLookingAtSunriseVillage {
	lock
	faceplayer
	speakername("Little Girl")
	msgbox(format(
		"If I squint hard enough, I'm sure I can see my friend on Sunrise Beach!"
	))
	closemessage
	applymovement(VAR_LAST_TALKED, moves(face_original_direction))
	waitmovement(0)
	release
	end
}

script YifuCity_EventScript_Chimecho {
	lock
	faceplayer
	setseenmon(SPECIES_CHIMECHO)
	setspeakertomonname(SPECIES_CHIMECHO)
	playmoncry(SPECIES_CHIMECHO, CRY_MODE_NORMAL)
	msgbox(format(
		"Ch-ching…"
	))
	waitmoncry
	closemessage
	release
	end
}

script YifuCity_EventScript_ChimechoLady {
	lock
	faceplayer
	speakername("Old Woman")
	msgbox(format(
		"I love listening to Chimecho's songs.\p"
		"They echo in its hollow body and produce the most wonderful harmonics.\p"
		"You should always be kind to Chimecho! They are sacred, and mistreating one\lwould surely bring you bad luck."
	))
	closemessage

	
	setnamedmon(SPECIES_CHIMECHO)
	setdescribedmon(SPECIES_CHIMECHO)

	release
	end
}

script YifuCity_EventScript_ChefOnBreak {
	lock
	faceplayer
	speakername("Cook")
	msgbox(format(
		"Huff…\n"
		"We're on break right now.\p"
		"The food service industry is truly merciless."
	))
	closemessage

	release
	end
}

script YifuCity_EventScript_Nacli {
	lock
    faceplayer
	setspeakertomonname(SPECIES_NACLI)
    msgbox(format(
        "Xrckl…"
    ))
	setseenmon(SPECIES_NACLI)
    playmoncry(SPECIES_NACLI, CRY_MODE_NORMAL)
    waitmoncry
    release
    end
}

script YifuCity_EventScript_NameRaterSign {
	msgbox(format(
		"What's In A Name\n“Humble name judge”"
	), MSGBOX_SIGN)
}

script YifuCity_EventScript_HotSpringsSign {
	msgbox(format(
		"Torkoal Tubs\n“The Yifu City Hot Springs”"
	), MSGBOX_SIGN)
}

script YifuCity_EventScript_CrobatShopSign {
	if (flag(FLAG_CLEARED_CROBAT_HIDEOUT)) {
		msgbox(format(
			"Yifu City Shop\n“Useful items and scrolls!”"
		), MSGBOX_SIGN)
	} else {
		msgbox(format(
			"Crobat Shop\n“Only legal stuff\lNothing to see here”"
		), MSGBOX_SIGN)
	}
}

script YifuCity_EventScript_RestaurantSign {
	msgbox(format(
		"The Golden Drampa\n“Hotpot & Restaurant”"
	), MSGBOX_SIGN)
}

script YifuCity_EventScript_ClothingShopSign {
	msgbox(format(
		"Leavanny Silk\n"
		"“Fashionable and affordable clothes!”"
	), MSGBOX_SIGN)
}

script YifuCity_EventScript_DecorationShopSign {
	msgbox(format(
		"Woodworker\n"
		"“We sell tables and chairs, mostly”"
	), MSGBOX_SIGN)
}

// NATSUKI

script YifuCity_EventScript_NastukiPostHideout {
	lock
	faceplayer

	if (var(VAR_HOTPOT_SCENE_STATE) == 0) {
		setvar(VAR_HOTPOT_SCENE_STATE, 1)
		gettime

		// Morning
		if (var(VAR_HOUR_OF_DAY) >= 5 && var(VAR_HOUR_OF_DAY) <= 11) {
			call(SpeakToNatsukiMorning)
			// Lunch or dinner
		} elif (
			(var(VAR_HOUR_OF_DAY) > 11 && var(VAR_HOUR_OF_DAY) <= 15)
			|| (var(VAR_HOUR_OF_DAY) > 18 && var(VAR_HOUR_OF_DAY) <= 22)
		) {
			call(SpeakToHarikoLunchOrDinner)
			// Snack
		} elif (var(VAR_HOUR_OF_DAY) > 15 && var(VAR_HOUR_OF_DAY) <= 18) {
			call(SpeakToNatsukiAfternoon)
			// Late night dinner (23h-5h)
		} else {
			call(SpeakToNatsukiNight)
		}

		closemessage
	} else {
		call(SpeakToNatsukiAgain)
	}

	release
}

script(local) SpeakToNatsukiMorning {
	call(NatsukiCanIComeToo)

	call(HarikoFacesPlayer)

	speakername("Hariko")
	msgbox(format(
		"But… It is too late for dinner!"
	))

	call(HarikoNotABreakfastPlace)
}

script(local) SpeakToNatsukiLunchOrDinner {
	call(NatsukiCanIComeToo)

	call(HarikoFacesPlayer)

	speakername("Hariko")
	msgbox(format(
		"It would be impolite not to invite\nyour friend, {PLAYER}."
	))

	applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(walk_in_place_fast_up))
	waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)

	msgbox(format(
		"I have selected the Golden Drampa for our outing."
	))

	call(HarikoFacesPlayer)

	msgbox(format(
		"Shall we go in?"
	))

	mcdialogue(
		"Let's go!",
		"Not right now…"
	)

	switch (var(VAR_RESULT)) {
		case 1:
			call(EnterRestaurant)
		case 0:
			call(NotRightNow)
	}
}

script(local) SpeakToNatsukiAfternoon {
	call(NatsukiCanIComeToo)

	call(HarikoFacesNatsuki)

	speakername("Hariko")
	msgbox(format(
		"But… It is too late for lunch,\nand too early for dinner!"
	))

	call(NatsukiFacesHariko)
	call(HarikoTurnsToNatsuki)

	speakername("Natsuki")
	msgbox(format(
		"Hey, you should chill.\nThe Golden Drampa's open all day!\p"
		"I actually haven't had lunch yet.\nI vote afternoon hotpot!"
	))

	call(NatsukiFacesPlayer)

	msgbox(format(
		"What d'you say, {PLAYER}?\nYou in or what?"
	))

	mcdialogue(
		"Afternoon hotpot!",
		"Not right now…"
	)

	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			call(EnterRestaurant)
		case 1:
			call(NotRightNow)
	}
}

script(local) SpeakToNatsukiNight {
	call(NatsukiCanIComeToo)

	call(HarikoFacesPlayer)

	speakername("Hariko")
	msgbox(format(
		"What? Clearly, you haven't looked\nat the time!"
	))

	call(HarikoTooLateForDinner)
}

// HARIKO

script YifuCity_EventScript_HarikoPostHideout {
	lock
	faceplayer

	if (var(VAR_HOTPOT_SCENE_STATE) == 0) {
		setvar(VAR_HOTPOT_SCENE_STATE, 1)
		gettime

		// Morning
		if (var(VAR_HOUR_OF_DAY) >= 5 && var(VAR_HOUR_OF_DAY) <= 11) {
			call(SpeakToHarikoMorning)
			// Lunch or dinner
		} elif (
			(var(VAR_HOUR_OF_DAY) > 11 && var(VAR_HOUR_OF_DAY) <= 15)
			|| (var(VAR_HOUR_OF_DAY) > 18 && var(VAR_HOUR_OF_DAY) <= 22)
		) {
			call(SpeakToHarikoLunchOrDinner)
			// Snack
		} elif (var(VAR_HOUR_OF_DAY) > 15 && var(VAR_HOUR_OF_DAY) <= 18) {
			call(SpeakToHarikoAfternoon)
			// Late night dinner (23h-5h)
		} else {
			call(SpeakToHarikoNight)
		}

		closemessage
	} else {
		call(SpeakToHarikoAgain)
	}

	release
}

script(local) SpeakToHarikoMorning {
	speakername("Hariko")
	msgbox(format(
		"{PLAYER}!\nYou are here too early!"
	))

	mcdialogue(
		"Too early for what?",
		"I'm hungry now!"
	)

	switch (var(VAR_RESULT)) {
		case 0:
			msgbox(format(
				"Do you not remember?\p"
				"You said we would eat together\nbefore leaving the city."
			))

		case 1:
			msgbox(format(
				"When we said we would eat together\nbefore leaving the city, I thought\lthat meant lunch, or dinner."
			))
	}

	call(HarikoNotABreakfastPlace)
}

script(local) HarikoNotABreakfastPlace {
	applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(walk_in_place_fast_up))
	waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)

	msgbox(format(
		"I had selected the Golden Drampa for our outing…"
	))

	call(HarikoFacesPlayer)

	msgbox(format(
		"But it isn't a breakfast place.\nIt is a lunch or dinner place.\p"
		"You should have told me you wanted to have breakfast!"
	))

	call(NatsukiFacesHariko)
	call(HarikoTurnsToNatsuki)

	speakername("Natsuki")
	msgbox(format(
		"Hey, you should chill.\nI'm sure they'll have breakfast options!\p"
		"I could actually go for a hotpot anyway!"
	))

	call(NatsukiFacesPlayer)
	speakername("Natsuki")
	msgbox(format(
		"What d'you say, {PLAYER}?\nYou in or what?"
	))

	mcdialogue(
		"Heck yeah!",
		"Not right now…"
	)

	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			call(EnterRestaurant)
		case 1:
			call(NotRightNow)
	}
}

script(local) HarikoTooLateForDinner {
	applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(walk_in_place_fast_up))
	waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)

	msgbox(format(
		"I had selected the Golden Drampa for our outing…"
	))

	faceplayer

	msgbox(format(
		"But it is too late for dinner, and definitely too early for lunch!"
	))

	call(NatsukiFacesHariko)
	call(HarikoTurnsToNatsuki)

	speakername("Natsuki")
	msgbox(format(
		"Hey, you should chill.\nThe Golden Drampa's open all night!\p"
		"I haven't eaten much today.\nI could go for a late night hotpot!\p"
		"What d'you say, {PLAYER}?\nYou in or what?"
	))

	mcdialogue(
		"Late night hotpot!",
		"Not right now…"
	)

	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			call(EnterRestaurant)
		case 1:
			call(NotRightNow)
	}
}

script(local) SpeakToHarikoLunchOrDinner {
	if (var(VAR_HOUR_OF_DAY) > 11 && var(VAR_HOUR_OF_DAY) <= 15) {
		bufferstring(STR_VAR_1, "lunch")
	} else {
		bufferstring(STR_VAR_1, "dinner")
	}

	speakername("Hariko")
	msgbox(format(
		"{PLAYER}! You are here to eat {STR_VAR_1}\nwith me, yes?"
	))

	call(HarikoFacesNatsuki)
	turnobject(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, DIR_WEST)

	speakername("Hariko")
	msgbox(format(
		"I saw Natsuki on the way here.\p"
		"It would be impolite not to invite him, too!"
	))
	
	call(NatsukiFacesPlayer)
	faceplayer

	speakername("Natsuki")
	msgbox(format(
		"Hey, {PLAYER}!\nSounds like fun, I'm in!"
	))

	mcdialogue(
		"Let's go!",
		"Not right now…"
	)

	switch (var(VAR_RESULT)) {
		case 1:
			call(EnterRestaurant)
		case 0:
			call(NotRightNow)
	}
}

script(local) SpeakToHarikoNight {
	speakername("Hariko")
	msgbox(format(
		"{PLAYER}!\nYou are very late!"
	))

	mcdialogue(
		"Late for what?",
		"I'm sorry!"
	)

	switch (var(VAR_RESULT)) {
		case 0:
			msgbox(format(
				"Do you not remember?\p"
				"You said we would eat together\nbefore leaving the city."
			))

		case 1:
			msgbox(format(
				"When we said we would eat together\nbefore leaving the city, I thought\lthat meant lunch, or dinner."
			))
	}

	call(HarikoTooLateForDinner)
}

script(local) SpeakToHarikoAfternoon {
	speakername("Hariko")
	msgbox(format(
		"{PLAYER}!\nYou are late!"
	))

	mcdialogue(
		"Late for what?",
		"I'm sorry!"
	)

	switch (var(VAR_RESULT)) {
		case 0:
			msgbox(format(
				"Do you not remember?\p"
				"You said we would eat together\nbefore leaving the city."
			))

		case 1:
			msgbox(format(
				"When we said we would eat together\nbefore leaving the city, I thought\lthat meant lunch, or dinner."
			))
	}

	applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(walk_in_place_fast_up))
	waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)

	msgbox(format(
		"I had selected the Golden Drampa for our outing…"
	))

	faceplayer

	msgbox(format(
		"But it is both too late for lunch, and too early for dinner!"
	))

	call(NatsukiFacesHariko)
	call(HarikoTurnsToNatsuki)

	speakername("Natsuki")
	msgbox(format(
		"Hey, you should chill.\nThe Golden Drampa's open all day!\p"
		"I actually haven't had lunch yet.\nI vote afternoon hotpot!"
	))

	call(NatsukiFacesPlayer)

	msgbox(format(
		"What d'you say, {PLAYER}?\nYou in or what?"
	))

	mcdialogue(
		"Afternoon hotpot!",
		"Not right now…"
	)

	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			call(EnterRestaurant)
		case 1:
			call(NotRightNow)
	}
}

script(local) SpeakToHarikoAgain {
	gettime

	call(NatsukiTurnsToPlayer)

	speakername("Hariko")

	gettime
	// Morning
	if (var(VAR_HOUR_OF_DAY) >= 5 && var(VAR_HOUR_OF_DAY) <= 11) {
		msgbox(format(
			"{PLAYER}. Shall we have… (Sigh)\n"
			"Hotpot breakfast, apparently?"
		))
		// Lunch or dinner
	} elif (
		(var(VAR_HOUR_OF_DAY) > 11 && var(VAR_HOUR_OF_DAY) <= 15)
		|| (var(VAR_HOUR_OF_DAY) > 18 && var(VAR_HOUR_OF_DAY) <= 22)
	) {
		msgbox(format(
			"{PLAYER}. Shall we have dinner\nat the hotpot together?"
		))
		// Snack
	} elif (var(VAR_HOUR_OF_DAY) > 15 && var(VAR_HOUR_OF_DAY) <= 18) {
		msgbox(format(
			"{PLAYER}. Shall we have… (Sigh)\n"
			"Hotpot snack, apparently?"
		))
		// Late night dinner (23h-5h)
	} else {
		msgbox(format(
			"{PLAYER}. Shall we have… (Sigh)\n"
			"Late night hotpot?"
		))
	}

	mcdialogue(
		"Let's go!",
		"Not right now…"
	)

	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			call(EnterRestaurant)
		case 1:
			call(NotRightNow)
	}
}

script(local) SpeakToNatsukiAgain {
	gettime

	call(HarikoTurnsToPlayer)

	speakername("Natsuki")

	// Morning
	if (var(VAR_HOUR_OF_DAY) >= 5 && var(VAR_HOUR_OF_DAY) <= 11) {
		msgbox(format(
			"Ready for a sick hotpot breakfast,\n{PLAYER}?"
		))
		// Lunch or dinner
	} elif (
		(var(VAR_HOUR_OF_DAY) > 11 && var(VAR_HOUR_OF_DAY) <= 15)
		|| (var(VAR_HOUR_OF_DAY) > 18 && var(VAR_HOUR_OF_DAY) <= 22)
	) {
		msgbox(format(
			"Ready for a sick hotpot meal,\n{PLAYER}?"
		))
		// Snack
	} elif (var(VAR_HOUR_OF_DAY) > 15 && var(VAR_HOUR_OF_DAY) <= 18) {
		msgbox(format(
			"Ready for a sick hotpot snack,\n{PLAYER}?"
		))
		// Late night dinner (23h-5h)
	} else {
		msgbox(format(
			"Ready for a sick late night hotpot,\n{PLAYER}?"
		))
	}

	mcdialogue(
		"Let's go!",
		"Not right now…"
	)

	closemessage

	switch(var(VAR_RESULT)) {
		case 0:
			call(EnterRestaurant)
		case 1:
			call(NotRightNow)
	}
}

script(local) NatsukiCanIComeToo {
	faceplayer

	speakername("Natsuki")
	msgbox(format(
		"{PLAYER}! What's up? Did you hear\nabout the whole lockdown thing?\l"
		"Wild stuff, huh?"
	))

	call(NatsukiFacesHariko)

	if (var(VAR_HOUR_OF_DAY) > 5 && var(VAR_HOUR_OF_DAY) <= 15) {
		bufferstring(STR_VAR_1, "lunch")
	} else {
		bufferstring(STR_VAR_1, "dinner")
	}

	msgbox(format(
		"I found your friend Hariko!\nSo you're having {STR_VAR_1} together?\l"
		"Sounds like fun, can I come too?"
	))
}

script(local) NotRightNow {
	call(HarikoFacesPlayer)
	speakername("Hariko")
	msgbox(format(
		"You still have errands to run?\nWe will be waiting here!"
	))

	call(NatsukiFacesPlayer)
	speakername("Natsuki")
	msgbox(format(
		"Be quick about it! I'm starving!"
	))
}

script(local) EnterRestaurant {
	setvar(VAR_HOTPOT_SCENE_STATE, 2)
	warp(MAP_YIFU_CITY_RESTAURANT, WARP_ID_RESTAURANT_CUTSCENE)
}

script(local) HarikoFacesPlayer {
	switch(var(VAR_FACING)) {
		case DIR_NORTH:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(walk_in_place_fast_down))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
		case DIR_SOUTH:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(walk_in_place_fast_up))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
		case DIR_EAST:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(walk_in_place_fast_left))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
		case DIR_WEST:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(walk_in_place_fast_right))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
	}
}

script(local) NatsukiFacesPlayer {
	switch(var(VAR_FACING)) {
		case DIR_NORTH:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(walk_in_place_fast_down))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
		case DIR_SOUTH:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(walk_in_place_fast_up))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
		case DIR_EAST:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(walk_in_place_fast_left))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
		case DIR_WEST:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(walk_in_place_fast_right))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
	}
}

script(local) HarikoTurnsToPlayer {
	switch(var(VAR_FACING)) {
		case DIR_NORTH:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(face_down))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
		case DIR_SOUTH:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(face_up))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
		case DIR_EAST:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(face_left))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
		case DIR_WEST:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(face_right))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
	}
}

script(local) NatsukiTurnsToPlayer {
	switch(var(VAR_FACING)) {
		case DIR_NORTH:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(face_down))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
		case DIR_SOUTH:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(face_up))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
		case DIR_EAST:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(face_left))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
		case DIR_WEST:
			applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(face_right))
			waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
	}
}

script(local) NatsukiFacesHariko {
	applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(walk_in_place_fast_left))
	waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
}

script(local) HarikoFacesNatsuki {
	applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(walk_in_place_fast_right))
	waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
}

script(local) NatsukiTurnsToHariko {
	applymovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI, moves(face_left))
	waitmovement(LOCALID_FRONT_OF_RESTAURANT_NATSUKI)
}

script(local) HarikoTurnsToNatsuki {
	applymovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO, moves(face_right))
	waitmovement(LOCALID_FRONT_OF_RESTAURANT_HARIKO)
}


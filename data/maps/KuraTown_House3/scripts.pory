const LOCALID_LADY = 1
const LOCALID_ESPEON = 2

mapscripts KuraTown_House3_MapScripts {
	
}

// Lady
script KuraTown_House3_EventScript_Lady {
	lock

	// First time talking to her
	if (var(VAR_SAKU_KURA_QUEST_ESPEON_STATE) == 0 || var(VAR_SAKU_KURA_QUEST_ESPEON_STATE) == 2) {
		faceplayer
		speakername("Woman")
		msgbox(format(
			"Hello! Ah, you came for your Pokémon!"
		))
		closemessage
		delay(60)

		speakername("Woman")
		msgbox(format(
			"Sorry, I thought you were someone else.\p"
			"See, this little one came to me a while ago. There was a thunderstorm and it looked scared and lost.\p"
			"I've been taking care of it while waiting for its master to show up!\p"
			"It's feeling a lot better now. Look how shiny his fur is! Such a pretty color, too!"
		))
		closemessage

		if (var(VAR_SAKU_KURA_QUEST_ESPEON_STATE) == 0) {
			// Talked to lady but not to Espeon's master
			setvar(VAR_SAKU_KURA_QUEST_ESPEON_STATE, 1)
			applymovement(LOCALID_LADY, Common_Movement_FaceOriginalDirection)
		} elif (var(VAR_SAKU_KURA_QUEST_ESPEON_STATE) == 2) {
			delay(60)
			// Espeon goes home
			call(KuraTown_House3_EventScript_EspeonGoesHome)
		}

		applymovement(LOCALID_LADY, Common_Movement_FaceOriginalDirection)
		// Talked to her before, but not Espeon's master
	} elif (var(VAR_SAKU_KURA_QUEST_ESPEON_STATE) == 1) {
		faceplayer
		speakername("Woman")
		msgbox(format(
			"Don't worry, kitten, we'll find your master eventually!"
		))
		closemessage

		// Told Espeon's master where his Pokémon was
	} elif (var(VAR_SAKU_KURA_QUEST_ESPEON_STATE) == 3) {
		faceplayer
		// Espeon goes home
		call(KuraTown_House3_EventScript_EspeonGoesHome)
		// Espeon went home
	} elif (var(VAR_SAKU_KURA_QUEST_ESPEON_STATE) == 4) {
		applymovement(VAR_LAST_TALKED, moves(emote_sad))
		faceplayer
		speakername("Woman")
		msgbox(format(
			"Sniffle… I'm going to miss him…"
		))
		closemessage
		
		// Talked to Espeon's master once are reunited
	} elif (var(VAR_SAKU_KURA_QUEST_ESPEON_STATE) == 5) {
		faceplayer
		speakername("Woman")
		msgbox(format(
			"That little lost kitten is reunited with its master?\p"
			"Do they look happy together?\nOh, how delightful!"
		))
		closemessage
	}
	
	release
}

script KuraTown_House3_EventScript_EspeonGoesHome {
		faceplayer
		playse(SE_PIN)
		applymovement(LOCALID_LADY, Common_Movement_ExclamationMark)
		waitmovement(0)
		delay(48)
		speakername("Woman")
		msgbox(format(
			"What? You're saying its master is waiting for him in Saku Village?"
		))
		closemessage

		applymovement(LOCALID_LADY, moves(emote_pensive, delay_16 * 6))
		waitmovement(0)

		speakername("Woman")
		msgbox(format(
			"I'd rather not try going over there, folks over at Saku Village are notoriously mean…\p"
			"But I guess… I can make an effort for this kitten here."
		))
		closemessage

		applymovement(LOCALID_LADY, Common_Movement_FaceLeft)
		applymovement(LOCALID_ESPEON, Common_Movement_FaceRight)

		waitmovement(0)
		applymovement(LOCALID_LADY, moves(emote_heart))
		speakername("Woman")
		msgbox(format(
			"Good news, sweetie! You're going home!"
		))
		closemessage

		applymovement(LOCALID_ESPEON, JumpInJoy)
		setseenmon(SPECIES_ESPEON)
		playmoncry(SPECIES_ESPEON, CRY_MODE_NORMAL)
		waitmoncry
		delay(40)

		fadescreenswapbuffers(FADE_TO_BLACK)
		removeobject(LOCALID_ESPEON)
		delay(60)
		fadescreenswapbuffers(FADE_FROM_BLACK)
		delay(40)

		applymovement(LOCALID_LADY, moves(emote_sad))
		speakername("Woman")
		msgbox(format(
			"Sniffle… I'm going to miss that little kitten…"
		))
		closemessage
		
		setvar(VAR_SAKU_KURA_QUEST_ESPEON_STATE, 4)
		setflag(FLAG_SAKU_KURA_HIDE_ESPEON_LADYS_HOUSE)
}

// Espeon
script KuraTown_House3_EventScript_Espeon {
	lock
	faceplayer
	setseenmon(SPECIES_ESPEON)
	setspeakertomonname(SPECIES_ESPEON)
	applymovement(VAR_LAST_TALKED, moves(emote_music))
	playmoncry(SPECIES_ESPEON, CRY_MODE_NORMAL)
	msgbox(format(
		"Speeee!"
	))
	waitmoncry
	closemessage
	delay(30)
	msgbox(format(
		"It looks happy and serene."
	))
	release
}

movement JumpInJoy {
	jump_in_place_right
}
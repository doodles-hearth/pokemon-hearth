const FLAG_HIDE_FISH_SELLER = FLAG_TEMP_1
const FLAG_TALKED_TO_FISH_SELLER = FLAG_TEMP_2

mapscripts MaguroHarbor_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		call(MaguroHarbor_DrawMetatiles)
    }

	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_MAGURO_HARBOR)


		getweekday
		if(
			var(VAR_RESULT) != WEEKDAY_MON
			&& var(VAR_RESULT) != WEEKDAY_WED
			&& var(VAR_RESULT) != WEEKDAY_FRI
			&& var(VAR_RESULT) != WEEKDAY_SUN
		) {
			setflag(FLAG_HIDE_FISH_SELLER)
		}
	}
}

script MaguroHarbor_DrawMetatiles{
    gettimeofday
	switch(var(VAR_0x8000)){
        case TIME_DEAD_NIGHT:
            setmetatile(30, 23, METATILE_Maguro_MaguroClosedWindows, TRUE)
			setmetatile(32, 22, METATILE_PorytilesPrimaryTutorial_ClosedDoorTop, TRUE)
            setmetatile(32, 23, METATILE_PorytilesPrimaryTutorial_ClosedDoorBottom, TRUE)
        case TIME_EARLY_MORNING:
        case TIME_MORNING:
        case TIME_LUNCHTIME:
        case TIME_AFTERNOON:
        case TIME_EVENING:
        case TIME_NIGHT:
        default:
            end
    }
}

script MaguroHarbor_EventScript_TownSign {
	msgbox(format("Maguro Harbor\n“A town that lives hand in hand with the sea.”"), MSGBOX_SIGN)
}

script MaguroHarbor_EventScript_DojoSign {
	msgbox(format(
		"Maguro Harbor Pokémon Dojo\nLeader: Koishi\l"
		"“Calm as a still pond,\lpassionate as a storm!”"
		), MSGBOX_SIGN)
}

script MaguroHarbor_EventScript_PokemonNerdSign {
	msgbox(format(
		"Pokémon Nerd"
		), MSGBOX_SIGN)
}

script MaguroHarbor_EventScript_LovestruckGirl {
	lock
	faceplayer
	if (var(VAR_DECAY_BEACHBOUND_ROUTE) == 1) {
		setspeaker(SP_NAME_GIRL)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_GIRL_1 EMOTE_NORMAL}What was that?\nOh, I hope my boyfriend's okay…"
		), MSGBOX_DEFAULT)
	} else {
		setspeaker("Lovestruck Girl")
		msgbox((
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_GIRL_1 EMOTE_NORMAL}I just spent the day with my boyfriend\nfrom Kura Village, but I already want\ltonight to end to see him again!"
		), MSGBOX_DEFAULT)
	}
	release
}

script MaguroHarbor_EventScript_FishcakeSalesman {
		gettimeofday
	switch(var(VAR_0x8000)) {
		case TIME_EARLY_MORNING:
			goto(MaguroHarbor_EventScript_FishcakeSalesmanSettingUp)
		case TIME_MORNING:
		case TIME_LUNCHTIME:
		case TIME_AFTERNOON:
		case TIME_EVENING:
			goto(MaguroHarbor_EventScript_FishcakeSalesmanSelling)
		case TIME_NIGHT:
			goto(MaguroHarbor_EventScript_FishcakeSalesmanClosingUp)
	}
}

script MaguroHarbor_EventScript_FishcakeSalesmanSettingUp {
	lock
	faceplayer
	
	setspeaker(SP_NAME_OLD_LADY)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SEASIDE_OLD_WOMAN EMOTE_NORMAL}I'm so sorry, dearie.\nWe haven't opened yet.\lCome back in a while!"
	), MSGBOX_DEFAULT)

	closemessage
	release
}

script MaguroHarbor_EventScript_FishcakeSalesmanSelling {
	lock
	faceplayer
	if (flag(FLAG_DAILY_FISHCAKE)) {
		setspeaker(SP_NAME_OLD_LADY)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_OLD_WOMAN EMOTE_NORMAL}Come back tomorrow if you'd like another fishcake!"
		), MSGBOX_DEFAULT)
	} else {
		setspeaker(SP_NAME_OLD_LADY)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_OLD_WOMAN EMOTE_NORMAL}Hello dear, want to try a fishcake?\n"
			"They're the town's specialty!\lOnly ¥500!"
		), MSGBOX_DEFAULT)

		showmoneybox(18, 0)

		mcdialogueB(
			"One please!",
			"Fishcakes?",
			"No, thanks."
		)
		closemessage

		switch(var(VAR_RESULT)) {
			case 0:
				if (checkmoney(500)) {
					removemoney(500)
					updatemoneybox

					setspeaker(SP_NAME_OLD_LADY)
					msgbox(format(
						"{CREATE_MUGSHOT MUGSHOT_SEASIDE_OLD_WOMAN EMOTE_NORMAL}One fishcake, coming right up!"
					))
					closemessage
					hidemoneybox

					giveitem(ITEM_FISHCAKE)
				} else {
					setspeaker(SP_NAME_OLD_LADY)
					msgbox(format(
						"{CREATE_MUGSHOT MUGSHOT_SEASIDE_OLD_WOMAN EMOTE_NORMAL}Oh dear!\nYou're a bit short on pocket money."
					))
					closemessage
					hidemoneybox
				}
			case 1:
				hidemoneybox
				setspeaker(SP_NAME_OLD_LADY)
				msgbox(format(
					"{CREATE_MUGSHOT MUGSHOT_SEASIDE_OLD_WOMAN EMOTE_NORMAL}What's in the Fishcakes, you ask?\p"
					"Uhh… Cake!"
				))
				closemessage
			default:
				hidemoneybox
		}
	}
	release
}

script MaguroHarbor_EventScript_FishcakeSalesmanClosingUp {
	lock
	faceplayer

	if (flag(FLAG_DAILY_FISHCAKE)) {
		setspeaker(SP_NAME_OLD_LADY)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_OLD_WOMAN EMOTE_NORMAL}I'm sorry but we've closed shop now, dearie.\l"
			"Come back tomorrow!"
		), MSGBOX_DEFAULT)
		closemessage
	} else {
		setspeaker(SP_NAME_OLD_LADY)
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_OLD_WOMAN EMOTE_NORMAL}I'm sorry but we've closed shop now, dearie.\l"
			"Come back tomorrow!\p"
			"Did you want to buy one of my Fishcakes?\p"
			"Here, you can have the leftovers from today!"
		), MSGBOX_DEFAULT)
		closemessage

		giveitem(ITEM_FISHCAKE)
		setflag(FLAG_DAILY_FISHCAKE)
	}
	
	release
}

script MaguroHarbor_EventScript_FishPokemonSalesman {
		gettimeofday
	switch(var(VAR_0x8000)) {
		case TIME_EARLY_MORNING:
			goto(MaguroHarbor_EventScript_FishPokemonSalesmanSettingUp)
		case TIME_MORNING:
		case TIME_LUNCHTIME:
		case TIME_AFTERNOON:
		case TIME_EVENING:
			goto(MaguroHarbor_EventScript_FishPokemonSalesmanSelling)
		case TIME_NIGHT:
			checkplayergender
			if(var(VAR_RESULT) == 0){
				bufferstring(STR_VAR_2, "laddie")
			}
			else {
				bufferstring(STR_VAR_2, "lassie")
			}
			call(StringVarSetterFishSalesman)
			goto(MaguroHarbor_EventScript_FishPokemonSalesmanClosingUp)
	}
}

script MaguroHarbor_EventScript_FishPokemonSalesmanSettingUp {
	lock
	faceplayer
	
	setspeaker(SP_NAME_SHOPKEEPER)

	if (var(VAR_DECAY_BEACHBOUND_ROUTE) == 1) {
		msgbox(format(
			"Just saw Master Koishi run toward Beachbound Route!\p"
			"She's so concerned with the safety of the village!"
		), MSGBOX_DEFAULT)
	} else {
		msgbox(format(
			"Aye! We're not open yet.\p"
			"Come back later for the fresh catch of the day."
		), MSGBOX_DEFAULT)
	}

	closemessage
	release
}

script MaguroHarbor_EventScript_FishPokemonSalesmanSelling {
	lock
	faceplayer
	if (var(VAR_DECAY_BEACHBOUND_ROUTE) == 1) {
		setspeaker(SP_NAME_SHOPKEEPER)
		msgbox(format(
			"Just saw Master Koishi run toward Beachbound Route!\p"
			"She's so concerned with the safety of the village!"
		))
		closemessage
	} else {
		setspeaker(SP_NAME_SHOPKEEPER)

		if (flag(FLAG_TALKED_TO_FISH_SELLER)) {
			msgbox(format(
				"Fresh fish! Come get yer fish!\p"
				"Have any fresh fish for me?"
			))
		} else {
			msgbox(format(
				"Fresh fish! Come get yer fish!\p"
				"Hullo there, kid.\nI buy and sell fish for a livin'.\p"
				"Wanna make a quick buck?\nBring me any fish Pokémon you find!\p"
				"I'll give you a fair price.\nAnd if they happen to be holdin' something in their mouth, it's all yours!\p"
				"What do you say?\nHave any fresh fish for me?"
			))
		}

		mcdialogueB(
			"Here's a fish!",
			"Sorry, no fish."
		)
		closemessage

		switch (var(VAR_RESULT)) {
			case 0:
				special(ChoosePartyMon)
				waitstate
				if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
					setspeaker(SP_NAME_SHOPKEEPER)
					msgbox(format("Ah c'mon, don't waste my time!"))
					closemessage
				} else {
					if (getpartysize == 1) {
						setspeaker(SP_NAME_SHOPKEEPER)
						msgbox(format("I'd feel bad leaving a Wielder with no Pokémon!"))
						closemessage
					} else {
						callnative(Native_GiveFish)

						if (var(VAR_0x8000) > 0) {
							buffernumberstring(STR_VAR_1, VAR_0x8000)
							setspeaker(SP_NAME_SHOPKEEPER)
							msgbox(format("I can offer you ¥{STR_VAR_1}.\nThat reasonable?"))

							mcdialogue("Yes!", "Actually, no.")

							switch (var(VAR_RESULT)) {
								case 0:
									incrementgamestat(GAME_STAT_SOLD_FISH)
									// TODO if i don't do this, VAR_0x8000 = 0 for some reason
									callnative(Native_GiveFish)
									addmoneyvar(VAR_0x8000)
									special(DestroySelectedPartyMon)

									if (var(VAR_0x8001) != ITEM_NONE) {
										setspeaker(SP_NAME_SHOPKEEPER)
										msgbox(format(
											"Great! Here you go.\p"
											"…Oh?\p"
											"Look what we found in its mouth!\nTake it, it's yours!"
										))
										closemessage

										giveitem(VAR_0x8001)

										setspeaker(SP_NAME_SHOPKEEPER)
										msgbox(format(
											"Pleasure doin' business.\p"
											"Come back tomorrow to trade some more!"
										))
										closemessage
									} else {
										setspeaker(SP_NAME_SHOPKEEPER)
										msgbox(format(
											"Great! Here you go.\nPleasure doin' business!"
										))
										closemessage
									}
								case 1:
									setspeaker(SP_NAME_SHOPKEEPER)
									msgbox(format("Nevermind then. Have yer fish back."))
									closemessage
							}

							closemessage
						} else {
							setspeaker(SP_NAME_SHOPKEEPER)
							msgbox(format("Nuh-huh, can't buy that.\nI only deal in fish."))
							closemessage
						}
					}
				}
			default:
				setspeaker(SP_NAME_SHOPKEEPER)
				msgbox(format(
					"If you find any good fish, you know where to find me!"
				))
				closemessage
		}
	}

	setflag(FLAG_TALKED_TO_FISH_SELLER)
	release
}

script MaguroHarbor_EventScript_FishPokemonSalesmanClosingUp {
	lock
	faceplayer
	
	setspeaker(SP_NAME_SHOPKEEPER)
	msgbox(
		"Ah! You're a bit too late {STR_VAR_2}. We're\nclosed for th' day.\p"
		"We'll be open again {STR_VAR_1}."
	)

	closemessage
	release
}

script MaguroHarbor_EventScript_FishermanInBoat {
	lock
	faceplayer
	setspeaker(SP_NAME_FISHERMAN)
	msgbox(format(
		"I'm letting my fishing partner Cramorant rest for a while."
	), MSGBOX_DEFAULT)
	closemessage
	release
}

script MaguroHarbor_EventScript_Cramorant {
	lock
	faceplayer
	setseenmon(SPECIES_CRAMORANT)
	playmoncry(SPECIES_CRAMORANT, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_CRAMORANT)
	msgbox(format(
		"Craaah!"
	))
	closemessage
	release
}

script MaguroHarbor_EventScript_FishermanOldRod {
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_OLD_ROD)) {
		setspeaker(SP_NAME_FISHERMAN)

		if (var(VAR_DECAY_BEACHBOUND_ROUTE) == 1) {
			msgbox(format(
				"That tremblin' nearly made me wee me pants!"
			), MSGBOX_AUTOCLOSE)
		} else {
			msgbox(format(
				"Heyo, wee fisherm'n! Hope ye be enjoyin' yer fishing rod!"
			))
		}

		closemessage

		applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
		waitmovement(0)
		release
		end
	}

	setspeaker(SP_NAME_FISHERMAN)
	msgbox(format(
		"Shush, ye fool child!\nYou be scarin' tha wee fishes!\p"
		"… … … …\p"
		"Yer eyes…\nThey be sparklin' with passion…\p"
		"Are ye by any chance an aspirin' fisherm'n?\p"
		"Ye are! I can feel it! I can see ye be dyin' to try it fer yerself!\p"
		"Now, I can't be givin' ye me loyal Cramorant here…\p"
		"But I can give ye this ol' rud o' mine!"
	))
	closemessage

	giveitem(ITEM_OLD_ROD)
	setflag(FLAG_RECEIVED_OLD_ROD)

	setspeaker(SP_NAME_FISHERMAN)
	if (checkplayergender == MALE) {
		msgbox(format(
			"It be a wee bit used, but I reckon it will serve ye mighty fine!\p"
			"Farewell, lad! May ye become a great fisherm'n!"
		))
	} else {
		msgbox(format(
			"It be a wee bit used, but I reckon it will serve ye mighty fine!\p"
			"Farewell, lass! May ye become a great fisherm'n!"
		))
	}
	closemessage

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)

	release
}

script MaguroHarbor_EventScript_Man {
	lock
	faceplayer
	setspeaker(SP_NAME_MAN)
	if (var(VAR_DECAY_BEACHBOUND_ROUTE) == 1) {
		
    	applymovement(VAR_LAST_TALKED, moves(emote_sad))
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_MAN_1 EMOTE_NORMAL}T-This was the biggest quake we've felt yet!"
		), MSGBOX_AUTOCLOSE)
	} else {
		msgbox(format(
			"{CREATE_MUGSHOT MUGSHOT_SEASIDE_MAN_1 EMOTE_NORMAL}I wish I swam as gracefully as the Dojo's Magikarp…"
		))
	}
	
	closemessage
	release
}

script MaguroHarbor_EventScript_Mart {
	gettimeofday
	switch(var(VAR_0x8000)) {
		case TIME_EARLY_MORNING:
			goto(MaguroHarbor_EventScript_MartSettingUp)
		case TIME_MORNING:
		case TIME_LUNCHTIME:
		case TIME_AFTERNOON:
		case TIME_EVENING:
		case TIME_NIGHT:
			goto(MaguroHarbor_EventScript_MartSelling)
	}
}

script MaguroHarbor_EventScript_MartSelling {
	lock
	faceplayer
	setspeaker(SP_NAME_SHOPKEEPER)
	message(format("{CREATE_MUGSHOT MUGSHOT_CLERK EMOTE_NORMAL}Hello, welcome to the Maguro Harbor market!"))
	waitmessage
	pokemart(MaguroHarbor_Mart)
	applymovement(VAR_LAST_TALKED, moves(emote_happy))
	message(format("{CREATE_MUGSHOT MUGSHOT_CLERK EMOTE_NORMAL}Come back soon!"))
	release
}

script MaguroHarbor_EventScript_MartSettingUp {
	lock
	faceplayer
	setspeaker(SP_NAME_SHOPKEEPER)
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_CLERK EMOTE_NORMAL}Oh, uh, the shop isn't open yet. Come back later!"), MSGBOX_DEFAULT)
	waitmessage
	release
}

script MaguroHarbor_EventScript_MartClosingUp {
	lock
	faceplayer
	setspeaker(SP_NAME_SHOPKEEPER)
	msgbox(format("{CREATE_MUGSHOT MUGSHOT_CLERK EMOTE_NORMAL}The shop's closed for today. Come back {STR_VAR_1}!"), MSGBOX_DEFAULT)
	waitmessage
	release
}

script(local) StringVarSetterFishSalesman {
	getweekday
	switch(var(VAR_RESULT)) {
		case WEEKDAY_WED:
			bufferstring(STR_VAR_1, "on Friday")
			return
		case WEEKDAY_FRI:
			bufferstring(STR_VAR_1, "on Sunday")
			return
		case WEEKDAY_SUN:
			bufferstring(STR_VAR_1, "tomorrow")
			return
		case WEEKDAY_MON:
			bufferstring(STR_VAR_1, "on Wednesday")
			return
	}
}

script MaguroHarbor_EventScript_FishPokemonSalesmanSign {
	msgbox(format(
		"We buy Fish Pokémon!\n"
		"Monday, Wednesday, Friday, Sunday\n"
	), MSGBOX_SIGN)
}

mart MaguroHarbor_Mart {
	ITEM_POKE_BALL
	ITEM_OINTMENT
	ITEM_SUPER_OINTMENT
	ITEM_REVIVAL_POWDER
	ITEM_PARALYZE_HEAL
	ITEM_ANTIDOTE
	ITEM_AWAKENING
	ITEM_BURN_HEAL
	ITEM_ESCAPE_ROPE
}

script MaguroHarbor_EventScript_WindycapeLegend {
	lock
	faceplayer
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SEASIDE_WOMAN_2 EMOTE_NORMAL}{SPEAKER NAME_WOMAN}If you're ever in Windycape up north, be sure to visit its famous statue!\p"
		"Legend has it that a woman waited for her lover to return from the seas…\l"
		"For so long, she turned to stone!\p"
		"Such a sad story.\nI wonder if it really happened."
	))
	closemessage

	release
}

script MaguroHarbor_EventScript_MarketChild {
	lock
	faceplayer
	waitse
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, moves(emote_music))
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SEASIDE_LITTLE_GIRL EMOTE_NORMAL}{SPEAKER NAME_LITTLE_GIRL}I love the market! It smells of fish kinda, but my mum buys me fishcakes!"
	))
	closemessage

	release
}

script MaguroHarbor_EventScript_MarketChildOnBeach {
	lock

	applymovement(LOCALID_MAGURO_WINGULL, moves(jump_in_place_left))
	playmoncry(SPECIES_WINGULL, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_WINGULL)
	setseenmon(SPECIES_WINGULL)
	msgbox(format(
		"Heehee!"
	))
	waitmoncry
	waitmovement(0)
	closemessage

	applymovement(LOCALID_MAGURO_LITTLE_GIRL, moves(jump_in_place_right))
	playmoncry(SPECIES_WINGULL, CRY_MODE_HIGH_PITCH)
	msgbox(format(
		"{CREATE_MUGSHOT MUGSHOT_SEASIDE_LITTLE_GIRL EMOTE_NORMAL}{SPEAKER NAME_LITTLE_GIRL}Heeheehee!"
	))
	waitmoncry
	waitmovement(0)
	closemessage

	playmoncry(SPECIES_WINGULL, CRY_MODE_NORMAL)
	applymovement(LOCALID_MAGURO_WINGULL, moves(emote_question_mark))
	waitmoncry
	waitmovement(0)

	release
}
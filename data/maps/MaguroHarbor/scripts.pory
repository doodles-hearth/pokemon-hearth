const FLAG_HIDE_MERCHANTS = FLAG_TEMP_1

mapscripts MaguroHarbor_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		call(MaguroHarbor_DrawMetatiles)
    }

	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_HIDE_MERCHANTS)

		getweekday
		if(var(VAR_RESULT) == 0 || var(VAR_RESULT) == 3 || var(VAR_RESULT) == 6) {
			clearflag(FLAG_HIDE_MERCHANTS)
		}
		
	}
}

script MaguroHarbor_DrawMetatiles{
    gettimeofday
	switch(var(VAR_RESULT)){
        case TIME_DEAD_NIGHT:
            setmetatile(30, 23, METATILE_Maguro_MaguroClosedWindows, TRUE)
			setmetatile(32, 22, METATILE_PorytilesPrimaryTutorial_ClosedDoorTop, TRUE)
            setmetatile(32, 23, METATILE_PorytilesPrimaryTutorial_ClosedDoorBottom, TRUE)
        case TIME_EARLY_MORNING:
        case TIME_MORNING:
        case TIME_LUNCHTIME:
        case TIME_AFTERNOON:
        case TIME_EVENING:
        case TIME_NIGHT:
        default:
            end
    }
}

script MaguroHarbor_EventScript_TownSign {
	msgbox(format("Maguro Harbor\n“A town that lives hand in hand with the sea.”"), MSGBOX_SIGN)
}

script MaguroHarbor_EventScript_DojoSign {
	msgbox(format(
		"Maguro Harbor Pokémon Dojo\nLeader: Koishi\l"
		"“Smooth as a still pond,\lpassionate as a storm!”"
		), MSGBOX_SIGN)
}

script MaguroHarbor_EventScript_PokemonNerdSign {
	msgbox(format(
		"“Pokémon Nerd”"
		), MSGBOX_SIGN)
}

script MaguroHarbor_EventScript_LovestruckGirl {
	lock
	faceplayer
	speakername("Lovestruck Girl")
	msgbox(
		format(
			"It's only been an hour since I saw my love, and yet I already want tomorrow to come just so I can see him again…"
		),
		MSGBOX_DEFAULT
	)
	release
}

script MaguroHarbor_EventScript_FishcakeSalesman {
		gettimeofday
	switch(var(VAR_RESULT)) {
		case TIME_EARLY_MORNING:
			goto(MaguroHarbor_EventScript_FishcakeSalesmanSettingUp)
		case TIME_MORNING:
		case TIME_LUNCHTIME:
		case TIME_AFTERNOON:
		case TIME_EVENING:
			goto(MaguroHarbor_EventScript_FishcakeSalesmanSelling)
		case TIME_NIGHT:
			call(StringVarSetter)
			goto(MaguroHarbor_EventScript_FishcakeSalesmanClosingUp)
	}
}

script MaguroHarbor_EventScript_FishcakeSalesmanSettingUp {
	lock
	faceplayer
	
	speakername("Old woman")
	msgbox(format(
		"Oh I'm so sorry, dearie. We haven't opened yet. Come back later!"
	), MSGBOX_DEFAULT)

	closemessage
	release
}

script MaguroHarbor_EventScript_FishcakeSalesmanSelling {
	lock
	faceplayer
	if (flag(FLAG_DAILY_FISHCAKE)) {
		speakername("Old woman")
		msgbox(format(
			"Come back tomorrow if you'd like another fishcake!"
		), MSGBOX_DEFAULT)
	} else {
		speakername("Old woman")
		msgbox(format(
			"Hello dear, want to try a fishcake?\p"
			"They're the town's specialty! Only 500¥!"
		), MSGBOX_DEFAULT)

		showmoneybox(18, 0)

		dynmultichoice(
			0,
			0,
			TRUE,
			10,
			0,
			DYN_MULTICHOICE_CB_NONE,
			"One please!",
			"No, thanks.",
			"Fishcakes?"
		)
		closemessage

		switch(var(VAR_RESULT)) {
			case 0:
				if (checkmoney(500)) {
					removemoney(500)
					updatemoneybox

					speakername("Old woman")
					msgbox(format(
						"One fishcake, coming right up!"
					))
					closemessage
					hidemoneybox

					giveitem(ITEM_FISHCAKE)
				} else {
					speakername("Old woman")
					msgbox(format(
						"Oh dear! It appears you're a little short on pocket money."
					))
					closemessage
					hidemoneybox
				}
			case 1:
				hidemoneybox
			case 2:
				hidemoneybox
				speakername("Old woman")
				msgbox(format(
					"What's in the Fishcakes, you ask?\p"
					"Uhh… Cake!"
				))
				closemessage
		}
	}
	release
}

script MaguroHarbor_EventScript_FishcakeSalesmanClosingUp {
	lock
	faceplayer

	if (flag(FLAG_DAILY_FISHCAKE)) {
		speakername("Old woman")
		msgbox(format(
			"I'm sorry but we've closed shop now, dearie.\p"
			"We'll be open again {STR_VAR_1}!"
		), MSGBOX_DEFAULT)
		closemessage
	} else {
		speakername("Old woman")
		msgbox(format(
			"Oh my! I'm sorry but we've closed shop now, dearie.\p"
			"We'll be open again {STR_VAR_1}!\p"
			"Did you want to buy one of my Fishcakes?\p"
			"Here, you can have the leftovers from today!"
		), MSGBOX_DEFAULT)
		closemessage

		giveitem(ITEM_FISHCAKE)
		setflag(FLAG_DAILY_FISHCAKE)
	}
	
	release
}

script MaguroHarbor_EventScript_FishPokemonSalesman {
		gettimeofday
	switch(var(VAR_RESULT)) {
		case TIME_EARLY_MORNING:
			goto(MaguroHarbor_EventScript_FishPokemonSalesmanSettingUp)
		case TIME_MORNING:
		case TIME_LUNCHTIME:
		case TIME_AFTERNOON:
		case TIME_EVENING:
			goto(MaguroHarbor_EventScript_FishPokemonSalesmanSelling)
		case TIME_NIGHT:
			checkplayergender
			if(var(VAR_RESULT) == 0){
				bufferstring(STR_VAR_2, "laddie")
			}
			else {
				bufferstring(STR_VAR_2, "lassie")
			}
			call(StringVarSetter)
			goto(MaguroHarbor_EventScript_FishPokemonSalesmanClosingUp)
	}
}

script MaguroHarbor_EventScript_FishPokemonSalesmanSettingUp {
	lock
	faceplayer
	
	speakername("Salesman")
	msgbox(format(
		"Aye! We're not open yet. Come back later for the fresh catch of the day."
	), MSGBOX_DEFAULT)

	closemessage
	release
}

script MaguroHarbor_EventScript_FishPokemonSalesmanSelling {
	lock
	faceplayer
	speakername("Salesman")
	msgbox(format(
		"Fresh fish! Come get yer fish!"
	), MSGBOX_DEFAULT)
	// TODO EVA fish mons
	closemessage
	release
}

script MaguroHarbor_EventScript_FishPokemonSalesmanClosingUp {
	lock
	faceplayer
	
	speakername("Salesman")
	msgbox(
		"Ah! You're a bit too late {STR_VAR_2}. We're\nclosed for th' day.\p"
		"We'll be open again\l{STR_VAR_1}."
	)

	closemessage
	release
}

script MaguroHarbor_EventScript_FishermanInBoat {
	lock
	faceplayer
	speakername("Fisherman")
	msgbox(format(
		"I'm letting my fishing partner Cramorant rest for a while."
	), MSGBOX_DEFAULT)
	closemessage
	release
}

script MaguroHarbor_EventScript_Cramorant {
	lock
	faceplayer
	setseenmon(SPECIES_CRAMORANT)
	playmoncry(SPECIES_CRAMORANT, CRY_MODE_NORMAL)
	speakername("Cramorant")
	msgbox(format(
		"Craaah!"
	), MSGBOX_DEFAULT)
	closemessage
	release
}

script MaguroHarbor_EventScript_FishermanOldRod {
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_OLD_ROD)) {
		speakername("Fisherman")
		msgbox(format(
			"Heyo, wee fisherm'n! Hope ye be enjoyin' yer fishing rod!"
		))
		closemessage

		applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
		waitmovement(0)
		release
		end
	}

	speakername("Fisherman")
	msgbox(format(
		"Shush, ye fool child! You're scarin' tha wee fishes!\p"
		"… … … …\p"
		"Yer eyes… They be sparklin' with passion… Are ye by any chance an aspirin' fisherm'n?\p"
		"Ye are! I can feel it! I can see ye be dyin' to try it fer yerself!\p"
		"Now, I can't be givin' ye me loyal Cramorant here…\p"
		"But I can give ye this ol' rud o' mine!"
	))
	closemessage

	giveitem(ITEM_OLD_ROD)
	setflag(FLAG_RECEIVED_OLD_ROD)

	speakername("Fisherman")
	if (checkplayergender == MALE) {
		msgbox(format(
			"It be a wee bit used, but I reckon it will serve ye mighty fine!\p"
			"Farewell, lad! May ye become a great fisherm'n!"
		))
	} else {
		msgbox(format(
			"It be a wee bit used, but I reckon it will serve ye mighty fine!\p"
			"Farewell, lass! May ye become a great fisherm'n!"
		))
	}
	closemessage

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)

	release
}

script MaguroHarbor_EventScript_Man {
	lock
	faceplayer
	speakername("Man")
	msgbox(format(
		"I wish I swam as gracefully as the Dojo's Magikarp…"
	))
	closemessage
	release
}

script MaguroHarbor_EventScript_Mart {
	gettimeofday
	switch(var(VAR_RESULT)) {
		case TIME_EARLY_MORNING:
			goto(MaguroHarbor_EventScript_MartSettingUp)
		case TIME_MORNING:
		case TIME_LUNCHTIME:
		case TIME_AFTERNOON:
		case TIME_EVENING:
		case TIME_NIGHT:
			goto(MaguroHarbor_EventScript_MartSelling)
	}
}

script MaguroHarbor_EventScript_MartSelling {
	lock
	faceplayer
	speakername("Shopkeeper")
	message(gText_HowMayIServeYou)
	waitmessage
	pokemart(MaguroHarbor_Mart)
	msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
}

script MaguroHarbor_EventScript_MartSettingUp {
	lock
	faceplayer
	speakername("Shopkeeper")
	msgbox(format("Oh, uh, the shop isn't open yet. Come back later."), MSGBOX_DEFAULT)
	waitmessage
	release
}

script MaguroHarbor_EventScript_MartClosingUp {
	lock
	faceplayer
	speakername("Shopkeeper")
	msgbox(format("The shop's closed for today. Come back {STR_VAR_1}."), MSGBOX_DEFAULT)
	waitmessage
	release
}

script(local) StringVarSetter {
	getweekday
	switch(var(VAR_RESULT)) {
		case WEEKDAY_WED:
			bufferstring(STR_VAR_1, "on Saturday")
			return
		case WEEKDAY_SAT:
			bufferstring(STR_VAR_1, "tomorrow")
			return
		case WEEKDAY_SUN:
			bufferstring(STR_VAR_1, "on Wednesday")
			return
	}
}

mart MaguroHarbor_Mart {
	ITEM_POKE_BALL
	ITEM_OINTMENT
	ITEM_SUPER_OINTMENT
	ITEM_PARALYZE_HEAL
	ITEM_ANTIDOTE
	ITEM_AWAKENING
	ITEM_BURN_HEAL
	ITEM_ESCAPE_ROPE
}
const FLAG_HIDE_NONSELLINGMERCHANTS = FLAG_TEMP_1
const FLAG_HIDE_SELLINGMERCHANTS = FLAG_TEMP_2

mapscripts MaguroHarbor_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_HIDE_NONSELLINGMERCHANTS)
		setflag(FLAG_HIDE_SELLINGMERCHANTS)

		getweekday
		if(var(VAR_RESULT) == 0 || var(VAR_RESULT) == 3 || var(VAR_RESULT) == 6) {
			clearflag(FLAG_HIDE_NONSELLINGMERCHANTS)
			clearflag(FLAG_HIDE_SELLINGMERCHANTS)
		}
		
	}
}

script MaguroHarbor_EventScript_TownSign {
	msgbox(format("Maguro Harbor\n“A town that lives hand in hand with the sea.”"), MSGBOX_SIGN)
}

script MaguroHarbor_EventScript_LovestruckGirl {
	lock
	faceplayer
	speakername("Lovestruck Girl")
	msgbox(
		format(
			"It's only been an hour since I saw my love, and yet I already want tomorrow to come just so I see him again…"
		),
		MSGBOX_DEFAULT
	)
	release
}

script MaguroHarbor_EventScript_FishcakeSalesman {
	lock
	faceplayer
	
	speakername("Old woman")
	msgbox(format(
		"Hello dear, want to try a fishcake?\p"
		"They're the town's specialty!"
	), MSGBOX_DEFAULT)
	
	giveitem(ITEM_FISHCAKE)

	closemessage
	release
}

script MaguroHarbor_EventScript_FishPokemonSalesman {
	lock
	faceplayer
	speakername("Salesman")
	msgbox(format(
		"Fresh fish! Come get yer fish!"
	), MSGBOX_DEFAULT)
	// TODO EVA fish mons
	closemessage
	release
}

script MaguroHarbor_EventScript_FishermanInBoat {
	lock
	faceplayer
	speakername("Fisherman")
	msgbox(format(
		"I'm letting my fishing partner Cramorant rest for a while."
	), MSGBOX_DEFAULT)
	closemessage
	release
}

script MaguroHarbor_EventScript_Cramorant {
	lock
	faceplayer
	playmoncry(SPECIES_CRAMORANT, CRY_MODE_NORMAL)
	speakername("Cramorant")
	msgbox(format(
		"Craaah!"
	), MSGBOX_DEFAULT)
	closemessage
	release
}

script MaguroHarbor_EventScript_FishermanOldRod {
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_OLD_ROD)) {
		speakername("Fisherman")
		msgbox(format(
			"Heyo, wee fisherm'n! Hope ye be enjoyin' yer fishing rod!"
		))
		closemessage

		applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
		waitmovement(0)
		release
		end
	}

	speakername("Fisherman")
	msgbox(format(
		"Shush, ye fool child! You're scarin' tha wee fishes!\p"
		"… … … …\p"
		"Yer eyes… They be sparklin' with passion… Are ye by any chance an aspirin' fisherm'n?\p"
		"Ye are! I can feel it! I can see ye be dyin' to try it fer yerself!\p"
		"Now, I can't be givin' ye me loyal Cramorant here…\p"
		"But I can give ye this ol' rud o' mine!"
	))
	closemessage

	giveitem(ITEM_OLD_ROD)
	setflag(FLAG_RECEIVED_OLD_ROD)

	speakername("Fisherman")
	if (checkplayergender == MALE) {
		msgbox(format(
			"It be a wee bit used, but I reckon it will serve ye mighty fine!\p"
			"Farewell, lad! May ye become a great fisherm'n!"
		))
	} else {
		msgbox(format(
			"It be a wee bit used, but I reckon it will serve ye mighty fine!\p"
			"Farewell, lass! May ye become a great fisherm'n!"
		))
	}
	closemessage

	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)

	release
}

script MaguroHarbor_EventScript_Man {
	lock
	faceplayer
	speakername("Man")
	msgbox(format(
		"I wish I swam as gracefully as the Dojo's Magikarp…"
	))
	closemessage
	release
}

script MaguroHarbor_EventScript_Mart {
	lock
	faceplayer
	speakername("Clerk")
	message(gText_HowMayIServeYou)
	waitmessage
	pokemart(MaguroHarbor_Mart)
	msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
}

mart MaguroHarbor_Mart {
	ITEM_POKE_BALL
	ITEM_BERRY_JUICE
	ITEM_PARALYZE_HEAL
	ITEM_ANTIDOTE
	ITEM_AWAKENING
	ITEM_ESCAPE_ROPE
	ITEM_REPEL
}
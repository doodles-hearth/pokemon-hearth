const CHOSEN_SLOT = VAR_0x8004
const SPOKE_TO_MEMORY_GIRL = FLAG_TEMP_8

script MemoryGirl_EventScript_BeginConversation {
    lock
    faceplayer
    createfieldmugshot(MUGSHOT_SCHOOL_KID_F)
    if (flag(SPOKE_TO_MEMORY_GIRL) == FALSE)
    {
        msgbox(
            "Hi, hi! I'm the Memory Girl!\p"
            "I know what you are thinking:\n"
            "“What does that mean?”\p"
            "It means I can read the\n"
            "memories of your Pokémon!\p"
            "Cool, huh?"
        )
    }
    setflag(SPOKE_TO_MEMORY_GIRL)
    msgbox(
        "Well… Should I read your \n"
        "Pokémon's memories for you?"
    )
    mcdialogue(
        "Yes",
        "No"
	)
    if (var(VAR_RESULT) == 1)
    {
        msgbox(
            "You're no fun!"
        )
        release
        end
    }
    msgbox(
        "OK! Which Pokemon should\n"
        "I look at?"
    )
    waitmessage
    removefieldmugshot
    special(ChoosePartyMon)
    waitstate
    createfieldmugshot(MUGSHOT_SCHOOL_KID_F)
    if (var(CHOSEN_SLOT) == PARTY_NOTHING_CHOSEN)
    {
        
        msgbox(
            "You're no fun!"
        )
        removefieldmugshot
        release
        end
    }
    bufferpartymonnick(STR_VAR_1, CHOSEN_SLOT)
    specialvar(VAR_RESULT, ScriptGetPartyMonSpecies)
    if (var(VAR_RESULT) == SPECIES_EGG)
    {
        msgbox(
            "I know what you are thinking:\n"
            "“This will be a funny joke.”\p"
            "But I can't read an Egg\n"
            "dummy!"
        )
        removefieldmugshot
        release
        end
    }
    msgbox(
        "Hm… lets see…\n"
        "… … …"
    )
    special(IsMonOTIDNotPlayers)
	if (var(VAR_RESULT) == FALSE
     && specialvar(VAR_RESULT, MonOTNameNotPlayer) == FALSE)
    {
        buffermemorymessage(CHOSEN_SLOT, MON_MEMORY_ORIGIN)
        msgbox(gStringVar4)
    }
    if (getmemory(CHOSEN_SLOT, MON_MEMORY_OLD) == 0 
     && getmemory(CHOSEN_SLOT, MON_MEMORY_NEW) == 0)
    {
        special(IsMonOTIDNotPlayers)
        if (var(VAR_RESULT) == TRUE
        || specialvar(VAR_RESULT, MonOTNameNotPlayer) == TRUE)
        {
            bufferoriginaltrainername(STR_VAR_3, CHOSEN_SLOT)
            msgbox(
                "{STR_VAR_1} was caught by {STR_VAR_3}\n"
                "but…\p"
                "That's all!\p"
                "It looks like {STR_VAR_1}\n"
                "hasn't done much!"
            )
        }
        else
        {
            msgbox(
                "That's all…\p"
                "It looks like {STR_VAR_1}\n"
                "hasn't done much!"
            )
        }
        removefieldmugshot
        release
        end
    }
    bufferoriginaltrainername(STR_VAR_3, CHOSEN_SLOT)
    switch(random(2))
    {
    case 0:
        msgbox(
            "{STR_VAR_1} and {STR_VAR_3} travelled\n"
            "together until…"
        )
    case 1:
        msgbox(
            "{STR_VAR_1} was with {STR_VAR_3} when…"
        )
    }
    if (getmemory(CHOSEN_SLOT, MON_MEMORY_OLD))
    {
        buffermemorymessage(CHOSEN_SLOT, MON_MEMORY_OLD)
        msgbox(gStringVar4)
        switch(random(2))
        {
        case 0:
            msgbox(
                "Sounds fun!"
            )
        case 1:
            msgbox(
                "Wow! I wish I could do that!"
            )
        }
        special(IsMonOTIDNotPlayers)
        if (var(VAR_RESULT) == TRUE
        || specialvar(VAR_RESULT, MonOTNameNotPlayer) == TRUE)
        {
            msgbox(
                "That's when…\n"
                "… … …"
            )
            buffermemorymessage(CHOSEN_SLOT, MON_MEMORY_ORIGIN)
            msgbox(gStringVar4)
        }
        if (getmemory(CHOSEN_SLOT, MON_MEMORY_NEW))
        {
            msgbox(
                "Then…\n"
                "… … …"
            )
            buffermemorymessage(CHOSEN_SLOT, MON_MEMORY_NEW)
            msgbox(gStringVar4)
            special(IsMonOTIDNotPlayers)
            if (var(VAR_RESULT) == TRUE
            || specialvar(VAR_RESULT, MonOTNameNotPlayer) == TRUE)
            {
                msgbox(
                    "Wowie! Your Pokémon has done a\n"
                    "lot of travelling!"
                )
                if (specialvar(VAR_RESULT, GiveMonHistoricRibbon))
                {
                    msgbox(
                        "I'm going to give them a\n"
                        "super-duper badge!"
                    )
                    removefieldmugshot
                    bufferpartymonnick(STR_VAR_1, CHOSEN_SLOT)
                    playfanfare(MUS_OBTAIN_ITEM)
                    message(
                        "{STR_VAR_1} received the Traveller Ribbon."
                    )
                    waitfanfare
                    createfieldmugshot(MUGSHOT_SCHOOL_KID_F)
                    msgbox(
                        "…now they can show off how\n"
                        "adventurous they are!"
                    )
                }
            }
            else
            {
                if (specialvar(VAR_RESULT, GiveMonHistoricRibbon) == TRUE)
                {
                    msgbox(
                    "Wowie! Your Pokémon has a lot\n"
                    "of great memories!\p"
                    "I'm going to give them a\n"
                    "super-duper badge!"
                    )
                    removefieldmugshot
                    bufferpartymonnick(STR_VAR_1, CHOSEN_SLOT)
                    playfanfare(MUS_OBTAIN_ITEM)
                    message(
                        "{STR_VAR_1} received the Historic Ribbon."
                    )
                    waitfanfare
                    createfieldmugshot(MUGSHOT_SCHOOL_KID_F)
                    msgbox(
                        "…now they can show off how\n"
                        "important they are!"
                    )
                }
                else
                {
                    msgbox(
                    "Wowie! Your Pokémon has a lot\n"
                    "of good memories!"
                    )
                }
            }
        }
    }
    else
    {
        buffermemorymessage(CHOSEN_SLOT, MON_MEMORY_NEW)
        msgbox(gStringVar4)
        msgbox(format(
            "…and… that's it!\p"
            "Your Pokémon has done some things,\n"
            "but not many!"
        ))
    }
    removefieldmugshot
    release
    end
}

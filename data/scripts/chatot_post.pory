script ChatotPost_EventScript_Chatot {
    lockall

    setnamedmon(SPECIES_CHATOT)
    setchatotlastspeaker

    waitse
    playse(SE_LEDGE)
    applymovement(VAR_LAST_TALKED, moves(jump_in_place_down))
    waitmovement(VAR_LAST_TALKED)
    waitse

    playmoncry(SPECIES_CHATOT, CRY_MODE_NORMAL)
    applymovement(VAR_LAST_TALKED, moves(emote_music))

    delay(16)
    if (checkchatotpost)
    {
        checkchatotpostnumber
        buffernumberstring(STR_VAR_1, VAR_RESULT)
        setspeakertomonname(SPECIES_CHATOT)
        if (var(VAR_RESULT) == 1) {
            msgbox(format("SQWAK!{PAUSE 8} POST FOR YOU!{PAUSE 8} SQWAK!"), MSGBOX_DEFAULT)
        } else {
            msgbox(format(
                "SQWAK!{PAUSE 8} POST FOR YOU!{PAUSE 8} SQWAK!{PAUSE 8}\n"
                "{STR_VAR_1} NEW MESSAGES!{PAUSE 8} SQWAK!"
            ))
        }
        closemessage
        delay(24)

        readchatotpost

        while (checkchatotpost)
        {
            call(ChatotPost_EventScript_NextMessage)
        }
    }
    else
    {
        setspeakertomonname(SPECIES_CHATOT)
        msgbox(format("SQWAK!{PAUSE 8} NO MORE POST!"))
        closemessage
        releaseall
        end
    }

    playse(SE_LEDGE)
    applymovement(VAR_LAST_TALKED, moves(jump_in_place_down))

    playmoncry(SPECIES_CHATOT, CRY_MODE_NORMAL)

    setspeakertomonname(SPECIES_CHATOT)
    msgbox(format("SQWAK!"), MSGBOX_DEFAULT)
    releaseall
    end
}

script(local) ChatotPost_EventScript_NextMessage {
    playmoncry(SPECIES_CHATOT, CRY_MODE_NORMAL)
    applymovement(VAR_LAST_TALKED, moves(emote_music))

    setspeakertomonname(SPECIES_CHATOT)

    random(4)
    switch(var(VAR_RESULT)) {
        case 0:
            msgbox(Text_Chatot_Next1)
        case 1:
            msgbox(Text_Chatot_Next2)
        case 2:
            msgbox(Text_Chatot_Next3)
        case 3:
            msgbox(Text_Chatot_Next4)
    }

    readchatotpost
}

script ChatotPost_EventScript_EmptyPost {
    lock
    setnamedmon(SPECIES_CHATOT)
    msgbox(format("It's a Chatot Post.\n… …No Chatot in sight!"))
    closemessage
    release
}

// Default no script behavior
script ChatotPost_EventScript_None {
    return
}

text(local) Text_Chatot_Next1 {
    format("HERE'S ANOTHER!{PAUSE 8} SQWAK!")
}

text(local) Text_Chatot_Next2 {
    format("SQWAK!{PAUSE 8} NEXT MESSAGE!")
}

text(local) Text_Chatot_Next3 {
    format("SQWAK!{PAUSE 8} ANOTHER MESSAGE!{PAUSE 8} SQWAK!")
}

text(local) Text_Chatot_Next4 {
    format("SQWAK SQWAK!{PAUSE 8} ONE MORE!")
}

//
// Chatot Post Scripts - Make more below and add to /src/data/chatot_post.h
//
script ChatotPost_EventScript_FromHaru {
    setspeakertomonname(SPECIES_CHATOT)
    msgbox(format(
        "“Hello {PLAYER}!\p"
        "Do you want to hear a fun fact?\p"
        "Did you know that Wailord aren't actually the biggest living things?\p"
        "The biggest living things are actually mushrooms, because…"
    ))
    fadescreen(FADE_TO_BLACK)
	delay(48)
    closemessage
	fadescreen(FADE_FROM_BLACK)
    setspeakertomonname(SPECIES_CHATOT)
    msgbox(format("…also did you know…\pWait… Chatot!\pCome back, Chatot!”"))
    closemessage

    setnamedmon(SPECIES_CHATOT)
    setnamedmon(SPECIES_WAILORD)
    return
}
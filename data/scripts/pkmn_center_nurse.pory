script Common_EventScript_PkmnCenterNurse_Chansey {
	lock
	setseenmon(SPECIES_CHANSEY)
	playmoncry(SPECIES_CHANSEY, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_CHANSEY)
	msgbox("Sey!")
	waitse
	closemessage
	release
}

script Common_EventScript_PkmnCenterNurse_Blissey {
	lock
	setseenmon(SPECIES_BLISSEY)
	playmoncry(SPECIES_BLISSEY, CRY_MODE_NORMAL)
	setspeakertomonname(SPECIES_BLISSEY)
	msgbox("Sey!")
	waitse
	closemessage
	release
}

script Common_EventScript_PkmnCenterNurse_Sleep {
	lock
	faceplayer
	
	if (checkmoney(500)) {
		speakername("Nurse")
		msgbox(format(
			"Hello! For 500¥, you and your Pokémon can spend the night here."
		))
		closemessage

		showmoneybox(18, 0)

		dynmultichoice(
			0,
			0,
			TRUE,
			10,
			0,
			DYN_MULTICHOICE_CB_NONE,
			"Sleep until morning",
			"Sleep until nighttime",
			"Maybe later."
		)
		
		switch(var(VAR_RESULT)) {
			case 0:
				call(Common_EventScript_PkmnCenterNurse_ChooseSleep)
				fadescreenswapbuffers(FADE_TO_BLACK)
				playfanfare(MUS_HEAL)
				waitfanfare
				special(HealPlayerParty)
				callnative(UpdateFollowingPokemon)
				fwdtime(8, 0)
				fadescreenswapbuffers(FADE_FROM_BLACK)
			case 1:
				call(Common_EventScript_PkmnCenterNurse_ChooseSleep)
				fadescreenswapbuffers(FADE_TO_BLACK)
				playfanfare(MUS_HEAL)
				waitfanfare
				special(HealPlayerParty)
				callnative(UpdateFollowingPokemon)
				fwdtime(22, 0)
				fadescreenswapbuffers(FADE_FROM_BLACK)
		}
	} else {
		speakername("Nurse")
		msgbox(format(
			"Hello! For 500¥, you and your Pokémon can spend the night here."
		))
		closemessage
	}

	hidemoneybox
	release
	end
}

script EventScript_PkmnCenterNurse_TakeAndHealPkmn {
	hidefollower(0)

	applymovement(VAR_0x800A, moves(face_left))
	waitmovement(VAR_0x800A)
	applymovement(VAR_0x800B, moves(walk_in_place_fast_right, delay_16 * 2))
	waitmovement(VAR_0x800B)
	applymovement(VAR_0x800A, moves(face_right))
	waitmovement(VAR_0x800A)

	dofieldeffect(FLDEFF_POKECENTER_HEAL)
	waitfieldeffect(FLDEFF_POKECENTER_HEAL)

	applymovement(VAR_0x800A, moves(face_left, delay_16 * 2))
	waitmovement(VAR_0x800A)
	applymovement(VAR_0x800B, moves(walk_in_place_fast_down))
	waitmovement(VAR_0x800B)
	applymovement(VAR_0x800A, moves(walk_in_place_fast_down))
	waitmovement(VAR_0x800A)

	special(HealPlayerParty)
	callnative(UpdateFollowingPokemon)
	return
}

script Common_EventScript_PkmnCenterNurse_ChooseSleep {
	playse(SE_SHOP)
	removemoney(500)
	updatemoneybox

	speakername("Nurse")
	msgbox(format(
		"Thank you! May you have a restful night."
	))
	closemessage

	hidemoneybox
}

script Common_EventScript_PkmnCenterNurse_CutTheBullshit {
	lock
	faceplayer
	setvar(VAR_0x8004, 0)

	speakername("Nurse")
	msgbox(format("Welcome to the refuge!\pLet me heal your Pokémon."))
	closemessage
	
	incrementgamestat(GAME_STAT_USED_POKECENTER)
	call(EventScript_PkmnCenterNurse_TakeAndHealPkmn)
	if (!flag(FLAG_POKERUS_EXPLAINED)) {
		call(EventScript_PkmnCenterNurse_CheckPokerus_CutTheBullshit)
	}
	
	speakername("Nurse")
	msgbox(format("Your Pokémon are all healed and rested."))
	closemessage

	applymovement(VAR_0x800B, moves(nurse_joy_bow, delay_4))
	waitmovement(0)

	speakername("Nurse")
	msgbox(format("Please come again!"))
	closemessage

	release
	end
}

script EventScript_PkmnCenterNurse_CheckPokerus_CutTheBullshit {
	specialvar(VAR_RESULT, IsPokerusInParty)
	if (var(VAR_RESULT) == TRUE) {
		message(format(
			"Um, excuse me…\p"
			"I think your Pokémon is under some sort of spell.\p"
			"I'm pretty good at feeling this kind of thing. I have seen this before.\p"
			"I'm not sure what it is, but I have seen the Pokémon that had this aura around them grow exceptionally well for a while."
		))
		setflag(FLAG_POKERUS_EXPLAINED)
	}
}

raw `Common_EventScript_PkmnCenterNurse::
	lock
	faceplayer
	setvar VAR_0x8004, 0
	specialvar VAR_RESULT, CountPlayerTrainerStars
	goto_if_eq VAR_RESULT, 4, EventScript_PkmnCenterNurse_GoldCard
	msgbox gText_WouldYouLikeToRestYourPkmn, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, EventScript_PkmnCenterNurse_HealPkmn
	goto_if_eq VAR_RESULT, NO, EventScript_PkmnCenterNurse_Goodbye
	end

EventScript_PkmnCenterNurse_Goodbye::
	message gText_WeHopeToSeeYouAgain
	return

@ VAR_0x8004 is 1 when player has Gold Card; jumps are identical
EventScript_PkmnCenterNurse_HealPkmn::
	incrementgamestat GAME_STAT_USED_POKECENTER
	call_if_eq VAR_0x8004, 0, EventScript_PkmnCenterNurse_IllTakeYourPkmn
	call_if_eq VAR_0x8004, 1, EventScript_PkmnCenterNurse_IllTakeYourPkmn2
	waitmessage
	call EventScript_PkmnCenterNurse_TakeAndHealPkmn
	goto_if_unset FLAG_POKERUS_EXPLAINED, EventScript_PkmnCenterNurse_CheckPokerus
	goto EventScript_PkmnCenterNurse_CheckTrainerHillAndUnionRoom
	end

EventScript_PkmnCenterNurse_IllTakeYourPkmn::
	message gText_IllTakeYourPkmn
	return

EventScript_PkmnCenterNurse_IllTakeYourPkmn2::
	message gText_IllTakeYourPkmn2
	return

EventScript_PkmnCenterNurse_CheckTrainerHillAndUnionRoom::
	specialvar VAR_RESULT, PlayerNotAtTrainerHillEntrance
	goto_if_eq VAR_RESULT, 0, EventScript_PkmnCenterNurse_ReturnPkmn
	specialvar VAR_RESULT, BufferUnionRoomPlayerName
	copyvar VAR_0x8008, VAR_RESULT
	goto_if_eq VAR_0x8008, 0, EventScript_PkmnCenterNurse_ReturnPkmn
	goto_if_eq VAR_0x8008, 1, EventScript_PkmnCenterNurse_PlayerWaitingInUnionRoom
	end

@ VAR_0x8004 is 1 when player has Gold Card
EventScript_PkmnCenterNurse_ReturnPkmn::
	goto_if_eq VAR_0x8004, 1, EventScript_PkmnCenterNurse_ReturnPkmn2
	message gText_RestoredPkmnToFullHealth
	waitmessage
	applymovement VAR_0x800B, Movement_PkmnCenterNurse_Bow
	waitmovement 0
	message gText_WeHopeToSeeYouAgain
	return

EventScript_PkmnCenterNurse_ReturnPkmn2::
	message gText_ThankYouForWaiting
	waitmessage
	applymovement VAR_0x800B, Movement_PkmnCenterNurse_Bow
	waitmovement 0
	message gText_WeHopeToSeeYouAgain2
	return

EventScript_PkmnCenterNurse_PlayerWaitingInUnionRoom::
	goto_if_set FLAG_NURSE_UNION_ROOM_REMINDER, EventScript_PkmnCenterNurse_ReturnPkmn
	msgbox gText_RestoredPkmnToFullHealth, MSGBOX_DEFAULT
	setflag FLAG_NURSE_UNION_ROOM_REMINDER
	message CableClub_Text_PlayerIsWaiting
	waitmessage
	applymovement VAR_0x800B, Movement_PkmnCenterNurse_Bow
	waitmovement 0
	message gText_WeHopeToSeeYouAgain
	return

EventScript_PkmnCenterNurse_CheckPokerus::
	specialvar VAR_RESULT, IsPokerusInParty
	goto_if_eq VAR_RESULT, TRUE, EventScript_PkmnCenterNurse_ExplainPokerus
	goto_if_eq VAR_RESULT, FALSE, EventScript_PkmnCenterNurse_CheckTrainerHillAndUnionRoom
	end

EventScript_PkmnCenterNurse_ExplainPokerus::
	message gText_PokerusExplanation
	setflag FLAG_POKERUS_EXPLAINED
	return

EventScript_PkmnCenterNurse_GoldCard::
	goto_if_set FLAG_NURSE_MENTIONS_GOLD_CARD, EventScript_PkmnCenterNurse_AskForUsual
	setflag FLAG_NURSE_MENTIONS_GOLD_CARD
	msgbox gText_WelcomeCutShort, MSGBOX_DEFAULT
	playse SE_PIN
	applymovement VAR_0x800B, Common_Movement_ExclamationMark
	waitmovement 0
	applymovement VAR_0x800B, Common_Movement_Delay48
	waitmovement 0
	msgbox gText_NoticesGoldCard, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, EventScript_PkmnCenterNurse_GoldCardHealPkmn
	message gText_WeHopeToSeeYouAgain2
	return

EventScript_PkmnCenterNurse_AskForUsual::
	msgbox gText_YouWantTheUsual, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, YES, EventScript_PkmnCenterNurse_GoldCardHealPkmn
	message gText_WeHopeToSeeYouAgain2
	return

EventScript_PkmnCenterNurse_GoldCardHealPkmn::
	setvar VAR_0x8004, 1
	goto EventScript_PkmnCenterNurse_HealPkmn
	end

Movement_PkmnCenterNurse_Bow:
	nurse_joy_bow
	delay_4
	step_end

Movement_PkmnCenterNurse_Turn:
	walk_in_place_faster_right
	step_end
`
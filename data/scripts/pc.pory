script Common_EventScript_PcGuy {
	lock
	faceplayer
	createfieldmugshot(VAR_0x8009, EMOTE_NORMAL)
	if (flag(FLAG_PC_EXPLAINED)) {
		msgbox(format(
			"{SPEAKER NAME_MAN}Need me to carry some Pokémon around for you? Sure thing!"
		))
	} else {
		msgbox(format(
			"{SPEAKER NAME_MAN}Hey there, Pokémon Wielder!\p"
			"Have you ever thought, “Darn, I wish I could catch this seventh Pokémon, but I only have room for six?”\p"
			"Today's your lucky day!\p"
			"I've been appointed by the Toku council to store Wielders' Pokémon for them while they travel around.\p"
			"We bring your Pokémon to a pasture where they live happily while waiting for you to get them back!\p"
			"Talk to me if you ever want to send or retrieve your Pokémon!"
		))
	}
	closemessage

	// call(EventScript_AccessPokemonStorage)
	special(ShowPokemonStorageSystemPC)
	waitstate

	createfieldmugshot(VAR_0x8009, EMOTE_NORMAL)
	if (flag(FLAG_PC_EXPLAINED)) {
		msgbox(format(
			"{SPEAKER NAME_MAN}Pant… pant… Thanks for using our Pokémon pasture service!"
		))
		closemessage
	} else {
		setflag(FLAG_PC_EXPLAINED)
		// TODO EVA do this only if we actually stored mons
		msgbox(format(
			"{SPEAKER NAME_MAN}Alrighty, I'll just do a quick trip to the pasture!\p"
			"Just a minute!"
		))
		closemessage

		fadescreenswapbuffers(FADE_TO_BLACK)
		delay(120)
		fadescreenswapbuffers(FADE_FROM_BLACK)

		createfieldmugshot(VAR_0x8009, EMOTE_NORMAL)
		msgbox(format(
			"{SPEAKER NAME_MAN}PANT…{PAUSE 8} PANT…\p"
			"Your…{PAUSE 8} pant…{PAUSE 8} Pokémon…{PAUSE 8} pant…{PAUSE 8} are all set!\p"
			"Whew, the pasture is a liiiittle far from here!{PAUSE 8} Pant…\p"
			"Come…{PAUSE 8} pant…{PAUSE 8} Come again!"
		))
		closemessage
	}

	release
	end
}

raw `EventScript_PC::
	lockall
	setvar VAR_0x8004, PC_LOCATION_OTHER
	special DoPCTurnOnEffect
	playse SE_PC_ON
	msgbox Text_BootUpPC, MSGBOX_DEFAULT
	goto EventScript_PCMainMenu
	end

EventScript_PCMainMenu::
	message gText_WhichPCShouldBeAccessed
	waitmessage
	special ScriptMenu_CreatePCMultichoice
	waitstate
	goto EventScript_AccessPC
	end

EventScript_AccessPC::
	switch VAR_RESULT
	case 0, EventScript_AccessPokemonStorage
	case 1, EventScript_AccessPlayersPC
	case 2, EventScript_AccessHallOfFame
	case 3, EventScript_TurnOffPC
	case MULTI_B_PRESSED, EventScript_TurnOffPC
	end

EventScript_AccessPlayersPC::
	playse SE_PC_LOGIN
	msgbox gText_AccessedPlayersPC, MSGBOX_DEFAULT
	special PlayerPC
	waitstate
	goto EventScript_PCMainMenu
	end

EventScript_AccessPokemonStorage::
	@ playse SE_PC_LOGIN
	@ call_if_unset FLAG_SYS_PC_LANETTE, EventScript_AccessSomeonesPC
	@ call_if_set FLAG_SYS_PC_LANETTE, EventScript_AccessLanettesPC
	@ msgbox gText_StorageSystemOpened, MSGBOX_DEFAULT
	special ShowPokemonStorageSystemPC
	waitstate
	@ goto EventScript_PCMainMenu
	releaseall
	end

EventScript_AccessSomeonesPC::
	msgbox gText_AccessedSomeonesPC, MSGBOX_DEFAULT
	return

EventScript_AccessLanettesPC::
	msgbox gText_AccessedLanettesPC, MSGBOX_DEFAULT
	return

EventScript_TurnOffPC::
	setvar VAR_0x8004, PC_LOCATION_OTHER
	playse SE_PC_OFF
	special DoPCTurnOffEffect
	releaseall
	end

EventScript_AccessHallOfFame::
	goto_if_unset FLAG_SYS_GAME_CLEAR, EventScript_TurnOffPC
	playse SE_PC_LOGIN
	special AccessHallOfFamePC
	waitstate
	goto EventScript_AccessPC
	end

EventScript_AccessPokemonBoxLink::
	playse SE_PC_LOGIN
	msgbox gText_StorageSystemOpened, MSGBOX_DEFAULT
	special ShowPokemonStorageSystemPC
	waitstate
	goto EventScript_TurnOffPC
	end
`